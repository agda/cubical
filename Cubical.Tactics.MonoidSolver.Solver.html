<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Cubical.Tactics.MonoidSolver.Solver</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Symbol">{-#</a> <a id="5" class="Keyword">OPTIONS</a> <a id="13" class="Pragma">--safe</a> <a id="20" class="Symbol">#-}</a>
<a id="24" class="Keyword">module</a> <a id="31" href="Cubical.Tactics.MonoidSolver.Solver.html" class="Module">Cubical.Tactics.MonoidSolver.Solver</a> <a id="67" class="Keyword">where</a>

<a id="74" class="Keyword">open</a> <a id="79" class="Keyword">import</a> <a id="86" href="Cubical.Foundations.Prelude.html" class="Module">Cubical.Foundations.Prelude</a>
<a id="114" class="Keyword">open</a> <a id="119" class="Keyword">import</a> <a id="126" href="Cubical.Foundations.Structure.html" class="Module">Cubical.Foundations.Structure</a>

<a id="157" class="Keyword">open</a> <a id="162" class="Keyword">import</a> <a id="169" href="Cubical.Data.FinData.html" class="Module">Cubical.Data.FinData</a> <a id="190" class="Keyword">using</a> <a id="196" class="Symbol">(</a><a id="197" href="Cubical.Data.FinData.Base.html#375" class="Datatype">Fin</a><a id="200" class="Symbol">)</a>
<a id="202" class="Keyword">open</a> <a id="207" class="Keyword">import</a> <a id="214" href="Cubical.Data.Nat.html" class="Module">Cubical.Data.Nat</a> <a id="231" class="Keyword">using</a> <a id="237" class="Symbol">(</a><a id="238" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a><a id="239" class="Symbol">)</a>
<a id="241" class="Keyword">open</a> <a id="246" class="Keyword">import</a> <a id="253" href="Cubical.Data.List.html" class="Module">Cubical.Data.List</a>
<a id="271" class="Keyword">open</a> <a id="276" class="Keyword">import</a> <a id="283" href="Cubical.Data.Vec.html" class="Module">Cubical.Data.Vec</a> <a id="300" class="Keyword">using</a> <a id="306" class="Symbol">(</a><a id="307" href="Cubical.Data.Vec.Base.html#328" class="Datatype">Vec</a><a id="310" class="Symbol">;</a> <a id="312" href="Cubical.Data.Vec.Base.html#1466" class="Function">lookup</a><a id="318" class="Symbol">)</a>

<a id="321" class="Keyword">open</a> <a id="326" class="Keyword">import</a> <a id="333" href="Cubical.Algebra.Monoid.html" class="Module">Cubical.Algebra.Monoid</a>

<a id="357" class="Keyword">open</a> <a id="362" class="Keyword">import</a> <a id="369" href="Cubical.Tactics.MonoidSolver.MonoidExpression.html" class="Module">Cubical.Tactics.MonoidSolver.MonoidExpression</a>


<a id="417" class="Keyword">private</a>
  <a id="427" class="Keyword">variable</a>
    <a id="440" href="Cubical.Tactics.MonoidSolver.Solver.html#440" class="Generalizable">ℓ</a> <a id="442" class="Symbol">:</a> <a id="444" href="Agda.Primitive.html#591" class="Postulate">Level</a>


<a id="452" class="Keyword">module</a> <a id="Eval"></a><a id="459" href="Cubical.Tactics.MonoidSolver.Solver.html#459" class="Module">Eval</a> <a id="464" class="Symbol">(</a><a id="465" href="Cubical.Tactics.MonoidSolver.Solver.html#465" class="Bound">M</a> <a id="467" class="Symbol">:</a> <a id="469" href="Cubical.Algebra.Monoid.Base.html#1088" class="Function">Monoid</a> <a id="476" href="Cubical.Tactics.MonoidSolver.Solver.html#440" class="Generalizable">ℓ</a><a id="477" class="Symbol">)</a> <a id="479" class="Keyword">where</a>
  <a id="487" class="Keyword">open</a> <a id="492" href="Cubical.Algebra.Monoid.Base.html#895" class="Module">MonoidStr</a> <a id="502" class="Symbol">(</a><a id="503" href="Agda.Builtin.Sigma.html#246" class="Field">snd</a> <a id="507" href="Cubical.Tactics.MonoidSolver.Solver.html#465" class="Bound">M</a><a id="508" class="Symbol">)</a>

  <a id="Eval.Env"></a><a id="513" href="Cubical.Tactics.MonoidSolver.Solver.html#513" class="Function">Env</a> <a id="517" class="Symbol">:</a> <a id="519" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a> <a id="521" class="Symbol">→</a> <a id="523" href="Agda.Primitive.html#320" class="Primitive">Type</a> <a id="528" href="Cubical.Tactics.MonoidSolver.Solver.html#476" class="Bound">ℓ</a>
  <a id="532" href="Cubical.Tactics.MonoidSolver.Solver.html#513" class="Function">Env</a> <a id="536" href="Cubical.Tactics.MonoidSolver.Solver.html#536" class="Bound">n</a> <a id="538" class="Symbol">=</a> <a id="540" href="Cubical.Data.Vec.Base.html#328" class="Datatype">Vec</a> <a id="544" href="Cubical.Foundations.Structure.html#639" class="Function Operator">⟨</a> <a id="546" href="Cubical.Tactics.MonoidSolver.Solver.html#465" class="Bound">M</a> <a id="548" href="Cubical.Foundations.Structure.html#639" class="Function Operator">⟩</a> <a id="550" href="Cubical.Tactics.MonoidSolver.Solver.html#536" class="Bound">n</a>

  <a id="555" class="Comment">-- evaluation of an expression (without normalization)</a>
  <a id="Eval.⟦_⟧"></a><a id="612" href="Cubical.Tactics.MonoidSolver.Solver.html#612" class="Function Operator">⟦_⟧</a> <a id="616" class="Symbol">:</a> <a id="618" class="Symbol">∀{</a><a id="620" href="Cubical.Tactics.MonoidSolver.Solver.html#620" class="Bound">n</a><a id="621" class="Symbol">}</a> <a id="623" class="Symbol">→</a> <a id="625" href="Cubical.Tactics.MonoidSolver.MonoidExpression.html#353" class="Datatype">Expr</a> <a id="630" href="Cubical.Foundations.Structure.html#639" class="Function Operator">⟨</a> <a id="632" href="Cubical.Tactics.MonoidSolver.Solver.html#465" class="Bound">M</a> <a id="634" href="Cubical.Foundations.Structure.html#639" class="Function Operator">⟩</a> <a id="636" href="Cubical.Tactics.MonoidSolver.Solver.html#620" class="Bound">n</a> <a id="638" class="Symbol">→</a> <a id="640" href="Cubical.Tactics.MonoidSolver.Solver.html#513" class="Function">Env</a> <a id="644" href="Cubical.Tactics.MonoidSolver.Solver.html#620" class="Bound">n</a> <a id="646" class="Symbol">→</a> <a id="648" href="Cubical.Foundations.Structure.html#639" class="Function Operator">⟨</a> <a id="650" href="Cubical.Tactics.MonoidSolver.Solver.html#465" class="Bound">M</a> <a id="652" href="Cubical.Foundations.Structure.html#639" class="Function Operator">⟩</a>
  <a id="656" href="Cubical.Tactics.MonoidSolver.Solver.html#612" class="Function Operator">⟦</a> <a id="658" href="Cubical.Tactics.MonoidSolver.MonoidExpression.html#421" class="InductiveConstructor">ε⊗</a> <a id="661" href="Cubical.Tactics.MonoidSolver.Solver.html#612" class="Function Operator">⟧</a> <a id="663" href="Cubical.Tactics.MonoidSolver.Solver.html#663" class="Bound">v</a>      <a id="670" class="Symbol">=</a> <a id="672" href="Cubical.Algebra.Monoid.Base.html#970" class="Function">ε</a>
  <a id="676" href="Cubical.Tactics.MonoidSolver.Solver.html#612" class="Function Operator">⟦</a> <a id="678" href="Cubical.Tactics.MonoidSolver.MonoidExpression.html#396" class="InductiveConstructor">∣</a> <a id="680" href="Cubical.Tactics.MonoidSolver.Solver.html#680" class="Bound">i</a> <a id="682" href="Cubical.Tactics.MonoidSolver.Solver.html#612" class="Function Operator">⟧</a> <a id="684" href="Cubical.Tactics.MonoidSolver.Solver.html#684" class="Bound">v</a>     <a id="690" class="Symbol">=</a> <a id="692" href="Cubical.Data.Vec.Base.html#1466" class="Function">lookup</a> <a id="699" href="Cubical.Tactics.MonoidSolver.Solver.html#680" class="Bound">i</a> <a id="701" href="Cubical.Tactics.MonoidSolver.Solver.html#684" class="Bound">v</a>
  <a id="705" href="Cubical.Tactics.MonoidSolver.Solver.html#612" class="Function Operator">⟦</a> <a id="707" href="Cubical.Tactics.MonoidSolver.Solver.html#707" class="Bound">e₁</a> <a id="710" href="Cubical.Tactics.MonoidSolver.MonoidExpression.html#438" class="InductiveConstructor Operator">⊗</a> <a id="712" href="Cubical.Tactics.MonoidSolver.Solver.html#712" class="Bound">e₂</a> <a id="715" href="Cubical.Tactics.MonoidSolver.Solver.html#612" class="Function Operator">⟧</a> <a id="717" href="Cubical.Tactics.MonoidSolver.Solver.html#717" class="Bound">v</a> <a id="719" class="Symbol">=</a> <a id="721" href="Cubical.Tactics.MonoidSolver.Solver.html#612" class="Function Operator">⟦</a> <a id="723" href="Cubical.Tactics.MonoidSolver.Solver.html#707" class="Bound">e₁</a> <a id="726" href="Cubical.Tactics.MonoidSolver.Solver.html#612" class="Function Operator">⟧</a> <a id="728" href="Cubical.Tactics.MonoidSolver.Solver.html#717" class="Bound">v</a> <a id="730" href="Cubical.Algebra.Monoid.Base.html#987" class="Function Operator">·</a> <a id="732" href="Cubical.Tactics.MonoidSolver.Solver.html#612" class="Function Operator">⟦</a> <a id="734" href="Cubical.Tactics.MonoidSolver.Solver.html#712" class="Bound">e₂</a> <a id="737" href="Cubical.Tactics.MonoidSolver.Solver.html#612" class="Function Operator">⟧</a> <a id="739" href="Cubical.Tactics.MonoidSolver.Solver.html#717" class="Bound">v</a>

  <a id="Eval.NormalForm"></a><a id="744" href="Cubical.Tactics.MonoidSolver.Solver.html#744" class="Function">NormalForm</a> <a id="755" class="Symbol">:</a> <a id="757" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a> <a id="759" class="Symbol">→</a> <a id="761" href="Agda.Primitive.html#320" class="Primitive">Type</a> <a id="766" class="Symbol">_</a>
  <a id="770" href="Cubical.Tactics.MonoidSolver.Solver.html#744" class="Function">NormalForm</a> <a id="781" href="Cubical.Tactics.MonoidSolver.Solver.html#781" class="Bound">n</a> <a id="783" class="Symbol">=</a> <a id="785" href="Agda.Builtin.List.html#130" class="Datatype">List</a> <a id="790" class="Symbol">(</a><a id="791" href="Cubical.Data.FinData.Base.html#375" class="Datatype">Fin</a> <a id="795" href="Cubical.Tactics.MonoidSolver.Solver.html#781" class="Bound">n</a><a id="796" class="Symbol">)</a>

  <a id="801" class="Comment">-- normalization of an expression</a>
  <a id="Eval.normalize"></a><a id="837" href="Cubical.Tactics.MonoidSolver.Solver.html#837" class="Function">normalize</a> <a id="847" class="Symbol">:</a> <a id="849" class="Symbol">∀{</a><a id="851" href="Cubical.Tactics.MonoidSolver.Solver.html#851" class="Bound">n</a><a id="852" class="Symbol">}</a> <a id="854" class="Symbol">→</a> <a id="856" href="Cubical.Tactics.MonoidSolver.MonoidExpression.html#353" class="Datatype">Expr</a> <a id="861" href="Cubical.Foundations.Structure.html#639" class="Function Operator">⟨</a> <a id="863" href="Cubical.Tactics.MonoidSolver.Solver.html#465" class="Bound">M</a> <a id="865" href="Cubical.Foundations.Structure.html#639" class="Function Operator">⟩</a> <a id="867" href="Cubical.Tactics.MonoidSolver.Solver.html#851" class="Bound">n</a> <a id="869" class="Symbol">→</a> <a id="871" href="Cubical.Tactics.MonoidSolver.Solver.html#744" class="Function">NormalForm</a> <a id="882" href="Cubical.Tactics.MonoidSolver.Solver.html#851" class="Bound">n</a>
  <a id="886" href="Cubical.Tactics.MonoidSolver.Solver.html#837" class="Function">normalize</a> <a id="896" class="Symbol">(</a><a id="897" href="Cubical.Tactics.MonoidSolver.MonoidExpression.html#396" class="InductiveConstructor">∣</a> <a id="899" href="Cubical.Tactics.MonoidSolver.Solver.html#899" class="Bound">i</a><a id="900" class="Symbol">)</a>     <a id="906" class="Symbol">=</a> <a id="908" href="Cubical.Tactics.MonoidSolver.Solver.html#899" class="Bound">i</a> <a id="910" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="912" href="Agda.Builtin.List.html#167" class="InductiveConstructor">[]</a>
  <a id="917" href="Cubical.Tactics.MonoidSolver.Solver.html#837" class="Function">normalize</a> <a id="927" href="Cubical.Tactics.MonoidSolver.MonoidExpression.html#421" class="InductiveConstructor">ε⊗</a>        <a id="937" class="Symbol">=</a> <a id="939" href="Agda.Builtin.List.html#167" class="InductiveConstructor">[]</a>
  <a id="944" href="Cubical.Tactics.MonoidSolver.Solver.html#837" class="Function">normalize</a> <a id="954" class="Symbol">(</a><a id="955" href="Cubical.Tactics.MonoidSolver.Solver.html#955" class="Bound">e₁</a> <a id="958" href="Cubical.Tactics.MonoidSolver.MonoidExpression.html#438" class="InductiveConstructor Operator">⊗</a> <a id="960" href="Cubical.Tactics.MonoidSolver.Solver.html#960" class="Bound">e₂</a><a id="962" class="Symbol">)</a> <a id="964" class="Symbol">=</a> <a id="966" class="Symbol">(</a><a id="967" href="Cubical.Tactics.MonoidSolver.Solver.html#837" class="Function">normalize</a> <a id="977" href="Cubical.Tactics.MonoidSolver.Solver.html#955" class="Bound">e₁</a><a id="979" class="Symbol">)</a> <a id="981" href="Cubical.Data.List.Base.html#319" class="Function Operator">++</a> <a id="984" class="Symbol">(</a><a id="985" href="Cubical.Tactics.MonoidSolver.Solver.html#837" class="Function">normalize</a> <a id="995" href="Cubical.Tactics.MonoidSolver.Solver.html#960" class="Bound">e₂</a><a id="997" class="Symbol">)</a>

  <a id="1002" class="Comment">-- evaluation of normalform</a>
  <a id="Eval.eval"></a><a id="1032" href="Cubical.Tactics.MonoidSolver.Solver.html#1032" class="Function">eval</a> <a id="1037" class="Symbol">:</a> <a id="1039" class="Symbol">∀</a> <a id="1041" class="Symbol">{</a><a id="1042" href="Cubical.Tactics.MonoidSolver.Solver.html#1042" class="Bound">n</a><a id="1043" class="Symbol">}</a> <a id="1045" class="Symbol">→</a> <a id="1047" href="Cubical.Tactics.MonoidSolver.Solver.html#744" class="Function">NormalForm</a> <a id="1058" href="Cubical.Tactics.MonoidSolver.Solver.html#1042" class="Bound">n</a> <a id="1060" class="Symbol">→</a> <a id="1062" href="Cubical.Tactics.MonoidSolver.Solver.html#513" class="Function">Env</a> <a id="1066" href="Cubical.Tactics.MonoidSolver.Solver.html#1042" class="Bound">n</a> <a id="1068" class="Symbol">→</a> <a id="1070" href="Cubical.Foundations.Structure.html#639" class="Function Operator">⟨</a> <a id="1072" href="Cubical.Tactics.MonoidSolver.Solver.html#465" class="Bound">M</a> <a id="1074" href="Cubical.Foundations.Structure.html#639" class="Function Operator">⟩</a>
  <a id="1078" href="Cubical.Tactics.MonoidSolver.Solver.html#1032" class="Function">eval</a> <a id="1083" href="Agda.Builtin.List.html#167" class="InductiveConstructor">[]</a> <a id="1086" href="Cubical.Tactics.MonoidSolver.Solver.html#1086" class="Bound">v</a>       <a id="1094" class="Symbol">=</a> <a id="1096" href="Cubical.Algebra.Monoid.Base.html#970" class="Function">ε</a>
  <a id="1100" href="Cubical.Tactics.MonoidSolver.Solver.html#1032" class="Function">eval</a> <a id="1105" class="Symbol">(</a><a id="1106" href="Cubical.Tactics.MonoidSolver.Solver.html#1106" class="Bound">x</a> <a id="1108" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="1110" href="Cubical.Tactics.MonoidSolver.Solver.html#1110" class="Bound">xs</a><a id="1112" class="Symbol">)</a> <a id="1114" href="Cubical.Tactics.MonoidSolver.Solver.html#1114" class="Bound">v</a> <a id="1116" class="Symbol">=</a> <a id="1118" class="Symbol">(</a><a id="1119" href="Cubical.Data.Vec.Base.html#1466" class="Function">lookup</a> <a id="1126" href="Cubical.Tactics.MonoidSolver.Solver.html#1106" class="Bound">x</a> <a id="1128" href="Cubical.Tactics.MonoidSolver.Solver.html#1114" class="Bound">v</a><a id="1129" class="Symbol">)</a> <a id="1131" href="Cubical.Algebra.Monoid.Base.html#987" class="Function Operator">·</a> <a id="1133" class="Symbol">(</a><a id="1134" href="Cubical.Tactics.MonoidSolver.Solver.html#1032" class="Function">eval</a> <a id="1139" href="Cubical.Tactics.MonoidSolver.Solver.html#1110" class="Bound">xs</a> <a id="1142" href="Cubical.Tactics.MonoidSolver.Solver.html#1114" class="Bound">v</a><a id="1143" class="Symbol">)</a>

  <a id="1148" class="Comment">-- some calculation</a>
  <a id="Eval.evalIsHom"></a><a id="1170" href="Cubical.Tactics.MonoidSolver.Solver.html#1170" class="Function">evalIsHom</a> <a id="1180" class="Symbol">:</a> <a id="1182" class="Symbol">∀</a> <a id="1184" class="Symbol">{</a><a id="1185" href="Cubical.Tactics.MonoidSolver.Solver.html#1185" class="Bound">n</a><a id="1186" class="Symbol">}</a> <a id="1188" class="Symbol">(</a><a id="1189" href="Cubical.Tactics.MonoidSolver.Solver.html#1189" class="Bound">x</a> <a id="1191" href="Cubical.Tactics.MonoidSolver.Solver.html#1191" class="Bound">y</a> <a id="1193" class="Symbol">:</a> <a id="1195" href="Cubical.Tactics.MonoidSolver.Solver.html#744" class="Function">NormalForm</a> <a id="1206" href="Cubical.Tactics.MonoidSolver.Solver.html#1185" class="Bound">n</a><a id="1207" class="Symbol">)</a> <a id="1209" class="Symbol">(</a><a id="1210" href="Cubical.Tactics.MonoidSolver.Solver.html#1210" class="Bound">v</a> <a id="1212" class="Symbol">:</a> <a id="1214" href="Cubical.Tactics.MonoidSolver.Solver.html#513" class="Function">Env</a> <a id="1218" href="Cubical.Tactics.MonoidSolver.Solver.html#1185" class="Bound">n</a><a id="1219" class="Symbol">)</a>
            <a id="1233" class="Symbol">→</a> <a id="1235" href="Cubical.Tactics.MonoidSolver.Solver.html#1032" class="Function">eval</a> <a id="1240" class="Symbol">(</a><a id="1241" href="Cubical.Tactics.MonoidSolver.Solver.html#1189" class="Bound">x</a> <a id="1243" href="Cubical.Data.List.Base.html#319" class="Function Operator">++</a> <a id="1246" href="Cubical.Tactics.MonoidSolver.Solver.html#1191" class="Bound">y</a><a id="1247" class="Symbol">)</a> <a id="1249" href="Cubical.Tactics.MonoidSolver.Solver.html#1210" class="Bound">v</a> <a id="1251" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="1253" href="Cubical.Tactics.MonoidSolver.Solver.html#1032" class="Function">eval</a> <a id="1258" href="Cubical.Tactics.MonoidSolver.Solver.html#1189" class="Bound">x</a> <a id="1260" href="Cubical.Tactics.MonoidSolver.Solver.html#1210" class="Bound">v</a> <a id="1262" href="Cubical.Algebra.Monoid.Base.html#987" class="Function Operator">·</a> <a id="1264" href="Cubical.Tactics.MonoidSolver.Solver.html#1032" class="Function">eval</a> <a id="1269" href="Cubical.Tactics.MonoidSolver.Solver.html#1191" class="Bound">y</a> <a id="1271" href="Cubical.Tactics.MonoidSolver.Solver.html#1210" class="Bound">v</a>
  <a id="1275" href="Cubical.Tactics.MonoidSolver.Solver.html#1170" class="Function">evalIsHom</a> <a id="1285" href="Agda.Builtin.List.html#167" class="InductiveConstructor">[]</a> <a id="1288" href="Cubical.Tactics.MonoidSolver.Solver.html#1288" class="Bound">y</a> <a id="1290" href="Cubical.Tactics.MonoidSolver.Solver.html#1290" class="Bound">v</a>       <a id="1298" class="Symbol">=</a> <a id="1300" href="Cubical.Foundations.Prelude.html#968" class="Function">sym</a> <a id="1304" class="Symbol">(</a><a id="1305" href="Cubical.Algebra.Monoid.Base.html#745" class="Function">·IdL</a> <a id="1310" class="Symbol">_)</a>
  <a id="1315" href="Cubical.Tactics.MonoidSolver.Solver.html#1170" class="Function">evalIsHom</a> <a id="1325" class="Symbol">(</a><a id="1326" href="Cubical.Tactics.MonoidSolver.Solver.html#1326" class="Bound">x</a> <a id="1328" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="1330" href="Cubical.Tactics.MonoidSolver.Solver.html#1330" class="Bound">xs</a><a id="1332" class="Symbol">)</a> <a id="1334" href="Cubical.Tactics.MonoidSolver.Solver.html#1334" class="Bound">y</a> <a id="1336" href="Cubical.Tactics.MonoidSolver.Solver.html#1336" class="Bound">v</a> <a id="1338" class="Symbol">=</a>
    <a id="1344" href="Cubical.Foundations.Prelude.html#1430" class="Function">cong</a> <a id="1349" class="Symbol">(λ</a> <a id="1352" href="Cubical.Tactics.MonoidSolver.Solver.html#1352" class="Bound">m</a> <a id="1354" class="Symbol">→</a> <a id="1356" class="Symbol">(</a><a id="1357" href="Cubical.Data.Vec.Base.html#1466" class="Function">lookup</a> <a id="1364" href="Cubical.Tactics.MonoidSolver.Solver.html#1326" class="Bound">x</a> <a id="1366" href="Cubical.Tactics.MonoidSolver.Solver.html#1336" class="Bound">v</a><a id="1367" class="Symbol">)</a> <a id="1369" href="Cubical.Algebra.Monoid.Base.html#987" class="Function Operator">·</a> <a id="1371" href="Cubical.Tactics.MonoidSolver.Solver.html#1352" class="Bound">m</a><a id="1372" class="Symbol">)</a> <a id="1374" class="Symbol">(</a><a id="1375" href="Cubical.Tactics.MonoidSolver.Solver.html#1170" class="Function">evalIsHom</a> <a id="1385" href="Cubical.Tactics.MonoidSolver.Solver.html#1330" class="Bound">xs</a> <a id="1388" href="Cubical.Tactics.MonoidSolver.Solver.html#1334" class="Bound">y</a> <a id="1390" href="Cubical.Tactics.MonoidSolver.Solver.html#1336" class="Bound">v</a><a id="1391" class="Symbol">)</a> <a id="1393" href="Cubical.Foundations.Prelude.html#4447" class="Function Operator">∙</a> <a id="1395" href="Cubical.Algebra.Semigroup.Base.html#1031" class="Function">·Assoc</a> <a id="1402" class="Symbol">_</a> <a id="1404" class="Symbol">_</a> <a id="1406" class="Symbol">_</a>

<a id="1409" class="Keyword">module</a> <a id="EqualityToNormalform"></a><a id="1416" href="Cubical.Tactics.MonoidSolver.Solver.html#1416" class="Module">EqualityToNormalform</a> <a id="1437" class="Symbol">(</a><a id="1438" href="Cubical.Tactics.MonoidSolver.Solver.html#1438" class="Bound">M</a> <a id="1440" class="Symbol">:</a> <a id="1442" href="Cubical.Algebra.Monoid.Base.html#1088" class="Function">Monoid</a> <a id="1449" href="Cubical.Tactics.MonoidSolver.Solver.html#440" class="Generalizable">ℓ</a><a id="1450" class="Symbol">)</a> <a id="1452" class="Keyword">where</a>
  <a id="1460" class="Keyword">open</a> <a id="1465" href="Cubical.Tactics.MonoidSolver.Solver.html#459" class="Module">Eval</a> <a id="1470" href="Cubical.Tactics.MonoidSolver.Solver.html#1438" class="Bound">M</a>
  <a id="1474" class="Keyword">open</a> <a id="1479" href="Cubical.Algebra.Monoid.Base.html#895" class="Module">MonoidStr</a> <a id="1489" class="Symbol">(</a><a id="1490" href="Agda.Builtin.Sigma.html#246" class="Field">snd</a> <a id="1494" href="Cubical.Tactics.MonoidSolver.Solver.html#1438" class="Bound">M</a><a id="1495" class="Symbol">)</a>

  <a id="1500" class="Comment">-- proof that evaluation of an expression is invariant under normalization</a>
  <a id="EqualityToNormalform.isEqualToNormalform"></a><a id="1577" href="Cubical.Tactics.MonoidSolver.Solver.html#1577" class="Function">isEqualToNormalform</a> <a id="1597" class="Symbol">:</a> <a id="1599" class="Symbol">(</a><a id="1600" href="Cubical.Tactics.MonoidSolver.Solver.html#1600" class="Bound">n</a> <a id="1602" class="Symbol">:</a> <a id="1604" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a><a id="1605" class="Symbol">)</a>
                      <a id="1629" class="Symbol">→</a> <a id="1631" class="Symbol">(</a><a id="1632" href="Cubical.Tactics.MonoidSolver.Solver.html#1632" class="Bound">e</a> <a id="1634" class="Symbol">:</a> <a id="1636" href="Cubical.Tactics.MonoidSolver.MonoidExpression.html#353" class="Datatype">Expr</a> <a id="1641" href="Cubical.Foundations.Structure.html#639" class="Function Operator">⟨</a> <a id="1643" href="Cubical.Tactics.MonoidSolver.Solver.html#1438" class="Bound">M</a> <a id="1645" href="Cubical.Foundations.Structure.html#639" class="Function Operator">⟩</a> <a id="1647" href="Cubical.Tactics.MonoidSolver.Solver.html#1600" class="Bound">n</a><a id="1648" class="Symbol">)</a>
                      <a id="1672" class="Symbol">→</a> <a id="1674" class="Symbol">(</a><a id="1675" href="Cubical.Tactics.MonoidSolver.Solver.html#1675" class="Bound">v</a> <a id="1677" class="Symbol">:</a> <a id="1679" href="Cubical.Tactics.MonoidSolver.Solver.html#513" class="Function">Env</a> <a id="1683" href="Cubical.Tactics.MonoidSolver.Solver.html#1600" class="Bound">n</a><a id="1684" class="Symbol">)</a>
                      <a id="1708" class="Symbol">→</a> <a id="1710" href="Cubical.Tactics.MonoidSolver.Solver.html#1032" class="Function">eval</a> <a id="1715" class="Symbol">(</a><a id="1716" href="Cubical.Tactics.MonoidSolver.Solver.html#837" class="Function">normalize</a> <a id="1726" href="Cubical.Tactics.MonoidSolver.Solver.html#1632" class="Bound">e</a><a id="1727" class="Symbol">)</a> <a id="1729" href="Cubical.Tactics.MonoidSolver.Solver.html#1675" class="Bound">v</a> <a id="1731" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="1733" href="Cubical.Tactics.MonoidSolver.Solver.html#612" class="Function Operator">⟦</a> <a id="1735" href="Cubical.Tactics.MonoidSolver.Solver.html#1632" class="Bound">e</a> <a id="1737" href="Cubical.Tactics.MonoidSolver.Solver.html#612" class="Function Operator">⟧</a> <a id="1739" href="Cubical.Tactics.MonoidSolver.Solver.html#1675" class="Bound">v</a>
  <a id="1743" href="Cubical.Tactics.MonoidSolver.Solver.html#1577" class="Function">isEqualToNormalform</a> <a id="1763" href="Cubical.Tactics.MonoidSolver.Solver.html#1763" class="Bound">n</a> <a id="1765" class="Symbol">(</a><a id="1766" href="Cubical.Tactics.MonoidSolver.MonoidExpression.html#396" class="InductiveConstructor">∣</a> <a id="1768" href="Cubical.Tactics.MonoidSolver.Solver.html#1768" class="Bound">i</a><a id="1769" class="Symbol">)</a> <a id="1771" href="Cubical.Tactics.MonoidSolver.Solver.html#1771" class="Bound">v</a>     <a id="1777" class="Symbol">=</a> <a id="1779" href="Cubical.Algebra.Monoid.Base.html#714" class="Function">·IdR</a> <a id="1784" class="Symbol">_</a>
  <a id="1788" href="Cubical.Tactics.MonoidSolver.Solver.html#1577" class="Function">isEqualToNormalform</a> <a id="1808" href="Cubical.Tactics.MonoidSolver.Solver.html#1808" class="Bound">n</a> <a id="1810" href="Cubical.Tactics.MonoidSolver.MonoidExpression.html#421" class="InductiveConstructor">ε⊗</a> <a id="1813" href="Cubical.Tactics.MonoidSolver.Solver.html#1813" class="Bound">v</a>        <a id="1822" class="Symbol">=</a> <a id="1824" href="Cubical.Foundations.Prelude.html#915" class="Function">refl</a>
  <a id="1831" href="Cubical.Tactics.MonoidSolver.Solver.html#1577" class="Function">isEqualToNormalform</a> <a id="1851" href="Cubical.Tactics.MonoidSolver.Solver.html#1851" class="Bound">n</a> <a id="1853" class="Symbol">(</a><a id="1854" href="Cubical.Tactics.MonoidSolver.Solver.html#1854" class="Bound">e₁</a> <a id="1857" href="Cubical.Tactics.MonoidSolver.MonoidExpression.html#438" class="InductiveConstructor Operator">⊗</a> <a id="1859" href="Cubical.Tactics.MonoidSolver.Solver.html#1859" class="Bound">e₂</a><a id="1861" class="Symbol">)</a> <a id="1863" href="Cubical.Tactics.MonoidSolver.Solver.html#1863" class="Bound">v</a> <a id="1865" class="Symbol">=</a>
    <a id="1871" href="Cubical.Tactics.MonoidSolver.Solver.html#1032" class="Function">eval</a> <a id="1876" class="Symbol">((</a><a id="1878" href="Cubical.Tactics.MonoidSolver.Solver.html#837" class="Function">normalize</a> <a id="1888" href="Cubical.Tactics.MonoidSolver.Solver.html#1854" class="Bound">e₁</a><a id="1890" class="Symbol">)</a> <a id="1892" href="Cubical.Data.List.Base.html#319" class="Function Operator">++</a> <a id="1895" class="Symbol">(</a><a id="1896" href="Cubical.Tactics.MonoidSolver.Solver.html#837" class="Function">normalize</a> <a id="1906" href="Cubical.Tactics.MonoidSolver.Solver.html#1859" class="Bound">e₂</a><a id="1908" class="Symbol">))</a> <a id="1911" href="Cubical.Tactics.MonoidSolver.Solver.html#1863" class="Bound">v</a>         <a id="1921" href="Cubical.Foundations.Prelude.html#7981" class="Function">≡⟨</a> <a id="1924" href="Cubical.Tactics.MonoidSolver.Solver.html#1170" class="Function">evalIsHom</a> <a id="1934" class="Symbol">(</a><a id="1935" href="Cubical.Tactics.MonoidSolver.Solver.html#837" class="Function">normalize</a> <a id="1945" href="Cubical.Tactics.MonoidSolver.Solver.html#1854" class="Bound">e₁</a><a id="1947" class="Symbol">)</a> <a id="1949" class="Symbol">(</a><a id="1950" href="Cubical.Tactics.MonoidSolver.Solver.html#837" class="Function">normalize</a> <a id="1960" href="Cubical.Tactics.MonoidSolver.Solver.html#1859" class="Bound">e₂</a><a id="1962" class="Symbol">)</a> <a id="1964" href="Cubical.Tactics.MonoidSolver.Solver.html#1863" class="Bound">v</a> <a id="1966" href="Cubical.Foundations.Prelude.html#7981" class="Function">⟩</a>
    <a id="1972" class="Symbol">(</a><a id="1973" href="Cubical.Tactics.MonoidSolver.Solver.html#1032" class="Function">eval</a> <a id="1978" class="Symbol">(</a><a id="1979" href="Cubical.Tactics.MonoidSolver.Solver.html#837" class="Function">normalize</a> <a id="1989" href="Cubical.Tactics.MonoidSolver.Solver.html#1854" class="Bound">e₁</a><a id="1991" class="Symbol">)</a> <a id="1993" href="Cubical.Tactics.MonoidSolver.Solver.html#1863" class="Bound">v</a><a id="1994" class="Symbol">)</a> <a id="1996" href="Cubical.Algebra.Monoid.Base.html#987" class="Function Operator">·</a> <a id="1998" class="Symbol">(</a><a id="1999" href="Cubical.Tactics.MonoidSolver.Solver.html#1032" class="Function">eval</a> <a id="2004" class="Symbol">(</a><a id="2005" href="Cubical.Tactics.MonoidSolver.Solver.html#837" class="Function">normalize</a> <a id="2015" href="Cubical.Tactics.MonoidSolver.Solver.html#1859" class="Bound">e₂</a><a id="2017" class="Symbol">)</a> <a id="2019" href="Cubical.Tactics.MonoidSolver.Solver.html#1863" class="Bound">v</a><a id="2020" class="Symbol">)</a> <a id="2022" href="Cubical.Foundations.Prelude.html#7981" class="Function">≡⟨</a> <a id="2025" href="Cubical.Foundations.Prelude.html#2025" class="Function">cong₂</a> <a id="2031" href="Cubical.Algebra.Monoid.Base.html#987" class="Function Operator">_·_</a> <a id="2035" class="Symbol">(</a><a id="2036" href="Cubical.Tactics.MonoidSolver.Solver.html#1577" class="Function">isEqualToNormalform</a> <a id="2056" href="Cubical.Tactics.MonoidSolver.Solver.html#1851" class="Bound">n</a> <a id="2058" href="Cubical.Tactics.MonoidSolver.Solver.html#1854" class="Bound">e₁</a> <a id="2061" href="Cubical.Tactics.MonoidSolver.Solver.html#1863" class="Bound">v</a><a id="2062" class="Symbol">)</a> <a id="2064" class="Symbol">(</a><a id="2065" href="Cubical.Tactics.MonoidSolver.Solver.html#1577" class="Function">isEqualToNormalform</a> <a id="2085" href="Cubical.Tactics.MonoidSolver.Solver.html#1851" class="Bound">n</a> <a id="2087" href="Cubical.Tactics.MonoidSolver.Solver.html#1859" class="Bound">e₂</a> <a id="2090" href="Cubical.Tactics.MonoidSolver.Solver.html#1863" class="Bound">v</a><a id="2091" class="Symbol">)</a> <a id="2093" href="Cubical.Foundations.Prelude.html#7981" class="Function">⟩</a>
    <a id="2099" href="Cubical.Tactics.MonoidSolver.Solver.html#612" class="Function Operator">⟦</a> <a id="2101" href="Cubical.Tactics.MonoidSolver.Solver.html#1854" class="Bound">e₁</a> <a id="2104" href="Cubical.Tactics.MonoidSolver.Solver.html#612" class="Function Operator">⟧</a> <a id="2106" href="Cubical.Tactics.MonoidSolver.Solver.html#1863" class="Bound">v</a> <a id="2108" href="Cubical.Algebra.Monoid.Base.html#987" class="Function Operator">·</a> <a id="2110" href="Cubical.Tactics.MonoidSolver.Solver.html#612" class="Function Operator">⟦</a> <a id="2112" href="Cubical.Tactics.MonoidSolver.Solver.html#1859" class="Bound">e₂</a> <a id="2115" href="Cubical.Tactics.MonoidSolver.Solver.html#612" class="Function Operator">⟧</a> <a id="2117" href="Cubical.Tactics.MonoidSolver.Solver.html#1863" class="Bound">v</a>                               <a id="2149" href="Cubical.Foundations.Prelude.html#8528" class="Function Operator">∎</a>

  <a id="EqualityToNormalform.solve"></a><a id="2154" href="Cubical.Tactics.MonoidSolver.Solver.html#2154" class="Function">solve</a> <a id="2160" class="Symbol">:</a> <a id="2162" class="Symbol">{</a><a id="2163" href="Cubical.Tactics.MonoidSolver.Solver.html#2163" class="Bound">n</a> <a id="2165" class="Symbol">:</a> <a id="2167" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a><a id="2168" class="Symbol">}</a>
        <a id="2178" class="Symbol">→</a> <a id="2180" class="Symbol">(</a><a id="2181" href="Cubical.Tactics.MonoidSolver.Solver.html#2181" class="Bound">e₁</a> <a id="2184" href="Cubical.Tactics.MonoidSolver.Solver.html#2184" class="Bound">e₂</a> <a id="2187" class="Symbol">:</a> <a id="2189" href="Cubical.Tactics.MonoidSolver.MonoidExpression.html#353" class="Datatype">Expr</a> <a id="2194" href="Cubical.Foundations.Structure.html#639" class="Function Operator">⟨</a> <a id="2196" href="Cubical.Tactics.MonoidSolver.Solver.html#1438" class="Bound">M</a> <a id="2198" href="Cubical.Foundations.Structure.html#639" class="Function Operator">⟩</a> <a id="2200" href="Cubical.Tactics.MonoidSolver.Solver.html#2163" class="Bound">n</a><a id="2201" class="Symbol">)</a>
        <a id="2211" class="Symbol">→</a> <a id="2213" class="Symbol">(</a><a id="2214" href="Cubical.Tactics.MonoidSolver.Solver.html#2214" class="Bound">v</a> <a id="2216" class="Symbol">:</a> <a id="2218" href="Cubical.Tactics.MonoidSolver.Solver.html#513" class="Function">Env</a> <a id="2222" href="Cubical.Tactics.MonoidSolver.Solver.html#2163" class="Bound">n</a><a id="2223" class="Symbol">)</a>
        <a id="2233" class="Symbol">→</a> <a id="2235" class="Symbol">(</a><a id="2236" href="Cubical.Tactics.MonoidSolver.Solver.html#2236" class="Bound">p</a> <a id="2238" class="Symbol">:</a> <a id="2240" href="Cubical.Tactics.MonoidSolver.Solver.html#1032" class="Function">eval</a> <a id="2245" class="Symbol">(</a><a id="2246" href="Cubical.Tactics.MonoidSolver.Solver.html#837" class="Function">normalize</a> <a id="2256" href="Cubical.Tactics.MonoidSolver.Solver.html#2181" class="Bound">e₁</a><a id="2258" class="Symbol">)</a> <a id="2260" href="Cubical.Tactics.MonoidSolver.Solver.html#2214" class="Bound">v</a> <a id="2262" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="2264" href="Cubical.Tactics.MonoidSolver.Solver.html#1032" class="Function">eval</a> <a id="2269" class="Symbol">(</a><a id="2270" href="Cubical.Tactics.MonoidSolver.Solver.html#837" class="Function">normalize</a> <a id="2280" href="Cubical.Tactics.MonoidSolver.Solver.html#2184" class="Bound">e₂</a><a id="2282" class="Symbol">)</a> <a id="2284" href="Cubical.Tactics.MonoidSolver.Solver.html#2214" class="Bound">v</a><a id="2285" class="Symbol">)</a>
        <a id="2295" class="Symbol">→</a> <a id="2297" href="Cubical.Tactics.MonoidSolver.Solver.html#612" class="Function Operator">⟦</a> <a id="2299" href="Cubical.Tactics.MonoidSolver.Solver.html#2181" class="Bound">e₁</a> <a id="2302" href="Cubical.Tactics.MonoidSolver.Solver.html#612" class="Function Operator">⟧</a> <a id="2304" href="Cubical.Tactics.MonoidSolver.Solver.html#2214" class="Bound">v</a> <a id="2306" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="2308" href="Cubical.Tactics.MonoidSolver.Solver.html#612" class="Function Operator">⟦</a> <a id="2310" href="Cubical.Tactics.MonoidSolver.Solver.html#2184" class="Bound">e₂</a> <a id="2313" href="Cubical.Tactics.MonoidSolver.Solver.html#612" class="Function Operator">⟧</a> <a id="2315" href="Cubical.Tactics.MonoidSolver.Solver.html#2214" class="Bound">v</a>
  <a id="2319" href="Cubical.Tactics.MonoidSolver.Solver.html#2154" class="Function">solve</a> <a id="2325" href="Cubical.Tactics.MonoidSolver.Solver.html#2325" class="Bound">e₁</a> <a id="2328" href="Cubical.Tactics.MonoidSolver.Solver.html#2328" class="Bound">e₂</a> <a id="2331" href="Cubical.Tactics.MonoidSolver.Solver.html#2331" class="Bound">v</a> <a id="2333" href="Cubical.Tactics.MonoidSolver.Solver.html#2333" class="Bound">p</a> <a id="2335" class="Symbol">=</a>
    <a id="2341" href="Cubical.Tactics.MonoidSolver.Solver.html#612" class="Function Operator">⟦</a> <a id="2343" href="Cubical.Tactics.MonoidSolver.Solver.html#2325" class="Bound">e₁</a> <a id="2346" href="Cubical.Tactics.MonoidSolver.Solver.html#612" class="Function Operator">⟧</a> <a id="2348" href="Cubical.Tactics.MonoidSolver.Solver.html#2331" class="Bound">v</a>              <a id="2363" href="Cubical.Foundations.Prelude.html#7981" class="Function">≡⟨</a> <a id="2366" href="Cubical.Foundations.Prelude.html#968" class="Function">sym</a> <a id="2370" class="Symbol">(</a><a id="2371" href="Cubical.Tactics.MonoidSolver.Solver.html#1577" class="Function">isEqualToNormalform</a> <a id="2391" class="Symbol">_</a> <a id="2393" href="Cubical.Tactics.MonoidSolver.Solver.html#2325" class="Bound">e₁</a> <a id="2396" href="Cubical.Tactics.MonoidSolver.Solver.html#2331" class="Bound">v</a><a id="2397" class="Symbol">)</a> <a id="2399" href="Cubical.Foundations.Prelude.html#7981" class="Function">⟩</a>
    <a id="2405" href="Cubical.Tactics.MonoidSolver.Solver.html#1032" class="Function">eval</a> <a id="2410" class="Symbol">(</a><a id="2411" href="Cubical.Tactics.MonoidSolver.Solver.html#837" class="Function">normalize</a> <a id="2421" href="Cubical.Tactics.MonoidSolver.Solver.html#2325" class="Bound">e₁</a><a id="2423" class="Symbol">)</a> <a id="2425" href="Cubical.Tactics.MonoidSolver.Solver.html#2331" class="Bound">v</a> <a id="2427" href="Cubical.Foundations.Prelude.html#7981" class="Function">≡⟨</a> <a id="2430" href="Cubical.Tactics.MonoidSolver.Solver.html#2333" class="Bound">p</a> <a id="2432" href="Cubical.Foundations.Prelude.html#7981" class="Function">⟩</a>
    <a id="2438" href="Cubical.Tactics.MonoidSolver.Solver.html#1032" class="Function">eval</a> <a id="2443" class="Symbol">(</a><a id="2444" href="Cubical.Tactics.MonoidSolver.Solver.html#837" class="Function">normalize</a> <a id="2454" href="Cubical.Tactics.MonoidSolver.Solver.html#2328" class="Bound">e₂</a><a id="2456" class="Symbol">)</a> <a id="2458" href="Cubical.Tactics.MonoidSolver.Solver.html#2331" class="Bound">v</a> <a id="2460" href="Cubical.Foundations.Prelude.html#7981" class="Function">≡⟨</a> <a id="2463" href="Cubical.Tactics.MonoidSolver.Solver.html#1577" class="Function">isEqualToNormalform</a> <a id="2483" class="Symbol">_</a> <a id="2485" href="Cubical.Tactics.MonoidSolver.Solver.html#2328" class="Bound">e₂</a> <a id="2488" href="Cubical.Tactics.MonoidSolver.Solver.html#2331" class="Bound">v</a> <a id="2490" href="Cubical.Foundations.Prelude.html#7981" class="Function">⟩</a>
    <a id="2496" href="Cubical.Tactics.MonoidSolver.Solver.html#612" class="Function Operator">⟦</a> <a id="2498" href="Cubical.Tactics.MonoidSolver.Solver.html#2328" class="Bound">e₂</a> <a id="2501" href="Cubical.Tactics.MonoidSolver.Solver.html#612" class="Function Operator">⟧</a> <a id="2503" href="Cubical.Tactics.MonoidSolver.Solver.html#2331" class="Bound">v</a>              <a id="2518" href="Cubical.Foundations.Prelude.html#8528" class="Function Operator">∎</a>

<a id="solve"></a><a id="2521" href="Cubical.Tactics.MonoidSolver.Solver.html#2521" class="Function">solve</a> <a id="2527" class="Symbol">:</a> <a id="2529" class="Symbol">(</a><a id="2530" href="Cubical.Tactics.MonoidSolver.Solver.html#2530" class="Bound">M</a> <a id="2532" class="Symbol">:</a> <a id="2534" href="Cubical.Algebra.Monoid.Base.html#1088" class="Function">Monoid</a> <a id="2541" href="Cubical.Tactics.MonoidSolver.Solver.html#440" class="Generalizable">ℓ</a><a id="2542" class="Symbol">)</a>
        <a id="2552" class="Symbol">{</a><a id="2553" href="Cubical.Tactics.MonoidSolver.Solver.html#2553" class="Bound">n</a> <a id="2555" class="Symbol">:</a> <a id="2557" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a><a id="2558" class="Symbol">}</a> <a id="2560" class="Symbol">(</a><a id="2561" href="Cubical.Tactics.MonoidSolver.Solver.html#2561" class="Bound">e₁</a> <a id="2564" href="Cubical.Tactics.MonoidSolver.Solver.html#2564" class="Bound">e₂</a> <a id="2567" class="Symbol">:</a> <a id="2569" href="Cubical.Tactics.MonoidSolver.MonoidExpression.html#353" class="Datatype">Expr</a> <a id="2574" href="Cubical.Foundations.Structure.html#639" class="Function Operator">⟨</a> <a id="2576" href="Cubical.Tactics.MonoidSolver.Solver.html#2530" class="Bound">M</a> <a id="2578" href="Cubical.Foundations.Structure.html#639" class="Function Operator">⟩</a> <a id="2580" href="Cubical.Tactics.MonoidSolver.Solver.html#2553" class="Bound">n</a><a id="2581" class="Symbol">)</a> <a id="2583" class="Symbol">(</a><a id="2584" href="Cubical.Tactics.MonoidSolver.Solver.html#2584" class="Bound">v</a> <a id="2586" class="Symbol">:</a> <a id="2588" href="Cubical.Tactics.MonoidSolver.Solver.html#513" class="Function">Eval.Env</a> <a id="2597" href="Cubical.Tactics.MonoidSolver.Solver.html#2530" class="Bound">M</a> <a id="2599" href="Cubical.Tactics.MonoidSolver.Solver.html#2553" class="Bound">n</a><a id="2600" class="Symbol">)</a>
        <a id="2610" class="Symbol">(</a><a id="2611" href="Cubical.Tactics.MonoidSolver.Solver.html#2611" class="Bound">p</a> <a id="2613" class="Symbol">:</a>  <a id="2616" href="Cubical.Tactics.MonoidSolver.Solver.html#1032" class="Function">Eval.eval</a> <a id="2626" href="Cubical.Tactics.MonoidSolver.Solver.html#2530" class="Bound">M</a> <a id="2628" class="Symbol">(</a><a id="2629" href="Cubical.Tactics.MonoidSolver.Solver.html#837" class="Function">Eval.normalize</a> <a id="2644" href="Cubical.Tactics.MonoidSolver.Solver.html#2530" class="Bound">M</a> <a id="2646" href="Cubical.Tactics.MonoidSolver.Solver.html#2561" class="Bound">e₁</a><a id="2648" class="Symbol">)</a> <a id="2650" href="Cubical.Tactics.MonoidSolver.Solver.html#2584" class="Bound">v</a> <a id="2652" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="2654" href="Cubical.Tactics.MonoidSolver.Solver.html#1032" class="Function">Eval.eval</a> <a id="2664" href="Cubical.Tactics.MonoidSolver.Solver.html#2530" class="Bound">M</a> <a id="2666" class="Symbol">(</a><a id="2667" href="Cubical.Tactics.MonoidSolver.Solver.html#837" class="Function">Eval.normalize</a> <a id="2682" href="Cubical.Tactics.MonoidSolver.Solver.html#2530" class="Bound">M</a> <a id="2684" href="Cubical.Tactics.MonoidSolver.Solver.html#2564" class="Bound">e₂</a><a id="2686" class="Symbol">)</a> <a id="2688" href="Cubical.Tactics.MonoidSolver.Solver.html#2584" class="Bound">v</a><a id="2689" class="Symbol">)</a>
        <a id="2699" class="Symbol">→</a> <a id="2701" class="Symbol">_</a>
<a id="2703" href="Cubical.Tactics.MonoidSolver.Solver.html#2521" class="Function">solve</a> <a id="2709" href="Cubical.Tactics.MonoidSolver.Solver.html#2709" class="Bound">M</a> <a id="2711" class="Symbol">=</a> <a id="2713" href="Cubical.Tactics.MonoidSolver.Solver.html#2154" class="Function">EqualityToNormalform.solve</a> <a id="2740" href="Cubical.Tactics.MonoidSolver.Solver.html#2709" class="Bound">M</a>
</pre></body></html>