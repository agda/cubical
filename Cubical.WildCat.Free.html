<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Cubical.WildCat.Free</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">{-
  This file defines a wild category, which might be the free wild category over a
  directed graph (I do not know). This is intended to be used in a solver for
  wild categories.
-}</a>
<a id="186" class="Symbol">{-#</a> <a id="190" class="Keyword">OPTIONS</a> <a id="198" class="Pragma">--safe</a> <a id="205" class="Symbol">#-}</a>

<a id="210" class="Keyword">module</a> <a id="217" href="Cubical.WildCat.Free.html" class="Module">Cubical.WildCat.Free</a> <a id="238" class="Keyword">where</a>

<a id="245" class="Keyword">open</a> <a id="250" class="Keyword">import</a> <a id="257" href="Cubical.Foundations.Prelude.html" class="Module">Cubical.Foundations.Prelude</a>

<a id="286" class="Keyword">open</a> <a id="291" class="Keyword">import</a> <a id="298" href="Cubical.Data.Graph.Base.html" class="Module">Cubical.Data.Graph.Base</a>
<a id="322" class="Keyword">open</a> <a id="327" class="Keyword">import</a> <a id="334" href="Cubical.Data.Graph.Path.html" class="Module">Cubical.Data.Graph.Path</a> <a id="358" class="Keyword">renaming</a> <a id="367" class="Symbol">(</a><a id="368" href="Cubical.Data.Graph.Path.html#476" class="Datatype">Path</a> <a id="373" class="Symbol">to</a> <a id="376" class="Datatype">GPath</a><a id="381" class="Symbol">)</a>

<a id="384" class="Keyword">open</a> <a id="389" class="Keyword">import</a> <a id="396" href="Cubical.WildCat.Base.html" class="Module">Cubical.WildCat.Base</a>
<a id="417" class="Keyword">open</a> <a id="422" class="Keyword">import</a> <a id="429" href="Cubical.WildCat.Functor.html" class="Module">Cubical.WildCat.Functor</a>
<a id="453" class="Keyword">open</a> <a id="458" class="Keyword">import</a> <a id="465" href="Cubical.WildCat.UnderlyingGraph.html" class="Module">Cubical.WildCat.UnderlyingGraph</a>

<a id="498" class="Keyword">open</a> <a id="503" href="Cubical.WildCat.Base.html#333" class="Module">WildCat</a>
<a id="511" class="Keyword">open</a> <a id="516" href="Cubical.WildCat.Functor.html#324" class="Module">WildFunctor</a>
<a id="528" class="Keyword">open</a> <a id="533" href="Cubical.Data.Graph.Base.html#258" class="Module">Graph</a>

<a id="540" class="Keyword">private</a>
  <a id="550" class="Keyword">variable</a>
    <a id="563" href="Cubical.WildCat.Free.html#563" class="Generalizable">ℓc</a> <a id="566" href="Cubical.WildCat.Free.html#566" class="Generalizable">ℓc&#39;</a> <a id="570" href="Cubical.WildCat.Free.html#570" class="Generalizable">ℓd</a> <a id="573" href="Cubical.WildCat.Free.html#573" class="Generalizable">ℓd&#39;</a> <a id="577" href="Cubical.WildCat.Free.html#577" class="Generalizable">ℓg</a> <a id="580" href="Cubical.WildCat.Free.html#580" class="Generalizable">ℓg&#39;</a> <a id="584" class="Symbol">:</a> <a id="586" href="Agda.Primitive.html#742" class="Postulate">Level</a>

<a id="Free"></a><a id="593" href="Cubical.WildCat.Free.html#593" class="Function">Free</a> <a id="598" class="Symbol">:</a> <a id="600" class="Symbol">(</a><a id="601" href="Cubical.WildCat.Free.html#601" class="Bound">G</a> <a id="603" class="Symbol">:</a> <a id="605" href="Cubical.Data.Graph.Base.html#258" class="Record">Graph</a> <a id="611" href="Cubical.WildCat.Free.html#577" class="Generalizable">ℓg</a> <a id="614" href="Cubical.WildCat.Free.html#580" class="Generalizable">ℓg&#39;</a><a id="617" class="Symbol">)</a> <a id="619" class="Symbol">→</a> <a id="621" href="Cubical.WildCat.Base.html#333" class="Record">WildCat</a> <a id="629" href="Cubical.WildCat.Free.html#577" class="Generalizable">ℓg</a> <a id="632" class="Symbol">(</a><a id="633" href="Agda.Primitive.html#961" class="Primitive">ℓ-max</a> <a id="639" href="Cubical.WildCat.Free.html#577" class="Generalizable">ℓg</a> <a id="642" href="Cubical.WildCat.Free.html#580" class="Generalizable">ℓg&#39;</a><a id="645" class="Symbol">)</a>
<a id="647" href="Cubical.WildCat.Base.html#410" class="Field">ob</a> <a id="650" class="Symbol">(</a><a id="651" href="Cubical.WildCat.Free.html#593" class="Function">Free</a> <a id="656" href="Cubical.WildCat.Free.html#656" class="Bound">G</a><a id="657" class="Symbol">)</a> <a id="659" class="Symbol">=</a> <a id="661" href="Cubical.WildCat.Free.html#656" class="Bound">G</a> <a id="663" class="Symbol">.</a><a id="664" href="Cubical.Data.Graph.Base.html#317" class="Field">Node</a>
<a id="669" href="Cubical.WildCat.Base.html#426" class="Field Operator">Hom[_,_]</a> <a id="678" class="Symbol">(</a><a id="679" href="Cubical.WildCat.Free.html#593" class="Function">Free</a> <a id="684" href="Cubical.WildCat.Free.html#684" class="Bound">G</a><a id="685" class="Symbol">)</a> <a id="687" href="Cubical.WildCat.Free.html#687" class="Bound">x</a> <a id="689" href="Cubical.WildCat.Free.html#689" class="Bound">y</a> <a id="691" class="Symbol">=</a> <a id="693" href="Cubical.WildCat.Free.html#376" class="Datatype">GPath</a> <a id="699" href="Cubical.WildCat.Free.html#684" class="Bound">G</a> <a id="701" href="Cubical.WildCat.Free.html#687" class="Bound">x</a> <a id="703" href="Cubical.WildCat.Free.html#689" class="Bound">y</a>
<a id="705" href="Cubical.WildCat.Base.html#459" class="Field">id</a> <a id="708" class="Symbol">(</a><a id="709" href="Cubical.WildCat.Free.html#593" class="Function">Free</a> <a id="714" href="Cubical.WildCat.Free.html#714" class="Bound">G</a><a id="715" class="Symbol">)</a> <a id="717" class="Symbol">=</a> <a id="719" href="Cubical.Data.Graph.Path.html#529" class="InductiveConstructor">pnil</a>
<a id="724" href="Cubical.WildCat.Base.html#491" class="Field Operator">_⋆_</a> <a id="728" class="Symbol">(</a><a id="729" href="Cubical.WildCat.Free.html#593" class="Function">Free</a> <a id="734" href="Cubical.WildCat.Free.html#734" class="Bound">G</a><a id="735" class="Symbol">)</a> <a id="737" href="Cubical.WildCat.Free.html#737" class="Bound">f</a> <a id="739" href="Cubical.WildCat.Free.html#739" class="Bound">g</a> <a id="741" class="Symbol">=</a> <a id="743" href="Cubical.WildCat.Free.html#737" class="Bound">f</a> <a id="745" href="Cubical.Data.Graph.Path.html#790" class="Function Operator">++</a> <a id="748" href="Cubical.WildCat.Free.html#739" class="Bound">g</a>
<a id="750" href="Cubical.WildCat.Base.html#565" class="Field">⋆IdL</a> <a id="755" class="Symbol">(</a><a id="756" href="Cubical.WildCat.Free.html#593" class="Function">Free</a> <a id="761" href="Cubical.WildCat.Free.html#761" class="Bound">G</a><a id="762" class="Symbol">)</a> <a id="764" class="Symbol">=</a> <a id="766" class="Symbol">λ</a> <a id="768" href="Cubical.WildCat.Free.html#768" class="Bound">_</a> <a id="770" class="Symbol">→</a> <a id="772" href="Cubical.Foundations.Prelude.html#915" class="Function">refl</a>
<a id="777" href="Cubical.WildCat.Base.html#616" class="Field">⋆IdR</a> <a id="782" class="Symbol">(</a><a id="783" href="Cubical.WildCat.Free.html#593" class="Function">Free</a> <a id="788" href="Cubical.WildCat.Free.html#788" class="Bound">G</a><a id="789" class="Symbol">)</a> <a id="791" class="Symbol">=</a> <a id="793" href="Cubical.Data.Graph.Path.html#974" class="Function">++pnil</a>
<a id="800" href="Cubical.WildCat.Base.html#667" class="Field">⋆Assoc</a> <a id="807" class="Symbol">(</a><a id="808" href="Cubical.WildCat.Free.html#593" class="Function">Free</a> <a id="813" href="Cubical.WildCat.Free.html#813" class="Bound">G</a><a id="814" class="Symbol">)</a> <a id="816" href="Cubical.WildCat.Free.html#816" class="Bound">f</a> <a id="818" href="Cubical.WildCat.Free.html#818" class="Bound">g</a> <a id="820" href="Cubical.WildCat.Free.html#820" class="Bound">h</a> <a id="822" class="Symbol">=</a> <a id="824" href="Cubical.Data.Graph.Path.html#1105" class="Function">++assoc</a> <a id="832" href="Cubical.WildCat.Free.html#816" class="Bound">f</a> <a id="834" href="Cubical.WildCat.Free.html#818" class="Bound">g</a> <a id="836" href="Cubical.WildCat.Free.html#820" class="Bound">h</a>

<a id="composeAll"></a><a id="839" href="Cubical.WildCat.Free.html#839" class="Function">composeAll</a> <a id="850" class="Symbol">:</a> <a id="852" class="Symbol">(</a><a id="853" href="Cubical.WildCat.Free.html#853" class="Bound">C</a> <a id="855" class="Symbol">:</a> <a id="857" href="Cubical.WildCat.Base.html#333" class="Record">WildCat</a> <a id="865" href="Cubical.WildCat.Free.html#563" class="Generalizable">ℓc</a> <a id="868" href="Cubical.WildCat.Free.html#566" class="Generalizable">ℓc&#39;</a><a id="871" class="Symbol">)</a> <a id="873" class="Symbol">{</a><a id="874" href="Cubical.WildCat.Free.html#874" class="Bound">x</a> <a id="876" href="Cubical.WildCat.Free.html#876" class="Bound">y</a> <a id="878" class="Symbol">:</a> <a id="880" href="Cubical.WildCat.Base.html#410" class="Field">ob</a> <a id="883" href="Cubical.WildCat.Free.html#853" class="Bound">C</a><a id="884" class="Symbol">}</a> <a id="886" class="Symbol">→</a> <a id="888" href="Cubical.WildCat.Free.html#376" class="Datatype">GPath</a> <a id="894" class="Symbol">(</a><a id="895" href="Cubical.WildCat.UnderlyingGraph.html#327" class="Function">Cat→Graph</a> <a id="905" href="Cubical.WildCat.Free.html#853" class="Bound">C</a><a id="906" class="Symbol">)</a> <a id="908" href="Cubical.WildCat.Free.html#874" class="Bound">x</a> <a id="910" href="Cubical.WildCat.Free.html#876" class="Bound">y</a> <a id="912" class="Symbol">→</a> <a id="914" href="Cubical.WildCat.Free.html#853" class="Bound">C</a> <a id="916" href="Cubical.WildCat.Base.html#960" class="Function Operator">[</a> <a id="918" href="Cubical.WildCat.Free.html#874" class="Bound">x</a> <a id="920" href="Cubical.WildCat.Base.html#960" class="Function Operator">,</a> <a id="922" href="Cubical.WildCat.Free.html#876" class="Bound">y</a> <a id="924" href="Cubical.WildCat.Base.html#960" class="Function Operator">]</a>
<a id="926" href="Cubical.WildCat.Free.html#839" class="Function">composeAll</a> <a id="937" href="Cubical.WildCat.Free.html#937" class="Bound">C</a> <a id="939" href="Cubical.Data.Graph.Path.html#529" class="InductiveConstructor">pnil</a> <a id="944" class="Symbol">=</a> <a id="946" href="Cubical.WildCat.Base.html#459" class="Field">id</a> <a id="949" href="Cubical.WildCat.Free.html#937" class="Bound">C</a>
<a id="951" href="Cubical.WildCat.Free.html#839" class="Function">composeAll</a> <a id="962" href="Cubical.WildCat.Free.html#962" class="Bound">C</a> <a id="964" class="Symbol">(</a><a id="965" href="Cubical.Data.Graph.Path.html#557" class="InductiveConstructor">pcons</a> <a id="971" href="Cubical.WildCat.Free.html#971" class="Bound">m</a> <a id="973" href="Cubical.WildCat.Free.html#973" class="Bound">path</a><a id="977" class="Symbol">)</a> <a id="979" class="Symbol">=</a> <a id="981" href="Cubical.WildCat.Free.html#971" class="Bound">m</a> <a id="983" href="Cubical.WildCat.Base.html#1121" class="Function">⋆⟨</a> <a id="986" href="Cubical.WildCat.Free.html#962" class="Bound">C</a> <a id="988" href="Cubical.WildCat.Base.html#1121" class="Function">⟩</a> <a id="990" href="Cubical.WildCat.Free.html#839" class="Function">composeAll</a> <a id="1001" href="Cubical.WildCat.Free.html#962" class="Bound">C</a> <a id="1003" href="Cubical.WildCat.Free.html#973" class="Bound">path</a>


<a id="composeAll++"></a><a id="1010" href="Cubical.WildCat.Free.html#1010" class="Function">composeAll++</a> <a id="1023" class="Symbol">:</a> <a id="1025" class="Symbol">(</a><a id="1026" href="Cubical.WildCat.Free.html#1026" class="Bound">C</a> <a id="1028" class="Symbol">:</a> <a id="1030" href="Cubical.WildCat.Base.html#333" class="Record">WildCat</a> <a id="1038" href="Cubical.WildCat.Free.html#563" class="Generalizable">ℓc</a> <a id="1041" href="Cubical.WildCat.Free.html#566" class="Generalizable">ℓc&#39;</a><a id="1044" class="Symbol">)</a> <a id="1046" class="Symbol">{</a><a id="1047" href="Cubical.WildCat.Free.html#1047" class="Bound">x</a> <a id="1049" href="Cubical.WildCat.Free.html#1049" class="Bound">y</a> <a id="1051" href="Cubical.WildCat.Free.html#1051" class="Bound">z</a> <a id="1053" class="Symbol">:</a> <a id="1055" href="Cubical.WildCat.Base.html#410" class="Field">ob</a> <a id="1058" href="Cubical.WildCat.Free.html#1026" class="Bound">C</a><a id="1059" class="Symbol">}</a>
               <a id="1076" class="Symbol">→</a> <a id="1078" class="Symbol">(</a><a id="1079" href="Cubical.WildCat.Free.html#1079" class="Bound">p</a> <a id="1081" class="Symbol">:</a> <a id="1083" href="Cubical.WildCat.Free.html#376" class="Datatype">GPath</a> <a id="1089" class="Symbol">(</a><a id="1090" href="Cubical.WildCat.UnderlyingGraph.html#327" class="Function">Cat→Graph</a> <a id="1100" href="Cubical.WildCat.Free.html#1026" class="Bound">C</a><a id="1101" class="Symbol">)</a> <a id="1103" href="Cubical.WildCat.Free.html#1047" class="Bound">x</a> <a id="1105" href="Cubical.WildCat.Free.html#1049" class="Bound">y</a><a id="1106" class="Symbol">)</a> <a id="1108" class="Symbol">→</a> <a id="1110" class="Symbol">(</a><a id="1111" href="Cubical.WildCat.Free.html#1111" class="Bound">q</a> <a id="1113" class="Symbol">:</a> <a id="1115" href="Cubical.WildCat.Free.html#376" class="Datatype">GPath</a> <a id="1121" class="Symbol">(</a><a id="1122" href="Cubical.WildCat.UnderlyingGraph.html#327" class="Function">Cat→Graph</a> <a id="1132" href="Cubical.WildCat.Free.html#1026" class="Bound">C</a><a id="1133" class="Symbol">)</a> <a id="1135" href="Cubical.WildCat.Free.html#1049" class="Bound">y</a> <a id="1137" href="Cubical.WildCat.Free.html#1051" class="Bound">z</a><a id="1138" class="Symbol">)</a>
               <a id="1155" class="Symbol">→</a> <a id="1157" href="Cubical.WildCat.Free.html#839" class="Function">composeAll</a> <a id="1168" href="Cubical.WildCat.Free.html#1026" class="Bound">C</a> <a id="1170" class="Symbol">(</a><a id="1171" href="Cubical.WildCat.Free.html#1079" class="Bound">p</a> <a id="1173" href="Cubical.Data.Graph.Path.html#790" class="Function Operator">++</a> <a id="1176" href="Cubical.WildCat.Free.html#1111" class="Bound">q</a><a id="1177" class="Symbol">)</a> <a id="1179" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="1181" href="Cubical.WildCat.Free.html#839" class="Function">composeAll</a> <a id="1192" href="Cubical.WildCat.Free.html#1026" class="Bound">C</a> <a id="1194" href="Cubical.WildCat.Free.html#1079" class="Bound">p</a> <a id="1196" href="Cubical.WildCat.Base.html#1121" class="Function">⋆⟨</a> <a id="1199" href="Cubical.WildCat.Free.html#1026" class="Bound">C</a> <a id="1201" href="Cubical.WildCat.Base.html#1121" class="Function">⟩</a> <a id="1203" href="Cubical.WildCat.Free.html#839" class="Function">composeAll</a> <a id="1214" href="Cubical.WildCat.Free.html#1026" class="Bound">C</a> <a id="1216" href="Cubical.WildCat.Free.html#1111" class="Bound">q</a>
<a id="1218" href="Cubical.WildCat.Free.html#1010" class="Function">composeAll++</a> <a id="1231" href="Cubical.WildCat.Free.html#1231" class="Bound">C</a> <a id="1233" href="Cubical.Data.Graph.Path.html#529" class="InductiveConstructor">pnil</a> <a id="1238" href="Cubical.WildCat.Free.html#1238" class="Bound">q</a> <a id="1240" class="Symbol">=</a> <a id="1242" href="Cubical.Foundations.Prelude.html#968" class="Function">sym</a> <a id="1246" class="Symbol">(</a><a id="1247" href="Cubical.WildCat.Base.html#565" class="Field">⋆IdL</a> <a id="1252" href="Cubical.WildCat.Free.html#1231" class="Bound">C</a> <a id="1254" class="Symbol">(</a><a id="1255" href="Cubical.WildCat.Free.html#839" class="Function">composeAll</a> <a id="1266" href="Cubical.WildCat.Free.html#1231" class="Bound">C</a> <a id="1268" href="Cubical.WildCat.Free.html#1238" class="Bound">q</a><a id="1269" class="Symbol">))</a>
<a id="1272" href="Cubical.WildCat.Free.html#1010" class="Function">composeAll++</a> <a id="1285" href="Cubical.WildCat.Free.html#1285" class="Bound">C</a> <a id="1287" class="Symbol">(</a><a id="1288" href="Cubical.Data.Graph.Path.html#557" class="InductiveConstructor">pcons</a> <a id="1294" href="Cubical.WildCat.Free.html#1294" class="Bound">m</a> <a id="1296" href="Cubical.WildCat.Free.html#1296" class="Bound">p</a><a id="1297" class="Symbol">)</a> <a id="1299" href="Cubical.WildCat.Free.html#1299" class="Bound">q</a> <a id="1301" class="Symbol">=</a>
  <a id="1305" href="Cubical.WildCat.Free.html#839" class="Function">composeAll</a> <a id="1316" href="Cubical.WildCat.Free.html#1285" class="Bound">C</a> <a id="1318" class="Symbol">(</a><a id="1319" href="Cubical.Data.Graph.Path.html#557" class="InductiveConstructor">pcons</a> <a id="1325" href="Cubical.WildCat.Free.html#1294" class="Bound">m</a> <a id="1327" href="Cubical.WildCat.Free.html#1296" class="Bound">p</a> <a id="1329" href="Cubical.Data.Graph.Path.html#790" class="Function Operator">++</a> <a id="1332" href="Cubical.WildCat.Free.html#1299" class="Bound">q</a><a id="1333" class="Symbol">)</a>                       <a id="1357" href="Cubical.Foundations.Prelude.html#9052" class="Function">≡⟨</a> <a id="1360" href="Cubical.WildCat.Free.html#1542" class="Function">step1</a> <a id="1366" href="Cubical.Foundations.Prelude.html#9052" class="Function">⟩</a>
  <a id="1370" href="Cubical.WildCat.Free.html#1294" class="Bound">m</a> <a id="1372" href="Cubical.WildCat.Base.html#1121" class="Function">⋆⟨</a> <a id="1375" href="Cubical.WildCat.Free.html#1285" class="Bound">C</a> <a id="1377" href="Cubical.WildCat.Base.html#1121" class="Function">⟩</a> <a id="1379" class="Symbol">((</a><a id="1381" href="Cubical.WildCat.Free.html#839" class="Function">composeAll</a> <a id="1392" href="Cubical.WildCat.Free.html#1285" class="Bound">C</a> <a id="1394" href="Cubical.WildCat.Free.html#1296" class="Bound">p</a><a id="1395" class="Symbol">)</a> <a id="1397" href="Cubical.WildCat.Base.html#1121" class="Function">⋆⟨</a> <a id="1400" href="Cubical.WildCat.Free.html#1285" class="Bound">C</a> <a id="1402" href="Cubical.WildCat.Base.html#1121" class="Function">⟩</a> <a id="1404" class="Symbol">(</a><a id="1405" href="Cubical.WildCat.Free.html#839" class="Function">composeAll</a> <a id="1416" href="Cubical.WildCat.Free.html#1285" class="Bound">C</a> <a id="1418" href="Cubical.WildCat.Free.html#1299" class="Bound">q</a><a id="1419" class="Symbol">))</a> <a id="1422" href="Cubical.Foundations.Prelude.html#9052" class="Function">≡⟨</a> <a id="1425" href="Cubical.Foundations.Prelude.html#968" class="Function">sym</a> <a id="1429" class="Symbol">(</a><a id="1430" href="Cubical.WildCat.Base.html#667" class="Field">⋆Assoc</a> <a id="1437" href="Cubical.WildCat.Free.html#1285" class="Bound">C</a> <a id="1439" href="Cubical.WildCat.Free.html#1294" class="Bound">m</a> <a id="1441" class="Symbol">(</a><a id="1442" href="Cubical.WildCat.Free.html#839" class="Function">composeAll</a> <a id="1453" href="Cubical.WildCat.Free.html#1285" class="Bound">C</a> <a id="1455" href="Cubical.WildCat.Free.html#1296" class="Bound">p</a><a id="1456" class="Symbol">)</a> <a id="1458" class="Symbol">(</a><a id="1459" href="Cubical.WildCat.Free.html#839" class="Function">composeAll</a> <a id="1470" href="Cubical.WildCat.Free.html#1285" class="Bound">C</a> <a id="1472" href="Cubical.WildCat.Free.html#1299" class="Bound">q</a><a id="1473" class="Symbol">))</a> <a id="1476" href="Cubical.Foundations.Prelude.html#9052" class="Function">⟩</a>
  <a id="1480" class="Symbol">(</a><a id="1481" href="Cubical.WildCat.Free.html#1294" class="Bound">m</a> <a id="1483" href="Cubical.WildCat.Base.html#1121" class="Function">⋆⟨</a> <a id="1486" href="Cubical.WildCat.Free.html#1285" class="Bound">C</a> <a id="1488" href="Cubical.WildCat.Base.html#1121" class="Function">⟩</a> <a id="1490" class="Symbol">(</a><a id="1491" href="Cubical.WildCat.Free.html#839" class="Function">composeAll</a> <a id="1502" href="Cubical.WildCat.Free.html#1285" class="Bound">C</a> <a id="1504" href="Cubical.WildCat.Free.html#1296" class="Bound">p</a><a id="1505" class="Symbol">))</a> <a id="1508" href="Cubical.WildCat.Base.html#1121" class="Function">⋆⟨</a> <a id="1511" href="Cubical.WildCat.Free.html#1285" class="Bound">C</a> <a id="1513" href="Cubical.WildCat.Base.html#1121" class="Function">⟩</a> <a id="1515" class="Symbol">(</a><a id="1516" href="Cubical.WildCat.Free.html#839" class="Function">composeAll</a> <a id="1527" href="Cubical.WildCat.Free.html#1285" class="Bound">C</a> <a id="1529" href="Cubical.WildCat.Free.html#1299" class="Bound">q</a><a id="1530" class="Symbol">)</a> <a id="1532" href="Cubical.Foundations.Prelude.html#9599" class="Function Operator">∎</a>
  <a id="1536" class="Keyword">where</a> <a id="1542" href="Cubical.WildCat.Free.html#1542" class="Function">step1</a> <a id="1548" class="Symbol">=</a> <a id="1550" href="Cubical.Foundations.Prelude.html#1430" class="Function">cong</a> <a id="1555" class="Symbol">(λ</a> <a id="1558" href="Cubical.WildCat.Free.html#1558" class="Bound">z</a> <a id="1560" class="Symbol">→</a> <a id="1562" href="Cubical.WildCat.Base.html#1121" class="Function">concatMor</a> <a id="1572" href="Cubical.WildCat.Free.html#1285" class="Bound">C</a> <a id="1574" href="Cubical.WildCat.Free.html#1294" class="Bound">m</a> <a id="1576" href="Cubical.WildCat.Free.html#1558" class="Bound">z</a><a id="1577" class="Symbol">)</a> <a id="1579" class="Symbol">(</a><a id="1580" href="Cubical.WildCat.Free.html#1010" class="Function">composeAll++</a> <a id="1593" href="Cubical.WildCat.Free.html#1285" class="Bound">C</a> <a id="1595" href="Cubical.WildCat.Free.html#1296" class="Bound">p</a> <a id="1597" href="Cubical.WildCat.Free.html#1299" class="Bound">q</a><a id="1598" class="Symbol">)</a>

<a id="1601" class="Keyword">module</a> <a id="UniversalProperty"></a><a id="1608" href="Cubical.WildCat.Free.html#1608" class="Module">UniversalProperty</a> <a id="1626" class="Symbol">(</a><a id="1627" href="Cubical.WildCat.Free.html#1627" class="Bound">G</a> <a id="1629" class="Symbol">:</a> <a id="1631" href="Cubical.Data.Graph.Base.html#258" class="Record">Graph</a> <a id="1637" href="Cubical.WildCat.Free.html#577" class="Generalizable">ℓg</a> <a id="1640" href="Cubical.WildCat.Free.html#580" class="Generalizable">ℓg&#39;</a><a id="1643" class="Symbol">)</a> <a id="1645" class="Keyword">where</a>


  <a id="UniversalProperty.incFree"></a><a id="1655" href="Cubical.WildCat.Free.html#1655" class="Function">incFree</a> <a id="1663" class="Symbol">:</a> <a id="1665" href="Cubical.Data.Graph.Base.html#595" class="Record">GraphHom</a> <a id="1674" href="Cubical.WildCat.Free.html#1627" class="Bound">G</a> <a id="1676" class="Symbol">(</a><a id="1677" href="Cubical.WildCat.UnderlyingGraph.html#327" class="Function">Cat→Graph</a> <a id="1687" class="Symbol">(</a><a id="1688" href="Cubical.WildCat.Free.html#593" class="Function">Free</a> <a id="1693" href="Cubical.WildCat.Free.html#1627" class="Bound">G</a><a id="1694" class="Symbol">))</a>
  <a id="1699" href="Cubical.WildCat.Free.html#1655" class="Function">incFree</a> <a id="1707" href="Cubical.Data.Graph.Base.html#733" class="Field Operator">$g</a> <a id="1710" href="Cubical.WildCat.Free.html#1710" class="Bound">x</a> <a id="1712" class="Symbol">=</a> <a id="1714" href="Cubical.WildCat.Free.html#1710" class="Bound">x</a>
  <a id="1718" href="Cubical.WildCat.Free.html#1655" class="Function">incFree</a> <a id="1726" href="Cubical.Data.Graph.Base.html#761" class="Field Operator">&lt;$g&gt;</a> <a id="1731" href="Cubical.WildCat.Free.html#1731" class="Bound">e</a> <a id="1733" class="Symbol">=</a> <a id="1735" href="Cubical.Data.Graph.Path.html#557" class="InductiveConstructor">pcons</a> <a id="1741" href="Cubical.WildCat.Free.html#1731" class="Bound">e</a> <a id="1743" href="Cubical.Data.Graph.Path.html#529" class="InductiveConstructor">pnil</a>

  <a id="1751" class="Comment">{-
     G ──→ Free G
      \    ∣
   ∀ F \   ∣ ∃ F&#39;
        ↘  ↓
          C
  -}</a>
  <a id="UniversalProperty.inducedMorphism"></a><a id="1835" href="Cubical.WildCat.Free.html#1835" class="Function">inducedMorphism</a> <a id="1851" class="Symbol">:</a> <a id="1853" class="Symbol">(</a><a id="1854" href="Cubical.WildCat.Free.html#1854" class="Bound">C</a> <a id="1856" class="Symbol">:</a> <a id="1858" href="Cubical.WildCat.Base.html#333" class="Record">WildCat</a> <a id="1866" href="Cubical.WildCat.Free.html#563" class="Generalizable">ℓc</a> <a id="1869" href="Cubical.WildCat.Free.html#566" class="Generalizable">ℓc&#39;</a><a id="1872" class="Symbol">)</a> <a id="1874" class="Symbol">→</a> <a id="1876" href="Cubical.Data.Graph.Base.html#595" class="Record">GraphHom</a> <a id="1885" href="Cubical.WildCat.Free.html#1627" class="Bound">G</a> <a id="1887" class="Symbol">(</a><a id="1888" href="Cubical.WildCat.UnderlyingGraph.html#327" class="Function">Cat→Graph</a> <a id="1898" href="Cubical.WildCat.Free.html#1854" class="Bound">C</a><a id="1899" class="Symbol">)</a> <a id="1901" class="Symbol">→</a> <a id="1903" href="Cubical.WildCat.Functor.html#324" class="Record">WildFunctor</a> <a id="1915" class="Symbol">(</a><a id="1916" href="Cubical.WildCat.Free.html#593" class="Function">Free</a> <a id="1921" href="Cubical.WildCat.Free.html#1627" class="Bound">G</a><a id="1922" class="Symbol">)</a> <a id="1924" href="Cubical.WildCat.Free.html#1854" class="Bound">C</a>
  <a id="1928" href="Cubical.WildCat.Free.html#1835" class="Function">inducedMorphism</a> <a id="1944" href="Cubical.WildCat.Free.html#1944" class="Bound">C</a> <a id="1946" href="Cubical.WildCat.Free.html#1946" class="Bound">F</a> <a id="1948" class="Symbol">=</a> <a id="1950" href="Cubical.WildCat.Free.html#1963" class="Function">F&#39;</a>
    <a id="1957" class="Keyword">where</a> <a id="1963" href="Cubical.WildCat.Free.html#1963" class="Function">F&#39;</a> <a id="1966" class="Symbol">:</a> <a id="1968" href="Cubical.WildCat.Functor.html#324" class="Record">WildFunctor</a> <a id="1980" class="Symbol">(</a><a id="1981" href="Cubical.WildCat.Free.html#593" class="Function">Free</a> <a id="1986" href="Cubical.WildCat.Free.html#1627" class="Bound">G</a><a id="1987" class="Symbol">)</a> <a id="1989" href="Cubical.WildCat.Free.html#1944" class="Bound">C</a>
          <a id="2001" href="Cubical.WildCat.Functor.html#469" class="Field">F-ob</a> <a id="2006" href="Cubical.WildCat.Free.html#1963" class="Function">F&#39;</a> <a id="2009" href="Cubical.WildCat.Free.html#2009" class="Bound">x</a> <a id="2011" class="Symbol">=</a> <a id="2013" href="Cubical.WildCat.Free.html#1946" class="Bound">F</a> <a id="2015" href="Cubical.Data.Graph.Base.html#733" class="Field Operator">$g</a> <a id="2018" href="Cubical.WildCat.Free.html#2009" class="Bound">x</a>
          <a id="2030" href="Cubical.WildCat.Functor.html#495" class="Field">F-hom</a> <a id="2036" href="Cubical.WildCat.Free.html#1963" class="Function">F&#39;</a> <a id="2039" href="Cubical.WildCat.Free.html#2039" class="Bound">m</a> <a id="2041" class="Symbol">=</a> <a id="2043" href="Cubical.WildCat.Free.html#839" class="Function">composeAll</a> <a id="2054" href="Cubical.WildCat.Free.html#1944" class="Bound">C</a> <a id="2056" class="Symbol">(</a><a id="2057" href="Cubical.Data.Graph.Path.html#3205" class="Function">map</a> <a id="2061" href="Cubical.WildCat.Free.html#1946" class="Bound">F</a> <a id="2063" href="Cubical.WildCat.Free.html#2039" class="Bound">m</a><a id="2064" class="Symbol">)</a>
          <a id="2076" href="Cubical.WildCat.Functor.html#559" class="Field">F-id</a> <a id="2081" href="Cubical.WildCat.Free.html#1963" class="Function">F&#39;</a> <a id="2084" class="Symbol">=</a> <a id="2086" href="Cubical.Foundations.Prelude.html#915" class="Function">refl</a>
          <a id="2101" href="Cubical.WildCat.Functor.html#613" class="Field">F-seq</a> <a id="2107" href="Cubical.WildCat.Free.html#1963" class="Function">F&#39;</a> <a id="2110" href="Cubical.WildCat.Free.html#2110" class="Bound">f</a> <a id="2112" href="Cubical.WildCat.Free.html#2112" class="Bound">g</a> <a id="2114" class="Symbol">=</a>
            <a id="2128" href="Cubical.WildCat.Free.html#839" class="Function">composeAll</a> <a id="2139" href="Cubical.WildCat.Free.html#1944" class="Bound">C</a> <a id="2141" class="Symbol">(</a><a id="2142" href="Cubical.Data.Graph.Path.html#3205" class="Function">map</a> <a id="2146" href="Cubical.WildCat.Free.html#1946" class="Bound">F</a> <a id="2148" class="Symbol">(</a><a id="2149" href="Cubical.WildCat.Free.html#2110" class="Bound">f</a> <a id="2151" href="Cubical.WildCat.Base.html#1121" class="Function">⋆⟨</a> <a id="2154" href="Cubical.WildCat.Free.html#593" class="Function">Free</a> <a id="2159" href="Cubical.WildCat.Free.html#1627" class="Bound">G</a> <a id="2161" href="Cubical.WildCat.Base.html#1121" class="Function">⟩</a> <a id="2163" href="Cubical.WildCat.Free.html#2112" class="Bound">g</a><a id="2164" class="Symbol">))</a>                  <a id="2184" href="Cubical.Foundations.Prelude.html#9052" class="Function">≡⟨</a> <a id="2187" href="Cubical.Foundations.Prelude.html#1430" class="Function">cong</a> <a id="2192" class="Symbol">(λ</a> <a id="2195" href="Cubical.WildCat.Free.html#2195" class="Bound">u</a> <a id="2197" class="Symbol">→</a> <a id="2199" href="Cubical.WildCat.Free.html#839" class="Function">composeAll</a> <a id="2210" href="Cubical.WildCat.Free.html#1944" class="Bound">C</a> <a id="2212" href="Cubical.WildCat.Free.html#2195" class="Bound">u</a><a id="2213" class="Symbol">)</a> <a id="2215" class="Symbol">(</a><a id="2216" href="Cubical.Data.Graph.Path.html#3373" class="Function">map++</a> <a id="2222" href="Cubical.WildCat.Free.html#1946" class="Bound">F</a> <a id="2224" href="Cubical.WildCat.Free.html#2110" class="Bound">f</a> <a id="2226" href="Cubical.WildCat.Free.html#2112" class="Bound">g</a><a id="2227" class="Symbol">)</a> <a id="2229" href="Cubical.Foundations.Prelude.html#9052" class="Function">⟩</a>
            <a id="2243" href="Cubical.WildCat.Free.html#839" class="Function">composeAll</a> <a id="2254" href="Cubical.WildCat.Free.html#1944" class="Bound">C</a> <a id="2256" class="Symbol">(</a><a id="2257" href="Cubical.Data.Graph.Path.html#3205" class="Function">map</a> <a id="2261" href="Cubical.WildCat.Free.html#1946" class="Bound">F</a> <a id="2263" href="Cubical.WildCat.Free.html#2110" class="Bound">f</a> <a id="2265" href="Cubical.Data.Graph.Path.html#790" class="Function Operator">++</a> <a id="2268" href="Cubical.Data.Graph.Path.html#3205" class="Function">map</a> <a id="2272" href="Cubical.WildCat.Free.html#1946" class="Bound">F</a> <a id="2274" href="Cubical.WildCat.Free.html#2112" class="Bound">g</a><a id="2275" class="Symbol">)</a>                       <a id="2299" href="Cubical.Foundations.Prelude.html#9052" class="Function">≡⟨</a> <a id="2302" href="Cubical.WildCat.Free.html#1010" class="Function">composeAll++</a> <a id="2315" href="Cubical.WildCat.Free.html#1944" class="Bound">C</a> <a id="2317" class="Symbol">(</a><a id="2318" href="Cubical.Data.Graph.Path.html#3205" class="Function">map</a> <a id="2322" href="Cubical.WildCat.Free.html#1946" class="Bound">F</a> <a id="2324" href="Cubical.WildCat.Free.html#2110" class="Bound">f</a><a id="2325" class="Symbol">)</a> <a id="2327" class="Symbol">(</a><a id="2328" href="Cubical.Data.Graph.Path.html#3205" class="Function">map</a> <a id="2332" href="Cubical.WildCat.Free.html#1946" class="Bound">F</a> <a id="2334" href="Cubical.WildCat.Free.html#2112" class="Bound">g</a><a id="2335" class="Symbol">)</a> <a id="2337" href="Cubical.Foundations.Prelude.html#9052" class="Function">⟩</a>
            <a id="2351" class="Symbol">(</a><a id="2352" href="Cubical.WildCat.Free.html#839" class="Function">composeAll</a> <a id="2363" href="Cubical.WildCat.Free.html#1944" class="Bound">C</a> <a id="2365" class="Symbol">(</a><a id="2366" href="Cubical.Data.Graph.Path.html#3205" class="Function">map</a> <a id="2370" href="Cubical.WildCat.Free.html#1946" class="Bound">F</a> <a id="2372" href="Cubical.WildCat.Free.html#2110" class="Bound">f</a><a id="2373" class="Symbol">))</a> <a id="2376" href="Cubical.WildCat.Base.html#1121" class="Function">⋆⟨</a> <a id="2379" href="Cubical.WildCat.Free.html#1944" class="Bound">C</a> <a id="2381" href="Cubical.WildCat.Base.html#1121" class="Function">⟩</a> <a id="2383" class="Symbol">(</a><a id="2384" href="Cubical.WildCat.Free.html#839" class="Function">composeAll</a> <a id="2395" href="Cubical.WildCat.Free.html#1944" class="Bound">C</a> <a id="2397" class="Symbol">(</a><a id="2398" href="Cubical.Data.Graph.Path.html#3205" class="Function">map</a> <a id="2402" href="Cubical.WildCat.Free.html#1946" class="Bound">F</a> <a id="2404" href="Cubical.WildCat.Free.html#2112" class="Bound">g</a><a id="2405" class="Symbol">))</a> <a id="2408" href="Cubical.Foundations.Prelude.html#9599" class="Function Operator">∎</a>
</pre></body></html>