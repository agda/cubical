<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Cubical.Codata.Conat.Base</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">{- Conatural numbers (Tesla Zhang, Feb. 2019)

This file defines:

- A coinductive natural number representation which is dual to
  the inductive version (zero | suc Nat → Nat) of natural numbers.

- Trivial operations (succ, pred) and the pattern synonyms on conaturals.

While this definition can be seen as a coinductive wrapper of an inductive
datatype, another way of definition is to define an inductive datatype that
wraps a coinductive thunk of Nat.
The standard library uses the second approach:

https://github.com/agda/agda-stdlib/blob/master/src/Codata/Conat.agda

The first approach is chosen to exploit guarded recursion and to avoid the use
of Sized Types.
-}</a>

<a id="677" class="Symbol">{-#</a> <a id="681" class="Keyword">OPTIONS</a> <a id="689" class="Pragma">--safe</a> <a id="696" class="Pragma">--guardedness</a> <a id="710" class="Symbol">#-}</a>
<a id="714" class="Keyword">module</a> <a id="721" href="Cubical.Codata.Conat.Base.html" class="Module">Cubical.Codata.Conat.Base</a> <a id="747" class="Keyword">where</a>

<a id="754" class="Keyword">open</a> <a id="759" class="Keyword">import</a> <a id="766" href="Cubical.Data.Unit.html" class="Module">Cubical.Data.Unit</a>
<a id="784" class="Keyword">open</a> <a id="789" class="Keyword">import</a> <a id="796" href="Cubical.Data.Sum.html" class="Module">Cubical.Data.Sum</a>

<a id="814" class="Keyword">open</a> <a id="819" class="Keyword">import</a> <a id="826" href="Cubical.Core.Everything.html" class="Module">Cubical.Core.Everything</a>

<a id="851" class="Keyword">record</a> <a id="Conat"></a><a id="858" href="Cubical.Codata.Conat.Base.html#858" class="Record">Conat</a> <a id="864" class="Symbol">:</a> <a id="866" href="Agda.Primitive.html#388" class="Primitive">Type₀</a>
<a id="Conat′"></a><a id="872" href="Cubical.Codata.Conat.Base.html#872" class="Function">Conat′</a> <a id="879" class="Symbol">=</a> <a id="881" href="Cubical.Data.Unit.Base.html#172" class="Record">Unit</a> <a id="886" href="Cubical.Data.Sum.Base.html#203" class="Datatype Operator">⊎</a> <a id="888" href="Cubical.Codata.Conat.Base.html#858" class="Record">Conat</a>
<a id="894" class="Keyword">record</a> <a id="901" href="Cubical.Codata.Conat.Base.html#858" class="Record">Conat</a> <a id="907" class="Keyword">where</a>
  <a id="915" class="Keyword">coinductive</a>
  <a id="929" class="Keyword">field</a> <a id="Conat.force"></a><a id="935" href="Cubical.Codata.Conat.Base.html#935" class="Field">force</a> <a id="941" class="Symbol">:</a> <a id="943" href="Cubical.Codata.Conat.Base.html#872" class="Function">Conat′</a>
<a id="950" class="Keyword">open</a> <a id="955" href="Cubical.Codata.Conat.Base.html#858" class="Module">Conat</a> <a id="961" class="Keyword">public</a>

<a id="969" class="Keyword">pattern</a> <a id="zero"></a><a id="977" href="Cubical.Codata.Conat.Base.html#977" class="InductiveConstructor">zero</a>  <a id="983" class="Symbol">=</a> <a id="985" href="Cubical.Data.Sum.Base.html#261" class="InductiveConstructor">inl</a> <a id="989" href="Agda.Builtin.Unit.html#212" class="InductiveConstructor">tt</a>
<a id="992" class="Keyword">pattern</a> <a id="suc"></a><a id="1000" href="Cubical.Codata.Conat.Base.html#1000" class="InductiveConstructor">suc</a> <a id="1004" href="Cubical.Codata.Conat.Base.html#1012" class="Bound">n</a> <a id="1006" class="Symbol">=</a> <a id="1008" href="Cubical.Data.Sum.Base.html#279" class="InductiveConstructor">inr</a> <a id="1012" href="Cubical.Codata.Conat.Base.html#1012" class="Bound">n</a>

<a id="conat"></a><a id="1015" href="Cubical.Codata.Conat.Base.html#1015" class="Function">conat</a> <a id="1021" class="Symbol">:</a> <a id="1023" href="Cubical.Codata.Conat.Base.html#872" class="Function">Conat′</a> <a id="1030" class="Symbol">→</a> <a id="1032" href="Cubical.Codata.Conat.Base.html#858" class="Record">Conat</a>
<a id="1038" href="Cubical.Codata.Conat.Base.html#935" class="Field">force</a> <a id="1044" class="Symbol">(</a><a id="1045" href="Cubical.Codata.Conat.Base.html#1015" class="Function">conat</a> <a id="1051" href="Cubical.Codata.Conat.Base.html#1051" class="Bound">a</a><a id="1052" class="Symbol">)</a> <a id="1054" class="Symbol">=</a> <a id="1056" href="Cubical.Codata.Conat.Base.html#1051" class="Bound">a</a>

<a id="succ"></a><a id="1059" href="Cubical.Codata.Conat.Base.html#1059" class="Function">succ</a> <a id="1064" class="Symbol">:</a> <a id="1066" href="Cubical.Codata.Conat.Base.html#858" class="Record">Conat</a> <a id="1072" class="Symbol">→</a> <a id="1074" href="Cubical.Codata.Conat.Base.html#858" class="Record">Conat</a>
<a id="1080" href="Cubical.Codata.Conat.Base.html#935" class="Field">force</a> <a id="1086" class="Symbol">(</a><a id="1087" href="Cubical.Codata.Conat.Base.html#1059" class="Function">succ</a> <a id="1092" href="Cubical.Codata.Conat.Base.html#1092" class="Bound">a</a><a id="1093" class="Symbol">)</a> <a id="1095" class="Symbol">=</a> <a id="1097" href="Cubical.Codata.Conat.Base.html#1000" class="InductiveConstructor">suc</a> <a id="1101" href="Cubical.Codata.Conat.Base.html#1092" class="Bound">a</a>

<a id="succ′"></a><a id="1104" href="Cubical.Codata.Conat.Base.html#1104" class="Function">succ′</a> <a id="1110" class="Symbol">:</a> <a id="1112" href="Cubical.Codata.Conat.Base.html#872" class="Function">Conat′</a> <a id="1119" class="Symbol">→</a> <a id="1121" href="Cubical.Codata.Conat.Base.html#872" class="Function">Conat′</a>
<a id="1128" href="Cubical.Codata.Conat.Base.html#1104" class="Function">succ′</a> <a id="1134" href="Cubical.Codata.Conat.Base.html#1134" class="Bound">n</a> <a id="1136" class="Symbol">=</a> <a id="1138" href="Cubical.Codata.Conat.Base.html#1000" class="InductiveConstructor">suc</a> <a id="1142" class="Symbol">λ</a> <a id="1144" class="Keyword">where</a> <a id="1150" class="Symbol">.</a><a id="1151" href="Cubical.Codata.Conat.Base.html#935" class="Field">force</a> <a id="1157" class="Symbol">→</a> <a id="1159" href="Cubical.Codata.Conat.Base.html#1134" class="Bound">n</a>

<a id="pred′"></a><a id="1162" href="Cubical.Codata.Conat.Base.html#1162" class="Function">pred′</a> <a id="1168" class="Symbol">:</a> <a id="1170" href="Cubical.Codata.Conat.Base.html#872" class="Function">Conat′</a> <a id="1177" class="Symbol">→</a> <a id="1179" href="Cubical.Codata.Conat.Base.html#872" class="Function">Conat′</a>
<a id="1186" href="Cubical.Codata.Conat.Base.html#1162" class="Function">pred′</a>  <a id="1193" href="Cubical.Codata.Conat.Base.html#977" class="InductiveConstructor">zero</a>   <a id="1200" class="Symbol">=</a> <a id="1202" href="Cubical.Codata.Conat.Base.html#977" class="InductiveConstructor">zero</a>
<a id="1207" href="Cubical.Codata.Conat.Base.html#1162" class="Function">pred′</a> <a id="1213" class="Symbol">(</a><a id="1214" href="Cubical.Codata.Conat.Base.html#1000" class="InductiveConstructor">suc</a> <a id="1218" href="Cubical.Codata.Conat.Base.html#1218" class="Bound">x</a><a id="1219" class="Symbol">)</a> <a id="1221" class="Symbol">=</a> <a id="1223" href="Cubical.Codata.Conat.Base.html#935" class="Field">force</a> <a id="1229" href="Cubical.Codata.Conat.Base.html#1218" class="Bound">x</a>

<a id="pred′′"></a><a id="1232" href="Cubical.Codata.Conat.Base.html#1232" class="Function">pred′′</a> <a id="1239" class="Symbol">:</a> <a id="1241" href="Cubical.Codata.Conat.Base.html#872" class="Function">Conat′</a> <a id="1248" class="Symbol">→</a> <a id="1250" href="Cubical.Codata.Conat.Base.html#858" class="Record">Conat</a>
<a id="1256" href="Cubical.Codata.Conat.Base.html#935" class="Field">force</a> <a id="1262" class="Symbol">(</a><a id="1263" href="Cubical.Codata.Conat.Base.html#1232" class="Function">pred′′</a> <a id="1270" href="Cubical.Codata.Conat.Base.html#977" class="InductiveConstructor">zero</a><a id="1274" class="Symbol">)</a> <a id="1276" class="Symbol">=</a> <a id="1278" href="Cubical.Codata.Conat.Base.html#977" class="InductiveConstructor">zero</a>
<a id="1283" href="Cubical.Codata.Conat.Base.html#1232" class="Function">pred′′</a> <a id="1290" class="Symbol">(</a><a id="1291" href="Cubical.Codata.Conat.Base.html#1000" class="InductiveConstructor">suc</a> <a id="1295" href="Cubical.Codata.Conat.Base.html#1295" class="Bound">x</a><a id="1296" class="Symbol">)</a> <a id="1298" class="Symbol">=</a> <a id="1300" href="Cubical.Codata.Conat.Base.html#1295" class="Bound">x</a>
</pre></body></html>