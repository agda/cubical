<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Cubical.Categories.Instances.Discrete</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">-- Discrete category over a type A</a>
<a id="36" class="Comment">-- A must be an h-groupoid for the homs to be sets</a>

<a id="88" class="Keyword">module</a> <a id="95" href="Cubical.Categories.Instances.Discrete.html" class="Module">Cubical.Categories.Instances.Discrete</a> <a id="133" class="Keyword">where</a>

<a id="140" class="Keyword">open</a> <a id="145" class="Keyword">import</a> <a id="152" href="Cubical.Categories.Category.Base.html" class="Module">Cubical.Categories.Category.Base</a>
<a id="185" class="Keyword">open</a> <a id="190" class="Keyword">import</a> <a id="197" href="Cubical.Categories.Functor.Base.html" class="Module">Cubical.Categories.Functor.Base</a>
<a id="229" class="Keyword">open</a> <a id="234" class="Keyword">import</a> <a id="241" href="Cubical.Foundations.GroupoidLaws.html" class="Module">Cubical.Foundations.GroupoidLaws</a>
<a id="274" class="Keyword">open</a> <a id="279" class="Keyword">import</a> <a id="286" href="Cubical.Foundations.HLevels.html" class="Module">Cubical.Foundations.HLevels</a>
<a id="314" class="Keyword">open</a> <a id="319" class="Keyword">import</a> <a id="326" href="Cubical.Foundations.Prelude.html" class="Module">Cubical.Foundations.Prelude</a>
<a id="354" class="Keyword">open</a> <a id="359" class="Keyword">import</a> <a id="366" href="Cubical.Foundations.Transport.html" class="Module">Cubical.Foundations.Transport</a>

<a id="397" class="Keyword">private</a>
  <a id="407" class="Keyword">variable</a>
    <a id="420" href="Cubical.Categories.Instances.Discrete.html#420" class="Generalizable">ℓ</a> <a id="422" href="Cubical.Categories.Instances.Discrete.html#422" class="Generalizable">ℓC</a> <a id="425" href="Cubical.Categories.Instances.Discrete.html#425" class="Generalizable">ℓC&#39;</a> <a id="429" class="Symbol">:</a> <a id="431" href="Agda.Primitive.html#742" class="Postulate">Level</a>

<a id="438" class="Keyword">open</a> <a id="443" href="Cubical.Categories.Category.Base.html#311" class="Module">Category</a>

<a id="453" class="Comment">-- Discrete category</a>
<a id="DiscreteCategory"></a><a id="474" href="Cubical.Categories.Instances.Discrete.html#474" class="Function">DiscreteCategory</a> <a id="491" class="Symbol">:</a> <a id="493" href="Cubical.Foundations.HLevels.html#2016" class="Function">hGroupoid</a> <a id="503" href="Cubical.Categories.Instances.Discrete.html#420" class="Generalizable">ℓ</a> <a id="505" class="Symbol">→</a> <a id="507" href="Cubical.Categories.Category.Base.html#311" class="Record">Category</a> <a id="516" href="Cubical.Categories.Instances.Discrete.html#420" class="Generalizable">ℓ</a> <a id="518" href="Cubical.Categories.Instances.Discrete.html#420" class="Generalizable">ℓ</a>
<a id="520" href="Cubical.Categories.Instances.Discrete.html#474" class="Function">DiscreteCategory</a> <a id="537" href="Cubical.Categories.Instances.Discrete.html#537" class="Bound">A</a> <a id="539" class="Symbol">.</a><a id="540" href="Cubical.Categories.Category.Base.html#429" class="Field">ob</a> <a id="543" class="Symbol">=</a> <a id="545" href="Cubical.Categories.Instances.Discrete.html#537" class="Bound">A</a> <a id="547" class="Symbol">.</a><a id="548" href="Agda.Builtin.Sigma.html#251" class="Field">fst</a>
<a id="552" href="Cubical.Categories.Instances.Discrete.html#474" class="Function">DiscreteCategory</a> <a id="569" href="Cubical.Categories.Instances.Discrete.html#569" class="Bound">A</a> <a id="571" class="Symbol">.</a><a id="572" href="Cubical.Categories.Category.Base.html#445" class="Field Operator">Hom[_,_]</a> <a id="581" href="Cubical.Categories.Instances.Discrete.html#581" class="Bound">a</a> <a id="583" href="Cubical.Categories.Instances.Discrete.html#583" class="Bound">a&#39;</a> <a id="586" class="Symbol">=</a> <a id="588" href="Cubical.Categories.Instances.Discrete.html#581" class="Bound">a</a> <a id="590" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="592" href="Cubical.Categories.Instances.Discrete.html#583" class="Bound">a&#39;</a>
<a id="595" href="Cubical.Categories.Instances.Discrete.html#474" class="Function">DiscreteCategory</a> <a id="612" href="Cubical.Categories.Instances.Discrete.html#612" class="Bound">A</a> <a id="614" class="Symbol">.</a><a id="615" href="Cubical.Categories.Category.Base.html#478" class="Field">id</a> <a id="618" class="Symbol">=</a> <a id="620" href="Cubical.Foundations.Prelude.html#892" class="Function">refl</a>
<a id="625" href="Cubical.Categories.Instances.Discrete.html#474" class="Function">DiscreteCategory</a> <a id="642" href="Cubical.Categories.Instances.Discrete.html#642" class="Bound">A</a> <a id="644" class="Symbol">.</a><a id="645" href="Cubical.Categories.Category.Base.html#510" class="Field Operator">_⋆_</a> <a id="649" class="Symbol">=</a> <a id="651" href="Cubical.Foundations.Prelude.html#5184" class="Function Operator">_∙_</a>
<a id="655" href="Cubical.Categories.Instances.Discrete.html#474" class="Function">DiscreteCategory</a> <a id="672" href="Cubical.Categories.Instances.Discrete.html#672" class="Bound">A</a> <a id="674" class="Symbol">.</a><a id="675" href="Cubical.Categories.Category.Base.html#584" class="Field">⋆IdL</a> <a id="680" href="Cubical.Categories.Instances.Discrete.html#680" class="Bound">f</a> <a id="682" class="Symbol">=</a> <a id="684" href="Cubical.Foundations.Prelude.html#945" class="Function">sym</a> <a id="688" class="Symbol">(</a><a id="689" href="Cubical.Foundations.GroupoidLaws.html#966" class="Function">lUnit</a> <a id="695" href="Cubical.Categories.Instances.Discrete.html#680" class="Bound">f</a><a id="696" class="Symbol">)</a>
<a id="698" href="Cubical.Categories.Instances.Discrete.html#474" class="Function">DiscreteCategory</a> <a id="715" href="Cubical.Categories.Instances.Discrete.html#715" class="Bound">A</a> <a id="717" class="Symbol">.</a><a id="718" href="Cubical.Categories.Category.Base.html#635" class="Field">⋆IdR</a> <a id="723" href="Cubical.Categories.Instances.Discrete.html#723" class="Bound">f</a> <a id="725" class="Symbol">=</a> <a id="727" href="Cubical.Foundations.Prelude.html#945" class="Function">sym</a> <a id="731" class="Symbol">(</a><a id="732" href="Cubical.Foundations.GroupoidLaws.html#400" class="Function">rUnit</a> <a id="738" href="Cubical.Categories.Instances.Discrete.html#723" class="Bound">f</a><a id="739" class="Symbol">)</a>
<a id="741" href="Cubical.Categories.Instances.Discrete.html#474" class="Function">DiscreteCategory</a> <a id="758" href="Cubical.Categories.Instances.Discrete.html#758" class="Bound">A</a> <a id="760" class="Symbol">.</a><a id="761" href="Cubical.Categories.Category.Base.html#686" class="Field">⋆Assoc</a> <a id="768" href="Cubical.Categories.Instances.Discrete.html#768" class="Bound">f</a> <a id="770" href="Cubical.Categories.Instances.Discrete.html#770" class="Bound">g</a> <a id="772" href="Cubical.Categories.Instances.Discrete.html#772" class="Bound">h</a> <a id="774" class="Symbol">=</a> <a id="776" href="Cubical.Foundations.Prelude.html#945" class="Function">sym</a> <a id="780" class="Symbol">(</a><a id="781" href="Cubical.Foundations.GroupoidLaws.html#2192" class="Function">assoc</a> <a id="787" href="Cubical.Categories.Instances.Discrete.html#768" class="Bound">f</a> <a id="789" href="Cubical.Categories.Instances.Discrete.html#770" class="Bound">g</a> <a id="791" href="Cubical.Categories.Instances.Discrete.html#772" class="Bound">h</a><a id="792" class="Symbol">)</a>
<a id="794" href="Cubical.Categories.Instances.Discrete.html#474" class="Function">DiscreteCategory</a> <a id="811" href="Cubical.Categories.Instances.Discrete.html#811" class="Bound">A</a> <a id="813" class="Symbol">.</a><a id="814" href="Cubical.Categories.Category.Base.html#807" class="Field">isSetHom</a> <a id="823" class="Symbol">{</a><a id="824" href="Cubical.Categories.Instances.Discrete.html#824" class="Bound">x</a><a id="825" class="Symbol">}</a> <a id="827" class="Symbol">{</a><a id="828" href="Cubical.Categories.Instances.Discrete.html#828" class="Bound">y</a><a id="829" class="Symbol">}</a> <a id="831" class="Symbol">=</a> <a id="833" href="Cubical.Categories.Instances.Discrete.html#811" class="Bound">A</a> <a id="835" class="Symbol">.</a><a id="836" href="Agda.Builtin.Sigma.html#263" class="Field">snd</a> <a id="840" href="Cubical.Categories.Instances.Discrete.html#824" class="Bound">x</a> <a id="842" href="Cubical.Categories.Instances.Discrete.html#828" class="Bound">y</a>


<a id="846" class="Keyword">module</a> <a id="853" href="Cubical.Categories.Instances.Discrete.html#853" class="Module">_</a> <a id="855" class="Symbol">{</a><a id="856" href="Cubical.Categories.Instances.Discrete.html#856" class="Bound">A</a> <a id="858" class="Symbol">:</a> <a id="860" href="Cubical.Foundations.HLevels.html#2016" class="Function">hGroupoid</a> <a id="870" href="Cubical.Categories.Instances.Discrete.html#420" class="Generalizable">ℓ</a><a id="871" class="Symbol">}</a>
         <a id="882" class="Symbol">{</a><a id="883" href="Cubical.Categories.Instances.Discrete.html#883" class="Bound">C</a> <a id="885" class="Symbol">:</a> <a id="887" href="Cubical.Categories.Category.Base.html#311" class="Record">Category</a> <a id="896" href="Cubical.Categories.Instances.Discrete.html#422" class="Generalizable">ℓC</a> <a id="899" href="Cubical.Categories.Instances.Discrete.html#425" class="Generalizable">ℓC&#39;</a><a id="902" class="Symbol">}</a> <a id="904" class="Keyword">where</a>
  <a id="912" class="Keyword">open</a> <a id="917" href="Cubical.Categories.Functor.Base.html#418" class="Module">Functor</a>

  <a id="928" class="Comment">-- Functions f: A → ob C give functors F: DiscreteCategory A → C</a>
  <a id="995" href="Cubical.Categories.Instances.Discrete.html#995" class="Function">DiscFunc</a> <a id="1004" class="Symbol">:</a> <a id="1006" class="Symbol">(</a><a id="1007" href="Agda.Builtin.Sigma.html#251" class="Field">fst</a> <a id="1011" href="Cubical.Categories.Instances.Discrete.html#856" class="Bound">A</a> <a id="1013" class="Symbol">→</a> <a id="1015" href="Cubical.Categories.Category.Base.html#429" class="Field">ob</a> <a id="1018" href="Cubical.Categories.Instances.Discrete.html#883" class="Bound">C</a><a id="1019" class="Symbol">)</a> <a id="1021" class="Symbol">→</a> <a id="1023" href="Cubical.Categories.Functor.Base.html#418" class="Record">Functor</a> <a id="1031" class="Symbol">(</a><a id="1032" href="Cubical.Categories.Instances.Discrete.html#474" class="Function">DiscreteCategory</a> <a id="1049" href="Cubical.Categories.Instances.Discrete.html#856" class="Bound">A</a><a id="1050" class="Symbol">)</a> <a id="1052" href="Cubical.Categories.Instances.Discrete.html#883" class="Bound">C</a>
  <a id="1056" href="Cubical.Categories.Instances.Discrete.html#995" class="Function">DiscFunc</a> <a id="1065" href="Cubical.Categories.Instances.Discrete.html#1065" class="Bound">f</a> <a id="1067" class="Symbol">.</a><a id="1068" href="Cubical.Categories.Functor.Base.html#578" class="Field">F-ob</a> <a id="1073" class="Symbol">=</a> <a id="1075" href="Cubical.Categories.Instances.Discrete.html#1065" class="Bound">f</a>
  <a id="1079" href="Cubical.Categories.Instances.Discrete.html#995" class="Function">DiscFunc</a> <a id="1088" href="Cubical.Categories.Instances.Discrete.html#1088" class="Bound">f</a> <a id="1090" class="Symbol">.</a><a id="1091" href="Cubical.Categories.Functor.Base.html#604" class="Field">F-hom</a> <a id="1097" class="Symbol">{</a><a id="1098" href="Cubical.Categories.Instances.Discrete.html#1098" class="Bound">x</a><a id="1099" class="Symbol">}</a> <a id="1101" href="Cubical.Categories.Instances.Discrete.html#1101" class="Bound">p</a> <a id="1103" class="Symbol">=</a> <a id="1105" href="Cubical.Foundations.Prelude.html#10205" class="Function">subst</a> <a id="1111" class="Symbol">(λ</a> <a id="1114" href="Cubical.Categories.Instances.Discrete.html#1114" class="Bound">z</a> <a id="1116" class="Symbol">→</a> <a id="1118" href="Cubical.Categories.Instances.Discrete.html#883" class="Bound">C</a> <a id="1120" href="Cubical.Categories.Category.Base.html#1210" class="Function Operator">[</a> <a id="1122" href="Cubical.Categories.Instances.Discrete.html#1088" class="Bound">f</a> <a id="1124" href="Cubical.Categories.Instances.Discrete.html#1098" class="Bound">x</a> <a id="1126" href="Cubical.Categories.Category.Base.html#1210" class="Function Operator">,</a> <a id="1128" href="Cubical.Categories.Instances.Discrete.html#1088" class="Bound">f</a> <a id="1130" href="Cubical.Categories.Instances.Discrete.html#1114" class="Bound">z</a> <a id="1132" href="Cubical.Categories.Category.Base.html#1210" class="Function Operator">]</a><a id="1133" class="Symbol">)</a> <a id="1135" href="Cubical.Categories.Instances.Discrete.html#1101" class="Bound">p</a> <a id="1137" class="Symbol">(</a><a id="1138" href="Cubical.Categories.Category.Base.html#478" class="Field">id</a> <a id="1141" href="Cubical.Categories.Instances.Discrete.html#883" class="Bound">C</a><a id="1142" class="Symbol">)</a>
  <a id="1146" href="Cubical.Categories.Instances.Discrete.html#995" class="Function">DiscFunc</a> <a id="1155" href="Cubical.Categories.Instances.Discrete.html#1155" class="Bound">f</a> <a id="1157" class="Symbol">.</a><a id="1158" href="Cubical.Categories.Functor.Base.html#668" class="Field">F-id</a> <a id="1163" class="Symbol">{</a><a id="1164" href="Cubical.Categories.Instances.Discrete.html#1164" class="Bound">x</a><a id="1165" class="Symbol">}</a> <a id="1167" class="Symbol">=</a> <a id="1169" href="Cubical.Foundations.Prelude.html#10467" class="Function">substRefl</a> <a id="1179" class="Symbol">{</a><a id="1180" class="Argument">B</a> <a id="1182" class="Symbol">=</a> <a id="1184" class="Symbol">λ</a> <a id="1186" href="Cubical.Categories.Instances.Discrete.html#1186" class="Bound">z</a> <a id="1188" class="Symbol">→</a> <a id="1190" href="Cubical.Categories.Instances.Discrete.html#883" class="Bound">C</a> <a id="1192" href="Cubical.Categories.Category.Base.html#1210" class="Function Operator">[</a> <a id="1194" href="Cubical.Categories.Instances.Discrete.html#1155" class="Bound">f</a> <a id="1196" href="Cubical.Categories.Instances.Discrete.html#1164" class="Bound">x</a> <a id="1198" href="Cubical.Categories.Category.Base.html#1210" class="Function Operator">,</a> <a id="1200" href="Cubical.Categories.Instances.Discrete.html#1155" class="Bound">f</a> <a id="1202" href="Cubical.Categories.Instances.Discrete.html#1186" class="Bound">z</a> <a id="1204" href="Cubical.Categories.Category.Base.html#1210" class="Function Operator">]</a><a id="1205" class="Symbol">}</a> <a id="1207" class="Symbol">(</a><a id="1208" href="Cubical.Categories.Category.Base.html#478" class="Field">id</a> <a id="1211" href="Cubical.Categories.Instances.Discrete.html#883" class="Bound">C</a><a id="1212" class="Symbol">)</a>

  <a id="1217" class="Comment">-- Preserves composition</a>
  <a id="1244" href="Cubical.Categories.Instances.Discrete.html#995" class="Function">DiscFunc</a> <a id="1253" href="Cubical.Categories.Instances.Discrete.html#1253" class="Bound">f</a> <a id="1255" class="Symbol">.</a><a id="1256" href="Cubical.Categories.Functor.Base.html#729" class="Field">F-seq</a> <a id="1262" class="Symbol">{</a><a id="1263" href="Cubical.Categories.Instances.Discrete.html#1263" class="Bound">x</a><a id="1264" class="Symbol">}</a> <a id="1266" class="Symbol">{</a><a id="1267" href="Cubical.Categories.Instances.Discrete.html#1267" class="Bound">y</a><a id="1268" class="Symbol">}</a> <a id="1270" href="Cubical.Categories.Instances.Discrete.html#1270" class="Bound">p</a> <a id="1272" href="Cubical.Categories.Instances.Discrete.html#1272" class="Bound">q</a> <a id="1274" class="Symbol">=</a>
      <a id="1282" class="Keyword">let</a> <a id="1286" class="Keyword">open</a> <a id="1291" href="Cubical.Categories.Category.Base.html#311" class="Module">Category</a> <a id="1300" href="Cubical.Categories.Instances.Discrete.html#883" class="Bound">C</a> <a id="1302" class="Keyword">using</a> <a id="1308" class="Symbol">()</a> <a id="1311" class="Keyword">renaming</a> <a id="1320" class="Symbol">(</a><a id="1321" href="Cubical.Categories.Category.Base.html#510" class="Field Operator">_⋆_</a> <a id="1325" class="Symbol">to</a> <a id="1328" class="Field Operator">_●_</a><a id="1331" class="Symbol">)</a> <a id="1333" class="Keyword">in</a>

      <a id="1343" class="Keyword">let</a> <a id="1347" href="Cubical.Categories.Instances.Discrete.html#1347" class="Bound">Hom[fx,f—]</a> <a id="1358" class="Symbol">=</a> <a id="1360" class="Symbol">(λ</a> <a id="1363" class="Symbol">(</a><a id="1364" href="Cubical.Categories.Instances.Discrete.html#1364" class="Bound">w</a> <a id="1366" class="Symbol">:</a> <a id="1368" href="Cubical.Categories.Instances.Discrete.html#856" class="Bound">A</a> <a id="1370" class="Symbol">.</a><a id="1371" href="Agda.Builtin.Sigma.html#251" class="Field">fst</a><a id="1374" class="Symbol">)</a> <a id="1376" class="Symbol">→</a> <a id="1378" href="Cubical.Categories.Instances.Discrete.html#883" class="Bound">C</a> <a id="1380" href="Cubical.Categories.Category.Base.html#1210" class="Function Operator">[</a> <a id="1382" href="Cubical.Categories.Instances.Discrete.html#1253" class="Bound">f</a> <a id="1384" href="Cubical.Categories.Instances.Discrete.html#1263" class="Bound">x</a> <a id="1386" href="Cubical.Categories.Category.Base.html#1210" class="Function Operator">,</a> <a id="1388" href="Cubical.Categories.Instances.Discrete.html#1253" class="Bound">f</a> <a id="1390" href="Cubical.Categories.Instances.Discrete.html#1364" class="Bound">w</a> <a id="1392" href="Cubical.Categories.Category.Base.html#1210" class="Function Operator">]</a><a id="1393" class="Symbol">)</a> <a id="1395" class="Keyword">in</a>
      <a id="1404" class="Keyword">let</a> <a id="1408" href="Cubical.Categories.Instances.Discrete.html#1408" class="Bound">Hom[fy,f—]</a> <a id="1419" class="Symbol">=</a> <a id="1421" class="Symbol">(λ</a> <a id="1424" class="Symbol">(</a><a id="1425" href="Cubical.Categories.Instances.Discrete.html#1425" class="Bound">w</a> <a id="1427" class="Symbol">:</a> <a id="1429" href="Cubical.Categories.Instances.Discrete.html#856" class="Bound">A</a> <a id="1431" class="Symbol">.</a><a id="1432" href="Agda.Builtin.Sigma.html#251" class="Field">fst</a><a id="1435" class="Symbol">)</a> <a id="1437" class="Symbol">→</a> <a id="1439" href="Cubical.Categories.Instances.Discrete.html#883" class="Bound">C</a> <a id="1441" href="Cubical.Categories.Category.Base.html#1210" class="Function Operator">[</a> <a id="1443" href="Cubical.Categories.Instances.Discrete.html#1253" class="Bound">f</a> <a id="1445" href="Cubical.Categories.Instances.Discrete.html#1267" class="Bound">y</a> <a id="1447" href="Cubical.Categories.Category.Base.html#1210" class="Function Operator">,</a> <a id="1449" href="Cubical.Categories.Instances.Discrete.html#1253" class="Bound">f</a> <a id="1451" href="Cubical.Categories.Instances.Discrete.html#1425" class="Bound">w</a> <a id="1453" href="Cubical.Categories.Category.Base.html#1210" class="Function Operator">]</a><a id="1454" class="Symbol">)</a> <a id="1456" class="Keyword">in</a>
      <a id="1465" class="Keyword">let</a> <a id="1469" href="Cubical.Categories.Instances.Discrete.html#1469" class="Bound">id-fx</a> <a id="1475" class="Symbol">=</a> <a id="1477" href="Cubical.Categories.Category.Base.html#478" class="Field">id</a> <a id="1480" href="Cubical.Categories.Instances.Discrete.html#883" class="Bound">C</a> <a id="1482" class="Symbol">{</a><a id="1483" href="Cubical.Categories.Instances.Discrete.html#1253" class="Bound">f</a> <a id="1485" href="Cubical.Categories.Instances.Discrete.html#1263" class="Bound">x</a><a id="1486" class="Symbol">}</a> <a id="1488" class="Keyword">in</a>
      <a id="1497" class="Keyword">let</a> <a id="1501" href="Cubical.Categories.Instances.Discrete.html#1501" class="Bound">id-fy</a> <a id="1507" class="Symbol">=</a> <a id="1509" href="Cubical.Categories.Category.Base.html#478" class="Field">id</a> <a id="1512" href="Cubical.Categories.Instances.Discrete.html#883" class="Bound">C</a> <a id="1514" class="Symbol">{</a><a id="1515" href="Cubical.Categories.Instances.Discrete.html#1253" class="Bound">f</a> <a id="1517" href="Cubical.Categories.Instances.Discrete.html#1267" class="Bound">y</a><a id="1518" class="Symbol">}</a> <a id="1520" class="Keyword">in</a>
      <a id="1529" class="Keyword">let</a> <a id="1533" href="Cubical.Categories.Instances.Discrete.html#1533" class="Bound">Fp</a> <a id="1536" class="Symbol">=</a> <a id="1538" class="Symbol">(</a><a id="1539" href="Cubical.Foundations.Prelude.html#10205" class="Function">subst</a> <a id="1545" href="Cubical.Categories.Instances.Discrete.html#1347" class="Bound">Hom[fx,f—]</a> <a id="1556" class="Symbol">(</a><a id="1557" href="Cubical.Categories.Instances.Discrete.html#1270" class="Bound">p</a><a id="1558" class="Symbol">)</a> <a id="1560" href="Cubical.Categories.Instances.Discrete.html#1469" class="Bound">id-fx</a><a id="1565" class="Symbol">)</a> <a id="1567" class="Keyword">in</a>

      <a id="1577" href="Cubical.Foundations.Prelude.html#10205" class="Function">subst</a> <a id="1583" href="Cubical.Categories.Instances.Discrete.html#1347" class="Bound">Hom[fx,f—]</a> <a id="1594" class="Symbol">(</a><a id="1595" href="Cubical.Categories.Instances.Discrete.html#1270" class="Bound">p</a> <a id="1597" href="Cubical.Foundations.Prelude.html#5184" class="Function Operator">∙</a> <a id="1599" href="Cubical.Categories.Instances.Discrete.html#1272" class="Bound">q</a><a id="1600" class="Symbol">)</a> <a id="1602" href="Cubical.Categories.Instances.Discrete.html#1469" class="Bound">id-fx</a>            <a id="1619" href="Cubical.Foundations.Prelude.html#9029" class="Function">≡⟨</a> <a id="1622" href="Cubical.Foundations.Transport.html#5832" class="Function">substComposite</a> <a id="1637" href="Cubical.Categories.Instances.Discrete.html#1347" class="Bound">Hom[fx,f—]</a> <a id="1648" class="Symbol">_</a> <a id="1650" class="Symbol">_</a> <a id="1652" class="Symbol">_</a> <a id="1654" href="Cubical.Foundations.Prelude.html#9029" class="Function">⟩</a>
      <a id="1662" href="Cubical.Foundations.Prelude.html#10205" class="Function">subst</a> <a id="1668" href="Cubical.Categories.Instances.Discrete.html#1347" class="Bound">Hom[fx,f—]</a> <a id="1679" class="Symbol">(</a><a id="1680" href="Cubical.Categories.Instances.Discrete.html#1272" class="Bound">q</a><a id="1681" class="Symbol">)</a> <a id="1683" class="Symbol">(</a><a id="1684" href="Cubical.Categories.Instances.Discrete.html#1533" class="Bound">Fp</a><a id="1686" class="Symbol">)</a>                 <a id="1704" href="Cubical.Foundations.Prelude.html#9029" class="Function">≡⟨</a> <a id="1707" href="Cubical.Foundations.Prelude.html#1407" class="Function">cong</a> <a id="1712" class="Symbol">(</a><a id="1713" href="Cubical.Foundations.Prelude.html#10205" class="Function">subst</a> <a id="1719" class="Symbol">_</a> <a id="1721" href="Cubical.Categories.Instances.Discrete.html#1272" class="Bound">q</a><a id="1722" class="Symbol">)</a> <a id="1724" class="Symbol">(</a><a id="1725" href="Cubical.Foundations.Prelude.html#945" class="Function">sym</a> <a id="1729" class="Symbol">(</a><a id="1730" href="Cubical.Categories.Category.Base.html#635" class="Field">⋆IdR</a> <a id="1735" href="Cubical.Categories.Instances.Discrete.html#883" class="Bound">C</a> <a id="1737" class="Symbol">_))</a> <a id="1741" href="Cubical.Foundations.Prelude.html#9029" class="Function">⟩</a>
      <a id="1749" href="Cubical.Foundations.Prelude.html#10205" class="Function">subst</a> <a id="1755" href="Cubical.Categories.Instances.Discrete.html#1347" class="Bound">Hom[fx,f—]</a> <a id="1766" class="Symbol">(</a><a id="1767" href="Cubical.Categories.Instances.Discrete.html#1272" class="Bound">q</a><a id="1768" class="Symbol">)</a> <a id="1770" class="Symbol">(</a><a id="1771" href="Cubical.Categories.Instances.Discrete.html#1533" class="Bound">Fp</a> <a id="1774" href="Cubical.Categories.Instances.Discrete.html#1328" class="Function Operator">●</a> <a id="1776" href="Cubical.Categories.Instances.Discrete.html#1501" class="Bound">id-fy</a><a id="1781" class="Symbol">)</a>         <a id="1791" href="Cubical.Foundations.Prelude.html#9029" class="Function">≡⟨</a> <a id="1794" href="Cubical.Foundations.Transport.html#6453" class="Function">substCommSlice</a> <a id="1809" class="Symbol">_</a> <a id="1811" class="Symbol">_</a> <a id="1813" class="Symbol">(λ</a> <a id="1816" href="Cubical.Categories.Instances.Discrete.html#1816" class="Bound">_</a> <a id="1818" class="Symbol">→</a> <a id="1820" href="Cubical.Categories.Instances.Discrete.html#1533" class="Bound">Fp</a> <a id="1823" href="Cubical.Categories.Instances.Discrete.html#1328" class="Function Operator">●_</a><a id="1825" class="Symbol">)</a> <a id="1827" href="Cubical.Categories.Instances.Discrete.html#1272" class="Bound">q</a> <a id="1829" class="Symbol">_</a> <a id="1831" href="Cubical.Foundations.Prelude.html#9029" class="Function">⟩</a>
      <a id="1839" href="Cubical.Categories.Instances.Discrete.html#1533" class="Bound">Fp</a> <a id="1842" href="Cubical.Categories.Instances.Discrete.html#1328" class="Function Operator">●</a> <a id="1844" class="Symbol">(</a><a id="1845" href="Cubical.Foundations.Prelude.html#10205" class="Function">subst</a> <a id="1851" href="Cubical.Categories.Instances.Discrete.html#1408" class="Bound">Hom[fy,f—]</a> <a id="1862" class="Symbol">(</a><a id="1863" href="Cubical.Categories.Instances.Discrete.html#1272" class="Bound">q</a><a id="1864" class="Symbol">)</a> <a id="1866" href="Cubical.Categories.Instances.Discrete.html#1501" class="Bound">id-fy</a><a id="1871" class="Symbol">)</a>         <a id="1881" href="Cubical.Foundations.Prelude.html#9576" class="Function Operator">∎</a>
</pre></body></html>