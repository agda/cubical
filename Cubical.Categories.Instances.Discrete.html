<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Cubical.Categories.Instances.Discrete</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">-- Discrete category over a type A</a>
<a id="36" class="Comment">-- A must be an h-groupoid for the homs to be sets</a>
<a id="87" class="Symbol">{-#</a> <a id="91" class="Keyword">OPTIONS</a> <a id="99" class="Pragma">--safe</a> <a id="106" class="Symbol">#-}</a>

<a id="111" class="Keyword">module</a> <a id="118" href="Cubical.Categories.Instances.Discrete.html" class="Module">Cubical.Categories.Instances.Discrete</a> <a id="156" class="Keyword">where</a>

<a id="163" class="Keyword">open</a> <a id="168" class="Keyword">import</a> <a id="175" href="Cubical.Categories.Category.Base.html" class="Module">Cubical.Categories.Category.Base</a>
<a id="208" class="Keyword">open</a> <a id="213" class="Keyword">import</a> <a id="220" href="Cubical.Categories.Functor.Base.html" class="Module">Cubical.Categories.Functor.Base</a>
<a id="252" class="Keyword">open</a> <a id="257" class="Keyword">import</a> <a id="264" href="Cubical.Foundations.GroupoidLaws.html" class="Module">Cubical.Foundations.GroupoidLaws</a>
<a id="297" class="Keyword">open</a> <a id="302" class="Keyword">import</a> <a id="309" href="Cubical.Foundations.HLevels.html" class="Module">Cubical.Foundations.HLevels</a>
<a id="337" class="Keyword">open</a> <a id="342" class="Keyword">import</a> <a id="349" href="Cubical.Foundations.Prelude.html" class="Module">Cubical.Foundations.Prelude</a>
<a id="377" class="Keyword">open</a> <a id="382" class="Keyword">import</a> <a id="389" href="Cubical.Foundations.Transport.html" class="Module">Cubical.Foundations.Transport</a>

<a id="420" class="Keyword">private</a>
  <a id="430" class="Keyword">variable</a>
    <a id="443" href="Cubical.Categories.Instances.Discrete.html#443" class="Generalizable">ℓ</a> <a id="445" href="Cubical.Categories.Instances.Discrete.html#445" class="Generalizable">ℓC</a> <a id="448" href="Cubical.Categories.Instances.Discrete.html#448" class="Generalizable">ℓC&#39;</a> <a id="452" class="Symbol">:</a> <a id="454" href="Agda.Primitive.html#742" class="Postulate">Level</a>

<a id="461" class="Keyword">open</a> <a id="466" href="Cubical.Categories.Category.Base.html#334" class="Module">Category</a>

<a id="476" class="Comment">-- Discrete category</a>
<a id="DiscreteCategory"></a><a id="497" href="Cubical.Categories.Instances.Discrete.html#497" class="Function">DiscreteCategory</a> <a id="514" class="Symbol">:</a> <a id="516" href="Cubical.Foundations.HLevels.html#2029" class="Function">hGroupoid</a> <a id="526" href="Cubical.Categories.Instances.Discrete.html#443" class="Generalizable">ℓ</a> <a id="528" class="Symbol">→</a> <a id="530" href="Cubical.Categories.Category.Base.html#334" class="Record">Category</a> <a id="539" href="Cubical.Categories.Instances.Discrete.html#443" class="Generalizable">ℓ</a> <a id="541" href="Cubical.Categories.Instances.Discrete.html#443" class="Generalizable">ℓ</a>
<a id="543" href="Cubical.Categories.Instances.Discrete.html#497" class="Function">DiscreteCategory</a> <a id="560" href="Cubical.Categories.Instances.Discrete.html#560" class="Bound">A</a> <a id="562" class="Symbol">.</a><a id="563" href="Cubical.Categories.Category.Base.html#452" class="Field">ob</a> <a id="566" class="Symbol">=</a> <a id="568" href="Cubical.Categories.Instances.Discrete.html#560" class="Bound">A</a> <a id="570" class="Symbol">.</a><a id="571" href="Agda.Builtin.Sigma.html#251" class="Field">fst</a>
<a id="575" href="Cubical.Categories.Instances.Discrete.html#497" class="Function">DiscreteCategory</a> <a id="592" href="Cubical.Categories.Instances.Discrete.html#592" class="Bound">A</a> <a id="594" class="Symbol">.</a><a id="595" href="Cubical.Categories.Category.Base.html#468" class="Field Operator">Hom[_,_]</a> <a id="604" href="Cubical.Categories.Instances.Discrete.html#604" class="Bound">a</a> <a id="606" href="Cubical.Categories.Instances.Discrete.html#606" class="Bound">a&#39;</a> <a id="609" class="Symbol">=</a> <a id="611" href="Cubical.Categories.Instances.Discrete.html#604" class="Bound">a</a> <a id="613" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="615" href="Cubical.Categories.Instances.Discrete.html#606" class="Bound">a&#39;</a>
<a id="618" href="Cubical.Categories.Instances.Discrete.html#497" class="Function">DiscreteCategory</a> <a id="635" href="Cubical.Categories.Instances.Discrete.html#635" class="Bound">A</a> <a id="637" class="Symbol">.</a><a id="638" href="Cubical.Categories.Category.Base.html#501" class="Field">id</a> <a id="641" class="Symbol">=</a> <a id="643" href="Cubical.Foundations.Prelude.html#915" class="Function">refl</a>
<a id="648" href="Cubical.Categories.Instances.Discrete.html#497" class="Function">DiscreteCategory</a> <a id="665" href="Cubical.Categories.Instances.Discrete.html#665" class="Bound">A</a> <a id="667" class="Symbol">.</a><a id="668" href="Cubical.Categories.Category.Base.html#533" class="Field Operator">_⋆_</a> <a id="672" class="Symbol">=</a> <a id="674" href="Cubical.Foundations.Prelude.html#4776" class="Function Operator">_∙_</a>
<a id="678" href="Cubical.Categories.Instances.Discrete.html#497" class="Function">DiscreteCategory</a> <a id="695" href="Cubical.Categories.Instances.Discrete.html#695" class="Bound">A</a> <a id="697" class="Symbol">.</a><a id="698" href="Cubical.Categories.Category.Base.html#607" class="Field">⋆IdL</a> <a id="703" href="Cubical.Categories.Instances.Discrete.html#703" class="Bound">f</a> <a id="705" class="Symbol">=</a> <a id="707" href="Cubical.Foundations.Prelude.html#968" class="Function">sym</a> <a id="711" class="Symbol">(</a><a id="712" href="Cubical.Foundations.GroupoidLaws.html#989" class="Function">lUnit</a> <a id="718" href="Cubical.Categories.Instances.Discrete.html#703" class="Bound">f</a><a id="719" class="Symbol">)</a>
<a id="721" href="Cubical.Categories.Instances.Discrete.html#497" class="Function">DiscreteCategory</a> <a id="738" href="Cubical.Categories.Instances.Discrete.html#738" class="Bound">A</a> <a id="740" class="Symbol">.</a><a id="741" href="Cubical.Categories.Category.Base.html#658" class="Field">⋆IdR</a> <a id="746" href="Cubical.Categories.Instances.Discrete.html#746" class="Bound">f</a> <a id="748" class="Symbol">=</a> <a id="750" href="Cubical.Foundations.Prelude.html#968" class="Function">sym</a> <a id="754" class="Symbol">(</a><a id="755" href="Cubical.Foundations.GroupoidLaws.html#423" class="Function">rUnit</a> <a id="761" href="Cubical.Categories.Instances.Discrete.html#746" class="Bound">f</a><a id="762" class="Symbol">)</a>
<a id="764" href="Cubical.Categories.Instances.Discrete.html#497" class="Function">DiscreteCategory</a> <a id="781" href="Cubical.Categories.Instances.Discrete.html#781" class="Bound">A</a> <a id="783" class="Symbol">.</a><a id="784" href="Cubical.Categories.Category.Base.html#709" class="Field">⋆Assoc</a> <a id="791" href="Cubical.Categories.Instances.Discrete.html#791" class="Bound">f</a> <a id="793" href="Cubical.Categories.Instances.Discrete.html#793" class="Bound">g</a> <a id="795" href="Cubical.Categories.Instances.Discrete.html#795" class="Bound">h</a> <a id="797" class="Symbol">=</a> <a id="799" href="Cubical.Foundations.Prelude.html#968" class="Function">sym</a> <a id="803" class="Symbol">(</a><a id="804" href="Cubical.Foundations.GroupoidLaws.html#2215" class="Function">assoc</a> <a id="810" href="Cubical.Categories.Instances.Discrete.html#791" class="Bound">f</a> <a id="812" href="Cubical.Categories.Instances.Discrete.html#793" class="Bound">g</a> <a id="814" href="Cubical.Categories.Instances.Discrete.html#795" class="Bound">h</a><a id="815" class="Symbol">)</a>
<a id="817" href="Cubical.Categories.Instances.Discrete.html#497" class="Function">DiscreteCategory</a> <a id="834" href="Cubical.Categories.Instances.Discrete.html#834" class="Bound">A</a> <a id="836" class="Symbol">.</a><a id="837" href="Cubical.Categories.Category.Base.html#830" class="Field">isSetHom</a> <a id="846" class="Symbol">{</a><a id="847" href="Cubical.Categories.Instances.Discrete.html#847" class="Bound">x</a><a id="848" class="Symbol">}</a> <a id="850" class="Symbol">{</a><a id="851" href="Cubical.Categories.Instances.Discrete.html#851" class="Bound">y</a><a id="852" class="Symbol">}</a> <a id="854" class="Symbol">=</a> <a id="856" href="Cubical.Categories.Instances.Discrete.html#834" class="Bound">A</a> <a id="858" class="Symbol">.</a><a id="859" href="Agda.Builtin.Sigma.html#263" class="Field">snd</a> <a id="863" href="Cubical.Categories.Instances.Discrete.html#847" class="Bound">x</a> <a id="865" href="Cubical.Categories.Instances.Discrete.html#851" class="Bound">y</a>


<a id="869" class="Keyword">module</a> <a id="876" href="Cubical.Categories.Instances.Discrete.html#876" class="Module">_</a> <a id="878" class="Symbol">{</a><a id="879" href="Cubical.Categories.Instances.Discrete.html#879" class="Bound">A</a> <a id="881" class="Symbol">:</a> <a id="883" href="Cubical.Foundations.HLevels.html#2029" class="Function">hGroupoid</a> <a id="893" href="Cubical.Categories.Instances.Discrete.html#443" class="Generalizable">ℓ</a><a id="894" class="Symbol">}</a>
         <a id="905" class="Symbol">{</a><a id="906" href="Cubical.Categories.Instances.Discrete.html#906" class="Bound">C</a> <a id="908" class="Symbol">:</a> <a id="910" href="Cubical.Categories.Category.Base.html#334" class="Record">Category</a> <a id="919" href="Cubical.Categories.Instances.Discrete.html#445" class="Generalizable">ℓC</a> <a id="922" href="Cubical.Categories.Instances.Discrete.html#448" class="Generalizable">ℓC&#39;</a><a id="925" class="Symbol">}</a> <a id="927" class="Keyword">where</a>
  <a id="935" class="Keyword">open</a> <a id="940" href="Cubical.Categories.Functor.Base.html#441" class="Module">Functor</a>

  <a id="951" class="Comment">-- Functions f: A → ob C give functors F: DiscreteCategory A → C</a>
  <a id="1018" href="Cubical.Categories.Instances.Discrete.html#1018" class="Function">DiscFunc</a> <a id="1027" class="Symbol">:</a> <a id="1029" class="Symbol">(</a><a id="1030" href="Agda.Builtin.Sigma.html#251" class="Field">fst</a> <a id="1034" href="Cubical.Categories.Instances.Discrete.html#879" class="Bound">A</a> <a id="1036" class="Symbol">→</a> <a id="1038" href="Cubical.Categories.Category.Base.html#452" class="Field">ob</a> <a id="1041" href="Cubical.Categories.Instances.Discrete.html#906" class="Bound">C</a><a id="1042" class="Symbol">)</a> <a id="1044" class="Symbol">→</a> <a id="1046" href="Cubical.Categories.Functor.Base.html#441" class="Record">Functor</a> <a id="1054" class="Symbol">(</a><a id="1055" href="Cubical.Categories.Instances.Discrete.html#497" class="Function">DiscreteCategory</a> <a id="1072" href="Cubical.Categories.Instances.Discrete.html#879" class="Bound">A</a><a id="1073" class="Symbol">)</a> <a id="1075" href="Cubical.Categories.Instances.Discrete.html#906" class="Bound">C</a>
  <a id="1079" href="Cubical.Categories.Instances.Discrete.html#1018" class="Function">DiscFunc</a> <a id="1088" href="Cubical.Categories.Instances.Discrete.html#1088" class="Bound">f</a> <a id="1090" class="Symbol">.</a><a id="1091" href="Cubical.Categories.Functor.Base.html#601" class="Field">F-ob</a> <a id="1096" class="Symbol">=</a> <a id="1098" href="Cubical.Categories.Instances.Discrete.html#1088" class="Bound">f</a>
  <a id="1102" href="Cubical.Categories.Instances.Discrete.html#1018" class="Function">DiscFunc</a> <a id="1111" href="Cubical.Categories.Instances.Discrete.html#1111" class="Bound">f</a> <a id="1113" class="Symbol">.</a><a id="1114" href="Cubical.Categories.Functor.Base.html#627" class="Field">F-hom</a> <a id="1120" class="Symbol">{</a><a id="1121" href="Cubical.Categories.Instances.Discrete.html#1121" class="Bound">x</a><a id="1122" class="Symbol">}</a> <a id="1124" href="Cubical.Categories.Instances.Discrete.html#1124" class="Bound">p</a> <a id="1126" class="Symbol">=</a> <a id="1128" href="Cubical.Foundations.Prelude.html#9486" class="Function">subst</a> <a id="1134" class="Symbol">(λ</a> <a id="1137" href="Cubical.Categories.Instances.Discrete.html#1137" class="Bound">z</a> <a id="1139" class="Symbol">→</a> <a id="1141" href="Cubical.Categories.Instances.Discrete.html#906" class="Bound">C</a> <a id="1143" href="Cubical.Categories.Category.Base.html#1083" class="Function Operator">[</a> <a id="1145" href="Cubical.Categories.Instances.Discrete.html#1111" class="Bound">f</a> <a id="1147" href="Cubical.Categories.Instances.Discrete.html#1121" class="Bound">x</a> <a id="1149" href="Cubical.Categories.Category.Base.html#1083" class="Function Operator">,</a> <a id="1151" href="Cubical.Categories.Instances.Discrete.html#1111" class="Bound">f</a> <a id="1153" href="Cubical.Categories.Instances.Discrete.html#1137" class="Bound">z</a> <a id="1155" href="Cubical.Categories.Category.Base.html#1083" class="Function Operator">]</a><a id="1156" class="Symbol">)</a> <a id="1158" href="Cubical.Categories.Instances.Discrete.html#1124" class="Bound">p</a> <a id="1160" class="Symbol">(</a><a id="1161" href="Cubical.Categories.Category.Base.html#501" class="Field">id</a> <a id="1164" href="Cubical.Categories.Instances.Discrete.html#906" class="Bound">C</a><a id="1165" class="Symbol">)</a>
  <a id="1169" href="Cubical.Categories.Instances.Discrete.html#1018" class="Function">DiscFunc</a> <a id="1178" href="Cubical.Categories.Instances.Discrete.html#1178" class="Bound">f</a> <a id="1180" class="Symbol">.</a><a id="1181" href="Cubical.Categories.Functor.Base.html#691" class="Field">F-id</a> <a id="1186" class="Symbol">{</a><a id="1187" href="Cubical.Categories.Instances.Discrete.html#1187" class="Bound">x</a><a id="1188" class="Symbol">}</a> <a id="1190" class="Symbol">=</a> <a id="1192" href="Cubical.Foundations.Prelude.html#9748" class="Function">substRefl</a> <a id="1202" class="Symbol">{</a><a id="1203" class="Argument">B</a> <a id="1205" class="Symbol">=</a> <a id="1207" class="Symbol">λ</a> <a id="1209" href="Cubical.Categories.Instances.Discrete.html#1209" class="Bound">z</a> <a id="1211" class="Symbol">→</a> <a id="1213" href="Cubical.Categories.Instances.Discrete.html#906" class="Bound">C</a> <a id="1215" href="Cubical.Categories.Category.Base.html#1083" class="Function Operator">[</a> <a id="1217" href="Cubical.Categories.Instances.Discrete.html#1178" class="Bound">f</a> <a id="1219" href="Cubical.Categories.Instances.Discrete.html#1187" class="Bound">x</a> <a id="1221" href="Cubical.Categories.Category.Base.html#1083" class="Function Operator">,</a> <a id="1223" href="Cubical.Categories.Instances.Discrete.html#1178" class="Bound">f</a> <a id="1225" href="Cubical.Categories.Instances.Discrete.html#1209" class="Bound">z</a> <a id="1227" href="Cubical.Categories.Category.Base.html#1083" class="Function Operator">]</a><a id="1228" class="Symbol">}</a> <a id="1230" class="Symbol">(</a><a id="1231" href="Cubical.Categories.Category.Base.html#501" class="Field">id</a> <a id="1234" href="Cubical.Categories.Instances.Discrete.html#906" class="Bound">C</a><a id="1235" class="Symbol">)</a>

  <a id="1240" class="Comment">-- Preserves composition</a>
  <a id="1267" href="Cubical.Categories.Instances.Discrete.html#1018" class="Function">DiscFunc</a> <a id="1276" href="Cubical.Categories.Instances.Discrete.html#1276" class="Bound">f</a> <a id="1278" class="Symbol">.</a><a id="1279" href="Cubical.Categories.Functor.Base.html#752" class="Field">F-seq</a> <a id="1285" class="Symbol">{</a><a id="1286" href="Cubical.Categories.Instances.Discrete.html#1286" class="Bound">x</a><a id="1287" class="Symbol">}</a> <a id="1289" class="Symbol">{</a><a id="1290" href="Cubical.Categories.Instances.Discrete.html#1290" class="Bound">y</a><a id="1291" class="Symbol">}</a> <a id="1293" href="Cubical.Categories.Instances.Discrete.html#1293" class="Bound">p</a> <a id="1295" href="Cubical.Categories.Instances.Discrete.html#1295" class="Bound">q</a> <a id="1297" class="Symbol">=</a>
      <a id="1305" class="Keyword">let</a> <a id="1309" class="Keyword">open</a> <a id="1314" href="Cubical.Categories.Category.Base.html#334" class="Module">Category</a> <a id="1323" href="Cubical.Categories.Instances.Discrete.html#906" class="Bound">C</a> <a id="1325" class="Keyword">using</a> <a id="1331" class="Symbol">()</a> <a id="1334" class="Keyword">renaming</a> <a id="1343" class="Symbol">(</a><a id="1344" href="Cubical.Categories.Category.Base.html#533" class="Field Operator">_⋆_</a> <a id="1348" class="Symbol">to</a> <a id="1351" class="Field Operator">_●_</a><a id="1354" class="Symbol">)</a> <a id="1356" class="Keyword">in</a>

      <a id="1366" class="Keyword">let</a> <a id="1370" href="Cubical.Categories.Instances.Discrete.html#1370" class="Bound">Hom[fx,f—]</a> <a id="1381" class="Symbol">=</a> <a id="1383" class="Symbol">(λ</a> <a id="1386" class="Symbol">(</a><a id="1387" href="Cubical.Categories.Instances.Discrete.html#1387" class="Bound">w</a> <a id="1389" class="Symbol">:</a> <a id="1391" href="Cubical.Categories.Instances.Discrete.html#879" class="Bound">A</a> <a id="1393" class="Symbol">.</a><a id="1394" href="Agda.Builtin.Sigma.html#251" class="Field">fst</a><a id="1397" class="Symbol">)</a> <a id="1399" class="Symbol">→</a> <a id="1401" href="Cubical.Categories.Instances.Discrete.html#906" class="Bound">C</a> <a id="1403" href="Cubical.Categories.Category.Base.html#1083" class="Function Operator">[</a> <a id="1405" href="Cubical.Categories.Instances.Discrete.html#1276" class="Bound">f</a> <a id="1407" href="Cubical.Categories.Instances.Discrete.html#1286" class="Bound">x</a> <a id="1409" href="Cubical.Categories.Category.Base.html#1083" class="Function Operator">,</a> <a id="1411" href="Cubical.Categories.Instances.Discrete.html#1276" class="Bound">f</a> <a id="1413" href="Cubical.Categories.Instances.Discrete.html#1387" class="Bound">w</a> <a id="1415" href="Cubical.Categories.Category.Base.html#1083" class="Function Operator">]</a><a id="1416" class="Symbol">)</a> <a id="1418" class="Keyword">in</a>
      <a id="1427" class="Keyword">let</a> <a id="1431" href="Cubical.Categories.Instances.Discrete.html#1431" class="Bound">Hom[fy,f—]</a> <a id="1442" class="Symbol">=</a> <a id="1444" class="Symbol">(λ</a> <a id="1447" class="Symbol">(</a><a id="1448" href="Cubical.Categories.Instances.Discrete.html#1448" class="Bound">w</a> <a id="1450" class="Symbol">:</a> <a id="1452" href="Cubical.Categories.Instances.Discrete.html#879" class="Bound">A</a> <a id="1454" class="Symbol">.</a><a id="1455" href="Agda.Builtin.Sigma.html#251" class="Field">fst</a><a id="1458" class="Symbol">)</a> <a id="1460" class="Symbol">→</a> <a id="1462" href="Cubical.Categories.Instances.Discrete.html#906" class="Bound">C</a> <a id="1464" href="Cubical.Categories.Category.Base.html#1083" class="Function Operator">[</a> <a id="1466" href="Cubical.Categories.Instances.Discrete.html#1276" class="Bound">f</a> <a id="1468" href="Cubical.Categories.Instances.Discrete.html#1290" class="Bound">y</a> <a id="1470" href="Cubical.Categories.Category.Base.html#1083" class="Function Operator">,</a> <a id="1472" href="Cubical.Categories.Instances.Discrete.html#1276" class="Bound">f</a> <a id="1474" href="Cubical.Categories.Instances.Discrete.html#1448" class="Bound">w</a> <a id="1476" href="Cubical.Categories.Category.Base.html#1083" class="Function Operator">]</a><a id="1477" class="Symbol">)</a> <a id="1479" class="Keyword">in</a>
      <a id="1488" class="Keyword">let</a> <a id="1492" href="Cubical.Categories.Instances.Discrete.html#1492" class="Bound">id-fx</a> <a id="1498" class="Symbol">=</a> <a id="1500" href="Cubical.Categories.Category.Base.html#501" class="Field">id</a> <a id="1503" href="Cubical.Categories.Instances.Discrete.html#906" class="Bound">C</a> <a id="1505" class="Symbol">{</a><a id="1506" href="Cubical.Categories.Instances.Discrete.html#1276" class="Bound">f</a> <a id="1508" href="Cubical.Categories.Instances.Discrete.html#1286" class="Bound">x</a><a id="1509" class="Symbol">}</a> <a id="1511" class="Keyword">in</a>
      <a id="1520" class="Keyword">let</a> <a id="1524" href="Cubical.Categories.Instances.Discrete.html#1524" class="Bound">id-fy</a> <a id="1530" class="Symbol">=</a> <a id="1532" href="Cubical.Categories.Category.Base.html#501" class="Field">id</a> <a id="1535" href="Cubical.Categories.Instances.Discrete.html#906" class="Bound">C</a> <a id="1537" class="Symbol">{</a><a id="1538" href="Cubical.Categories.Instances.Discrete.html#1276" class="Bound">f</a> <a id="1540" href="Cubical.Categories.Instances.Discrete.html#1290" class="Bound">y</a><a id="1541" class="Symbol">}</a> <a id="1543" class="Keyword">in</a>
      <a id="1552" class="Keyword">let</a> <a id="1556" href="Cubical.Categories.Instances.Discrete.html#1556" class="Bound">Fp</a> <a id="1559" class="Symbol">=</a> <a id="1561" class="Symbol">(</a><a id="1562" href="Cubical.Foundations.Prelude.html#9486" class="Function">subst</a> <a id="1568" href="Cubical.Categories.Instances.Discrete.html#1370" class="Bound">Hom[fx,f—]</a> <a id="1579" class="Symbol">(</a><a id="1580" href="Cubical.Categories.Instances.Discrete.html#1293" class="Bound">p</a><a id="1581" class="Symbol">)</a> <a id="1583" href="Cubical.Categories.Instances.Discrete.html#1492" class="Bound">id-fx</a><a id="1588" class="Symbol">)</a> <a id="1590" class="Keyword">in</a>

      <a id="1600" href="Cubical.Foundations.Prelude.html#9486" class="Function">subst</a> <a id="1606" href="Cubical.Categories.Instances.Discrete.html#1370" class="Bound">Hom[fx,f—]</a> <a id="1617" class="Symbol">(</a><a id="1618" href="Cubical.Categories.Instances.Discrete.html#1293" class="Bound">p</a> <a id="1620" href="Cubical.Foundations.Prelude.html#4776" class="Function Operator">∙</a> <a id="1622" href="Cubical.Categories.Instances.Discrete.html#1295" class="Bound">q</a><a id="1623" class="Symbol">)</a> <a id="1625" href="Cubical.Categories.Instances.Discrete.html#1492" class="Bound">id-fx</a>            <a id="1642" href="Cubical.Foundations.Prelude.html#8310" class="Function">≡⟨</a> <a id="1645" href="Cubical.Foundations.Transport.html#5623" class="Function">substComposite</a> <a id="1660" href="Cubical.Categories.Instances.Discrete.html#1370" class="Bound">Hom[fx,f—]</a> <a id="1671" class="Symbol">_</a> <a id="1673" class="Symbol">_</a> <a id="1675" class="Symbol">_</a> <a id="1677" href="Cubical.Foundations.Prelude.html#8310" class="Function">⟩</a>
      <a id="1685" href="Cubical.Foundations.Prelude.html#9486" class="Function">subst</a> <a id="1691" href="Cubical.Categories.Instances.Discrete.html#1370" class="Bound">Hom[fx,f—]</a> <a id="1702" class="Symbol">(</a><a id="1703" href="Cubical.Categories.Instances.Discrete.html#1295" class="Bound">q</a><a id="1704" class="Symbol">)</a> <a id="1706" class="Symbol">(</a><a id="1707" href="Cubical.Categories.Instances.Discrete.html#1556" class="Bound">Fp</a><a id="1709" class="Symbol">)</a>                 <a id="1727" href="Cubical.Foundations.Prelude.html#8310" class="Function">≡⟨</a> <a id="1730" href="Cubical.Foundations.Prelude.html#1430" class="Function">cong</a> <a id="1735" class="Symbol">(</a><a id="1736" href="Cubical.Foundations.Prelude.html#9486" class="Function">subst</a> <a id="1742" class="Symbol">_</a> <a id="1744" href="Cubical.Categories.Instances.Discrete.html#1295" class="Bound">q</a><a id="1745" class="Symbol">)</a> <a id="1747" class="Symbol">(</a><a id="1748" href="Cubical.Foundations.Prelude.html#968" class="Function">sym</a> <a id="1752" class="Symbol">(</a><a id="1753" href="Cubical.Categories.Category.Base.html#658" class="Field">⋆IdR</a> <a id="1758" href="Cubical.Categories.Instances.Discrete.html#906" class="Bound">C</a> <a id="1760" class="Symbol">_))</a> <a id="1764" href="Cubical.Foundations.Prelude.html#8310" class="Function">⟩</a>
      <a id="1772" href="Cubical.Foundations.Prelude.html#9486" class="Function">subst</a> <a id="1778" href="Cubical.Categories.Instances.Discrete.html#1370" class="Bound">Hom[fx,f—]</a> <a id="1789" class="Symbol">(</a><a id="1790" href="Cubical.Categories.Instances.Discrete.html#1295" class="Bound">q</a><a id="1791" class="Symbol">)</a> <a id="1793" class="Symbol">(</a><a id="1794" href="Cubical.Categories.Instances.Discrete.html#1556" class="Bound">Fp</a> <a id="1797" href="Cubical.Categories.Instances.Discrete.html#1351" class="Function Operator">●</a> <a id="1799" href="Cubical.Categories.Instances.Discrete.html#1524" class="Bound">id-fy</a><a id="1804" class="Symbol">)</a>         <a id="1814" href="Cubical.Foundations.Prelude.html#8310" class="Function">≡⟨</a> <a id="1817" href="Cubical.Foundations.Transport.html#6244" class="Function">substCommSlice</a> <a id="1832" class="Symbol">_</a> <a id="1834" class="Symbol">_</a> <a id="1836" class="Symbol">(λ</a> <a id="1839" href="Cubical.Categories.Instances.Discrete.html#1839" class="Bound">_</a> <a id="1841" class="Symbol">→</a> <a id="1843" href="Cubical.Categories.Instances.Discrete.html#1556" class="Bound">Fp</a> <a id="1846" href="Cubical.Categories.Instances.Discrete.html#1351" class="Function Operator">●_</a><a id="1848" class="Symbol">)</a> <a id="1850" href="Cubical.Categories.Instances.Discrete.html#1295" class="Bound">q</a> <a id="1852" class="Symbol">_</a> <a id="1854" href="Cubical.Foundations.Prelude.html#8310" class="Function">⟩</a>
      <a id="1862" href="Cubical.Categories.Instances.Discrete.html#1556" class="Bound">Fp</a> <a id="1865" href="Cubical.Categories.Instances.Discrete.html#1351" class="Function Operator">●</a> <a id="1867" class="Symbol">(</a><a id="1868" href="Cubical.Foundations.Prelude.html#9486" class="Function">subst</a> <a id="1874" href="Cubical.Categories.Instances.Discrete.html#1431" class="Bound">Hom[fy,f—]</a> <a id="1885" class="Symbol">(</a><a id="1886" href="Cubical.Categories.Instances.Discrete.html#1295" class="Bound">q</a><a id="1887" class="Symbol">)</a> <a id="1889" href="Cubical.Categories.Instances.Discrete.html#1524" class="Bound">id-fy</a><a id="1894" class="Symbol">)</a>         <a id="1904" href="Cubical.Foundations.Prelude.html#8857" class="Function Operator">∎</a>
</pre></body></html>