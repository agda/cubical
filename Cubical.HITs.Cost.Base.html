<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Cubical.HITs.Cost.Base</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Symbol">{-#</a> <a id="5" class="Keyword">OPTIONS</a> <a id="13" class="Pragma">--safe</a> <a id="20" class="Symbol">#-}</a>
<a id="24" class="Keyword">module</a> <a id="31" href="Cubical.HITs.Cost.Base.html" class="Module">Cubical.HITs.Cost.Base</a> <a id="54" class="Keyword">where</a>

<a id="61" class="Keyword">open</a> <a id="66" class="Keyword">import</a> <a id="73" href="Cubical.Foundations.Prelude.html" class="Module">Cubical.Foundations.Prelude</a>
<a id="101" class="Keyword">open</a> <a id="106" class="Keyword">import</a> <a id="113" href="Cubical.Foundations.Function.html" class="Module">Cubical.Foundations.Function</a>
<a id="142" class="Keyword">open</a> <a id="147" class="Keyword">import</a> <a id="154" href="Cubical.HITs.PropositionalTruncation.html" class="Module">Cubical.HITs.PropositionalTruncation</a>
<a id="191" class="Keyword">open</a> <a id="196" class="Keyword">import</a> <a id="203" href="Cubical.Data.Nat.html" class="Module">Cubical.Data.Nat</a>
<a id="220" class="Keyword">open</a> <a id="225" class="Keyword">import</a> <a id="232" href="Cubical.Data.Sigma.html" class="Module">Cubical.Data.Sigma</a>

<a id="252" class="Keyword">private</a>
  <a id="262" class="Keyword">variable</a>
    <a id="275" href="Cubical.HITs.Cost.Base.html#275" class="Generalizable">ℓ</a> <a id="277" class="Symbol">:</a> <a id="279" href="Agda.Primitive.html#591" class="Postulate">Level</a>
    <a id="289" href="Cubical.HITs.Cost.Base.html#289" class="Generalizable">A</a> <a id="291" href="Cubical.HITs.Cost.Base.html#291" class="Generalizable">B</a> <a id="293" href="Cubical.HITs.Cost.Base.html#293" class="Generalizable">C</a> <a id="295" class="Symbol">:</a> <a id="297" href="Agda.Primitive.html#320" class="Primitive">Type</a> <a id="302" href="Cubical.HITs.Cost.Base.html#275" class="Generalizable">ℓ</a>

<a id="Cost"></a><a id="305" href="Cubical.HITs.Cost.Base.html#305" class="Function">Cost</a> <a id="310" class="Symbol">:</a> <a id="312" class="Symbol">(</a><a id="313" href="Cubical.HITs.Cost.Base.html#313" class="Bound">A</a> <a id="315" class="Symbol">:</a> <a id="317" href="Agda.Primitive.html#320" class="Primitive">Type</a> <a id="322" href="Cubical.HITs.Cost.Base.html#275" class="Generalizable">ℓ</a><a id="323" class="Symbol">)</a> <a id="325" class="Symbol">→</a> <a id="327" href="Agda.Primitive.html#320" class="Primitive">Type</a> <a id="332" href="Cubical.HITs.Cost.Base.html#275" class="Generalizable">ℓ</a>
<a id="334" href="Cubical.HITs.Cost.Base.html#305" class="Function">Cost</a> <a id="339" href="Cubical.HITs.Cost.Base.html#339" class="Bound">A</a> <a id="341" class="Symbol">=</a> <a id="343" href="Cubical.HITs.Cost.Base.html#339" class="Bound">A</a> <a id="345" href="Cubical.Data.Sigma.Base.html#461" class="Function Operator">×</a> <a id="347" href="Cubical.HITs.PropositionalTruncation.Base.html#249" class="Datatype Operator">∥</a> <a id="349" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a> <a id="351" href="Cubical.HITs.PropositionalTruncation.Base.html#249" class="Datatype Operator">∥₁</a>

<a id="355" class="Comment">-- To compare two elements of Cost A we only need to look at the first parts</a>
<a id="Cost≡"></a><a id="432" href="Cubical.HITs.Cost.Base.html#432" class="Function">Cost≡</a> <a id="438" class="Symbol">:</a> <a id="440" class="Symbol">(</a><a id="441" href="Cubical.HITs.Cost.Base.html#441" class="Bound">x</a> <a id="443" href="Cubical.HITs.Cost.Base.html#443" class="Bound">y</a> <a id="445" class="Symbol">:</a> <a id="447" href="Cubical.HITs.Cost.Base.html#305" class="Function">Cost</a> <a id="452" href="Cubical.HITs.Cost.Base.html#289" class="Generalizable">A</a><a id="453" class="Symbol">)</a> <a id="455" class="Symbol">→</a> <a id="457" href="Cubical.HITs.Cost.Base.html#441" class="Bound">x</a> <a id="459" class="Symbol">.</a><a id="460" href="Agda.Builtin.Sigma.html#234" class="Field">fst</a> <a id="464" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="466" href="Cubical.HITs.Cost.Base.html#443" class="Bound">y</a> <a id="468" class="Symbol">.</a><a id="469" href="Agda.Builtin.Sigma.html#234" class="Field">fst</a> <a id="473" class="Symbol">→</a> <a id="475" href="Cubical.HITs.Cost.Base.html#441" class="Bound">x</a> <a id="477" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="479" href="Cubical.HITs.Cost.Base.html#443" class="Bound">y</a>
<a id="481" href="Cubical.HITs.Cost.Base.html#432" class="Function">Cost≡</a> <a id="487" class="Symbol">_</a> <a id="489" class="Symbol">_</a> <a id="491" class="Symbol">=</a> <a id="493" href="Cubical.Data.Sigma.Properties.html#13039" class="Function">Σ≡Prop</a> <a id="500" class="Symbol">λ</a> <a id="502" href="Cubical.HITs.Cost.Base.html#502" class="Bound">_</a> <a id="504" class="Symbol">→</a> <a id="506" href="Cubical.HITs.PropositionalTruncation.Base.html#308" class="InductiveConstructor">squash₁</a>

<a id="515" class="Comment">-- To make it easier to program with Cost A we prove that it forms a</a>
<a id="584" class="Comment">-- monad which counts the number of calls to &gt;&gt;=. We could also turn</a>
<a id="653" class="Comment">-- it into a proper state monad which tracks the cost, but for the</a>
<a id="720" class="Comment">-- programs we write later this simple version suffices</a>
<a id="_&gt;&gt;=_"></a><a id="776" href="Cubical.HITs.Cost.Base.html#776" class="Function Operator">_&gt;&gt;=_</a> <a id="782" class="Symbol">:</a> <a id="784" href="Cubical.HITs.Cost.Base.html#305" class="Function">Cost</a> <a id="789" href="Cubical.HITs.Cost.Base.html#289" class="Generalizable">A</a> <a id="791" class="Symbol">→</a> <a id="793" class="Symbol">(</a><a id="794" href="Cubical.HITs.Cost.Base.html#289" class="Generalizable">A</a> <a id="796" class="Symbol">→</a> <a id="798" href="Cubical.HITs.Cost.Base.html#305" class="Function">Cost</a> <a id="803" href="Cubical.HITs.Cost.Base.html#291" class="Generalizable">B</a><a id="804" class="Symbol">)</a> <a id="806" class="Symbol">→</a> <a id="808" href="Cubical.HITs.Cost.Base.html#305" class="Function">Cost</a> <a id="813" href="Cubical.HITs.Cost.Base.html#291" class="Generalizable">B</a>
<a id="815" class="Symbol">(</a><a id="816" href="Cubical.HITs.Cost.Base.html#816" class="Bound">x</a> <a id="818" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="820" href="Cubical.HITs.Cost.Base.html#820" class="Bound">m</a><a id="821" class="Symbol">)</a> <a id="823" href="Cubical.HITs.Cost.Base.html#776" class="Function Operator">&gt;&gt;=</a> <a id="827" href="Cubical.HITs.Cost.Base.html#827" class="Bound">g</a> <a id="829" class="Keyword">with</a> <a id="834" href="Cubical.HITs.Cost.Base.html#827" class="Bound">g</a> <a id="836" href="Cubical.HITs.Cost.Base.html#816" class="Bound">x</a>
<a id="838" class="Symbol">...</a> <a id="842" class="Symbol">|</a> <a id="844" class="Symbol">(</a><a id="845" href="Cubical.HITs.Cost.Base.html#845" class="Bound">y</a> <a id="847" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="849" href="Cubical.HITs.Cost.Base.html#849" class="Bound">n</a><a id="850" class="Symbol">)</a> <a id="852" class="Symbol">=</a> <a id="854" class="Symbol">(</a><a id="855" href="Cubical.HITs.Cost.Base.html#845" class="Bound">y</a> <a id="857" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="859" href="Cubical.HITs.PropositionalTruncation.Properties.html#6519" class="Function">map</a> <a id="863" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="867" class="Symbol">(</a><a id="868" href="Cubical.HITs.PropositionalTruncation.Properties.html#6585" class="Function">map2</a> <a id="873" href="Agda.Builtin.Nat.html#319" class="Primitive Operator">_+_</a> <a id="877" class="Bound">m</a> <a id="879" href="Cubical.HITs.Cost.Base.html#849" class="Bound">n</a><a id="880" class="Symbol">))</a>

<a id="return"></a><a id="884" href="Cubical.HITs.Cost.Base.html#884" class="Function">return</a> <a id="891" class="Symbol">:</a> <a id="893" href="Cubical.HITs.Cost.Base.html#289" class="Generalizable">A</a> <a id="895" class="Symbol">→</a> <a id="897" href="Cubical.HITs.Cost.Base.html#305" class="Function">Cost</a> <a id="902" href="Cubical.HITs.Cost.Base.html#289" class="Generalizable">A</a>
<a id="904" href="Cubical.HITs.Cost.Base.html#884" class="Function">return</a> <a id="911" href="Cubical.HITs.Cost.Base.html#911" class="Bound">x</a> <a id="913" class="Symbol">=</a> <a id="915" class="Symbol">(</a><a id="916" href="Cubical.HITs.Cost.Base.html#911" class="Bound">x</a> <a id="918" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="920" href="Cubical.HITs.PropositionalTruncation.Base.html#288" class="InductiveConstructor Operator">∣</a> <a id="922" class="Number">0</a> <a id="924" href="Cubical.HITs.PropositionalTruncation.Base.html#288" class="InductiveConstructor Operator">∣₁</a><a id="926" class="Symbol">)</a>

<a id="929" class="Comment">-- The monad laws all hold by Cost≡</a>

<a id="&gt;&gt;=-return"></a><a id="966" href="Cubical.HITs.Cost.Base.html#966" class="Function">&gt;&gt;=-return</a> <a id="977" class="Symbol">:</a> <a id="979" class="Symbol">(</a><a id="980" href="Cubical.HITs.Cost.Base.html#980" class="Bound">f</a> <a id="982" class="Symbol">:</a> <a id="984" href="Cubical.HITs.Cost.Base.html#305" class="Function">Cost</a> <a id="989" href="Cubical.HITs.Cost.Base.html#289" class="Generalizable">A</a><a id="990" class="Symbol">)</a> <a id="992" class="Symbol">→</a> <a id="994" href="Cubical.HITs.Cost.Base.html#980" class="Bound">f</a> <a id="996" href="Cubical.HITs.Cost.Base.html#776" class="Function Operator">&gt;&gt;=</a> <a id="1000" href="Cubical.HITs.Cost.Base.html#884" class="Function">return</a> <a id="1007" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="1009" href="Cubical.HITs.Cost.Base.html#980" class="Bound">f</a>
<a id="1011" href="Cubical.HITs.Cost.Base.html#966" class="Function">&gt;&gt;=-return</a> <a id="1022" href="Cubical.HITs.Cost.Base.html#1022" class="Bound">f</a> <a id="1024" class="Symbol">=</a> <a id="1026" href="Cubical.HITs.Cost.Base.html#432" class="Function">Cost≡</a> <a id="1032" class="Symbol">_</a> <a id="1034" class="Symbol">_</a> <a id="1036" href="Cubical.Foundations.Prelude.html#915" class="Function">refl</a>

<a id="return-&gt;&gt;="></a><a id="1042" href="Cubical.HITs.Cost.Base.html#1042" class="Function">return-&gt;&gt;=</a> <a id="1053" class="Symbol">:</a> <a id="1055" class="Symbol">(</a><a id="1056" href="Cubical.HITs.Cost.Base.html#1056" class="Bound">a</a> <a id="1058" class="Symbol">:</a> <a id="1060" href="Cubical.HITs.Cost.Base.html#289" class="Generalizable">A</a><a id="1061" class="Symbol">)</a> <a id="1063" class="Symbol">(</a><a id="1064" href="Cubical.HITs.Cost.Base.html#1064" class="Bound">f</a> <a id="1066" class="Symbol">:</a> <a id="1068" href="Cubical.HITs.Cost.Base.html#289" class="Generalizable">A</a> <a id="1070" class="Symbol">→</a> <a id="1072" href="Cubical.HITs.Cost.Base.html#305" class="Function">Cost</a> <a id="1077" href="Cubical.HITs.Cost.Base.html#291" class="Generalizable">B</a><a id="1078" class="Symbol">)</a> <a id="1080" class="Symbol">→</a> <a id="1082" href="Cubical.HITs.Cost.Base.html#884" class="Function">return</a> <a id="1089" href="Cubical.HITs.Cost.Base.html#1056" class="Bound">a</a> <a id="1091" href="Cubical.HITs.Cost.Base.html#776" class="Function Operator">&gt;&gt;=</a> <a id="1095" href="Cubical.HITs.Cost.Base.html#1064" class="Bound">f</a> <a id="1097" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="1099" href="Cubical.HITs.Cost.Base.html#1064" class="Bound">f</a> <a id="1101" href="Cubical.HITs.Cost.Base.html#1056" class="Bound">a</a>
<a id="1103" href="Cubical.HITs.Cost.Base.html#1042" class="Function">return-&gt;&gt;=</a> <a id="1114" href="Cubical.HITs.Cost.Base.html#1114" class="Bound">a</a> <a id="1116" href="Cubical.HITs.Cost.Base.html#1116" class="Bound">f</a> <a id="1118" class="Symbol">=</a> <a id="1120" href="Cubical.HITs.Cost.Base.html#432" class="Function">Cost≡</a> <a id="1126" class="Symbol">_</a> <a id="1128" class="Symbol">_</a> <a id="1130" href="Cubical.Foundations.Prelude.html#915" class="Function">refl</a>

<a id="&gt;&gt;=-assoc"></a><a id="1136" href="Cubical.HITs.Cost.Base.html#1136" class="Function">&gt;&gt;=-assoc</a> <a id="1146" class="Symbol">:</a> <a id="1148" class="Symbol">(</a><a id="1149" href="Cubical.HITs.Cost.Base.html#1149" class="Bound">f</a> <a id="1151" class="Symbol">:</a> <a id="1153" href="Cubical.HITs.Cost.Base.html#305" class="Function">Cost</a> <a id="1158" href="Cubical.HITs.Cost.Base.html#289" class="Generalizable">A</a><a id="1159" class="Symbol">)</a> <a id="1161" class="Symbol">(</a><a id="1162" href="Cubical.HITs.Cost.Base.html#1162" class="Bound">g</a> <a id="1164" class="Symbol">:</a> <a id="1166" href="Cubical.HITs.Cost.Base.html#289" class="Generalizable">A</a> <a id="1168" class="Symbol">→</a> <a id="1170" href="Cubical.HITs.Cost.Base.html#305" class="Function">Cost</a> <a id="1175" href="Cubical.HITs.Cost.Base.html#291" class="Generalizable">B</a><a id="1176" class="Symbol">)</a> <a id="1178" class="Symbol">(</a><a id="1179" href="Cubical.HITs.Cost.Base.html#1179" class="Bound">h</a> <a id="1181" class="Symbol">:</a> <a id="1183" href="Cubical.HITs.Cost.Base.html#291" class="Generalizable">B</a> <a id="1185" class="Symbol">→</a> <a id="1187" href="Cubical.HITs.Cost.Base.html#305" class="Function">Cost</a> <a id="1192" href="Cubical.HITs.Cost.Base.html#293" class="Generalizable">C</a><a id="1193" class="Symbol">)</a>
          <a id="1205" class="Symbol">→</a> <a id="1207" class="Symbol">(</a><a id="1208" href="Cubical.HITs.Cost.Base.html#1149" class="Bound">f</a> <a id="1210" href="Cubical.HITs.Cost.Base.html#776" class="Function Operator">&gt;&gt;=</a> <a id="1214" href="Cubical.HITs.Cost.Base.html#1162" class="Bound">g</a><a id="1215" class="Symbol">)</a> <a id="1217" href="Cubical.HITs.Cost.Base.html#776" class="Function Operator">&gt;&gt;=</a> <a id="1221" href="Cubical.HITs.Cost.Base.html#1179" class="Bound">h</a> <a id="1223" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="1225" href="Cubical.HITs.Cost.Base.html#1149" class="Bound">f</a> <a id="1227" href="Cubical.HITs.Cost.Base.html#776" class="Function Operator">&gt;&gt;=</a> <a id="1231" class="Symbol">(λ</a> <a id="1234" href="Cubical.HITs.Cost.Base.html#1234" class="Bound">x</a> <a id="1236" class="Symbol">→</a> <a id="1238" href="Cubical.HITs.Cost.Base.html#1162" class="Bound">g</a> <a id="1240" href="Cubical.HITs.Cost.Base.html#1234" class="Bound">x</a> <a id="1242" href="Cubical.HITs.Cost.Base.html#776" class="Function Operator">&gt;&gt;=</a> <a id="1246" href="Cubical.HITs.Cost.Base.html#1179" class="Bound">h</a><a id="1247" class="Symbol">)</a>
<a id="1249" href="Cubical.HITs.Cost.Base.html#1136" class="Function">&gt;&gt;=-assoc</a> <a id="1259" href="Cubical.HITs.Cost.Base.html#1259" class="Bound">f</a> <a id="1261" href="Cubical.HITs.Cost.Base.html#1261" class="Bound">g</a> <a id="1263" href="Cubical.HITs.Cost.Base.html#1263" class="Bound">h</a> <a id="1265" class="Symbol">=</a> <a id="1267" href="Cubical.HITs.Cost.Base.html#432" class="Function">Cost≡</a> <a id="1273" class="Symbol">_</a> <a id="1275" class="Symbol">_</a> <a id="1277" href="Cubical.Foundations.Prelude.html#915" class="Function">refl</a>


<a id="1284" class="Comment">-- An inefficient version of addition which recurses on both arguments</a>
<a id="addBad"></a><a id="1355" href="Cubical.HITs.Cost.Base.html#1355" class="Function">addBad</a> <a id="1362" class="Symbol">:</a> <a id="1364" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a> <a id="1366" class="Symbol">→</a> <a id="1368" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a> <a id="1370" class="Symbol">→</a> <a id="1372" href="Cubical.HITs.Cost.Base.html#305" class="Function">Cost</a> <a id="1377" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a>
<a id="1379" href="Cubical.HITs.Cost.Base.html#1355" class="Function">addBad</a> <a id="1386" class="Number">0</a> <a id="1388" class="Number">0</a>       <a id="1396" class="Symbol">=</a> <a id="1398" href="Cubical.HITs.Cost.Base.html#884" class="Function">return</a> <a id="1405" class="Number">0</a>
<a id="1407" href="Cubical.HITs.Cost.Base.html#1355" class="Function">addBad</a> <a id="1414" class="Number">0</a> <a id="1416" class="Symbol">(</a><a id="1417" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="1421" href="Cubical.HITs.Cost.Base.html#1421" class="Bound">y</a><a id="1422" class="Symbol">)</a> <a id="1424" class="Symbol">=</a> <a id="1426" class="Keyword">do</a>
  <a id="1431" href="Cubical.HITs.Cost.Base.html#1431" class="Bound">x</a> <a id="1433" href="Cubical.HITs.Cost.Base.html#776" class="Function Operator">←</a> <a id="1435" href="Cubical.HITs.Cost.Base.html#1355" class="Function">addBad</a> <a id="1442" class="Number">0</a> <a id="1444" href="Cubical.HITs.Cost.Base.html#1421" class="Bound">y</a>
  <a id="1448" href="Cubical.HITs.Cost.Base.html#884" class="Function">return</a> <a id="1455" class="Symbol">(</a><a id="1456" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="1460" href="Cubical.HITs.Cost.Base.html#1431" class="Bound">x</a><a id="1461" class="Symbol">)</a>
<a id="1463" href="Cubical.HITs.Cost.Base.html#1355" class="Function">addBad</a> <a id="1470" class="Symbol">(</a><a id="1471" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="1475" href="Cubical.HITs.Cost.Base.html#1475" class="Bound">x</a><a id="1476" class="Symbol">)</a> <a id="1478" href="Cubical.HITs.Cost.Base.html#1478" class="Bound">y</a> <a id="1480" class="Symbol">=</a> <a id="1482" class="Keyword">do</a>
  <a id="1487" href="Cubical.HITs.Cost.Base.html#1487" class="Bound">z</a> <a id="1489" href="Cubical.HITs.Cost.Base.html#776" class="Function Operator">←</a> <a id="1491" href="Cubical.HITs.Cost.Base.html#1355" class="Function">addBad</a> <a id="1498" href="Cubical.HITs.Cost.Base.html#1475" class="Bound">x</a> <a id="1500" href="Cubical.HITs.Cost.Base.html#1478" class="Bound">y</a>
  <a id="1504" href="Cubical.HITs.Cost.Base.html#884" class="Function">return</a> <a id="1511" class="Symbol">(</a><a id="1512" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="1516" href="Cubical.HITs.Cost.Base.html#1487" class="Bound">z</a><a id="1517" class="Symbol">)</a>

<a id="1520" class="Comment">-- More efficient addition which only recurse on the first argument</a>
<a id="add"></a><a id="1588" href="Cubical.HITs.Cost.Base.html#1588" class="Function">add</a> <a id="1592" class="Symbol">:</a> <a id="1594" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a> <a id="1596" class="Symbol">→</a> <a id="1598" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a> <a id="1600" class="Symbol">→</a> <a id="1602" href="Cubical.HITs.Cost.Base.html#305" class="Function">Cost</a> <a id="1607" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a>
<a id="1609" href="Cubical.HITs.Cost.Base.html#1588" class="Function">add</a> <a id="1613" class="Number">0</a>       <a id="1621" href="Cubical.HITs.Cost.Base.html#1621" class="Bound">y</a> <a id="1623" class="Symbol">=</a> <a id="1625" href="Cubical.HITs.Cost.Base.html#884" class="Function">return</a> <a id="1632" href="Cubical.HITs.Cost.Base.html#1621" class="Bound">y</a>
<a id="1634" href="Cubical.HITs.Cost.Base.html#1588" class="Function">add</a> <a id="1638" class="Symbol">(</a><a id="1639" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="1643" href="Cubical.HITs.Cost.Base.html#1643" class="Bound">x</a><a id="1644" class="Symbol">)</a> <a id="1646" href="Cubical.HITs.Cost.Base.html#1646" class="Bound">y</a> <a id="1648" class="Symbol">=</a> <a id="1650" class="Keyword">do</a>
  <a id="1655" href="Cubical.HITs.Cost.Base.html#1655" class="Bound">z</a> <a id="1657" href="Cubical.HITs.Cost.Base.html#776" class="Function Operator">←</a> <a id="1659" href="Cubical.HITs.Cost.Base.html#1588" class="Function">add</a> <a id="1663" href="Cubical.HITs.Cost.Base.html#1643" class="Bound">x</a> <a id="1665" href="Cubical.HITs.Cost.Base.html#1646" class="Bound">y</a>
  <a id="1669" href="Cubical.HITs.Cost.Base.html#884" class="Function">return</a> <a id="1676" class="Symbol">(</a><a id="1677" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="1681" href="Cubical.HITs.Cost.Base.html#1655" class="Bound">z</a><a id="1682" class="Symbol">)</a>

<a id="1685" class="Keyword">private</a>
  <a id="1695" class="Comment">-- addBad x y will do x + y calls</a>
  <a id="1731" href="Cubical.HITs.Cost.Base.html#1731" class="Function">_</a> <a id="1733" class="Symbol">:</a> <a id="1735" href="Cubical.HITs.Cost.Base.html#1355" class="Function">addBad</a> <a id="1742" class="Number">3</a> <a id="1744" class="Number">5</a> <a id="1746" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="1748" class="Symbol">(</a><a id="1749" class="Number">8</a> <a id="1751" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="1753" href="Cubical.HITs.PropositionalTruncation.Base.html#288" class="InductiveConstructor Operator">∣</a> <a id="1755" class="Number">8</a> <a id="1757" href="Cubical.HITs.PropositionalTruncation.Base.html#288" class="InductiveConstructor Operator">∣₁</a><a id="1759" class="Symbol">)</a>
  <a id="1763" class="Symbol">_</a> <a id="1765" class="Symbol">=</a> <a id="1767" href="Cubical.Foundations.Prelude.html#915" class="Function">refl</a>

  <a id="1775" class="Comment">-- add x y will only do x recursive calls</a>
  <a id="1819" href="Cubical.HITs.Cost.Base.html#1819" class="Function">_</a> <a id="1821" class="Symbol">:</a> <a id="1823" href="Cubical.HITs.Cost.Base.html#1588" class="Function">add</a> <a id="1827" class="Number">3</a> <a id="1829" class="Number">5</a> <a id="1831" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="1833" class="Symbol">(</a><a id="1834" class="Number">8</a> <a id="1836" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="1838" href="Cubical.HITs.PropositionalTruncation.Base.html#288" class="InductiveConstructor Operator">∣</a> <a id="1840" class="Number">3</a> <a id="1842" href="Cubical.HITs.PropositionalTruncation.Base.html#288" class="InductiveConstructor Operator">∣₁</a><a id="1844" class="Symbol">)</a>
  <a id="1848" class="Symbol">_</a> <a id="1850" class="Symbol">=</a> <a id="1852" href="Cubical.Foundations.Prelude.html#915" class="Function">refl</a>

<a id="1858" class="Comment">-- Despite addBad and add having different cost we can still prove</a>
<a id="1925" class="Comment">-- that they are equal functions</a>
<a id="add≡addBad"></a><a id="1958" href="Cubical.HITs.Cost.Base.html#1958" class="Function">add≡addBad</a> <a id="1969" class="Symbol">:</a> <a id="1971" href="Cubical.HITs.Cost.Base.html#1588" class="Function">add</a> <a id="1975" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="1977" href="Cubical.HITs.Cost.Base.html#1355" class="Function">addBad</a>
<a id="1984" href="Cubical.HITs.Cost.Base.html#1958" class="Function">add≡addBad</a> <a id="1995" href="Cubical.HITs.Cost.Base.html#1995" class="Bound">i</a> <a id="1997" href="Cubical.HITs.Cost.Base.html#1997" class="Bound">x</a> <a id="1999" href="Cubical.HITs.Cost.Base.html#1999" class="Bound">y</a> <a id="2001" class="Symbol">=</a> <a id="2003" href="Cubical.HITs.Cost.Base.html#432" class="Function">Cost≡</a> <a id="2009" class="Symbol">(</a><a id="2010" href="Cubical.HITs.Cost.Base.html#1588" class="Function">add</a> <a id="2014" href="Cubical.HITs.Cost.Base.html#1997" class="Bound">x</a> <a id="2016" href="Cubical.HITs.Cost.Base.html#1999" class="Bound">y</a><a id="2017" class="Symbol">)</a> <a id="2019" class="Symbol">(</a><a id="2020" href="Cubical.HITs.Cost.Base.html#1355" class="Function">addBad</a> <a id="2027" href="Cubical.HITs.Cost.Base.html#1997" class="Bound">x</a> <a id="2029" href="Cubical.HITs.Cost.Base.html#1999" class="Bound">y</a><a id="2030" class="Symbol">)</a> <a id="2032" class="Symbol">(</a><a id="2033" href="Cubical.HITs.Cost.Base.html#2054" class="Function">rem</a> <a id="2037" href="Cubical.HITs.Cost.Base.html#1997" class="Bound">x</a> <a id="2039" href="Cubical.HITs.Cost.Base.html#1999" class="Bound">y</a><a id="2040" class="Symbol">)</a> <a id="2042" href="Cubical.HITs.Cost.Base.html#1995" class="Bound">i</a>
  <a id="2046" class="Keyword">where</a>
  <a id="2054" href="Cubical.HITs.Cost.Base.html#2054" class="Function">rem</a> <a id="2058" class="Symbol">:</a> <a id="2060" class="Symbol">(</a><a id="2061" href="Cubical.HITs.Cost.Base.html#2061" class="Bound">x</a> <a id="2063" href="Cubical.HITs.Cost.Base.html#2063" class="Bound">y</a> <a id="2065" class="Symbol">:</a> <a id="2067" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a><a id="2068" class="Symbol">)</a> <a id="2070" class="Symbol">→</a> <a id="2072" href="Cubical.HITs.Cost.Base.html#1588" class="Function">add</a> <a id="2076" href="Cubical.HITs.Cost.Base.html#2061" class="Bound">x</a> <a id="2078" href="Cubical.HITs.Cost.Base.html#2063" class="Bound">y</a> <a id="2080" class="Symbol">.</a><a id="2081" href="Agda.Builtin.Sigma.html#234" class="Field">fst</a> <a id="2085" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="2087" href="Cubical.HITs.Cost.Base.html#1355" class="Function">addBad</a> <a id="2094" href="Cubical.HITs.Cost.Base.html#2061" class="Bound">x</a> <a id="2096" href="Cubical.HITs.Cost.Base.html#2063" class="Bound">y</a> <a id="2098" class="Symbol">.</a><a id="2099" href="Agda.Builtin.Sigma.html#234" class="Field">fst</a>
  <a id="2105" href="Cubical.HITs.Cost.Base.html#2054" class="Function">rem</a> <a id="2109" class="Number">0</a> <a id="2111" class="Number">0</a>       <a id="2119" class="Symbol">=</a> <a id="2121" href="Cubical.Foundations.Prelude.html#915" class="Function">refl</a>
  <a id="2128" href="Cubical.HITs.Cost.Base.html#2054" class="Function">rem</a> <a id="2132" class="Number">0</a> <a id="2134" class="Symbol">(</a><a id="2135" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="2139" href="Cubical.HITs.Cost.Base.html#2139" class="Bound">y</a><a id="2140" class="Symbol">)</a> <a id="2142" class="Symbol">=</a> <a id="2144" href="Cubical.Foundations.Prelude.html#1430" class="Function">cong</a> <a id="2149" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="2153" class="Symbol">(</a><a id="2154" href="Cubical.HITs.Cost.Base.html#2054" class="Function">rem</a> <a id="2158" class="Number">0</a> <a id="2160" href="Cubical.HITs.Cost.Base.html#2139" class="Bound">y</a><a id="2161" class="Symbol">)</a>
  <a id="2165" href="Cubical.HITs.Cost.Base.html#2054" class="Function">rem</a> <a id="2169" class="Symbol">(</a><a id="2170" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="2174" href="Cubical.HITs.Cost.Base.html#2174" class="Bound">x</a><a id="2175" class="Symbol">)</a> <a id="2177" href="Cubical.HITs.Cost.Base.html#2177" class="Bound">y</a> <a id="2179" class="Symbol">=</a> <a id="2181" href="Cubical.Foundations.Prelude.html#1430" class="Function">cong</a> <a id="2186" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="2190" class="Symbol">(</a><a id="2191" href="Cubical.HITs.Cost.Base.html#2054" class="Function">rem</a> <a id="2195" href="Cubical.HITs.Cost.Base.html#2174" class="Bound">x</a> <a id="2197" href="Cubical.HITs.Cost.Base.html#2177" class="Bound">y</a><a id="2198" class="Symbol">)</a>


<a id="2202" class="Comment">-- Another example: computing Fibonacci numbers</a>

<a id="fib"></a><a id="2251" href="Cubical.HITs.Cost.Base.html#2251" class="Function">fib</a> <a id="2255" class="Symbol">:</a> <a id="2257" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a> <a id="2259" class="Symbol">→</a> <a id="2261" href="Cubical.HITs.Cost.Base.html#305" class="Function">Cost</a> <a id="2266" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a>
<a id="2268" href="Cubical.HITs.Cost.Base.html#2251" class="Function">fib</a> <a id="2272" class="Number">0</a> <a id="2274" class="Symbol">=</a> <a id="2276" href="Cubical.HITs.Cost.Base.html#884" class="Function">return</a> <a id="2283" class="Number">0</a>
<a id="2285" href="Cubical.HITs.Cost.Base.html#2251" class="Function">fib</a> <a id="2289" class="Number">1</a> <a id="2291" class="Symbol">=</a> <a id="2293" href="Cubical.HITs.Cost.Base.html#884" class="Function">return</a> <a id="2300" class="Number">1</a>
<a id="2302" href="Cubical.HITs.Cost.Base.html#2251" class="Function">fib</a> <a id="2306" class="Symbol">(</a><a id="2307" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="2311" class="Symbol">(</a><a id="2312" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="2316" href="Cubical.HITs.Cost.Base.html#2316" class="Bound">x</a><a id="2317" class="Symbol">))</a> <a id="2320" class="Symbol">=</a> <a id="2322" class="Keyword">do</a>
  <a id="2327" href="Cubical.HITs.Cost.Base.html#2327" class="Bound">y</a> <a id="2329" href="Cubical.HITs.Cost.Base.html#776" class="Function Operator">←</a> <a id="2331" href="Cubical.HITs.Cost.Base.html#2251" class="Function">fib</a> <a id="2335" class="Symbol">(</a><a id="2336" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="2340" href="Cubical.HITs.Cost.Base.html#2316" class="Bound">x</a><a id="2341" class="Symbol">)</a>
  <a id="2345" href="Cubical.HITs.Cost.Base.html#2345" class="Bound">z</a> <a id="2347" href="Cubical.HITs.Cost.Base.html#776" class="Function Operator">←</a> <a id="2349" href="Cubical.HITs.Cost.Base.html#2251" class="Function">fib</a> <a id="2353" href="Cubical.HITs.Cost.Base.html#2316" class="Bound">x</a>
  <a id="2357" href="Cubical.HITs.Cost.Base.html#884" class="Function">return</a> <a id="2364" class="Symbol">(</a><a id="2365" href="Cubical.HITs.Cost.Base.html#2327" class="Bound">y</a> <a id="2367" href="Agda.Builtin.Nat.html#319" class="Primitive Operator">+</a> <a id="2369" href="Cubical.HITs.Cost.Base.html#2345" class="Bound">z</a><a id="2370" class="Symbol">)</a>

<a id="fibTail"></a><a id="2373" href="Cubical.HITs.Cost.Base.html#2373" class="Function">fibTail</a> <a id="2381" class="Symbol">:</a> <a id="2383" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a> <a id="2385" class="Symbol">→</a> <a id="2387" href="Cubical.HITs.Cost.Base.html#305" class="Function">Cost</a> <a id="2392" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a>
<a id="2394" href="Cubical.HITs.Cost.Base.html#2373" class="Function">fibTail</a> <a id="2402" class="Number">0</a>       <a id="2410" class="Symbol">=</a> <a id="2412" href="Cubical.HITs.Cost.Base.html#884" class="Function">return</a> <a id="2419" class="Number">0</a>
<a id="2421" href="Cubical.HITs.Cost.Base.html#2373" class="Function">fibTail</a> <a id="2429" class="Symbol">(</a><a id="2430" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="2434" href="Cubical.HITs.Cost.Base.html#2434" class="Bound">x</a><a id="2435" class="Symbol">)</a> <a id="2437" class="Symbol">=</a> <a id="2439" href="Cubical.HITs.Cost.Base.html#2462" class="Function">fibAux</a> <a id="2446" href="Cubical.HITs.Cost.Base.html#2434" class="Bound">x</a> <a id="2448" class="Number">1</a> <a id="2450" class="Number">0</a>
  <a id="2454" class="Keyword">where</a>
  <a id="2462" href="Cubical.HITs.Cost.Base.html#2462" class="Function">fibAux</a> <a id="2469" class="Symbol">:</a> <a id="2471" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a> <a id="2473" class="Symbol">→</a> <a id="2475" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a> <a id="2477" class="Symbol">→</a> <a id="2479" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a> <a id="2481" class="Symbol">→</a> <a id="2483" href="Cubical.HITs.Cost.Base.html#305" class="Function">Cost</a> <a id="2488" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a>
  <a id="2492" href="Cubical.HITs.Cost.Base.html#2462" class="Function">fibAux</a> <a id="2499" class="Number">0</a> <a id="2501" href="Cubical.HITs.Cost.Base.html#2501" class="Bound">res</a> <a id="2505" class="Symbol">_</a>          <a id="2516" class="Symbol">=</a> <a id="2518" href="Cubical.HITs.Cost.Base.html#884" class="Function">return</a> <a id="2525" href="Cubical.HITs.Cost.Base.html#2501" class="Bound">res</a>
  <a id="2531" href="Cubical.HITs.Cost.Base.html#2462" class="Function">fibAux</a> <a id="2538" class="Symbol">(</a><a id="2539" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="2543" href="Cubical.HITs.Cost.Base.html#2543" class="Bound">x</a><a id="2544" class="Symbol">)</a> <a id="2546" href="Cubical.HITs.Cost.Base.html#2546" class="Bound">res</a> <a id="2550" href="Cubical.HITs.Cost.Base.html#2550" class="Bound">prev</a> <a id="2555" class="Symbol">=</a> <a id="2557" class="Keyword">do</a>
    <a id="2564" href="Cubical.HITs.Cost.Base.html#2564" class="Bound">r</a> <a id="2566" href="Cubical.HITs.Cost.Base.html#776" class="Function Operator">←</a> <a id="2568" href="Cubical.HITs.Cost.Base.html#2462" class="Function">fibAux</a> <a id="2575" href="Cubical.HITs.Cost.Base.html#2543" class="Bound">x</a> <a id="2577" class="Symbol">(</a><a id="2578" href="Cubical.HITs.Cost.Base.html#2546" class="Bound">res</a> <a id="2582" href="Agda.Builtin.Nat.html#319" class="Primitive Operator">+</a> <a id="2584" href="Cubical.HITs.Cost.Base.html#2550" class="Bound">prev</a><a id="2588" class="Symbol">)</a> <a id="2590" href="Cubical.HITs.Cost.Base.html#2546" class="Bound">res</a>
    <a id="2598" href="Cubical.HITs.Cost.Base.html#884" class="Function">return</a> <a id="2605" href="Cubical.HITs.Cost.Base.html#2564" class="Bound">r</a>


<a id="2609" class="Keyword">private</a>
  <a id="2619" href="Cubical.HITs.Cost.Base.html#2619" class="Function">_</a> <a id="2621" class="Symbol">:</a> <a id="2623" href="Cubical.HITs.Cost.Base.html#2251" class="Function">fib</a> <a id="2627" class="Number">10</a> <a id="2630" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="2632" class="Symbol">(</a><a id="2633" class="Number">55</a> <a id="2636" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="2638" href="Cubical.HITs.PropositionalTruncation.Base.html#288" class="InductiveConstructor Operator">∣</a> <a id="2640" class="Number">176</a> <a id="2644" href="Cubical.HITs.PropositionalTruncation.Base.html#288" class="InductiveConstructor Operator">∣₁</a><a id="2646" class="Symbol">)</a>
  <a id="2650" class="Symbol">_</a> <a id="2652" class="Symbol">=</a> <a id="2654" href="Cubical.Foundations.Prelude.html#915" class="Function">refl</a>

  <a id="2662" href="Cubical.HITs.Cost.Base.html#2662" class="Function">_</a> <a id="2664" class="Symbol">:</a> <a id="2666" href="Cubical.HITs.Cost.Base.html#2373" class="Function">fibTail</a> <a id="2674" class="Number">10</a> <a id="2677" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="2679" class="Symbol">(</a><a id="2680" class="Number">55</a> <a id="2683" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="2685" href="Cubical.HITs.PropositionalTruncation.Base.html#288" class="InductiveConstructor Operator">∣</a> <a id="2687" class="Number">9</a> <a id="2689" href="Cubical.HITs.PropositionalTruncation.Base.html#288" class="InductiveConstructor Operator">∣₁</a><a id="2691" class="Symbol">)</a>
  <a id="2695" class="Symbol">_</a> <a id="2697" class="Symbol">=</a> <a id="2699" href="Cubical.Foundations.Prelude.html#915" class="Function">refl</a>

  <a id="2707" href="Cubical.HITs.Cost.Base.html#2707" class="Function">_</a> <a id="2709" class="Symbol">:</a> <a id="2711" href="Cubical.HITs.Cost.Base.html#2251" class="Function">fib</a> <a id="2715" class="Number">20</a> <a id="2718" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="2720" class="Symbol">(</a><a id="2721" class="Number">6765</a> <a id="2726" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="2728" href="Cubical.HITs.PropositionalTruncation.Base.html#288" class="InductiveConstructor Operator">∣</a> <a id="2730" class="Number">21890</a> <a id="2736" href="Cubical.HITs.PropositionalTruncation.Base.html#288" class="InductiveConstructor Operator">∣₁</a><a id="2738" class="Symbol">)</a>
  <a id="2742" class="Symbol">_</a> <a id="2744" class="Symbol">=</a> <a id="2746" href="Cubical.Foundations.Prelude.html#915" class="Function">refl</a>

  <a id="2754" href="Cubical.HITs.Cost.Base.html#2754" class="Function">_</a> <a id="2756" class="Symbol">:</a> <a id="2758" href="Cubical.HITs.Cost.Base.html#2373" class="Function">fibTail</a> <a id="2766" class="Number">20</a> <a id="2769" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="2771" class="Symbol">(</a><a id="2772" class="Number">6765</a> <a id="2777" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="2779" href="Cubical.HITs.PropositionalTruncation.Base.html#288" class="InductiveConstructor Operator">∣</a> <a id="2781" class="Number">19</a> <a id="2784" href="Cubical.HITs.PropositionalTruncation.Base.html#288" class="InductiveConstructor Operator">∣₁</a><a id="2786" class="Symbol">)</a>
  <a id="2790" class="Symbol">_</a> <a id="2792" class="Symbol">=</a> <a id="2794" href="Cubical.Foundations.Prelude.html#915" class="Function">refl</a>

<a id="2800" class="Comment">-- Exercise left to the reader: prove that fib and fibTail are equal functions</a>
<a id="2879" class="Comment">-- fib≡fibTail : fib ≡ fibTail</a>
<a id="2910" class="Comment">-- fib≡fibTail i x = Cost≡ (fib x) (fibTail x) (rem x) i</a>
<a id="2967" class="Comment">--   where</a>
<a id="2978" class="Comment">--   rem : (x : ℕ) → fib x .fst ≡ fibTail x .fst</a>
<a id="3027" class="Comment">--   rem zero = refl</a>
<a id="3048" class="Comment">--   rem (suc zero) = refl</a>
<a id="3075" class="Comment">--   rem (suc (suc x)) = {!!}</a>
</pre></body></html>