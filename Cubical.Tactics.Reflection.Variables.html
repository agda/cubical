<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Cubical.Tactics.Reflection.Variables</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">{-
  This code contains some helper functions for solvers.
  Variables in the sense of this files are things that are treated like variables by a solver.
  A ring solver might want to treat &quot;f x&quot; in an equation &quot;f x + 0 ≡ f x&quot; like a variable &quot;y&quot;.
  During the inspection of the lhs and rhs of an equation, terms like &quot;f x&quot; are found and saved
  and later, indices are assigned to them. These indices will be the indices of the variables
  in the normal forms the solver uses.
-}</a>
<a id="481" class="Keyword">module</a> <a id="488" href="Cubical.Tactics.Reflection.Variables.html" class="Module">Cubical.Tactics.Reflection.Variables</a> <a id="525" class="Keyword">where</a>

<a id="532" class="Keyword">open</a> <a id="537" class="Keyword">import</a> <a id="544" href="Cubical.Foundations.Prelude.html" class="Module">Cubical.Foundations.Prelude</a> <a id="572" class="Keyword">hiding</a> <a id="579" class="Symbol">(</a><a id="580" href="Agda.Primitive.html#388" class="Primitive">Type</a><a id="584" class="Symbol">)</a>

<a id="587" class="Keyword">open</a> <a id="592" class="Keyword">import</a> <a id="599" href="Agda.Builtin.Reflection.html" class="Module">Agda.Builtin.Reflection</a> <a id="623" class="Keyword">hiding</a> <a id="630" class="Symbol">(</a><a id="631" href="Agda.Builtin.Reflection.html#5064" class="Function">Type</a><a id="635" class="Symbol">)</a>
<a id="637" class="Keyword">open</a> <a id="642" class="Keyword">import</a> <a id="649" href="Agda.Builtin.String.html" class="Module">Agda.Builtin.String</a>
<a id="669" class="Keyword">open</a> <a id="674" class="Keyword">import</a> <a id="681" href="Agda.Builtin.Float.html" class="Module">Agda.Builtin.Float</a>
<a id="700" class="Keyword">open</a> <a id="705" class="Keyword">import</a> <a id="712" href="Agda.Builtin.Word.html" class="Module">Agda.Builtin.Word</a>
<a id="730" class="Keyword">open</a> <a id="735" class="Keyword">import</a> <a id="742" href="Agda.Builtin.Char.html" class="Module">Agda.Builtin.Char</a>
<a id="760" class="Keyword">open</a> <a id="765" class="Keyword">import</a> <a id="772" href="Agda.Builtin.Nat.html" class="Module">Agda.Builtin.Nat</a> <a id="789" class="Keyword">using</a> <a id="795" class="Symbol">()</a> <a id="798" class="Keyword">renaming</a> <a id="807" class="Symbol">(</a><a id="808" href="Agda.Builtin.Nat.html#631" class="Primitive Operator">_==_</a> <a id="813" class="Symbol">to</a> <a id="816" class="Primitive Operator">_=ℕ_</a><a id="820" class="Symbol">)</a>

<a id="823" class="Keyword">open</a> <a id="828" class="Keyword">import</a> <a id="835" href="Cubical.Reflection.Base.html" class="Module">Cubical.Reflection.Base</a>
<a id="859" class="Keyword">open</a> <a id="864" class="Keyword">import</a> <a id="871" href="Cubical.Data.Bool.html" class="Module">Cubical.Data.Bool</a>
<a id="889" class="Keyword">open</a> <a id="894" class="Keyword">import</a> <a id="901" href="Cubical.Data.List.html" class="Module">Cubical.Data.List</a>
<a id="919" class="Keyword">open</a> <a id="924" class="Keyword">import</a> <a id="931" href="Cubical.Data.Maybe.html" class="Module">Cubical.Data.Maybe</a>
<a id="950" class="Keyword">open</a> <a id="955" class="Keyword">import</a> <a id="962" href="Cubical.Data.Nat.html" class="Module">Cubical.Data.Nat</a> <a id="979" class="Keyword">using</a> <a id="985" class="Symbol">(</a><a id="986" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a><a id="987" class="Symbol">)</a>

<a id="990" class="Keyword">open</a> <a id="995" class="Keyword">import</a> <a id="1002" href="Cubical.Tactics.Reflection.html" class="Module">Cubical.Tactics.Reflection</a>

<a id="1030" class="Keyword">private</a>
  <a id="1040" class="Keyword">variable</a>
    <a id="1053" href="Cubical.Tactics.Reflection.Variables.html#1053" class="Generalizable">ℓ</a> <a id="1055" class="Symbol">:</a> <a id="1057" href="Agda.Primitive.html#742" class="Postulate">Level</a>


<a id="Vars"></a><a id="1065" href="Cubical.Tactics.Reflection.Variables.html#1065" class="Function">Vars</a> <a id="1070" class="Symbol">=</a> <a id="1072" href="Agda.Builtin.List.html#147" class="Datatype">List</a> <a id="1077" href="Agda.Builtin.Reflection.html#4993" class="Datatype">Term</a>
<a id="VarAss"></a><a id="1082" href="Cubical.Tactics.Reflection.Variables.html#1082" class="Function">VarAss</a> <a id="1089" class="Symbol">=</a> <a id="1091" href="Agda.Builtin.Reflection.html#4993" class="Datatype">Term</a> <a id="1096" class="Symbol">→</a> <a id="1098" href="Cubical.Data.Maybe.Base.html#157" class="Datatype">Maybe</a> <a id="1104" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a>
<a id="Template"></a><a id="1106" href="Cubical.Tactics.Reflection.Variables.html#1106" class="Function">Template</a> <a id="1115" class="Symbol">=</a> <a id="1117" href="Cubical.Tactics.Reflection.Variables.html#1082" class="Function">VarAss</a> <a id="1124" class="Symbol">→</a> <a id="1126" href="Agda.Builtin.Reflection.html#4993" class="Datatype">Term</a>

<a id="1132" class="Keyword">private</a>
  <a id="_=N_"></a><a id="1142" href="Cubical.Tactics.Reflection.Variables.html#1142" class="Function Operator">_=N_</a> <a id="1147" class="Symbol">=</a> <a id="1149" href="Agda.Builtin.Reflection.html#539" class="Primitive">primQNameEquality</a>
  <a id="_=M_"></a><a id="1169" href="Cubical.Tactics.Reflection.Variables.html#1169" class="Function Operator">_=M_</a> <a id="1174" class="Symbol">=</a> <a id="1176" href="Agda.Builtin.Reflection.html#2511" class="Primitive">primMetaEquality</a>

  <a id="_=L_"></a><a id="1196" href="Cubical.Tactics.Reflection.Variables.html#1196" class="Function Operator">_=L_</a> <a id="1201" class="Symbol">:</a> <a id="1203" href="Agda.Builtin.Reflection.html#4397" class="Datatype">Literal</a> <a id="1211" class="Symbol">→</a> <a id="1213" href="Agda.Builtin.Reflection.html#4397" class="Datatype">Literal</a> <a id="1221" class="Symbol">→</a> <a id="1223" href="Agda.Builtin.Bool.html#173" class="Datatype">Bool</a>
  <a id="1230" href="Agda.Builtin.Reflection.html#4419" class="InductiveConstructor">nat</a> <a id="1234" href="Cubical.Tactics.Reflection.Variables.html#1234" class="Bound">n</a> <a id="1236" href="Cubical.Tactics.Reflection.Variables.html#1196" class="Function Operator">=L</a> <a id="1239" href="Agda.Builtin.Reflection.html#4419" class="InductiveConstructor">nat</a> <a id="1243" href="Cubical.Tactics.Reflection.Variables.html#1243" class="Bound">m</a> <a id="1245" class="Symbol">=</a> <a id="1247" href="Cubical.Tactics.Reflection.Variables.html#1234" class="Bound">n</a> <a id="1249" href="Cubical.Tactics.Reflection.Variables.html#816" class="Primitive Operator">=ℕ</a> <a id="1252" href="Cubical.Tactics.Reflection.Variables.html#1243" class="Bound">m</a>
  <a id="1256" href="Agda.Builtin.Reflection.html#4453" class="InductiveConstructor">word64</a> <a id="1263" href="Cubical.Tactics.Reflection.Variables.html#1263" class="Bound">n</a> <a id="1265" href="Cubical.Tactics.Reflection.Variables.html#1196" class="Function Operator">=L</a> <a id="1268" href="Agda.Builtin.Reflection.html#4453" class="InductiveConstructor">word64</a> <a id="1275" href="Cubical.Tactics.Reflection.Variables.html#1275" class="Bound">m</a> <a id="1277" class="Symbol">=</a> <a id="1279" href="Agda.Builtin.Word.html#264" class="Primitive">primWord64ToNat</a> <a id="1295" href="Cubical.Tactics.Reflection.Variables.html#1263" class="Bound">n</a> <a id="1297" href="Cubical.Tactics.Reflection.Variables.html#816" class="Primitive Operator">=ℕ</a> <a id="1300" href="Agda.Builtin.Word.html#264" class="Primitive">primWord64ToNat</a> <a id="1316" href="Cubical.Tactics.Reflection.Variables.html#1275" class="Bound">m</a>
  <a id="1320" href="Agda.Builtin.Reflection.html#4487" class="InductiveConstructor">float</a> <a id="1326" href="Cubical.Tactics.Reflection.Variables.html#1326" class="Bound">x</a> <a id="1328" href="Cubical.Tactics.Reflection.Variables.html#1196" class="Function Operator">=L</a> <a id="1331" href="Agda.Builtin.Reflection.html#4487" class="InductiveConstructor">float</a> <a id="1337" href="Cubical.Tactics.Reflection.Variables.html#1337" class="Bound">y</a> <a id="1339" class="Symbol">=</a> <a id="1341" href="Agda.Builtin.Float.html#473" class="Primitive">primFloatEquality</a> <a id="1359" href="Cubical.Tactics.Reflection.Variables.html#1326" class="Bound">x</a> <a id="1361" href="Cubical.Tactics.Reflection.Variables.html#1337" class="Bound">y</a>
  <a id="1365" href="Agda.Builtin.Reflection.html#4521" class="InductiveConstructor">char</a> <a id="1370" href="Cubical.Tactics.Reflection.Variables.html#1370" class="Bound">c</a> <a id="1372" href="Cubical.Tactics.Reflection.Variables.html#1196" class="Function Operator">=L</a> <a id="1375" href="Agda.Builtin.Reflection.html#4521" class="InductiveConstructor">char</a> <a id="1380" href="Cubical.Tactics.Reflection.Variables.html#1380" class="Bound">c&#39;</a> <a id="1383" class="Symbol">=</a> <a id="1385" href="Agda.Builtin.Char.html#506" class="Primitive">primCharEquality</a> <a id="1402" href="Cubical.Tactics.Reflection.Variables.html#1370" class="Bound">c</a> <a id="1404" href="Cubical.Tactics.Reflection.Variables.html#1380" class="Bound">c&#39;</a>
  <a id="1409" href="Agda.Builtin.Reflection.html#4555" class="InductiveConstructor">string</a> <a id="1416" href="Cubical.Tactics.Reflection.Variables.html#1416" class="Bound">s</a> <a id="1418" href="Cubical.Tactics.Reflection.Variables.html#1196" class="Function Operator">=L</a> <a id="1421" href="Agda.Builtin.Reflection.html#4555" class="InductiveConstructor">string</a> <a id="1428" href="Cubical.Tactics.Reflection.Variables.html#1428" class="Bound">s&#39;</a> <a id="1431" class="Symbol">=</a> <a id="1433" href="Agda.Builtin.String.html#585" class="Primitive">primStringEquality</a> <a id="1452" href="Cubical.Tactics.Reflection.Variables.html#1416" class="Bound">s</a> <a id="1454" href="Cubical.Tactics.Reflection.Variables.html#1428" class="Bound">s&#39;</a>
  <a id="1459" href="Agda.Builtin.Reflection.html#4589" class="InductiveConstructor">name</a> <a id="1464" href="Cubical.Tactics.Reflection.Variables.html#1464" class="Bound">x</a> <a id="1466" href="Cubical.Tactics.Reflection.Variables.html#1196" class="Function Operator">=L</a> <a id="1469" href="Agda.Builtin.Reflection.html#4589" class="InductiveConstructor">name</a> <a id="1474" href="Cubical.Tactics.Reflection.Variables.html#1474" class="Bound">y</a> <a id="1476" class="Symbol">=</a> <a id="1478" href="Cubical.Tactics.Reflection.Variables.html#1464" class="Bound">x</a> <a id="1480" href="Cubical.Tactics.Reflection.Variables.html#1142" class="Function Operator">=N</a> <a id="1483" href="Cubical.Tactics.Reflection.Variables.html#1474" class="Bound">y</a>
  <a id="1487" href="Agda.Builtin.Reflection.html#4623" class="InductiveConstructor">meta</a> <a id="1492" href="Cubical.Tactics.Reflection.Variables.html#1492" class="Bound">x</a> <a id="1494" href="Cubical.Tactics.Reflection.Variables.html#1196" class="Function Operator">=L</a> <a id="1497" href="Agda.Builtin.Reflection.html#4623" class="InductiveConstructor">meta</a> <a id="1502" href="Cubical.Tactics.Reflection.Variables.html#1502" class="Bound">y</a> <a id="1504" class="Symbol">=</a> <a id="1506" href="Cubical.Tactics.Reflection.Variables.html#1492" class="Bound">x</a> <a id="1508" href="Cubical.Tactics.Reflection.Variables.html#1169" class="Function Operator">=M</a> <a id="1511" href="Cubical.Tactics.Reflection.Variables.html#1502" class="Bound">y</a>
  <a id="1515" class="CatchallClause Symbol">_</a><a id="1516" class="CatchallClause"> </a><a id="1517" href="Cubical.Tactics.Reflection.Variables.html#1196" class="CatchallClause Function Operator">=L</a><a id="1519" class="CatchallClause"> </a><a id="1520" class="CatchallClause Symbol">_</a> <a id="1522" class="Symbol">=</a> <a id="1524" href="Agda.Builtin.Bool.html#192" class="InductiveConstructor">false</a>

  <a id="compareVargs"></a><a id="1533" href="Cubical.Tactics.Reflection.Variables.html#1533" class="Function">compareVargs</a> <a id="1546" class="Symbol">:</a> <a id="1548" href="Agda.Builtin.List.html#147" class="Datatype">List</a> <a id="1553" class="Symbol">(</a><a id="1554" href="Agda.Builtin.Reflection.html#3696" class="Datatype">Arg</a> <a id="1558" href="Agda.Builtin.Reflection.html#4993" class="Datatype">Term</a><a id="1562" class="Symbol">)</a> <a id="1564" class="Symbol">→</a> <a id="1566" href="Agda.Builtin.List.html#147" class="Datatype">List</a> <a id="1571" class="Symbol">(</a><a id="1572" href="Agda.Builtin.Reflection.html#3696" class="Datatype">Arg</a> <a id="1576" href="Agda.Builtin.Reflection.html#4993" class="Datatype">Term</a><a id="1580" class="Symbol">)</a> <a id="1582" class="Symbol">→</a> <a id="1584" href="Agda.Builtin.Bool.html#173" class="Datatype">Bool</a>

  <a id="_=T_"></a><a id="1592" href="Cubical.Tactics.Reflection.Variables.html#1592" class="Function Operator">_=T_</a> <a id="1597" class="Symbol">:</a> <a id="1599" href="Agda.Builtin.Reflection.html#4993" class="Datatype">Term</a> <a id="1604" class="Symbol">→</a> <a id="1606" href="Agda.Builtin.Reflection.html#4993" class="Datatype">Term</a> <a id="1611" class="Symbol">→</a> <a id="1613" href="Agda.Builtin.Bool.html#173" class="Datatype">Bool</a>  <a id="1619" class="Comment">-- this should be a TC, since it should error out sometimes</a>
  <a id="1681" href="Agda.Builtin.Reflection.html#5138" class="InductiveConstructor">var</a> <a id="1685" href="Cubical.Tactics.Reflection.Variables.html#1685" class="Bound">index</a> <a id="1691" href="Cubical.Tactics.Reflection.Variables.html#1691" class="Bound">args</a> <a id="1696" href="Cubical.Tactics.Reflection.Variables.html#1592" class="Function Operator">=T</a> <a id="1699" href="Agda.Builtin.Reflection.html#5138" class="InductiveConstructor">var</a> <a id="1703" href="Cubical.Tactics.Reflection.Variables.html#1703" class="Bound">index&#39;</a> <a id="1710" href="Cubical.Tactics.Reflection.Variables.html#1710" class="Bound">args&#39;</a> <a id="1716" class="Symbol">=</a> <a id="1718" class="Symbol">(</a><a id="1719" href="Cubical.Tactics.Reflection.Variables.html#1685" class="Bound">index</a> <a id="1725" href="Cubical.Tactics.Reflection.Variables.html#816" class="Primitive Operator">=ℕ</a> <a id="1728" href="Cubical.Tactics.Reflection.Variables.html#1703" class="Bound">index&#39;</a><a id="1734" class="Symbol">)</a> <a id="1736" href="Cubical.Data.Bool.Base.html#499" class="Function Operator">and</a> <a id="1740" href="Cubical.Tactics.Reflection.Variables.html#1533" class="Function">compareVargs</a> <a id="1753" href="Cubical.Tactics.Reflection.Variables.html#1691" class="Bound">args</a> <a id="1758" href="Cubical.Tactics.Reflection.Variables.html#1710" class="Bound">args&#39;</a>
  <a id="1766" href="Agda.Builtin.Reflection.html#5194" class="InductiveConstructor">con</a> <a id="1770" href="Cubical.Tactics.Reflection.Variables.html#1770" class="Bound">c</a> <a id="1772" href="Cubical.Tactics.Reflection.Variables.html#1772" class="Bound">args</a> <a id="1777" href="Cubical.Tactics.Reflection.Variables.html#1592" class="Function Operator">=T</a> <a id="1780" href="Agda.Builtin.Reflection.html#5194" class="InductiveConstructor">con</a> <a id="1784" href="Cubical.Tactics.Reflection.Variables.html#1784" class="Bound">c&#39;</a> <a id="1787" href="Cubical.Tactics.Reflection.Variables.html#1787" class="Bound">args&#39;</a>   <a id="1795" class="Symbol">=</a> <a id="1797" class="Symbol">(</a><a id="1798" href="Cubical.Tactics.Reflection.Variables.html#1770" class="Bound">c</a> <a id="1800" href="Cubical.Tactics.Reflection.Variables.html#1142" class="Function Operator">=N</a> <a id="1803" href="Cubical.Tactics.Reflection.Variables.html#1784" class="Bound">c&#39;</a><a id="1805" class="Symbol">)</a> <a id="1807" href="Cubical.Data.Bool.Base.html#499" class="Function Operator">and</a> <a id="1811" href="Cubical.Tactics.Reflection.Variables.html#1533" class="Function">compareVargs</a> <a id="1824" href="Cubical.Tactics.Reflection.Variables.html#1772" class="Bound">args</a> <a id="1829" href="Cubical.Tactics.Reflection.Variables.html#1787" class="Bound">args&#39;</a>
  <a id="1837" href="Agda.Builtin.Reflection.html#5251" class="InductiveConstructor">def</a> <a id="1841" href="Cubical.Tactics.Reflection.Variables.html#1841" class="Bound">f</a> <a id="1843" href="Cubical.Tactics.Reflection.Variables.html#1843" class="Bound">args</a> <a id="1848" href="Cubical.Tactics.Reflection.Variables.html#1592" class="Function Operator">=T</a> <a id="1851" href="Agda.Builtin.Reflection.html#5251" class="InductiveConstructor">def</a> <a id="1855" href="Cubical.Tactics.Reflection.Variables.html#1855" class="Bound">f&#39;</a> <a id="1858" href="Cubical.Tactics.Reflection.Variables.html#1858" class="Bound">args&#39;</a>   <a id="1866" class="Symbol">=</a> <a id="1868" class="Symbol">(</a><a id="1869" href="Cubical.Tactics.Reflection.Variables.html#1841" class="Bound">f</a> <a id="1871" href="Cubical.Tactics.Reflection.Variables.html#1142" class="Function Operator">=N</a> <a id="1874" href="Cubical.Tactics.Reflection.Variables.html#1855" class="Bound">f&#39;</a><a id="1876" class="Symbol">)</a> <a id="1878" href="Cubical.Data.Bool.Base.html#499" class="Function Operator">and</a> <a id="1882" href="Cubical.Tactics.Reflection.Variables.html#1533" class="Function">compareVargs</a> <a id="1895" href="Cubical.Tactics.Reflection.Variables.html#1843" class="Bound">args</a> <a id="1900" href="Cubical.Tactics.Reflection.Variables.html#1858" class="Bound">args&#39;</a>
  <a id="1908" href="Agda.Builtin.Reflection.html#5509" class="InductiveConstructor">lit</a> <a id="1912" href="Cubical.Tactics.Reflection.Variables.html#1912" class="Bound">l</a> <a id="1914" href="Cubical.Tactics.Reflection.Variables.html#1592" class="Function Operator">=T</a> <a id="1917" href="Agda.Builtin.Reflection.html#5509" class="InductiveConstructor">lit</a> <a id="1921" href="Cubical.Tactics.Reflection.Variables.html#1921" class="Bound">l&#39;</a>              <a id="1937" class="Symbol">=</a> <a id="1939" href="Cubical.Tactics.Reflection.Variables.html#1912" class="Bound">l</a> <a id="1941" href="Cubical.Tactics.Reflection.Variables.html#1196" class="Function Operator">=L</a> <a id="1944" href="Cubical.Tactics.Reflection.Variables.html#1921" class="Bound">l&#39;</a>
  <a id="1949" href="Agda.Builtin.Reflection.html#5544" class="InductiveConstructor">meta</a> <a id="1954" href="Cubical.Tactics.Reflection.Variables.html#1954" class="Bound">x</a> <a id="1956" href="Cubical.Tactics.Reflection.Variables.html#1956" class="Bound">args</a> <a id="1961" href="Cubical.Tactics.Reflection.Variables.html#1592" class="Function Operator">=T</a> <a id="1964" href="Agda.Builtin.Reflection.html#5544" class="InductiveConstructor">meta</a> <a id="1969" href="Cubical.Tactics.Reflection.Variables.html#1969" class="Bound">x&#39;</a> <a id="1972" href="Cubical.Tactics.Reflection.Variables.html#1972" class="Bound">args&#39;</a> <a id="1978" class="Symbol">=</a> <a id="1980" class="Symbol">(</a><a id="1981" href="Cubical.Tactics.Reflection.Variables.html#1954" class="Bound">x</a> <a id="1983" href="Cubical.Tactics.Reflection.Variables.html#1169" class="Function Operator">=M</a> <a id="1986" href="Cubical.Tactics.Reflection.Variables.html#1969" class="Bound">x&#39;</a><a id="1988" class="Symbol">)</a> <a id="1990" href="Cubical.Data.Bool.Base.html#499" class="Function Operator">and</a> <a id="1994" href="Cubical.Tactics.Reflection.Variables.html#1533" class="Function">compareVargs</a> <a id="2007" href="Cubical.Tactics.Reflection.Variables.html#1956" class="Bound">args</a> <a id="2012" href="Cubical.Tactics.Reflection.Variables.html#1972" class="Bound">args&#39;</a>
  <a id="2020" class="CatchallClause Symbol">_</a><a id="2021" class="CatchallClause"> </a><a id="2022" href="Cubical.Tactics.Reflection.Variables.html#1592" class="CatchallClause Function Operator">=T</a><a id="2024" class="CatchallClause"> </a><a id="2025" class="CatchallClause Symbol">_</a>                       <a id="2049" class="Symbol">=</a> <a id="2051" href="Agda.Builtin.Bool.html#192" class="InductiveConstructor">false</a>  <a id="2058" class="Comment">-- this should be fixed</a>

<a id="2083" href="Cubical.Tactics.Reflection.Variables.html#1533" class="Function">compareVargs</a> <a id="2096" href="Agda.Builtin.List.html#184" class="InductiveConstructor">[]</a> <a id="2099" href="Agda.Builtin.List.html#184" class="InductiveConstructor">[]</a> <a id="2102" class="Symbol">=</a> <a id="2104" href="Agda.Builtin.Bool.html#198" class="InductiveConstructor">true</a>
<a id="2109" href="Cubical.Tactics.Reflection.Variables.html#1533" class="Function">compareVargs</a> <a id="2122" class="Symbol">(</a><a id="2123" href="Cubical.Tactics.Reflection.Variables.html#2123" class="Bound">x</a> <a id="2125" href="Cubical.Reflection.Base.html#777" class="InductiveConstructor Operator">v∷</a> <a id="2128" href="Cubical.Tactics.Reflection.Variables.html#2128" class="Bound">l</a><a id="2129" class="Symbol">)</a> <a id="2131" class="Symbol">(</a><a id="2132" href="Cubical.Tactics.Reflection.Variables.html#2132" class="Bound">x&#39;</a> <a id="2135" href="Cubical.Reflection.Base.html#777" class="InductiveConstructor Operator">v∷</a> <a id="2138" href="Cubical.Tactics.Reflection.Variables.html#2138" class="Bound">l&#39;</a><a id="2140" class="Symbol">)</a> <a id="2142" class="Symbol">=</a> <a id="2144" class="Symbol">(</a><a id="2145" href="Cubical.Tactics.Reflection.Variables.html#2123" class="Bound">x</a> <a id="2147" href="Cubical.Tactics.Reflection.Variables.html#1592" class="Function Operator">=T</a> <a id="2150" href="Cubical.Tactics.Reflection.Variables.html#2132" class="Bound">x&#39;</a><a id="2152" class="Symbol">)</a> <a id="2154" href="Cubical.Data.Bool.Base.html#499" class="Function Operator">and</a> <a id="2158" href="Cubical.Tactics.Reflection.Variables.html#1533" class="Function">compareVargs</a> <a id="2171" href="Cubical.Tactics.Reflection.Variables.html#2128" class="Bound">l</a> <a id="2173" href="Cubical.Tactics.Reflection.Variables.html#2138" class="Bound">l&#39;</a>
<a id="2176" href="Cubical.Tactics.Reflection.Variables.html#1533" class="Function">compareVargs</a> <a id="2189" class="Symbol">(_</a> <a id="2192" href="Cubical.Reflection.Base.html#807" class="InductiveConstructor Operator">h∷</a> <a id="2195" href="Cubical.Tactics.Reflection.Variables.html#2195" class="Bound">l</a><a id="2196" class="Symbol">)</a> <a id="2198" class="Symbol">(_</a> <a id="2201" href="Cubical.Reflection.Base.html#807" class="InductiveConstructor Operator">h∷</a> <a id="2204" href="Cubical.Tactics.Reflection.Variables.html#2204" class="Bound">l&#39;</a><a id="2206" class="Symbol">)</a> <a id="2208" class="Symbol">=</a> <a id="2210" href="Cubical.Tactics.Reflection.Variables.html#1533" class="Function">compareVargs</a> <a id="2223" href="Cubical.Tactics.Reflection.Variables.html#2195" class="Bound">l</a> <a id="2225" href="Cubical.Tactics.Reflection.Variables.html#2204" class="Bound">l&#39;</a> <a id="2228" class="Comment">-- ignore hargs, not sure this is good</a>
<a id="2267" href="Cubical.Tactics.Reflection.Variables.html#1533" class="CatchallClause Function">compareVargs</a><a id="2279" class="CatchallClause"> </a><a id="2280" class="CatchallClause Symbol">_</a><a id="2281" class="CatchallClause"> </a><a id="2282" class="CatchallClause Symbol">_</a> <a id="2284" class="Symbol">=</a> <a id="2286" href="Agda.Builtin.Bool.html#192" class="InductiveConstructor">false</a>

<a id="addWithoutRepetition"></a><a id="2293" href="Cubical.Tactics.Reflection.Variables.html#2293" class="Function">addWithoutRepetition</a> <a id="2314" class="Symbol">:</a> <a id="2316" href="Agda.Builtin.Reflection.html#4993" class="Datatype">Term</a> <a id="2321" class="Symbol">→</a> <a id="2323" href="Cubical.Tactics.Reflection.Variables.html#1065" class="Function">Vars</a> <a id="2328" class="Symbol">→</a> <a id="2330" href="Cubical.Tactics.Reflection.Variables.html#1065" class="Function">Vars</a>
<a id="2335" href="Cubical.Tactics.Reflection.Variables.html#2293" class="Function">addWithoutRepetition</a> <a id="2356" href="Cubical.Tactics.Reflection.Variables.html#2356" class="Bound">t</a> <a id="2358" href="Cubical.Tactics.Reflection.Variables.html#2358" class="Bound">l</a><a id="2359" class="Symbol">@(</a><a id="2361" href="Cubical.Tactics.Reflection.Variables.html#2361" class="Bound">t&#39;</a> <a id="2364" href="Agda.Builtin.List.html#199" class="InductiveConstructor Operator">∷</a> <a id="2366" href="Cubical.Tactics.Reflection.Variables.html#2366" class="Bound">l&#39;</a><a id="2368" class="Symbol">)</a> <a id="2370" class="Symbol">=</a> <a id="2372" href="Cubical.Data.Bool.Base.html#645" class="Function Operator">if</a> <a id="2375" class="Symbol">(</a><a id="2376" href="Cubical.Tactics.Reflection.Variables.html#2356" class="Bound">t</a> <a id="2378" href="Cubical.Tactics.Reflection.Variables.html#1592" class="Function Operator">=T</a> <a id="2381" href="Cubical.Tactics.Reflection.Variables.html#2361" class="Bound">t&#39;</a><a id="2383" class="Symbol">)</a> <a id="2385" href="Cubical.Data.Bool.Base.html#645" class="Function Operator">then</a> <a id="2390" href="Cubical.Tactics.Reflection.Variables.html#2358" class="Bound">l</a> <a id="2392" href="Cubical.Data.Bool.Base.html#645" class="Function Operator">else</a> <a id="2397" href="Cubical.Tactics.Reflection.Variables.html#2361" class="Bound">t&#39;</a> <a id="2400" href="Agda.Builtin.List.html#199" class="InductiveConstructor Operator">∷</a> <a id="2402" href="Cubical.Tactics.Reflection.Variables.html#2293" class="Function">addWithoutRepetition</a> <a id="2423" href="Cubical.Tactics.Reflection.Variables.html#2356" class="Bound">t</a> <a id="2425" href="Cubical.Tactics.Reflection.Variables.html#2366" class="Bound">l&#39;</a>
<a id="2428" href="Cubical.Tactics.Reflection.Variables.html#2293" class="Function">addWithoutRepetition</a> <a id="2449" href="Cubical.Tactics.Reflection.Variables.html#2449" class="Bound">t</a> <a id="2451" href="Agda.Builtin.List.html#184" class="InductiveConstructor">[]</a>      <a id="2459" class="Symbol">=</a> <a id="2461" href="Cubical.Tactics.Reflection.Variables.html#2449" class="Bound">t</a> <a id="2463" href="Agda.Builtin.List.html#199" class="InductiveConstructor Operator">∷</a> <a id="2465" href="Agda.Builtin.List.html#184" class="InductiveConstructor">[]</a>

<a id="appendWithoutRepetition"></a><a id="2469" href="Cubical.Tactics.Reflection.Variables.html#2469" class="Function">appendWithoutRepetition</a> <a id="2493" class="Symbol">:</a> <a id="2495" href="Cubical.Tactics.Reflection.Variables.html#1065" class="Function">Vars</a> <a id="2500" class="Symbol">→</a> <a id="2502" href="Cubical.Tactics.Reflection.Variables.html#1065" class="Function">Vars</a> <a id="2507" class="Symbol">→</a> <a id="2509" href="Cubical.Tactics.Reflection.Variables.html#1065" class="Function">Vars</a>
<a id="2514" href="Cubical.Tactics.Reflection.Variables.html#2469" class="Function">appendWithoutRepetition</a> <a id="2538" class="Symbol">(</a><a id="2539" href="Cubical.Tactics.Reflection.Variables.html#2539" class="Bound">x</a> <a id="2541" href="Agda.Builtin.List.html#199" class="InductiveConstructor Operator">∷</a> <a id="2543" href="Cubical.Tactics.Reflection.Variables.html#2543" class="Bound">l</a><a id="2544" class="Symbol">)</a> <a id="2546" href="Cubical.Tactics.Reflection.Variables.html#2546" class="Bound">l&#39;</a> <a id="2549" class="Symbol">=</a> <a id="2551" href="Cubical.Tactics.Reflection.Variables.html#2469" class="Function">appendWithoutRepetition</a> <a id="2575" href="Cubical.Tactics.Reflection.Variables.html#2543" class="Bound">l</a> <a id="2577" class="Symbol">(</a><a id="2578" href="Cubical.Tactics.Reflection.Variables.html#2293" class="Function">addWithoutRepetition</a> <a id="2599" href="Cubical.Tactics.Reflection.Variables.html#2539" class="Bound">x</a> <a id="2601" href="Cubical.Tactics.Reflection.Variables.html#2546" class="Bound">l&#39;</a><a id="2603" class="Symbol">)</a>
<a id="2605" href="Cubical.Tactics.Reflection.Variables.html#2469" class="Function">appendWithoutRepetition</a> <a id="2629" href="Agda.Builtin.List.html#184" class="InductiveConstructor">[]</a> <a id="2632" href="Cubical.Tactics.Reflection.Variables.html#2632" class="Bound">l&#39;</a> <a id="2635" class="Symbol">=</a> <a id="2637" href="Cubical.Tactics.Reflection.Variables.html#2632" class="Bound">l&#39;</a>

<a id="2641" class="Comment">-- this can be used to get a map from variables to numbers 0,...,n</a>
<a id="indexOf"></a><a id="2708" href="Cubical.Tactics.Reflection.Variables.html#2708" class="Function">indexOf</a> <a id="2716" class="Symbol">:</a> <a id="2718" href="Agda.Builtin.Reflection.html#4993" class="Datatype">Term</a> <a id="2723" class="Symbol">→</a> <a id="2725" href="Cubical.Tactics.Reflection.Variables.html#1065" class="Function">Vars</a> <a id="2730" class="Symbol">→</a> <a id="2732" href="Cubical.Data.Maybe.Base.html#157" class="Datatype">Maybe</a> <a id="2738" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a>
<a id="2740" href="Cubical.Tactics.Reflection.Variables.html#2708" class="Function">indexOf</a> <a id="2748" href="Cubical.Tactics.Reflection.Variables.html#2748" class="Bound">t</a> <a id="2750" class="Symbol">(</a><a id="2751" href="Cubical.Tactics.Reflection.Variables.html#2751" class="Bound">t&#39;</a> <a id="2754" href="Agda.Builtin.List.html#199" class="InductiveConstructor Operator">∷</a> <a id="2756" href="Cubical.Tactics.Reflection.Variables.html#2756" class="Bound">l</a><a id="2757" class="Symbol">)</a> <a id="2759" class="Symbol">=</a>
  <a id="2763" href="Cubical.Data.Bool.Base.html#645" class="Function Operator">if</a> <a id="2766" class="Symbol">(</a><a id="2767" href="Cubical.Tactics.Reflection.Variables.html#2748" class="Bound">t</a> <a id="2769" href="Cubical.Tactics.Reflection.Variables.html#1592" class="Function Operator">=T</a> <a id="2772" href="Cubical.Tactics.Reflection.Variables.html#2751" class="Bound">t&#39;</a><a id="2774" class="Symbol">)</a>
  <a id="2778" href="Cubical.Data.Bool.Base.html#645" class="Function Operator">then</a> <a id="2783" href="Cubical.Data.Maybe.Base.html#213" class="InductiveConstructor">just</a> <a id="2788" class="Number">0</a>
  <a id="2792" href="Cubical.Data.Bool.Base.html#645" class="Function Operator">else</a> <a id="2797" href="Cubical.Data.Maybe.Base.html#327" class="Function">map-Maybe</a> <a id="2807" class="Symbol">(λ</a> <a id="2810" href="Cubical.Tactics.Reflection.Variables.html#2810" class="Bound">k</a> <a id="2812" class="Symbol">→</a> <a id="2814" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">ℕ.suc</a> <a id="2820" href="Cubical.Tactics.Reflection.Variables.html#2810" class="Bound">k</a><a id="2821" class="Symbol">)</a> <a id="2823" class="Symbol">(</a><a id="2824" href="Cubical.Tactics.Reflection.Variables.html#2708" class="Function">indexOf</a> <a id="2832" href="Cubical.Tactics.Reflection.Variables.html#2748" class="Bound">t</a> <a id="2834" href="Cubical.Tactics.Reflection.Variables.html#2756" class="Bound">l</a><a id="2835" class="Symbol">)</a>
<a id="2837" href="Cubical.Tactics.Reflection.Variables.html#2708" class="Function">indexOf</a> <a id="2845" href="Cubical.Tactics.Reflection.Variables.html#2845" class="Bound">t</a> <a id="2847" href="Agda.Builtin.List.html#184" class="InductiveConstructor">[]</a> <a id="2850" class="Symbol">=</a> <a id="2852" href="Cubical.Data.Maybe.Base.html#193" class="InductiveConstructor">nothing</a>
</pre></body></html>