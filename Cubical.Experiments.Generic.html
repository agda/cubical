<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Cubical.Experiments.Generic</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">-- Two fun examples of generic programming using univalence</a>

<a id="62" class="Keyword">module</a> <a id="69" href="Cubical.Experiments.Generic.html" class="Module">Cubical.Experiments.Generic</a> <a id="97" class="Keyword">where</a>

<a id="104" class="Keyword">open</a> <a id="109" class="Keyword">import</a> <a id="116" href="Agda.Builtin.String.html" class="Module">Agda.Builtin.String</a>
<a id="136" class="Keyword">open</a> <a id="141" class="Keyword">import</a> <a id="148" href="Agda.Builtin.List.html" class="Module">Agda.Builtin.List</a>

<a id="167" class="Keyword">open</a> <a id="172" class="Keyword">import</a> <a id="179" href="Cubical.Foundations.Prelude.html" class="Module">Cubical.Foundations.Prelude</a>
<a id="207" class="Keyword">open</a> <a id="212" class="Keyword">import</a> <a id="219" href="Cubical.Foundations.Equiv.html" class="Module">Cubical.Foundations.Equiv</a>
<a id="245" class="Keyword">open</a> <a id="250" class="Keyword">import</a> <a id="257" href="Cubical.Foundations.Univalence.html" class="Module">Cubical.Foundations.Univalence</a>

<a id="289" class="Keyword">open</a> <a id="294" class="Keyword">import</a> <a id="301" href="Cubical.Data.Nat.html" class="Module">Cubical.Data.Nat</a>
<a id="318" class="Keyword">open</a> <a id="323" class="Keyword">import</a> <a id="330" href="Cubical.Data.Int.html" class="Module">Cubical.Data.Int</a>
<a id="347" class="Keyword">open</a> <a id="352" class="Keyword">import</a> <a id="359" href="Cubical.Data.Prod.html" class="Module">Cubical.Data.Prod</a>

<a id="378" class="Comment">----------------------------------------------------------------------------</a>
<a id="455" class="Comment">--</a>
<a id="458" class="Comment">-- Dan Licata&#39;s example from slide 47 of:</a>
<a id="500" class="Comment">-- http://dlicata.web.wesleyan.edu/pubs/l13jobtalk/l13jobtalk.pdf</a>

<a id="There"></a><a id="567" href="Cubical.Experiments.Generic.html#567" class="Function">There</a> <a id="573" class="Symbol">:</a> <a id="575" href="Agda.Primitive.html#388" class="Primitive">Type₀</a> <a id="581" class="Symbol">→</a> <a id="583" href="Agda.Primitive.html#388" class="Primitive">Type₀</a>
<a id="589" href="Cubical.Experiments.Generic.html#567" class="Function">There</a> <a id="595" href="Cubical.Experiments.Generic.html#595" class="Bound">X</a> <a id="597" class="Symbol">=</a> <a id="599" href="Agda.Builtin.List.html#147" class="Datatype">List</a> <a id="604" class="Symbol">(</a><a id="605" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a> <a id="607" href="Cubical.Data.Prod.Base.html#645" class="Datatype Operator">×</a> <a id="609" href="Agda.Builtin.String.html#335" class="Postulate">String</a> <a id="616" href="Cubical.Data.Prod.Base.html#645" class="Datatype Operator">×</a> <a id="618" class="Symbol">(</a><a id="619" href="Cubical.Experiments.Generic.html#595" class="Bound">X</a> <a id="621" href="Cubical.Data.Prod.Base.html#645" class="Datatype Operator">×</a> <a id="623" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a><a id="624" class="Symbol">))</a>

<a id="Database"></a><a id="628" href="Cubical.Experiments.Generic.html#628" class="Function">Database</a> <a id="637" class="Symbol">:</a> <a id="639" href="Agda.Primitive.html#388" class="Primitive">Type₀</a>
<a id="645" href="Cubical.Experiments.Generic.html#628" class="Function">Database</a> <a id="654" class="Symbol">=</a> <a id="656" href="Cubical.Experiments.Generic.html#567" class="Function">There</a> <a id="662" class="Symbol">(</a><a id="663" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a> <a id="665" href="Cubical.Data.Prod.Base.html#645" class="Datatype Operator">×</a> <a id="667" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a><a id="668" class="Symbol">)</a>

<a id="us"></a><a id="671" href="Cubical.Experiments.Generic.html#671" class="Function">us</a> <a id="674" class="Symbol">:</a> <a id="676" href="Cubical.Experiments.Generic.html#628" class="Function">Database</a>
<a id="685" href="Cubical.Experiments.Generic.html#671" class="Function">us</a> <a id="688" class="Symbol">=</a> <a id="690" class="Symbol">(</a><a id="691" class="Number">4</a>  <a id="694" href="Cubical.Data.Prod.Base.html#704" class="InductiveConstructor Operator">,</a> <a id="696" class="String">&quot;John&quot;</a>  <a id="704" href="Cubical.Data.Prod.Base.html#704" class="InductiveConstructor Operator">,</a> <a id="706" class="Symbol">(</a><a id="707" class="Number">30</a> <a id="710" href="Cubical.Data.Prod.Base.html#704" class="InductiveConstructor Operator">,</a>  <a id="713" class="Number">5</a><a id="714" class="Symbol">)</a> <a id="716" href="Cubical.Data.Prod.Base.html#704" class="InductiveConstructor Operator">,</a> <a id="718" class="Number">1956</a><a id="722" class="Symbol">)</a>
   <a id="727" href="Agda.Builtin.List.html#199" class="InductiveConstructor Operator">∷</a> <a id="729" class="Symbol">(</a><a id="730" class="Number">8</a>  <a id="733" href="Cubical.Data.Prod.Base.html#704" class="InductiveConstructor Operator">,</a> <a id="735" class="String">&quot;Hugo&quot;</a>  <a id="743" href="Cubical.Data.Prod.Base.html#704" class="InductiveConstructor Operator">,</a> <a id="745" class="Symbol">(</a><a id="746" class="Number">29</a> <a id="749" href="Cubical.Data.Prod.Base.html#704" class="InductiveConstructor Operator">,</a> <a id="751" class="Number">12</a><a id="753" class="Symbol">)</a> <a id="755" href="Cubical.Data.Prod.Base.html#704" class="InductiveConstructor Operator">,</a> <a id="757" class="Number">1978</a><a id="761" class="Symbol">)</a>
   <a id="766" href="Agda.Builtin.List.html#199" class="InductiveConstructor Operator">∷</a> <a id="768" class="Symbol">(</a><a id="769" class="Number">15</a> <a id="772" href="Cubical.Data.Prod.Base.html#704" class="InductiveConstructor Operator">,</a> <a id="774" class="String">&quot;James&quot;</a> <a id="782" href="Cubical.Data.Prod.Base.html#704" class="InductiveConstructor Operator">,</a> <a id="784" class="Symbol">(</a><a id="785" class="Number">1</a>  <a id="788" href="Cubical.Data.Prod.Base.html#704" class="InductiveConstructor Operator">,</a>  <a id="791" class="Number">7</a><a id="792" class="Symbol">)</a> <a id="794" href="Cubical.Data.Prod.Base.html#704" class="InductiveConstructor Operator">,</a> <a id="796" class="Number">1968</a><a id="800" class="Symbol">)</a>
   <a id="805" href="Agda.Builtin.List.html#199" class="InductiveConstructor Operator">∷</a> <a id="807" class="Symbol">(</a><a id="808" class="Number">16</a> <a id="811" href="Cubical.Data.Prod.Base.html#704" class="InductiveConstructor Operator">,</a> <a id="813" class="String">&quot;Sayid&quot;</a> <a id="821" href="Cubical.Data.Prod.Base.html#704" class="InductiveConstructor Operator">,</a> <a id="823" class="Symbol">(</a><a id="824" class="Number">2</a>  <a id="827" href="Cubical.Data.Prod.Base.html#704" class="InductiveConstructor Operator">,</a> <a id="829" class="Number">10</a><a id="831" class="Symbol">)</a> <a id="833" href="Cubical.Data.Prod.Base.html#704" class="InductiveConstructor Operator">,</a> <a id="835" class="Number">1967</a><a id="839" class="Symbol">)</a>
   <a id="844" href="Agda.Builtin.List.html#199" class="InductiveConstructor Operator">∷</a> <a id="846" class="Symbol">(</a><a id="847" class="Number">23</a> <a id="850" href="Cubical.Data.Prod.Base.html#704" class="InductiveConstructor Operator">,</a> <a id="852" class="String">&quot;Jack&quot;</a>  <a id="860" href="Cubical.Data.Prod.Base.html#704" class="InductiveConstructor Operator">,</a> <a id="862" class="Symbol">(</a><a id="863" class="Number">3</a>  <a id="866" href="Cubical.Data.Prod.Base.html#704" class="InductiveConstructor Operator">,</a> <a id="868" class="Number">12</a><a id="870" class="Symbol">)</a> <a id="872" href="Cubical.Data.Prod.Base.html#704" class="InductiveConstructor Operator">,</a> <a id="874" class="Number">1969</a><a id="878" class="Symbol">)</a>
   <a id="883" href="Agda.Builtin.List.html#199" class="InductiveConstructor Operator">∷</a> <a id="885" class="Symbol">(</a><a id="886" class="Number">42</a> <a id="889" href="Cubical.Data.Prod.Base.html#704" class="InductiveConstructor Operator">,</a> <a id="891" class="String">&quot;Sun&quot;</a>   <a id="899" href="Cubical.Data.Prod.Base.html#704" class="InductiveConstructor Operator">,</a> <a id="901" class="Symbol">(</a><a id="902" class="Number">20</a> <a id="905" href="Cubical.Data.Prod.Base.html#704" class="InductiveConstructor Operator">,</a>  <a id="908" class="Number">3</a><a id="909" class="Symbol">)</a> <a id="911" href="Cubical.Data.Prod.Base.html#704" class="InductiveConstructor Operator">,</a> <a id="913" class="Number">1980</a><a id="917" class="Symbol">)</a>
   <a id="922" href="Agda.Builtin.List.html#199" class="InductiveConstructor Operator">∷</a> <a id="924" href="Agda.Builtin.List.html#184" class="InductiveConstructor">[]</a>

<a id="convert"></a><a id="928" href="Cubical.Experiments.Generic.html#928" class="Function">convert</a> <a id="936" class="Symbol">:</a> <a id="938" href="Cubical.Experiments.Generic.html#628" class="Function">Database</a> <a id="947" class="Symbol">→</a> <a id="949" href="Cubical.Experiments.Generic.html#628" class="Function">Database</a>
<a id="958" href="Cubical.Experiments.Generic.html#928" class="Function">convert</a> <a id="966" href="Cubical.Experiments.Generic.html#966" class="Bound">d</a> <a id="968" class="Symbol">=</a> <a id="970" href="Cubical.Foundations.Prelude.html#9674" class="Function">transport</a> <a id="980" class="Symbol">(λ</a> <a id="983" href="Cubical.Experiments.Generic.html#983" class="Bound">i</a> <a id="985" class="Symbol">→</a> <a id="987" href="Cubical.Experiments.Generic.html#567" class="Function">There</a> <a id="993" class="Symbol">(</a><a id="994" href="Cubical.Data.Prod.Properties.html#1014" class="Function">swapEq</a> <a id="1001" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a> <a id="1003" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a> <a id="1005" href="Cubical.Experiments.Generic.html#983" class="Bound">i</a><a id="1006" class="Symbol">))</a> <a id="1009" href="Cubical.Experiments.Generic.html#966" class="Bound">d</a>

<a id="1012" class="Comment">-- Swap the dates of the American database to get the European format</a>
<a id="eu"></a><a id="1082" href="Cubical.Experiments.Generic.html#1082" class="Function">eu</a> <a id="1085" class="Symbol">:</a> <a id="1087" href="Cubical.Experiments.Generic.html#628" class="Function">Database</a>
<a id="1096" href="Cubical.Experiments.Generic.html#1082" class="Function">eu</a> <a id="1099" class="Symbol">=</a> <a id="1101" href="Cubical.Experiments.Generic.html#928" class="Function">convert</a> <a id="1109" href="Cubical.Experiments.Generic.html#671" class="Function">us</a>

<a id="1113" class="Comment">-- A sanity check</a>
<a id="1131" href="Cubical.Experiments.Generic.html#1131" class="Function">_</a> <a id="1133" class="Symbol">:</a> <a id="1135" href="Cubical.Experiments.Generic.html#671" class="Function">us</a> <a id="1138" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="1140" href="Cubical.Experiments.Generic.html#928" class="Function">convert</a> <a id="1148" href="Cubical.Experiments.Generic.html#1082" class="Function">eu</a>
<a id="1151" class="Symbol">_</a> <a id="1153" class="Symbol">=</a> <a id="1155" href="Cubical.Foundations.Prelude.html#892" class="Function">refl</a>


<a id="1162" class="Comment">----------------------------------------------------------------------------</a>
<a id="1239" class="Comment">--</a>
<a id="1242" class="Comment">-- Example inspired by:</a>
<a id="1266" class="Comment">--</a>
<a id="1269" class="Comment">-- Scrap Your Boilerplate: A Practical Design Pattern for Generic Programming</a>
<a id="1347" class="Comment">-- Ralf Lämmel &amp; Simon Peyton Jones, TLDI&#39;03</a>

<a id="Address"></a><a id="1393" href="Cubical.Experiments.Generic.html#1393" class="Function">Address</a> <a id="1401" class="Symbol">:</a> <a id="1403" href="Agda.Primitive.html#388" class="Primitive">Type₀</a>
<a id="1409" href="Cubical.Experiments.Generic.html#1393" class="Function">Address</a> <a id="1417" class="Symbol">=</a> <a id="1419" href="Agda.Builtin.String.html#335" class="Postulate">String</a>

<a id="Name"></a><a id="1427" href="Cubical.Experiments.Generic.html#1427" class="Function">Name</a> <a id="1432" class="Symbol">:</a> <a id="1434" href="Agda.Primitive.html#388" class="Primitive">Type₀</a>
<a id="1440" href="Cubical.Experiments.Generic.html#1427" class="Function">Name</a> <a id="1445" class="Symbol">=</a> <a id="1447" href="Agda.Builtin.String.html#335" class="Postulate">String</a>

<a id="1455" class="Keyword">data</a> <a id="Person"></a><a id="1460" href="Cubical.Experiments.Generic.html#1460" class="Datatype">Person</a> <a id="1467" class="Symbol">:</a> <a id="1469" href="Agda.Primitive.html#388" class="Primitive">Type₀</a> <a id="1475" class="Keyword">where</a>
  <a id="Person.P"></a><a id="1483" href="Cubical.Experiments.Generic.html#1483" class="InductiveConstructor">P</a> <a id="1485" class="Symbol">:</a> <a id="1487" href="Cubical.Experiments.Generic.html#1427" class="Function">Name</a> <a id="1492" class="Symbol">→</a> <a id="1494" href="Cubical.Experiments.Generic.html#1393" class="Function">Address</a> <a id="1502" class="Symbol">→</a> <a id="1504" href="Cubical.Experiments.Generic.html#1460" class="Datatype">Person</a>

<a id="1512" class="Keyword">data</a> <a id="Salary"></a><a id="1517" href="Cubical.Experiments.Generic.html#1517" class="Datatype">Salary</a> <a id="1524" class="Symbol">(</a><a id="1525" href="Cubical.Experiments.Generic.html#1525" class="Bound">A</a> <a id="1527" class="Symbol">:</a> <a id="1529" href="Agda.Primitive.html#388" class="Primitive">Type₀</a><a id="1534" class="Symbol">)</a> <a id="1536" class="Symbol">:</a> <a id="1538" href="Agda.Primitive.html#388" class="Primitive">Type₀</a> <a id="1544" class="Keyword">where</a>
  <a id="Salary.S"></a><a id="1552" href="Cubical.Experiments.Generic.html#1552" class="InductiveConstructor">S</a> <a id="1554" class="Symbol">:</a> <a id="1556" href="Cubical.Experiments.Generic.html#1525" class="Bound">A</a> <a id="1558" class="Symbol">→</a> <a id="1560" href="Cubical.Experiments.Generic.html#1517" class="Datatype">Salary</a> <a id="1567" href="Cubical.Experiments.Generic.html#1525" class="Bound">A</a>

<a id="1570" class="Keyword">data</a> <a id="Employee"></a><a id="1575" href="Cubical.Experiments.Generic.html#1575" class="Datatype">Employee</a> <a id="1584" class="Symbol">(</a><a id="1585" href="Cubical.Experiments.Generic.html#1585" class="Bound">A</a> <a id="1587" class="Symbol">:</a> <a id="1589" href="Agda.Primitive.html#388" class="Primitive">Type₀</a><a id="1594" class="Symbol">)</a> <a id="1596" class="Symbol">:</a> <a id="1598" href="Agda.Primitive.html#388" class="Primitive">Type₀</a> <a id="1604" class="Keyword">where</a>
  <a id="Employee.E"></a><a id="1612" href="Cubical.Experiments.Generic.html#1612" class="InductiveConstructor">E</a> <a id="1614" class="Symbol">:</a> <a id="1616" href="Cubical.Experiments.Generic.html#1460" class="Datatype">Person</a> <a id="1623" class="Symbol">→</a> <a id="1625" href="Cubical.Experiments.Generic.html#1517" class="Datatype">Salary</a> <a id="1632" href="Cubical.Experiments.Generic.html#1585" class="Bound">A</a> <a id="1634" class="Symbol">→</a> <a id="1636" href="Cubical.Experiments.Generic.html#1575" class="Datatype">Employee</a> <a id="1645" href="Cubical.Experiments.Generic.html#1585" class="Bound">A</a>

<a id="Manager"></a><a id="1648" href="Cubical.Experiments.Generic.html#1648" class="Function">Manager</a> <a id="1656" class="Symbol">:</a> <a id="1658" href="Agda.Primitive.html#388" class="Primitive">Type₀</a> <a id="1664" class="Symbol">→</a> <a id="1666" href="Agda.Primitive.html#388" class="Primitive">Type₀</a>
<a id="1672" href="Cubical.Experiments.Generic.html#1648" class="Function">Manager</a> <a id="1680" href="Cubical.Experiments.Generic.html#1680" class="Bound">A</a> <a id="1682" class="Symbol">=</a> <a id="1684" href="Cubical.Experiments.Generic.html#1575" class="Datatype">Employee</a> <a id="1693" href="Cubical.Experiments.Generic.html#1680" class="Bound">A</a>

<a id="1696" class="Comment">-- First test of &quot;mutual&quot;</a>
<a id="1722" class="Keyword">mutual</a>
  <a id="1731" class="Keyword">data</a> <a id="Dept"></a><a id="1736" href="Cubical.Experiments.Generic.html#1736" class="Datatype">Dept</a> <a id="1741" class="Symbol">(</a><a id="1742" href="Cubical.Experiments.Generic.html#1742" class="Bound">A</a> <a id="1744" class="Symbol">:</a> <a id="1746" href="Agda.Primitive.html#388" class="Primitive">Type₀</a><a id="1751" class="Symbol">)</a> <a id="1753" class="Symbol">:</a> <a id="1755" href="Agda.Primitive.html#388" class="Primitive">Type₀</a> <a id="1761" class="Keyword">where</a>
    <a id="Dept.D"></a><a id="1771" href="Cubical.Experiments.Generic.html#1771" class="InductiveConstructor">D</a> <a id="1773" class="Symbol">:</a> <a id="1775" href="Cubical.Experiments.Generic.html#1427" class="Function">Name</a> <a id="1780" class="Symbol">→</a> <a id="1782" href="Cubical.Experiments.Generic.html#1648" class="Function">Manager</a> <a id="1790" href="Cubical.Experiments.Generic.html#1742" class="Bound">A</a> <a id="1792" class="Symbol">→</a> <a id="1794" href="Agda.Builtin.List.html#147" class="Datatype">List</a> <a id="1799" class="Symbol">(</a><a id="1800" href="Cubical.Experiments.Generic.html#1828" class="Datatype">SubUnit</a> <a id="1808" href="Cubical.Experiments.Generic.html#1742" class="Bound">A</a><a id="1809" class="Symbol">)</a> <a id="1811" class="Symbol">→</a> <a id="1813" href="Cubical.Experiments.Generic.html#1736" class="Datatype">Dept</a> <a id="1818" href="Cubical.Experiments.Generic.html#1742" class="Bound">A</a>

  <a id="1823" class="Keyword">data</a> <a id="SubUnit"></a><a id="1828" href="Cubical.Experiments.Generic.html#1828" class="Datatype">SubUnit</a> <a id="1836" class="Symbol">(</a><a id="1837" href="Cubical.Experiments.Generic.html#1837" class="Bound">A</a> <a id="1839" class="Symbol">:</a> <a id="1841" href="Agda.Primitive.html#388" class="Primitive">Type₀</a><a id="1846" class="Symbol">)</a> <a id="1848" class="Symbol">:</a> <a id="1850" href="Agda.Primitive.html#388" class="Primitive">Type₀</a> <a id="1856" class="Keyword">where</a>
    <a id="SubUnit.PU"></a><a id="1866" href="Cubical.Experiments.Generic.html#1866" class="InductiveConstructor">PU</a> <a id="1869" class="Symbol">:</a> <a id="1871" href="Cubical.Experiments.Generic.html#1575" class="Datatype">Employee</a> <a id="1880" href="Cubical.Experiments.Generic.html#1837" class="Bound">A</a> <a id="1882" class="Symbol">→</a> <a id="1884" href="Cubical.Experiments.Generic.html#1828" class="Datatype">SubUnit</a> <a id="1892" href="Cubical.Experiments.Generic.html#1837" class="Bound">A</a>
    <a id="SubUnit.DU"></a><a id="1898" href="Cubical.Experiments.Generic.html#1898" class="InductiveConstructor">DU</a> <a id="1901" class="Symbol">:</a> <a id="1903" href="Cubical.Experiments.Generic.html#1736" class="Datatype">Dept</a> <a id="1908" href="Cubical.Experiments.Generic.html#1837" class="Bound">A</a> <a id="1910" class="Symbol">→</a> <a id="1912" href="Cubical.Experiments.Generic.html#1828" class="Datatype">SubUnit</a> <a id="1920" href="Cubical.Experiments.Generic.html#1837" class="Bound">A</a>

<a id="1923" class="Keyword">data</a> <a id="Company"></a><a id="1928" href="Cubical.Experiments.Generic.html#1928" class="Datatype">Company</a> <a id="1936" class="Symbol">(</a><a id="1937" href="Cubical.Experiments.Generic.html#1937" class="Bound">A</a> <a id="1939" class="Symbol">:</a> <a id="1941" href="Agda.Primitive.html#388" class="Primitive">Type₀</a><a id="1946" class="Symbol">)</a> <a id="1948" class="Symbol">:</a> <a id="1950" href="Agda.Primitive.html#388" class="Primitive">Type₀</a> <a id="1956" class="Keyword">where</a>
  <a id="Company.C"></a><a id="1964" href="Cubical.Experiments.Generic.html#1964" class="InductiveConstructor">C</a> <a id="1966" class="Symbol">:</a> <a id="1968" href="Agda.Builtin.List.html#147" class="Datatype">List</a> <a id="1973" class="Symbol">(</a><a id="1974" href="Cubical.Experiments.Generic.html#1736" class="Datatype">Dept</a> <a id="1979" href="Cubical.Experiments.Generic.html#1937" class="Bound">A</a><a id="1980" class="Symbol">)</a> <a id="1982" class="Symbol">→</a> <a id="1984" href="Cubical.Experiments.Generic.html#1928" class="Datatype">Company</a> <a id="1992" href="Cubical.Experiments.Generic.html#1937" class="Bound">A</a>


<a id="1996" class="Comment">-- A small example</a>

<a id="anders"></a><a id="2016" href="Cubical.Experiments.Generic.html#2016" class="Function">anders</a> <a id="2023" class="Symbol">:</a> <a id="2025" href="Cubical.Experiments.Generic.html#1575" class="Datatype">Employee</a> <a id="2034" href="Cubical.Data.Int.Base.html#419" class="Datatype">ℤ</a>
<a id="2036" href="Cubical.Experiments.Generic.html#2016" class="Function">anders</a> <a id="2043" class="Symbol">=</a> <a id="2045" href="Cubical.Experiments.Generic.html#1612" class="InductiveConstructor">E</a> <a id="2047" class="Symbol">(</a><a id="2048" href="Cubical.Experiments.Generic.html#1483" class="InductiveConstructor">P</a> <a id="2050" class="String">&quot;Anders&quot;</a> <a id="2059" class="String">&quot;Pittsburgh&quot;</a><a id="2071" class="Symbol">)</a> <a id="2073" class="Symbol">(</a><a id="2074" href="Cubical.Experiments.Generic.html#1552" class="InductiveConstructor">S</a> <a id="2076" class="Symbol">(</a><a id="2077" href="Cubical.Data.Int.Base.html#437" class="InductiveConstructor">pos</a> <a id="2081" class="Number">2500</a><a id="2085" class="Symbol">))</a>

<a id="andrea"></a><a id="2089" href="Cubical.Experiments.Generic.html#2089" class="Function">andrea</a> <a id="2096" class="Symbol">:</a> <a id="2098" href="Cubical.Experiments.Generic.html#1575" class="Datatype">Employee</a> <a id="2107" href="Cubical.Data.Int.Base.html#419" class="Datatype">ℤ</a>
<a id="2109" href="Cubical.Experiments.Generic.html#2089" class="Function">andrea</a> <a id="2116" class="Symbol">=</a> <a id="2118" href="Cubical.Experiments.Generic.html#1612" class="InductiveConstructor">E</a> <a id="2120" class="Symbol">(</a><a id="2121" href="Cubical.Experiments.Generic.html#1483" class="InductiveConstructor">P</a> <a id="2123" class="String">&quot;Andrea&quot;</a> <a id="2132" class="String">&quot;Copenhagen&quot;</a><a id="2144" class="Symbol">)</a> <a id="2146" class="Symbol">(</a><a id="2147" href="Cubical.Experiments.Generic.html#1552" class="InductiveConstructor">S</a> <a id="2149" class="Symbol">(</a><a id="2150" href="Cubical.Data.Int.Base.html#437" class="InductiveConstructor">pos</a> <a id="2154" class="Number">2000</a><a id="2158" class="Symbol">))</a>

<a id="andreas"></a><a id="2162" href="Cubical.Experiments.Generic.html#2162" class="Function">andreas</a> <a id="2170" class="Symbol">:</a> <a id="2172" href="Cubical.Experiments.Generic.html#1575" class="Datatype">Employee</a> <a id="2181" href="Cubical.Data.Int.Base.html#419" class="Datatype">ℤ</a>
<a id="2183" href="Cubical.Experiments.Generic.html#2162" class="Function">andreas</a> <a id="2191" class="Symbol">=</a> <a id="2193" href="Cubical.Experiments.Generic.html#1612" class="InductiveConstructor">E</a> <a id="2195" class="Symbol">(</a><a id="2196" href="Cubical.Experiments.Generic.html#1483" class="InductiveConstructor">P</a> <a id="2198" class="String">&quot;Andreas&quot;</a> <a id="2208" class="String">&quot;Gothenburg&quot;</a><a id="2220" class="Symbol">)</a> <a id="2222" class="Symbol">(</a><a id="2223" href="Cubical.Experiments.Generic.html#1552" class="InductiveConstructor">S</a> <a id="2225" class="Symbol">(</a><a id="2226" href="Cubical.Data.Int.Base.html#437" class="InductiveConstructor">pos</a> <a id="2230" class="Number">3000</a><a id="2234" class="Symbol">))</a>

<a id="2238" class="Comment">-- For now we have a small company</a>
<a id="genCom"></a><a id="2273" href="Cubical.Experiments.Generic.html#2273" class="Function">genCom</a> <a id="2280" class="Symbol">:</a> <a id="2282" href="Cubical.Experiments.Generic.html#1928" class="Datatype">Company</a> <a id="2290" href="Cubical.Data.Int.Base.html#419" class="Datatype">ℤ</a>
<a id="2292" href="Cubical.Experiments.Generic.html#2273" class="Function">genCom</a> <a id="2299" class="Symbol">=</a>
  <a id="2303" href="Cubical.Experiments.Generic.html#1964" class="InductiveConstructor">C</a> <a id="2305" class="Symbol">(</a> <a id="2307" href="Cubical.Experiments.Generic.html#1771" class="InductiveConstructor">D</a> <a id="2309" class="String">&quot;Research&quot;</a> <a id="2320" href="Cubical.Experiments.Generic.html#2162" class="Function">andreas</a> <a id="2328" class="Symbol">(</a><a id="2329" href="Cubical.Experiments.Generic.html#1866" class="InductiveConstructor">PU</a> <a id="2332" href="Cubical.Experiments.Generic.html#2016" class="Function">anders</a> <a id="2339" href="Agda.Builtin.List.html#199" class="InductiveConstructor Operator">∷</a> <a id="2341" href="Cubical.Experiments.Generic.html#1866" class="InductiveConstructor">PU</a> <a id="2344" href="Cubical.Experiments.Generic.html#2089" class="Function">andrea</a> <a id="2351" href="Agda.Builtin.List.html#199" class="InductiveConstructor Operator">∷</a> <a id="2353" href="Agda.Builtin.List.html#184" class="InductiveConstructor">[]</a><a id="2355" class="Symbol">)</a>
    <a id="2361" href="Agda.Builtin.List.html#199" class="InductiveConstructor Operator">∷</a> <a id="2363" href="Agda.Builtin.List.html#184" class="InductiveConstructor">[]</a><a id="2365" class="Symbol">)</a>

<a id="2368" class="Comment">-- Increase the salary for everyone by 1</a>
<a id="incSalary"></a><a id="2409" href="Cubical.Experiments.Generic.html#2409" class="Function">incSalary</a> <a id="2419" class="Symbol">:</a> <a id="2421" href="Cubical.Experiments.Generic.html#1928" class="Datatype">Company</a> <a id="2429" href="Cubical.Data.Int.Base.html#419" class="Datatype">ℤ</a> <a id="2431" class="Symbol">→</a> <a id="2433" href="Cubical.Experiments.Generic.html#1928" class="Datatype">Company</a> <a id="2441" href="Cubical.Data.Int.Base.html#419" class="Datatype">ℤ</a>
<a id="2443" href="Cubical.Experiments.Generic.html#2409" class="Function">incSalary</a> <a id="2453" href="Cubical.Experiments.Generic.html#2453" class="Bound">c</a> <a id="2455" class="Symbol">=</a> <a id="2457" href="Cubical.Foundations.Prelude.html#9674" class="Function">transport</a> <a id="2467" class="Symbol">(λ</a> <a id="2470" href="Cubical.Experiments.Generic.html#2470" class="Bound">i</a> <a id="2472" class="Symbol">→</a> <a id="2474" href="Cubical.Experiments.Generic.html#1928" class="Datatype">Company</a> <a id="2482" class="Symbol">(</a><a id="2483" href="Cubical.Data.Int.Properties.html#23619" class="Function">sucPathℤ</a> <a id="2492" href="Cubical.Experiments.Generic.html#2470" class="Bound">i</a><a id="2493" class="Symbol">))</a> <a id="2496" href="Cubical.Experiments.Generic.html#2453" class="Bound">c</a>

<a id="genCom1"></a><a id="2499" href="Cubical.Experiments.Generic.html#2499" class="Function">genCom1</a> <a id="2507" class="Symbol">:</a> <a id="2509" href="Cubical.Experiments.Generic.html#1928" class="Datatype">Company</a> <a id="2517" href="Cubical.Data.Int.Base.html#419" class="Datatype">ℤ</a>
<a id="2519" href="Cubical.Experiments.Generic.html#2499" class="Function">genCom1</a> <a id="2527" class="Symbol">=</a> <a id="2529" href="Cubical.Experiments.Generic.html#2409" class="Function">incSalary</a> <a id="2539" href="Cubical.Experiments.Generic.html#2273" class="Function">genCom</a>


<a id="2548" class="Comment">-- Increase the salary more</a>
<a id="incSalaryℕ"></a><a id="2576" href="Cubical.Experiments.Generic.html#2576" class="Function">incSalaryℕ</a> <a id="2587" class="Symbol">:</a> <a id="2589" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a> <a id="2591" class="Symbol">→</a> <a id="2593" href="Cubical.Experiments.Generic.html#1928" class="Datatype">Company</a> <a id="2601" href="Cubical.Data.Int.Base.html#419" class="Datatype">ℤ</a> <a id="2603" class="Symbol">→</a> <a id="2605" href="Cubical.Experiments.Generic.html#1928" class="Datatype">Company</a> <a id="2613" href="Cubical.Data.Int.Base.html#419" class="Datatype">ℤ</a>
<a id="2615" href="Cubical.Experiments.Generic.html#2576" class="Function">incSalaryℕ</a> <a id="2626" href="Cubical.Experiments.Generic.html#2626" class="Bound">n</a> <a id="2628" href="Cubical.Experiments.Generic.html#2628" class="Bound">c</a> <a id="2630" class="Symbol">=</a> <a id="2632" href="Cubical.Foundations.Prelude.html#9674" class="Function">transport</a> <a id="2642" class="Symbol">(λ</a> <a id="2645" href="Cubical.Experiments.Generic.html#2645" class="Bound">i</a> <a id="2647" class="Symbol">→</a> <a id="2649" href="Cubical.Experiments.Generic.html#1928" class="Datatype">Company</a> <a id="2657" class="Symbol">(</a><a id="2658" href="Cubical.Data.Int.Properties.html#23706" class="Function">addEq</a> <a id="2664" href="Cubical.Experiments.Generic.html#2626" class="Bound">n</a> <a id="2666" href="Cubical.Experiments.Generic.html#2645" class="Bound">i</a><a id="2667" class="Symbol">))</a> <a id="2670" href="Cubical.Experiments.Generic.html#2628" class="Bound">c</a>

<a id="genCom2"></a><a id="2673" href="Cubical.Experiments.Generic.html#2673" class="Function">genCom2</a> <a id="2681" class="Symbol">:</a> <a id="2683" href="Cubical.Experiments.Generic.html#1928" class="Datatype">Company</a> <a id="2691" href="Cubical.Data.Int.Base.html#419" class="Datatype">ℤ</a>
<a id="2693" href="Cubical.Experiments.Generic.html#2673" class="Function">genCom2</a> <a id="2701" class="Symbol">=</a> <a id="2703" href="Cubical.Experiments.Generic.html#2576" class="Function">incSalaryℕ</a> <a id="2714" class="Number">2</a> <a id="2716" href="Cubical.Experiments.Generic.html#2273" class="Function">genCom</a>

<a id="genCom10"></a><a id="2724" href="Cubical.Experiments.Generic.html#2724" class="Function">genCom10</a> <a id="2733" class="Symbol">:</a> <a id="2735" href="Cubical.Experiments.Generic.html#1928" class="Datatype">Company</a> <a id="2743" href="Cubical.Data.Int.Base.html#419" class="Datatype">ℤ</a>
<a id="2745" href="Cubical.Experiments.Generic.html#2724" class="Function">genCom10</a> <a id="2754" class="Symbol">=</a> <a id="2756" href="Cubical.Experiments.Generic.html#2576" class="Function">incSalaryℕ</a> <a id="2767" class="Number">10</a> <a id="2770" href="Cubical.Experiments.Generic.html#2273" class="Function">genCom</a>
</pre></body></html>