{-# OPTIONS --safe --experimental-lossy-unification #-}
module Cubical.ZCohomology.Groups.RP2 where

open import Cubical.ZCohomology.Base
open import Cubical.ZCohomology.GroupStructure
open import Cubical.ZCohomology.Properties
open import Cubical.ZCohomology.Groups.KleinBottle
open import Cubical.Foundations.HLevels
open import Cubical.Foundations.Prelude
open import Cubical.Foundations.Function
open import Cubical.Foundations.GroupoidLaws
open import Cubical.HITs.SetTruncation renaming (rec to sRec ; rec2 to pRec2 ; elim to sElim ; elim2 to sElim2 ; map to sMap)
open import Cubical.HITs.PropositionalTruncation renaming (rec to pRec ; elim to pElim) hiding (map)
open import Cubical.HITs.Truncation renaming (elim to trElim ; rec to trRec ; elim2 to trElim2)
open import Cubical.Algebra.Group renaming (â„¤ to â„¤Group ; Bool to BoolGroup ; Unit to UnitGroup)

open import Cubical.Foundations.Equiv.HalfAdjoint
open import Cubical.Foundations.Transport

open import Cubical.ZCohomology.Groups.Connected

open import Cubical.Data.Sigma

open import Cubical.Foundations.Isomorphism
open import Cubical.HITs.S1
open import Cubical.HITs.Sn
open import Cubical.Foundations.Equiv
open import Cubical.Homotopy.Connected
open import Cubical.HITs.RPn.Base

open import Cubical.Data.Empty renaming (rec to âŠ¥-rec)
open import Cubical.Data.Bool
open import Cubical.Data.Int

open import Cubical.Foundations.Path

private
  variable
    â„“ : Level
    A : Type â„“

funSpaceIso-RPÂ² : Iso (RPÂ² â†’ A) (Î£[ x âˆˆ A ] Î£[ p âˆˆ x â‰¡ x ] p â‰¡ sym p)
Iso.fun funSpaceIso-RPÂ² f = f point , (cong f line , Î» i j â†’ f (square i j))
Iso.inv funSpaceIso-RPÂ² (x , p , P) point = x
Iso.inv funSpaceIso-RPÂ² (x , p , P) (line i) = p i
Iso.inv funSpaceIso-RPÂ² (x , p , P) (square i j) = P i j
Iso.rightInv funSpaceIso-RPÂ² (x , p , P) i = x , p , P
Iso.leftInv funSpaceIso-RPÂ² f _ point = f point
Iso.leftInv funSpaceIso-RPÂ² f _ (line i) = f (line i)
Iso.leftInv funSpaceIso-RPÂ² f _ (square i j) = f (square i j)

private
  pathIso : {x : A} {p : x â‰¡ x} â†’ Iso (p â‰¡ sym p) (p âˆ™ p â‰¡ refl)
  pathIso {p = p} = compIso (congIso (equivToIso (_ , compPathr-isEquiv p)))
                            (pathToIso (cong (p âˆ™ p â‰¡_) (lCancel p)))

--- Hâ°(RPÂ²) â‰… â„¤ ----
Hâ°-RPÂ²â‰…â„¤ : GroupIso (coHomGr 0 RPÂ²) â„¤Group
Hâ°-RPÂ²â‰…â„¤ = Hâ°-connected point connectedRPÂ¹
  where
  connectedRPÂ¹ : (x : RPÂ²) â†’ âˆ¥ point â‰¡ x âˆ¥
  connectedRPÂ¹ point = âˆ£ refl âˆ£
  connectedRPÂ¹ (line i) =
    isOfHLevelâ†’isOfHLevelDep 1 {B = Î» x â†’ âˆ¥ point â‰¡ x âˆ¥}
      (Î» _ â†’ isPropPropTrunc) âˆ£ refl âˆ£ âˆ£ refl âˆ£ line i
  connectedRPÂ¹ (square i j) = helper i j
    where
    helper : SquareP (Î» i j â†’ âˆ¥ point â‰¡ square i j âˆ¥)
                     (isOfHLevelâ†’isOfHLevelDep 1 {B = Î» x â†’ âˆ¥ point â‰¡ x âˆ¥}
                       (Î» _ â†’ isPropPropTrunc) âˆ£ refl âˆ£ âˆ£ refl âˆ£ line)
                     (symP (isOfHLevelâ†’isOfHLevelDep 1 {B = Î» x â†’ âˆ¥ point â‰¡ x âˆ¥}
                             (Î» _ â†’ isPropPropTrunc) âˆ£ refl âˆ£ âˆ£ refl âˆ£ line))
                     refl refl
    helper = toPathP (isOfHLevelPathP 1 isPropPropTrunc _ _ _ _)

--- HÂ¹(RPÂ²) â‰… 0 ----
isContr-HÂ¹-RPÂ²-helper : isContr âˆ¥ Î£[ x âˆˆ coHomK 1 ] Î£[ p âˆˆ x â‰¡ x ] p âˆ™ p â‰¡ refl âˆ¥â‚‚
fst isContr-HÂ¹-RPÂ²-helper = âˆ£ 0â‚– 1 , refl , sym (rUnit refl) âˆ£â‚‚
snd isContr-HÂ¹-RPÂ²-helper =
  sElim (Î» _ â†’ isOfHLevelPath 2 isSetSetTrunc _ _)
    (uncurry
      (trElim (Î» _ â†’ isGroupoidÎ  Î» _ â†’ isOfHLevelPlus {n = 1} 2 (isSetSetTrunc _ _))
      (toPropElim (Î» _ â†’ isPropÎ  (Î» _ â†’ isSetSetTrunc _ _))
         Î» {(p , nilp)
            â†’ cong âˆ£_âˆ£â‚‚ (Î£PathP (refl , Î£â‰¡Prop (Î» _ â†’ isOfHLevelTrunc 3 _ _ _ _)
                                         (rUnit refl
                                       âˆ™âˆ™ cong (Knâ†’Î©Kn+1 0) (sym (nilpotentâ†’â‰¡0 (Î©Kn+1â†’Kn 0 p)
                                                                                 (sym (Î©Kn+1â†’Kn-hom 0 p p)
                                                                                âˆ™ cong (Î©Kn+1â†’Kn 0) nilp)))
                                       âˆ™âˆ™ Iso.rightInv (Iso-Kn-Î©Kn+1 0) p)))})))

HÂ¹-RPÂ²â‰…0 : GroupIso (coHomGr 1 RPÂ²) UnitGroup
HÂ¹-RPÂ²â‰…0 =
  contrGroupIsoUnit
    (isOfHLevelRetractFromIso 0
      (setTruncIso (compIso funSpaceIso-RPÂ²
                            (Î£-cong-iso-snd (Î» _ â†’ Î£-cong-iso-snd Î» _ â†’ pathIso))))
      isContr-HÂ¹-RPÂ²-helper)

--- HÂ²(RPÂ²) â‰… â„¤/2â„¤ ----

Iso-HÂ²-RPÂ²â‚ : Iso âˆ¥ Î£[ x âˆˆ coHomK 2 ] Î£[ p âˆˆ x â‰¡ x ] p â‰¡ sym p âˆ¥â‚‚
                  âˆ¥ Î£[ p âˆˆ 0â‚– 2 â‰¡ 0â‚– 2 ] p â‰¡ sym p âˆ¥â‚‚
Iso.fun Iso-HÂ²-RPÂ²â‚ =
  sRec isSetSetTrunc
    (uncurry
      (trElim (Î» _ â†’ is2GroupoidÎ  Î» _ â†’ isOfHLevelPlus {n = 2} 2 isSetSetTrunc)
        (sphereElim _ (Î» _ â†’ isSetÎ  (Î» _ â†’ isSetSetTrunc))
          Î» p â†’ âˆ£ fst p , snd p âˆ£â‚‚)))
Iso.inv Iso-HÂ²-RPÂ²â‚ = sMap Î» p â†’ (0â‚– 2) , p
Iso.rightInv Iso-HÂ²-RPÂ²â‚ = sElim (Î» _ â†’ isOfHLevelPath 2 isSetSetTrunc _ _)
                           Î» _ â†’ refl
Iso.leftInv Iso-HÂ²-RPÂ²â‚ =
  sElim (Î» _ â†’ isOfHLevelPath 2 isSetSetTrunc _ _)
    (uncurry (trElim (Î» _ â†’ is2GroupoidÎ  Î» _ â†’ isOfHLevelPlus {n = 1} 3 (isSetSetTrunc _ _))
      (sphereToPropElim _ (Î» _ â†’ isPropÎ  (Î» _ â†’ isSetSetTrunc _ _))
        Î» p â†’ refl)))

Iso-HÂ²-RPÂ²â‚‚ : Iso âˆ¥ Î£[ p âˆˆ 0â‚– 2 â‰¡ 0â‚– 2 ] p â‰¡ sym p âˆ¥â‚‚ Bool
Iso-HÂ²-RPÂ²â‚‚ = compIso (setTruncIso (Î£-cong-iso-snd Î» _ â†’ pathIso))
                (compIso Iso-HÂ²-ğ•‚Â²â‚‚ testIso)


HÂ²-RPÂ²â‰…Bool : GroupIso (coHomGr 2 RPÂ²) BoolGroup
HÂ²-RPÂ²â‰…Bool = invGroupIso (â‰…Bool (compIso
                                    (compIso (setTruncIso funSpaceIso-RPÂ²)
                                             Iso-HÂ²-RPÂ²â‚)
                                    Iso-HÂ²-RPÂ²â‚‚))
