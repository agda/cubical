{-

   The definition of compact open subfunctors of a ‚Ñ§-functor X:

     U ‚Ü™ Sp(A) is compact open if it is given by a f.g. ideal of A,
     i.e. if ‚àÉ f‚ÇÅ, ... ,f‚Çô : A s.t. for all rings B:
                U(B) = { œÜ : Hom(A,B) | ‚ü® œÜf‚ÇÅ , ... , œÜf‚Çô ‚ü© = B }

     U ‚Ü™ X is compact open, if pulling back along any A-valued point
     Sp(A) ‚Üí X gives a compact open of Sp(A).

     By observing that compact open subfunctors of affine schemes
     are in 1-1 correspondence with radicals of f.g. ideals,
     we get that compact open subfunctors are classified by the
     ‚Ñ§-functor that sends a ring to its Zariski lattice.

-}


{-# OPTIONS --lossy-unification #-}
module Cubical.AlgebraicGeometry.Functorial.ZFunctors.CompactOpen where

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.Equiv
open import Cubical.Foundations.Isomorphism
open import Cubical.Foundations.Function
open import Cubical.Foundations.Powerset
open import Cubical.Foundations.HLevels


open import Cubical.Functions.FunExtEquiv

open import Cubical.Data.Sigma
open import Cubical.Data.Nat using (‚Ñï)

open import Cubical.Data.FinData

open import Cubical.Algebra.Ring
open import Cubical.Algebra.CommRing
open import Cubical.Algebra.CommRing.Localisation
open import Cubical.Algebra.CommRing.RadicalIdeal
open import Cubical.Algebra.Semilattice
open import Cubical.Algebra.Lattice
open import Cubical.Algebra.DistLattice
open import Cubical.Algebra.DistLattice.BigOps

open import Cubical.AlgebraicGeometry.ZariskiLattice.Base
open import Cubical.AlgebraicGeometry.ZariskiLattice.UniversalProperty
open import Cubical.AlgebraicGeometry.ZariskiLattice.Properties
open import Cubical.AlgebraicGeometry.Functorial.ZFunctors.Base

open import Cubical.Categories.Category renaming (isIso to isIsoC)
open import Cubical.Categories.Functor
open import Cubical.Categories.Instances.CommRings
open import Cubical.Categories.Instances.DistLattice
open import Cubical.Categories.Instances.DistLattices
open import Cubical.Categories.Site.Coverage
open import Cubical.Categories.Site.Sheaf
open import Cubical.Categories.Site.Instances.ZariskiCommRing
open import Cubical.Categories.NaturalTransformation
open import Cubical.Categories.Yoneda

open import Cubical.HITs.PropositionalTruncation as PT
open import Cubical.HITs.SetQuotients as SQ

open import Cubical.Relation.Binary.Order.Poset


module _ {‚Ñì : Level} where

  open Iso
  open Functor
  open NatTrans
  open NatIso
  open DistLatticeStr ‚¶É...‚¶Ñ
  open CommRingStr ‚¶É...‚¶Ñ
  open IsCommRingHom
  open IsLatticeHom
  open ZarLat
  open ZarLatUniversalProp

  -- the Zariski lattice functor classifying compact open subobjects
  ZarLatFun : ‚Ñ§Functor {‚Ñì = ‚Ñì}
  F-ob ZarLatFun A = ZL A , SQ.squash/
  F-hom ZarLatFun œÜ = inducedZarLatHom œÜ .fst
  F-id ZarLatFun {A} = cong fst (inducedZarLatHomId A)
  F-seq ZarLatFun œÜ œà = cong fst (inducedZarLatHomSeq œÜ œà)

  -- this is a separated presheaf
  -- (TODO: prove this a sheaf)
  isSeparatedZarLatFun : isSeparated zariskiCoverage ZarLatFun
  isSeparatedZarLatFun A (unimodvec n f 1‚àà‚ü®f‚ÇÅ,‚ãØ,f‚Çô‚ü©) u w uRest‚â°wRest =
    u                         ‚â°‚ü® sym (‚àßlLid _) ‚ü©
    1l ‚àßl u                  ‚â°‚ü® congL _‚àßl_ D1‚â°‚ãÅDf·µ¢ ‚ü©
    (‚ãÅ (D A ‚àò f)) ‚àßl u       ‚â°‚ü® ‚ãÅMeetldist _ _ ‚ü©
    ‚ãÅ (Œª i ‚Üí D A (f i) ‚àßl u) ‚â°‚ü® ‚ãÅExt Df·µ¢‚àßu‚â°Df·µ¢‚àßw ‚ü©
    ‚ãÅ (Œª i ‚Üí D A (f i) ‚àßl w) ‚â°‚ü® sym (‚ãÅMeetldist _ _) ‚ü©
    (‚ãÅ (D A ‚àò f)) ‚àßl w       ‚â°‚ü® congL _‚àßl_ (sym D1‚â°‚ãÅDf·µ¢) ‚ü©
    1l ‚àßl w                  ‚â°‚ü® ‚àßlLid _ ‚ü©
    w ‚àé
    where
    open Join (ZariskiLattice A)
    open JoinSemilattice (Lattice‚ÜíJoinSemilattice (DistLattice‚ÜíLattice (ZariskiLattice A)))
         using (IndPoset)
    open LatticeTheory (DistLattice‚ÜíLattice (ZariskiLattice A))
    open PosetStr (IndPoset .snd)
    open IsSupport (isSupportD A)
    open RadicalIdeal A
    instance
      _ = A .snd
      _ = ZariskiLattice A .snd

    D1‚â°‚ãÅDf·µ¢ : 1l ‚â° ‚ãÅ (D A ‚àò f)
    D1‚â°‚ãÅDf·µ¢ = is-antisym _ _
                (supportRadicalIneq f 1r (‚àà‚Üí‚àà‚àö _ _ 1‚àà‚ü®f‚ÇÅ,‚ãØ,f‚Çô‚ü©))
                  (1lRightAnnihilates‚à®l _)

    Df·µ¢‚àßu‚â°Df·µ¢‚àßw : ‚àÄ i ‚Üí D A (f i) ‚àßl u ‚â° D A (f i) ‚àßl w
    Df·µ¢‚àßu‚â°Df·µ¢‚àßw i =
        D A (f i) ‚àßl u
      ‚â°‚ü® sym (cong fst (funExt‚Åª (cong fst toLocToDown‚â°ToDown) u)) ‚ü©
        locToDownHom .fst (inducedZarLatHom /1AsCommRingHom .fst u) .fst
      ‚â°‚ü® cong (Œª x ‚Üí locToDownHom .fst x .fst) (uRest‚â°wRest i) ‚ü©
        locToDownHom .fst (inducedZarLatHom /1AsCommRingHom .fst w) .fst
      ‚â°‚ü® cong fst (funExt‚Åª (cong fst toLocToDown‚â°ToDown) w) ‚ü©
        D A (f i) ‚àßl w ‚àé
      where
      open InvertingElementsBase.UniversalProp A (f i)
      open LocDownSetIso A (f i)

  CompactOpen : ‚Ñ§Functor ‚Üí Type (‚Ñì-suc ‚Ñì)
  CompactOpen X = X ‚áí ZarLatFun

  -- the induced subfunctor
  ‚ü¶_‚üß·∂ú·µí : {X : ‚Ñ§Functor} (U : CompactOpen X) ‚Üí ‚Ñ§Functor
  F-ob (‚ü¶_‚üß·∂ú·µí {X = X} U) A = (Œ£[ x ‚àà X .F-ob A .fst  ] U .N-ob A x ‚â° D A 1r)
                                , isSetŒ£SndProp (X .F-ob A .snd) Œª _ ‚Üí squash/ _ _
   where instance _ = snd A
  F-hom (‚ü¶_‚üß·∂ú·µí {X = X} U) {x = A} {y = B} œÜ (x , Ux‚â°D1) = (X .F-hom œÜ x) , path
    where
    instance
      _ = A .snd
      _ = B .snd
    open IsLatticeHom
    path : U .N-ob B (X .F-hom œÜ x) ‚â° D B 1r
    path = U .N-ob B (X .F-hom œÜ x)         ‚â°‚ü® funExt‚Åª (U .N-hom œÜ) x ‚ü©
           ZarLatFun .F-hom œÜ (U .N-ob A x) ‚â°‚ü® cong (ZarLatFun .F-hom œÜ) Ux‚â°D1 ‚ü©
           ZarLatFun .F-hom œÜ (D A 1r)      ‚â°‚ü® inducedZarLatHom œÜ .snd .pres1 ‚ü©
           D B 1r ‚àé
  F-id (‚ü¶_‚üß·∂ú·µí {X = X} U) = funExt (Œª x ‚Üí Œ£‚â°Prop (Œª _ ‚Üí squash/ _ _)
                                     (funExt‚Åª (X .F-id) (x .fst)))
  F-seq (‚ü¶_‚üß·∂ú·µí {X = X} U) œÜ œà = funExt (Œª x ‚Üí Œ£‚â°Prop (Œª _ ‚Üí squash/ _ _)
                                          (funExt‚Åª (X .F-seq œÜ œà) (x .fst)))


  isAffineCompactOpen : {X : ‚Ñ§Functor} (U : CompactOpen X) ‚Üí Type (‚Ñì-suc ‚Ñì)
  isAffineCompactOpen U = isAffine ‚ü¶ U ‚üß·∂ú·µí


  -- the (big) dist. lattice of compact opens
  CompOpenDistLattice : Functor ‚Ñ§FUNCTOR (DistLatticesCategory {‚Ñì = ‚Ñì-suc ‚Ñì} ^op)
  fst (F-ob CompOpenDistLattice X) = CompactOpen X

  -- lattice structure induce by internal lattice object ZarLatFun
  N-ob (DistLatticeStr.0l (snd (F-ob CompOpenDistLattice X))) A _ = 0l
    where instance _ = ZariskiLattice A .snd
  N-hom (DistLatticeStr.0l (snd (F-ob CompOpenDistLattice X))) _ = funExt Œª _ ‚Üí refl

  N-ob (DistLatticeStr.1l (snd (F-ob CompOpenDistLattice X))) A _ = 1l
    where instance _ = ZariskiLattice A .snd
  N-hom (DistLatticeStr.1l (snd (F-ob CompOpenDistLattice X))) {x = A} {y = B} œÜ = funExt Œª _ ‚Üí path
    where
    instance
      _ = A .snd
      _ = B .snd
      _ = ZariskiLattice B .snd
    path : D B 1r ‚â° D B (œÜ .fst 1r) ‚à®l 0l
    path = cong (D B) (sym (œÜ .snd .pres1)) ‚àô sym (‚à®lRid _)

  N-ob ((snd (F-ob CompOpenDistLattice X) DistLatticeStr.‚à®l U) V) A x = U .N-ob A x ‚à®l V .N-ob A x
    where instance _ = ZariskiLattice A .snd
  N-hom ((snd (F-ob CompOpenDistLattice X) DistLatticeStr.‚à®l U) V)  {x = A} {y = B} œÜ = funExt path
    where
    instance
      _ = ZariskiLattice A .snd
      _ = ZariskiLattice B .snd
    path : ‚àÄ x ‚Üí U .N-ob B (X .F-hom œÜ x) ‚à®l V .N-ob B (X .F-hom œÜ x)
               ‚â° ZarLatFun .F-hom œÜ (U .N-ob A x ‚à®l V .N-ob A x)
    path x = U .N-ob B (X .F-hom œÜ x) ‚à®l V .N-ob B (X .F-hom œÜ x)
           ‚â°‚ü® cong‚ÇÇ _‚à®l_ (funExt‚Åª (U .N-hom œÜ) x) (funExt‚Åª (V .N-hom œÜ) x) ‚ü©
             ZarLatFun .F-hom œÜ (U .N-ob A x) ‚à®l ZarLatFun .F-hom œÜ (V .N-ob A x)
           ‚â°‚ü® sym (inducedZarLatHom œÜ .snd .pres‚à®l _ _) ‚ü©
             ZarLatFun .F-hom œÜ (U .N-ob A x ‚à®l V .N-ob A x) ‚àé

  N-ob ((snd (F-ob CompOpenDistLattice X) DistLatticeStr.‚àßl U) V) A x = U .N-ob A x ‚àßl V .N-ob A x
    where instance _ = ZariskiLattice A .snd
  N-hom ((snd (F-ob CompOpenDistLattice X) DistLatticeStr.‚àßl U) V)  {x = A} {y = B} œÜ = funExt path
    where
    instance
      _ = ZariskiLattice A .snd
      _ = ZariskiLattice B .snd
    path : ‚àÄ x ‚Üí U .N-ob B (X .F-hom œÜ x) ‚àßl V .N-ob B (X .F-hom œÜ x)
               ‚â° ZarLatFun .F-hom œÜ (U .N-ob A x ‚àßl V .N-ob A x)
    path x = U .N-ob B (X .F-hom œÜ x) ‚àßl V .N-ob B (X .F-hom œÜ x)
           ‚â°‚ü® cong‚ÇÇ _‚àßl_ (funExt‚Åª (U .N-hom œÜ) x) (funExt‚Åª (V .N-hom œÜ) x) ‚ü©
             ZarLatFun .F-hom œÜ (U .N-ob A x) ‚àßl ZarLatFun .F-hom œÜ (V .N-ob A x)
           ‚â°‚ü® sym (inducedZarLatHom œÜ .snd .pres‚àßl _ _) ‚ü©
             ZarLatFun .F-hom œÜ (U .N-ob A x ‚àßl V .N-ob A x) ‚àé

  DistLatticeStr.isDistLattice (snd (F-ob CompOpenDistLattice X)) = makeIsDistLattice‚àßlOver‚à®l
    isSetNatTrans
    (Œª _ _ _ ‚Üí makeNatTransPath (funExt‚ÇÇ
                 (Œª A _ ‚Üí ZariskiLattice A .snd .DistLatticeStr.‚à®lAssoc _ _ _)))
    (Œª _ ‚Üí makeNatTransPath (funExt‚ÇÇ (Œª A _ ‚Üí ZariskiLattice A .snd .DistLatticeStr.‚à®lRid _)))
    (Œª _ _ ‚Üí makeNatTransPath (funExt‚ÇÇ (Œª A _ ‚Üí ZariskiLattice A .snd .DistLatticeStr.‚à®lComm _ _)))
    (Œª _ _ _ ‚Üí makeNatTransPath (funExt‚ÇÇ
                 (Œª A _ ‚Üí ZariskiLattice A .snd .DistLatticeStr.‚àßlAssoc _ _ _)))
    (Œª _ ‚Üí makeNatTransPath (funExt‚ÇÇ (Œª A _ ‚Üí ZariskiLattice A .snd .DistLatticeStr.‚àßlRid _)))
    (Œª _ _ ‚Üí makeNatTransPath (funExt‚ÇÇ (Œª A _ ‚Üí ZariskiLattice A .snd .DistLatticeStr.‚àßlComm _ _)))
    (Œª _ _ ‚Üí makeNatTransPath (funExt‚ÇÇ -- don't know why ‚àßlAbsorb‚à®l doesn't work
               (Œª A _ ‚Üí ZariskiLattice A .snd .DistLatticeStr.absorb _ _ .snd)))
    (Œª _ _ _ ‚Üí makeNatTransPath (funExt‚ÇÇ -- same here
                 (Œª A _ ‚Üí ZariskiLattice A .snd .DistLatticeStr.‚àßl-dist-‚à®l _ _ _ .fst)))

  -- (contravariant) action on morphisms
  fst (F-hom CompOpenDistLattice Œ±) = Œ± ‚óè·µõ_
  pres0 (snd (F-hom CompOpenDistLattice Œ±)) = makeNatTransPath (funExt‚ÇÇ Œª _ _ ‚Üí refl)
  pres1 (snd (F-hom CompOpenDistLattice Œ±)) = makeNatTransPath (funExt‚ÇÇ Œª _ _ ‚Üí refl)
  pres‚à®l (snd (F-hom CompOpenDistLattice Œ±)) _ _ = makeNatTransPath (funExt‚ÇÇ Œª _ _ ‚Üí refl)
  pres‚àßl (snd (F-hom CompOpenDistLattice Œ±)) _ _ = makeNatTransPath (funExt‚ÇÇ Œª _ _ ‚Üí refl)

  -- functoriality
  F-id CompOpenDistLattice = LatticeHom‚â°f _ _
                               (funExt Œª _ ‚Üí makeNatTransPath (funExt‚ÇÇ Œª _ _ ‚Üí refl))
  F-seq CompOpenDistLattice _ _ = LatticeHom‚â°f _ _
                                    (funExt Œª _ ‚Üí makeNatTransPath (funExt‚ÇÇ Œª _ _ ‚Üí refl))


  -- useful lemmas
  module _ (X : ‚Ñ§Functor) where
    open isIsoC
    private instance _ = (CompOpenDistLattice .F-ob X) .snd

    compOpenTopNatIso : NatIso X ‚ü¶ 1l ‚üß·∂ú·µí
    N-ob (trans compOpenTopNatIso) _ œÜ = œÜ , refl
    N-hom (trans compOpenTopNatIso) _ = funExt Œª _ ‚Üí Œ£‚â°Prop (Œª _ ‚Üí squash/ _ _) refl
    inv (nIso compOpenTopNatIso B) = fst
    sec (nIso compOpenTopNatIso B) = funExt Œª _ ‚Üí Œ£‚â°Prop (Œª _ ‚Üí squash/ _ _) refl
    ret (nIso compOpenTopNatIso B) = funExt Œª _ ‚Üí refl


  module _ (X : ‚Ñ§Functor) where
    open isIsoC
    open Join (CompOpenDistLattice .F-ob X)
    open JoinSemilattice (Lattice‚ÜíJoinSemilattice (DistLattice‚ÜíLattice (CompOpenDistLattice .F-ob X)))
    open PosetStr (IndPoset .snd) hiding (_‚â§_)
    open LatticeTheory ‚¶É...‚¶Ñ
    private instance _ = (CompOpenDistLattice .F-ob X) .snd

    compOpenGlobalIncl : (U : CompactOpen X) ‚Üí ‚ü¶ U ‚üß·∂ú·µí ‚áí X
    N-ob (compOpenGlobalIncl U) A = fst
    N-hom (compOpenGlobalIncl U) œÜ = refl

    compOpenIncl : {U V : CompactOpen X} ‚Üí V ‚â§ U ‚Üí ‚ü¶ V ‚üß·∂ú·µí ‚áí ‚ü¶ U ‚üß·∂ú·µí
    N-ob (compOpenIncl {U = U} {V = V} V‚â§U) A (x , Vx‚â°D1) = x , path
      where
      instance
        _ = A .snd
        _ = ZariskiLattice A .snd
        _ = DistLattice‚ÜíLattice (ZariskiLattice A)
      path : U .N-ob A x ‚â° D A 1r
      path = U .N-ob A x                ‚â°‚ü® funExt‚Åª (funExt‚Åª (cong N-ob (sym V‚â§U)) A) x ‚ü©
             V .N-ob A x ‚à®l U .N-ob A x ‚â°‚ü® cong (_‚à®l U .N-ob A x) Vx‚â°D1 ‚ü©
             D A 1r ‚à®l U .N-ob A x      ‚â°‚ü® 1lLeftAnnihilates‚à®l _ ‚ü©
             D A 1r ‚àé
    N-hom (compOpenIncl V‚â§U) œÜ = funExt Œª x ‚Üí Œ£‚â°Prop (Œª _ ‚Üí squash/ _ _) refl

    -- this is essentially U‚àß_
    compOpenDownHom : (U : CompactOpen X)
                    ‚Üí DistLatticeHom (CompOpenDistLattice .F-ob X)
                                     (CompOpenDistLattice .F-ob ‚ü¶ U ‚üß·∂ú·µí)
    compOpenDownHom U = CompOpenDistLattice .F-hom (compOpenGlobalIncl U)

    module _ {U V : CompactOpen X} (V‚â§U : V ‚â§ U) where
      -- We need this separate definition to avoid termination checker issues,
      -- but we don't understand why.
      private
        compOpenDownHomFun : (A : CommRing ‚Ñì)
                           ‚Üí ‚ü¶ V ‚üß·∂ú·µí .F-ob A .fst
                           ‚Üí ‚ü¶ compOpenDownHom U .fst V ‚üß·∂ú·µí .F-ob A .fst
        compOpenDownHomFun A v = (compOpenIncl V‚â§U ‚ü¶ A ‚üß) v , snd v

      compOpenDownHomNatIso : NatIso ‚ü¶ V ‚üß·∂ú·µí ‚ü¶ compOpenDownHom U .fst V ‚üß·∂ú·µí
      N-ob (trans compOpenDownHomNatIso) = compOpenDownHomFun
      N-hom (trans compOpenDownHomNatIso) _ =
        funExt Œª _ ‚Üí Œ£‚â°Prop (Œª _ ‚Üí squash/ _ _) (Œ£‚â°Prop (Œª _ ‚Üí squash/ _ _) refl)
      inv (nIso compOpenDownHomNatIso A) ((x , Ux‚â°D1) , Vx‚â°D1) = x , Vx‚â°D1
      sec (nIso compOpenDownHomNatIso A) =
        funExt Œª _ ‚Üí Œ£‚â°Prop (Œª _ ‚Üí squash/ _ _) (Œ£‚â°Prop (Œª _ ‚Üí squash/ _ _) refl)
      ret (nIso compOpenDownHomNatIso A) = funExt Œª _ ‚Üí Œ£‚â°Prop (Œª _ ‚Üí squash/ _ _) refl

    compOpenInclId : ‚àÄ {U : CompactOpen X} ‚Üí compOpenIncl (is-refl U) ‚â° idTrans ‚ü¶ U ‚üß·∂ú·µí
    compOpenInclId = makeNatTransPath (funExt‚ÇÇ (Œª _ _ ‚Üí Œ£‚â°Prop (Œª _ ‚Üí squash/ _ _) refl))

    compOpenInclSeq : ‚àÄ {U V W : CompactOpen X} (U‚â§V : U ‚â§ V) (V‚â§W : V ‚â§ W)
                    ‚Üí compOpenIncl (is-trans _ _ _ U‚â§V V‚â§W)
                    ‚â° compOpenIncl U‚â§V ‚óè·µõ compOpenIncl V‚â§W
    compOpenInclSeq _ _ = makeNatTransPath
                            (funExt‚ÇÇ (Œª _ _ ‚Üí Œ£‚â°Prop (Œª _ ‚Üí squash/ _ _) refl))


    -- the structure sheaf
    private CO·µí·µñ = (DistLatticeCategory (CompOpenDistLattice .F-ob X)) ^op

    strDLSh : Functor CO·µí·µñ (CommRingsCategory {‚Ñì = ‚Ñì-suc ‚Ñì})
    F-ob strDLSh  U = ùìû .F-ob ‚ü¶ U ‚üß·∂ú·µí
    F-hom strDLSh U‚â•V = ùìû .F-hom (compOpenIncl U‚â•V)
    F-id strDLSh = cong (ùìû .F-hom) compOpenInclId ‚àô ùìû .F-id
    F-seq strDLSh _ _ = cong (ùìû .F-hom) (compOpenInclSeq _ _) ‚àô ùìû .F-seq _ _


  -- important lemma
  -- Compact opens of Zariski sheaves are sheaves
  presLocalCompactOpen : (X : ‚Ñ§Functor) (U : CompactOpen X) ‚Üí isLocal X ‚Üí isLocal ‚ü¶ U ‚üß·∂ú·µí
  presLocalCompactOpen X U isLocalX R um@(unimodvec _ f _) = isoToIsEquiv isoU
    where
    open Coverage zariskiCoverage
    open InvertingElementsBase R
    instance _ = R .snd

    f·µ¢CoverR = covers R .snd um

    isoX : Iso (X .F-ob R .fst) (CompatibleFamily X f·µ¢CoverR)
    isoX = equivToIso (elementToCompatibleFamily _ _ , isLocalX R um)

    compatibleFamIncl : (CompatibleFamily ‚ü¶ U ‚üß·∂ú·µí f·µ¢CoverR) ‚Üí (CompatibleFamily X f·µ¢CoverR)
    compatibleFamIncl fam = (fst ‚àò fst fam)
                          , Œª i j B œÜ œà œÜœàComm ‚Üí cong fst (fam .snd i j B œÜ œà œÜœàComm)

    compatibleFamIncl‚â° : ‚àÄ (y : Œ£[ x ‚àà X .F-ob R .fst  ] U .N-ob R x ‚â° D R 1r)
                       ‚Üí compatibleFamIncl (elementToCompatibleFamily ‚ü¶ U ‚üß·∂ú·µí f·µ¢CoverR y)
                       ‚â° elementToCompatibleFamily X f·µ¢CoverR (y .fst)
    compatibleFamIncl‚â° y = CompatibleFamily‚â° _ _ _ _ Œª _ ‚Üí refl

    isoU : Iso (Œ£[ x ‚àà X .F-ob R .fst  ] U .N-ob R x ‚â° D R 1r)
               (CompatibleFamily ‚ü¶ U ‚üß·∂ú·µí f·µ¢CoverR)
    fun isoU = elementToCompatibleFamily _ _
    fst (inv isoU fam) = isoX .inv (compatibleFamIncl fam)
    snd (inv isoU fam) = -- U (x) ‚â° D(1)
                         -- knowing that U(x/1)¬∏‚â° D(1) in R[1/f·µ¢]
      let x = isoX .inv (compatibleFamIncl fam) in
      isSeparatedZarLatFun R um (U .N-ob R x) (D R 1r)
        Œª i ‚Üí let open UniversalProp (f i)
                  instance _ = R[1/ (f i) ]AsCommRing .snd in

                inducedZarLatHom /1AsCommRingHom .fst (U .N-ob R x)

              ‚â°‚ü® funExt‚Åª (sym (U .N-hom /1AsCommRingHom)) x ‚ü©

                U .N-ob R[1/ (f i) ]AsCommRing (X .F-hom /1AsCommRingHom x)

              ‚â°‚ü® cong (U .N-ob R[1/ f i ]AsCommRing)
                      (funExt‚Åª (cong fst (isoX .rightInv (compatibleFamIncl fam))) i) ‚ü©

                U .N-ob R[1/ (f i) ]AsCommRing (fam .fst i .fst)

              ‚â°‚ü® fam .fst i .snd ‚ü©

                D R[1/ (f i) ]AsCommRing 1r

              ‚â°‚ü® sym (inducedZarLatHom /1AsCommRingHom .snd .pres1) ‚ü©

                inducedZarLatHom /1AsCommRingHom .fst (D R 1r) ‚àé

    rightInv isoU fam =
      Œ£‚â°Prop (Œª _ ‚Üí isPropIsCompatibleFamily _ _ _)
        (funExt Œª i ‚Üí Œ£‚â°Prop (Œª _ ‚Üí squash/ _ _)
                        (funExt‚Åª (cong fst
                          (isoX .rightInv (compatibleFamIncl fam))) i))
    leftInv isoU y = Œ£‚â°Prop (Œª _ ‚Üí squash/ _ _)
                            (cong (isoX .inv) (compatibleFamIncl‚â° y)
                              ‚àô isoX .leftInv (y .fst))
