{-

  A â„¤-functor is just a functor from rings to sets.

  NOTE: we consider the functor category [ Ring â„“ , Set â„“ ] for some universe level â„“
        and not [ Ring â„“ , Set (â„“+1) ] as is done in
        "Introduction to Algebraic Geometry and Algebraic Groups"
        by Demazure & Gabriel!

  The category of â„¤-functors contains the category of (qcqs-) schemes
  as a full subcategory and satisfies a "universal property"
  similar to the one of schemes:

    There is a coadjunction ğ“ âŠ£áµ¢ Sp
    (relative to the inclusion i : CommRing â„“ â†’ CommRing (â„“+1))
    between the "global sections functor" ğ“
    and the fully-faithful embedding of affines Sp,
    whose counit is a natural isomorphism

-}

{-# OPTIONS --lossy-unification #-}
module Cubical.AlgebraicGeometry.Functorial.ZFunctors.Base where

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.Equiv
open import Cubical.Foundations.Isomorphism
open import Cubical.Foundations.Function
open import Cubical.Foundations.Powerset
open import Cubical.Foundations.HLevels

open import Cubical.Functions.FunExtEquiv

open import Cubical.Data.Sigma
open import Cubical.Data.Nat using (â„•)

open import Cubical.Algebra.Ring
open import Cubical.Algebra.CommRing

open import Cubical.Categories.Category renaming (isIso to isIsoC)
open import Cubical.Categories.Functor
open import Cubical.Categories.Instances.Sets
open import Cubical.Categories.Instances.CommRings
open import Cubical.Categories.Instances.Functors
open import Cubical.Categories.NaturalTransformation
open import Cubical.Categories.Presheaf
open import Cubical.Categories.Yoneda
open import Cubical.Categories.Site.Sheaf
open import Cubical.Categories.Site.Instances.ZariskiCommRing

open import Cubical.HITs.PropositionalTruncation as PT


open Category hiding (_âˆ˜_)


module _ {â„“ : Level} where

  open Functor
  open NatTrans
  open CommRingStr â¦ƒ...â¦„
  open IsCommRingHom

  Aff : Category (â„“-suc â„“) â„“
  Aff = CommRingsCategory {â„“ = â„“} ^op

  -- using the naming conventions of Demazure & Gabriel
  â„¤Functor = Presheaf Aff â„“
  â„¤FUNCTOR = PresheafCategory Aff â„“

  -- Yoneda in the notation of Demazure & Gabriel,
  -- uses that double op is original category definitionally
  Sp : Functor Aff â„¤FUNCTOR
  Sp = YO

  -- TODO: should probably just be hasUniversalElement
  isAffine : (X : â„¤Functor) â†’ Type (â„“-suc â„“)
  isAffine X = âˆƒ[ A âˆˆ CommRing â„“ ] NatIso (Sp .F-ob A) X
  -- TODO: ğ”¸Â¹ â‰… Sp â„¤[x] and ğ”¾â‚˜ â‰… Sp â„¤[x,xâ»Â¹] â‰… D(x) â†ª ğ”¸Â¹ as first examples of affine schemes

  -- a â„¤-functor that is a sheaf wrt the Zariski coverage is called local
  isLocal : â„¤Functor â†’ Type (â„“-suc â„“)
  isLocal X = isSheaf zariskiCoverage X

  -- the forgetful functor
  -- aka the affine line
  -- (aka the representable of â„¤[x])
  ğ”¸Â¹ : â„¤Functor
  ğ”¸Â¹ = ForgetfulCommRingâ†’Set âˆ˜F fromOpOp

  -- the global sections functor
  ğ“ : Functor â„¤FUNCTOR (CommRingsCategory {â„“ = â„“-suc â„“} ^op)
  fst (F-ob ğ“ X) = X â‡’ ğ”¸Â¹

  -- ring struncture induced by internal ring object ğ”¸Â¹
  N-ob (CommRingStr.0r (snd (F-ob ğ“ X))) A _ = 0r
    where instance _ = A .snd
  N-hom (CommRingStr.0r (snd (F-ob ğ“ X))) Ï† = funExt Î» _ â†’ sym (Ï† .snd .pres0)

  N-ob (CommRingStr.1r (snd (F-ob ğ“ X))) A _ = 1r
    where instance _ = A .snd
  N-hom (CommRingStr.1r (snd (F-ob ğ“ X))) Ï† = funExt Î» _ â†’ sym (Ï† .snd .pres1)

  N-ob ((snd (F-ob ğ“ X) CommRingStr.+ Î±) Î²) A x = Î± .N-ob A x + Î² .N-ob A x
    where instance _ = A .snd
  N-hom ((snd (F-ob ğ“ X) CommRingStr.+ Î±) Î²) {x = A} {y = B} Ï† = funExt path
    where
    instance
      _ = A .snd
      _ = B .snd
    path : âˆ€ x â†’ Î± .N-ob B (X .F-hom Ï† x) + Î² .N-ob B (X .F-hom Ï† x)
               â‰¡ Ï† .fst (Î± .N-ob A x + Î² .N-ob A x)
    path x = Î± .N-ob B (X .F-hom Ï† x) + Î² .N-ob B (X .F-hom Ï† x)
           â‰¡âŸ¨ congâ‚‚ _+_ (funExtâ» (Î± .N-hom Ï†) x) (funExtâ» (Î² .N-hom Ï†) x) âŸ©
             Ï† .fst (Î± .N-ob A x) + Ï† .fst (Î² .N-ob A x)
           â‰¡âŸ¨ sym (Ï† .snd .pres+ _ _) âŸ©
             Ï† .fst (Î± .N-ob A x + Î² .N-ob A x) âˆ

  N-ob ((snd (F-ob ğ“ X) CommRingStr.Â· Î±) Î²) A x = Î± .N-ob A x Â· Î² .N-ob A x
    where instance _ = A .snd
  N-hom ((snd (F-ob ğ“ X) CommRingStr.Â· Î±) Î²) {x = A} {y = B} Ï† = funExt path
    where
    instance
      _ = A .snd
      _ = B .snd
    path : âˆ€ x â†’ Î± .N-ob B (X .F-hom Ï† x) Â· Î² .N-ob B (X .F-hom Ï† x)
               â‰¡ Ï† .fst (Î± .N-ob A x Â· Î² .N-ob A x)
    path x = Î± .N-ob B (X .F-hom Ï† x) Â· Î² .N-ob B (X .F-hom Ï† x)
           â‰¡âŸ¨ congâ‚‚ _Â·_ (funExtâ» (Î± .N-hom Ï†) x) (funExtâ» (Î² .N-hom Ï†) x) âŸ©
             Ï† .fst (Î± .N-ob A x) Â· Ï† .fst (Î² .N-ob A x)
           â‰¡âŸ¨ sym (Ï† .snd .presÂ· _ _) âŸ©
             Ï† .fst (Î± .N-ob A x Â· Î² .N-ob A x) âˆ

  N-ob ((CommRingStr.- snd (F-ob ğ“ X)) Î±) A x = - Î± .N-ob A x
    where instance _ = A .snd
  N-hom ((CommRingStr.- snd (F-ob ğ“ X)) Î±) {x = A} {y = B} Ï† = funExt path
    where
    instance
      _ = A .snd
      _ = B .snd
    path : âˆ€ x â†’ - Î± .N-ob B (X .F-hom Ï† x) â‰¡ Ï† .fst (- Î± .N-ob A x)
    path x = - Î± .N-ob B (X .F-hom Ï† x) â‰¡âŸ¨ cong -_ (funExtâ» (Î± .N-hom Ï†) x) âŸ©
             - Ï† .fst (Î± .N-ob A x)     â‰¡âŸ¨ sym (Ï† .snd .pres- _) âŸ©
             Ï† .fst (- Î± .N-ob A x)     âˆ

  CommRingStr.isCommRing (snd (F-ob ğ“ X)) = makeIsCommRing
    isSetNatTrans
    (Î» _ _ _ â†’ makeNatTransPath (funExtâ‚‚ Î» A _ â†’ A .snd .CommRingStr.+Assoc _ _ _))
    (Î» _ â†’ makeNatTransPath (funExtâ‚‚ Î» A _ â†’ A .snd .CommRingStr.+IdR _))
    (Î» _ â†’ makeNatTransPath (funExtâ‚‚ Î» A _ â†’ A .snd .CommRingStr.+InvR _))
    (Î» _ _ â†’ makeNatTransPath (funExtâ‚‚ Î» A _ â†’ A .snd .CommRingStr.+Comm _ _))
    (Î» _ _ _ â†’ makeNatTransPath (funExtâ‚‚ Î» A _ â†’ A .snd .CommRingStr.Â·Assoc _ _ _))
    (Î» _ â†’ makeNatTransPath (funExtâ‚‚ Î» A _ â†’ A .snd .CommRingStr.Â·IdR _))
    (Î» _ _ _ â†’ makeNatTransPath (funExtâ‚‚ Î» A _ â†’ A .snd .CommRingStr.Â·DistR+ _ _ _))
    (Î» _ _ â†’ makeNatTransPath (funExtâ‚‚ Î» A _ â†’ A .snd .CommRingStr.Â·Comm _ _))

  -- action on natural transformations
  fst (F-hom ğ“ Î±) = Î± â—áµ›_
  pres0 (snd (F-hom ğ“ Î±)) = makeNatTransPath (funExtâ‚‚ Î» _ _ â†’ refl)
  pres1 (snd (F-hom ğ“ Î±)) = makeNatTransPath (funExtâ‚‚ Î» _ _ â†’ refl)
  pres+ (snd (F-hom ğ“ Î±)) _ _ = makeNatTransPath (funExtâ‚‚ Î» _ _ â†’ refl)
  presÂ· (snd (F-hom ğ“ Î±)) _ _ = makeNatTransPath (funExtâ‚‚ Î» _ _ â†’ refl)
  pres- (snd (F-hom ğ“ Î±)) _ = makeNatTransPath (funExtâ‚‚ Î» _ _ â†’ refl)

  -- functoriality of ğ“
  F-id ğ“ = CommRingHomâ‰¡ (funExt Î» _ â†’ makeNatTransPath (funExtâ‚‚ Î» _ _ â†’ refl))
  F-seq ğ“ _ _ = CommRingHomâ‰¡ (funExt Î» _ â†’ makeNatTransPath (funExtâ‚‚ Î» _ _ â†’ refl))



-- There is a coadjunction ğ“ âŠ£áµ¢ Sp
-- (relative to the inclusion i : CommRing â„“ â†’ CommRing (â„“+1))
-- between the "global sections functor" ğ“
-- and the fully-faithful embedding of affines Sp,
-- whose counit is a natural isomorphism
module AdjBij {â„“ : Level} where

  open Functor
  open NatTrans
  open Iso
  open IsCommRingHom

  private module _ {A : CommRing â„“} {X : â„¤Functor {â„“}} where
    _â™­ : CommRingHom A (ğ“ .F-ob X) â†’ X â‡’ Sp .F-ob A
    fst (N-ob (Ï† â™­) B x) a = Ï† .fst a .N-ob B x

    pres0 (snd (N-ob (Ï† â™­) B x)) = cong (Î» y â†’ y .N-ob B x) (Ï† .snd .pres0)
    pres1 (snd (N-ob (Ï† â™­) B x)) = cong (Î» y â†’ y .N-ob B x) (Ï† .snd .pres1)
    pres+ (snd (N-ob (Ï† â™­) B x)) _ _ = cong (Î» y â†’ y .N-ob B x) (Ï† .snd .pres+ _ _)
    presÂ· (snd (N-ob (Ï† â™­) B x)) _ _ = cong (Î» y â†’ y .N-ob B x) (Ï† .snd .presÂ· _ _)
    pres- (snd (N-ob (Ï† â™­) B x)) _ = cong (Î» y â†’ y .N-ob B x) (Ï† .snd .pres- _)

    N-hom (Ï† â™­) Ïˆ = funExt (Î» x â†’ CommRingHomâ‰¡ (funExt Î» a â†’ funExtâ» (Ï† .fst a .N-hom Ïˆ) x))


    -- the other direction is just precomposition modulo Yoneda
    _â™¯ : X â‡’ Sp .F-ob A â†’ CommRingHom A (ğ“ .F-ob X)
    fst (Î± â™¯) a = Î± â—áµ› yonedaá´¾ ğ”¸Â¹ A .inv a

    pres0 (snd (Î± â™¯)) = makeNatTransPath (funExtâ‚‚ Î» B x â†’ Î± .N-ob B x .snd .pres0)
    pres1 (snd (Î± â™¯)) = makeNatTransPath (funExtâ‚‚ Î» B x â†’ Î± .N-ob B x .snd .pres1)
    pres+ (snd (Î± â™¯)) _ _ = makeNatTransPath (funExtâ‚‚ Î» B x â†’ Î± .N-ob B x .snd .pres+ _ _)
    presÂ· (snd (Î± â™¯)) _ _ = makeNatTransPath (funExtâ‚‚ Î» B x â†’ Î± .N-ob B x .snd .presÂ· _ _)
    pres- (snd (Î± â™¯)) _ = makeNatTransPath (funExtâ‚‚ Î» B x â†’ Î± .N-ob B x .snd .pres- _)


    -- the two maps are inverse to each other
    â™­â™¯Id : âˆ€ (Î±  : X â‡’ Sp .F-ob A) â†’ ((Î± â™¯) â™­) â‰¡ Î±
    â™­â™¯Id _ = makeNatTransPath (funExtâ‚‚ Î» _ _ â†’ CommRingHomâ‰¡ (funExt (Î» _ â†’ refl)))

    â™¯â™­Id : âˆ€ (Ï† : CommRingHom A (ğ“ .F-ob X)) â†’ ((Ï† â™­) â™¯) â‰¡ Ï†
    â™¯â™­Id _ = CommRingHomâ‰¡ (funExt Î» _ â†’ makeNatTransPath (funExtâ‚‚ Î» _ _ â†’ refl))


  -- we get a relative adjunction ğ“ âŠ£áµ¢ Sp
  -- with respect to the inclusion i : CommRing â„“ â†’ CommRing (â„“+1)
  ğ“âŠ£SpIso : {A : CommRing â„“} {X : â„¤Functor {â„“}}
          â†’ Iso (CommRingHom A (ğ“ .F-ob X)) (X â‡’ Sp .F-ob A)
  fun ğ“âŠ£SpIso = _â™­
  inv ğ“âŠ£SpIso = _â™¯
  rightInv ğ“âŠ£SpIso = â™­â™¯Id
  leftInv ğ“âŠ£SpIso = â™¯â™­Id

  ğ“âŠ£SpNatâ„¤Functor : {A : CommRing â„“} {X Y : â„¤Functor {â„“}} (Î± : X â‡’ Sp .F-ob A) (Î² : Y â‡’ X)
                  â†’ (Î² â—áµ› Î±) â™¯ â‰¡ (ğ“ .F-hom Î²) âˆ˜cr (Î± â™¯)
  ğ“âŠ£SpNatâ„¤Functor _ _ = CommRingHomâ‰¡ (funExt (Î» _ â†’ makeNatTransPath (funExtâ‚‚ (Î» _ _ â†’ refl))))

  ğ“âŠ£SpNatCommRing : {X : â„¤Functor {â„“}} {A B : CommRing â„“}
                    (Ï† : CommRingHom A (ğ“ .F-ob X)) (Ïˆ : CommRingHom B A)
                  â†’ (Ï† âˆ˜cr Ïˆ) â™­ â‰¡ (Ï† â™­) â—áµ› Sp .F-hom Ïˆ
  ğ“âŠ£SpNatCommRing _ _ = makeNatTransPath (funExtâ‚‚ Î» _ _ â†’ CommRingHomâ‰¡ (funExt (Î» _ â†’ refl)))

  -- the counit is an equivalence
  private
    Îµ : (A : CommRing â„“) â†’ CommRingHom A ((ğ“ âˆ˜F Sp) .F-ob A)
    Îµ A = (idTrans (Sp .F-ob A)) â™¯

  ğ“âŠ£SpCounitEquiv : (A : CommRing â„“) â†’ CommRingEquiv A ((ğ“ âˆ˜F Sp) .F-ob A)
  fst (ğ“âŠ£SpCounitEquiv A) = isoToEquiv theIso
    where
    theIso : Iso (A .fst) ((ğ“ âˆ˜F Sp) .F-ob A .fst)
    fun theIso = Îµ A .fst
    inv theIso = yonedaá´¾ ğ”¸Â¹ A .fun
    rightInv theIso Î± = â„¤FUNCTOR .â‹†IdL _ âˆ™ yonedaá´¾ ğ”¸Â¹ A .leftInv Î±
    leftInv theIso a = path -- I get yellow otherwise
      where
      path : yonedaá´¾ ğ”¸Â¹ A .fun ((idTrans (Sp .F-ob A)) â—áµ› yonedaá´¾ ğ”¸Â¹ A .inv a) â‰¡ a
      path = cong (yonedaá´¾ ğ”¸Â¹ A .fun) (â„¤FUNCTOR .â‹†IdL _) âˆ™ yonedaá´¾ ğ”¸Â¹ A .rightInv a
  snd (ğ“âŠ£SpCounitEquiv A) = Îµ A .snd
