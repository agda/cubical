{-# OPTIONS --cubical --safe #-}
module Cubical.Structures.Group where

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.Equiv
open import Cubical.Foundations.HLevels
open import Cubical.Foundations.FunExtEquiv
open import Cubical.Data.Prod.Base hiding (_Ã—_) renaming (_Ã—Î£_ to _Ã—_)

open import Cubical.Foundations.SIP renaming (SNS-PathP to SNS)

open import Cubical.Structures.InftyMagma hiding (âŸ¨_âŸ©)
open import Cubical.Structures.Monoid hiding (âŸ¨_âŸ©)


private
  variable
    â„“ â„“' : Level

group-axioms : (X : Type â„“) â†’ âˆ-magma-structure X â†’ Type â„“
group-axioms G (_Â·_) = i Ã— ii Ã— iii
  where
  i  = isSet G
  ii = (x y z : G) â†’ (x Â· (y Â· z)) â‰¡ ((x Â· y) Â· z)
  iii = Î£[ e âˆˆ G ] ((x : G) â†’ (x Â· e â‰¡ x) Ã— (e Â· x â‰¡ x)) Ã—
                   ((x : G) â†’ Î£[ x' âˆˆ G ] (x Â· x' â‰¡ e) Ã— (x' Â· x â‰¡ e))                   

group-structure : Type â„“ â†’ Type â„“
group-structure = add-to-structure (âˆ-magma-structure) group-axioms

Groups : Type (â„“-suc â„“)
Groups {â„“} = TypeWithStr â„“ group-structure

-- Extracting components of a group
âŸ¨_âŸ© : Groups {â„“} â†’ Type â„“
âŸ¨ G , _ âŸ© = G

group-operation : (G : Groups {â„“}) â†’ âŸ¨ G âŸ© â†’ âŸ¨ G âŸ© â†’ âŸ¨ G âŸ©
group-operation (_ , f , _) = f

module group-operation-syntax where

  group-operation-syntax : (G : Groups {â„“}) â†’ âŸ¨ G âŸ© â†’ âŸ¨ G âŸ© â†’ âŸ¨ G âŸ©
  group-operation-syntax = group-operation
  infixl 20 group-operation-syntax
  syntax group-operation-syntax G x y = x Â·âŸ¨ G âŸ© y

open group-operation-syntax

group-is-set : (G : Groups {â„“}) â†’ isSet âŸ¨ G âŸ©
group-is-set (_ , _ , P , _) = P

group-assoc : (G : Groups {â„“})
            â†’ (x y z : âŸ¨ G âŸ©) â†’ (x Â·âŸ¨ G âŸ© (y Â·âŸ¨ G âŸ© z)) â‰¡ ((x Â·âŸ¨ G âŸ© y) Â·âŸ¨ G âŸ© z)
group-assoc (_ , _ , _ , P , _) = P

-- Defining identity

id : (G : Groups {â„“}) â†’ âŸ¨ G âŸ©
id (_ , _ , _ , _ , P) = fst P

group-rid : (G : Groups {â„“})
          â†’ (x : âŸ¨ G âŸ©) â†’ x Â·âŸ¨ G âŸ© (id G) â‰¡ x
group-rid (_ , _ , _ , _ , P) x = fst ((fst (snd P)) x)

group-lid : (G : Groups {â„“})
          â†’ (x : âŸ¨ G âŸ©) â†’ (id G) Â·âŸ¨ G âŸ© x â‰¡ x
group-lid (_ , _ , _ , _ , P) x = snd ((fst (snd P)) x)

-- Defining the inverse function
inv : (G : Groups {â„“}) â†’ âŸ¨ G âŸ© â†’ âŸ¨ G âŸ©
inv (_ , _ , _ , _ , P) x = fst ((snd (snd P)) x)

group-rinv : (G : Groups {â„“})
               â†’ (x : âŸ¨ G âŸ©) â†’ x Â·âŸ¨ G âŸ© (inv G x) â‰¡ id G
group-rinv (_ , _ , _ , _ , P) x = fst (snd ((snd (snd P)) x))

group-linv : (G : Groups {â„“})
               â†’ (x : âŸ¨ G âŸ©) â†’ (inv G x) Â·âŸ¨ G âŸ© x â‰¡ id G
group-linv (_ , _ , _ , _ , P) x = snd (snd ((snd (snd P)) x))

-- Additive notation for groups
module additive-notation (G : Groups {â„“}) where

  â‚€ : âŸ¨ G âŸ©
  â‚€ = id G

  _+_ : âŸ¨ G âŸ© â†’ âŸ¨ G âŸ© â†’ âŸ¨ G âŸ©
  _+_ = group-operation G

  -_ : âŸ¨ G âŸ© â†’ âŸ¨ G âŸ©
  -_ = inv G

--Multiplicative notation for groups
module multiplicative-notation (G : Groups {â„“}) where

  â‚ : âŸ¨ G âŸ©
  â‚ = id G

  _Â·_ : âŸ¨ G âŸ© â†’ âŸ¨ G âŸ© â†’ âŸ¨ G âŸ©
  _Â·_ = group-operation G

  _â»Â¹ : âŸ¨ G âŸ© â†’ âŸ¨ G âŸ©
  _â»Â¹ = inv G

  Â·-is-assoc = group-assoc G

  Â·â‚  = group-rid G

  â‚Â·  = group-lid G

  Â·â»Â¹ = group-rinv G

  â»Â¹Â· = group-linv G

-- Iso for groups are those for monoids
group-iso : StrIso group-structure â„“
group-iso = add-to-iso âˆ-magma-structure âˆ-magma-iso group-axioms

-- Group axioms isProp

group-axioms-isProp : (X : Type â„“)
                    â†’ (s : âˆ-magma-structure X)
                    â†’ isProp (group-axioms X s)
group-axioms-isProp X s t = Î· t
  where
  ğ’¢ : Groups
  ğ’¢ = X , s , t

  is-identity : X â†’ Type _
  is-identity e = (x : X) â†’ (x Â·âŸ¨ ğ’¢ âŸ© e â‰¡ x) Ã— (e Â·âŸ¨ ğ’¢ âŸ© x â‰¡ x)

  Î± : (e : X) â†’ isProp (is-identity e)
  Î± e = isPropPi (Î» _ â†’ isPropÎ£ (group-is-set ğ’¢ _ _) (Î» _ â†’ group-is-set ğ’¢ _ _))

  Î² : (e : X) â†’ is-identity e â†’ isProp ((x : X) â†’ Î£[ x' âˆˆ X ] (x Â·âŸ¨ ğ’¢ âŸ© x' â‰¡ e) Ã— (x' Â·âŸ¨ ğ’¢ âŸ© x â‰¡ e))
  Î² e is-identity-e =
   isPropPi Î» { x (x' , _ , P) (x'' , Q , _) â†’ Î£Propâ‰¡ (Î» _ â†’ isPropÎ£ (group-is-set ğ’¢ _ _) Î» _ â†’ group-is-set ğ’¢ _ _)
                                                      (inv-lemma â„³ x x' x'' P Q) }
   where
    â„³ : Monoids
    â„³ = âŸ¨ ğ’¢ âŸ© , (e , group-operation ğ’¢) ,
        group-is-set ğ’¢ ,
        group-assoc ğ’¢ ,
        (Î» x â†’ fst (is-identity-e x)) ,
        (Î» x â†’ snd (is-identity-e x))
                                                      

  Î³ : isProp (Î£[ e âˆˆ X ] ((x : X) â†’ (x Â·âŸ¨ ğ’¢ âŸ© e â‰¡ x) Ã— (e Â·âŸ¨ ğ’¢ âŸ© x â‰¡ x)) Ã—
                         ((x : X) â†’ Î£[ x' âˆˆ X ] (x Â·âŸ¨ ğ’¢ âŸ© x' â‰¡ e) Ã— (x' Â·âŸ¨ ğ’¢ âŸ© x â‰¡ e)))
  Î³ (e , P , _) (e' , Q , _) = Î£Propâ‰¡ (Î» e â†’ isPropÎ£ (Î± e) Î» is-identity-e â†’ Î² e is-identity-e)
                                      (e          â‰¡âŸ¨ sym (fst (Q e)) âŸ©
                                      e Â·âŸ¨ ğ’¢ âŸ© e' â‰¡âŸ¨ snd (P e') âŸ©
                                      e' âˆ)

  Î· : isProp (group-axioms X s)
  Î· = isPropÎ£ isPropIsSet
      Î» _ â†’ isPropÎ£ (isPropPi (Î» x â†’ isPropPi (Î» y â†’ isPropPi (Î» z â†’ group-is-set ğ’¢ _ _))))
      Î» _ â†’ Î³

-- Group paths equivalent to equality
group-is-SNS : SNS {â„“} group-structure group-iso
group-is-SNS = add-axioms-SNS âˆ-magma-structure âˆ-magma-iso
               group-axioms group-axioms-isProp âˆ-magma-is-SNS

GroupPath : (M N : Groups {â„“}) â†’ (M â‰ƒ[ group-iso ] N) â‰ƒ (M â‰¡ N)
GroupPath M N = SIP group-structure group-iso group-is-SNS M N

-- TODO: SEPARATE TE FOLLOWING TO DIFFERENT FILES
---------------------------------------------------------------------
-- Groups basic properties 
---------------------------------------------------------------------

-- We will use the multiplicative notation for groups

module _ (G : Groups {â„“}) where

  open multiplicative-notation G

  id-is-unique : isContr (Î£[ x âˆˆ âŸ¨ G âŸ© ] âˆ€ (y : âŸ¨ G âŸ©) â†’ (x Â·âŸ¨ G âŸ© y â‰¡ y) Ã— (y Â·âŸ¨ G âŸ© x â‰¡ y))
  id-is-unique = (â‚ , Î» y â†’ â‚Â· y , Â·â‚ y) ,
                 Î» { (e , is-unit) â†’ Î£Propâ‰¡ (Î» x â†’ isPropPi Î» y â†’ isPropÎ£ (group-is-set G _ _)
                                                                    Î» _ â†’    group-is-set G _ _)
                                              (â‚          â‰¡âŸ¨ sym (snd (is-unit (id G))) âŸ©
                                               â‚ Â·âŸ¨ G âŸ© e â‰¡âŸ¨ â‚Â· e âŸ©
                                               e âˆ
                                              )}

  are-inverses : âˆ€ (x y : âŸ¨ G âŸ©)
               â†’ x Â· y â‰¡ â‚
               â†’ (y â‰¡ x â»Â¹) Ã— (x â‰¡ y â»Â¹)
  are-inverses x y eq = (y                â‰¡âŸ¨ sym (â‚Â· y) âŸ©
                         â‚ Â· y            â‰¡âŸ¨ sym (Â·-is-assoc _ _ _ âˆ™ cong (_Â· y) (â»Â¹Â· _)) âŸ©
                         (x â»Â¹) Â· (x Â· y) â‰¡âŸ¨ cong ((x â»Â¹) Â·_) eq âŸ©
                         (x â»Â¹) Â· â‚       â‰¡âŸ¨ Â·â‚ _ âŸ©
                         x â»Â¹ âˆ)
                     , (x                 â‰¡âŸ¨ sym (Â·â‚ x) âŸ©
                        x Â· â‚             â‰¡âŸ¨ cong (x Â·_) (sym (Â·â»Â¹ y)) âˆ™ Â·-is-assoc _ _ _ âŸ©
                        (x Â· y) Â· (y â»Â¹)  â‰¡âŸ¨ cong (_Â· (y â»Â¹)) eq âŸ©
                        â‚ Â· (y â»Â¹)        â‰¡âŸ¨ â‚Â· _ âŸ©
                        y â»Â¹ âˆ)

  inv-involutive : âˆ€ (x : âŸ¨ G âŸ©)
                 â†’ (x â»Â¹) â»Â¹ â‰¡ x
  inv-involutive x = sym (snd (are-inverses x (x â»Â¹) (Â·â»Â¹ x)))

  inv-distr : âˆ€ (x y : âŸ¨ G âŸ©) â†’ (x Â· y) â»Â¹ â‰¡ (y â»Â¹) Â· (x â»Â¹)
  inv-distr x y = sym (fst (are-inverses _ _ Î³))
    where Î³ : (x Â· y) Â· ((y â»Â¹) Â· (x â»Â¹)) â‰¡ â‚
          Î³ = (x Â· y) Â· ((y â»Â¹) Â· (x â»Â¹)) â‰¡âŸ¨ sym (cong (x Â·_) (sym (Â·-is-assoc _ _ _)) âˆ™ Â·-is-assoc _ _ _) âŸ©
              x Â· ((y Â· (y â»Â¹)) Â· (x â»Â¹)) â‰¡âŸ¨ cong (Î» - â†’ x Â· (- Â· (x â»Â¹))) (Â·â»Â¹ y) âŸ©
              x Â· (â‚ Â· (x â»Â¹))            â‰¡âŸ¨ cong (x Â·_) (â‚Â· (x â»Â¹)) âŸ©
              x Â· (x â»Â¹)                  â‰¡âŸ¨ Â·â»Â¹ x âŸ©
              â‚ âˆ

  left-cancel : âˆ€ (x y z : âŸ¨ G âŸ©) â†’ x Â· y â‰¡ x Â· z â†’ y â‰¡ z
  left-cancel x y z eq = y                â‰¡âŸ¨ sym (cong (_Â· y) (â»Â¹Â· x) âˆ™ â‚Â· y) âŸ©
                         ((x â»Â¹) Â· x) Â· y â‰¡âŸ¨ sym (Â·-is-assoc _ _ _) âŸ©
                         (x â»Â¹) Â· (x Â· y) â‰¡âŸ¨ cong ((x â»Â¹) Â·_) eq âŸ©
                         (x â»Â¹) Â· (x Â· z) â‰¡âŸ¨ Â·-is-assoc _ _ _ âŸ©
                         ((x â»Â¹) Â· x) Â· z â‰¡âŸ¨ cong (_Â· z) (â»Â¹Â· x) âˆ™ â‚Â· z âŸ©
                         z âˆ
                         
  right-cancel : âˆ€ (x y z : âŸ¨ G âŸ©) â†’ x Â· z â‰¡ y Â· z â†’ x â‰¡ y
  right-cancel x y z eq = x                â‰¡âŸ¨ sym (cong (x Â·_) (Â·â»Â¹ z) âˆ™ Â·â‚ x) âŸ©
                          x Â· (z Â· (z â»Â¹)) â‰¡âŸ¨ Â·-is-assoc _ _ _ âŸ©
                          (x Â· z) Â· (z â»Â¹) â‰¡âŸ¨ cong (_Â· (z â»Â¹)) eq âŸ©
                          (y Â· z) Â· (z â»Â¹) â‰¡âŸ¨ sym (Â·-is-assoc _ _ _) âŸ©
                          y Â· (z Â· (z â»Â¹)) â‰¡âŸ¨ cong (y Â·_) (Â·â»Â¹ z) âˆ™ Â·â‚ y âŸ©
                          y âˆ

{-  -- Potencias


  _^+_ : âŸ¨ G âŸ© â†’ â„• â†’ âŸ¨ G âŸ©
  _^+_ x 0       = â‚
  _^+_ x (suc n) = x Â· (_^+_ x n)

  ^+-+ : âˆ€ (x : âŸ¨ G âŸ©) (n m : â„•) â†’  (x ^+ n) Â· (x ^+ m) â‰¡ x ^+ (n +â„• m)
  ^+-+ x zero m    = â‚Â· _
  ^+-+ x (suc n) m = (x Â· (x ^+ n)) Â· (x ^+ m) â‰¡âŸ¨ sym (Â·-is-assoc _ _ _) âŸ©
                     x Â· ((x ^+ n) Â· (x ^+ m)) â‰¡âŸ¨ cong (x Â·_) IH âŸ©
                     x Â· (x ^+ (n +â„• m)) âˆ
    where IH : (x ^+ n) Â· (x ^+ m) â‰¡ x ^+ (n +â„• m)
          IH = ^+-+ x n m

  _^-_ : âŸ¨ G âŸ© â†’ â„• â†’ âŸ¨ G âŸ©
  x ^- m = (x â»Â¹) ^+ m

  ^--+ : âˆ€ (x : âŸ¨ G âŸ©) (n m : â„•) â†’  (x ^- n) Â· (x ^- m) â‰¡ x ^- (n +â„• m)
  ^--+ x = ^+-+ (x â»Â¹)

  _^_ : âŸ¨ G âŸ© â†’ â„¤ â†’ âŸ¨ G âŸ©
  x ^ pos zero = â‚
  x ^ pos (suc n) = x Â· (x ^ (pos n))
  x ^ neg n = (x â»Â¹) ^+ n
  x ^ posneg i = â‚

  ^-+ : âˆ€ (x : âŸ¨ G âŸ©) (n m : â„¤) â†’  (x ^ n) Â· (x ^ m) â‰¡ x ^ (n +â„¤ m)
  ^-+ x n (pos zero) = Â·â‚ _
  ^-+ x n (pos (suc m)) = {!!}
  ^-+ x n (neg zero) = Â·â‚ _
  ^-+ x n (neg (suc m)) = {!!}
  ^-+ x n (posneg i) = Â·â‚ (x ^ n)
-}

