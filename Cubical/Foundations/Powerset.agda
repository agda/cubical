{-

This file introduces the "powerset" of a type in the style of
EscardÃ³'s lecture notes:

https://www.cs.bham.ac.uk/~mhe/HoTT-UF-in-Agda-Lecture-Notes/HoTT-UF-Agda.html#propositionalextensionality

-}
{-# OPTIONS --safe #-}
module Cubical.Foundations.Powerset where

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.Equiv
open import Cubical.Foundations.HLevels
open import Cubical.Foundations.Isomorphism
open import Cubical.Foundations.Structure
open import Cubical.Foundations.Function
open import Cubical.Foundations.Univalence using (hPropExt)

open import Cubical.Data.Sigma
open import Cubical.Data.Unit
open import Cubical.Data.Empty

open import Cubical.HITs.PropositionalTruncation.Base

open import Cubical.Relation.Nullary using (Â¬_)

private
  variable
    â„“ : Level
    X : Type â„“

â„™ : Type â„“ â†’ Type (â„“-suc â„“)
â„™ X = X â†’ hProp _

isSetâ„™ : isSet (â„™ X)
isSetâ„™ = isSetÎ  Î» x â†’ isSetHProp

infix 5 _âˆˆ_

_âˆˆ_ : {X : Type â„“} â†’ X â†’ â„™ X â†’ Type â„“
x âˆˆ A = âŸ¨ A x âŸ©

_âŠ†_ : {X : Type â„“} â†’ â„™ X â†’ â„™ X â†’ Type â„“
A âŠ† B = âˆ€ x â†’ x âˆˆ A â†’ x âˆˆ B

âˆˆ-isProp : (A : â„™ X) (x : X) â†’ isProp (x âˆˆ A)
âˆˆ-isProp A = snd âˆ˜ A

infix 5 _âˆˆâ‚š_

-- x âˆˆâ‚š A differs from x âˆˆ A
-- in the sense that it produces an hProp
_âˆˆâ‚š_ : {X : Type â„“} â†’ X â†’ â„™ X â†’ hProp â„“
x âˆˆâ‚š A = x âˆˆ A , âˆˆ-isProp A x

-- "not in" relations

infix 5 _âˆ‰_
_âˆ‰_ : {X : Type â„“} â†’ X â†’ â„™ X â†’ Type â„“
x âˆ‰ A = Â¬ x âˆˆ A

âˆ‰-isProp : (A : â„™ X) (x : X) â†’ isProp (x âˆ‰ A)
âˆ‰-isProp A x = isPropÎ  Î» _ â†’ isPropâŠ¥

infix 5 _âˆ‰â‚š_
_âˆ‰â‚š_ : {X : Type â„“} â†’ X â†’ â„™ X â†’ hProp â„“
x âˆ‰â‚š A = x âˆ‰ A , âˆ‰-isProp A x

âŠ†-isProp : (A B : â„™ X) â†’ isProp (A âŠ† B)
âŠ†-isProp A B = isPropÎ 2 (Î» x _ â†’ âˆˆ-isProp B x)

âŠ†-refl : (A : â„™ X) â†’ A âŠ† A
âŠ†-refl A x = idfun (x âˆˆ A)

subst-âˆˆ : (A : â„™ X) {x y : X} â†’ x â‰¡ y â†’ x âˆˆ A â†’ y âˆˆ A
subst-âˆˆ A = subst (_âˆˆ A)

âŠ†-refl-consequence : (A B : â„™ X) â†’ A â‰¡ B â†’ (A âŠ† B) Ã— (B âŠ† A)
âŠ†-refl-consequence A B p = subst (A âŠ†_) p (âŠ†-refl A)
                         , subst (B âŠ†_) (sym p) (âŠ†-refl B)

âŠ†-extensionality : (A B : â„™ X) â†’ (A âŠ† B) Ã— (B âŠ† A) â†’ A â‰¡ B
âŠ†-extensionality A B (Ï† , Ïˆ) =
  funExt (Î» x â†’ TypeOfHLevelâ‰¡ 1 (hPropExt (A x .snd) (B x .snd) (Ï† x) (Ïˆ x)))

âŠ†-extensionalityEquiv : (A B : â„™ X) â†’ (A âŠ† B) Ã— (B âŠ† A) â‰ƒ (A â‰¡ B)
âŠ†-extensionalityEquiv A B = isoToEquiv (iso (âŠ†-extensionality A B)
                                            (âŠ†-refl-consequence A B)
                                            (Î» _ â†’ isSetâ„™ A B _ _)
                                            (Î» _ â†’ isPropÎ£ (âŠ†-isProp A B) (Î» _ â†’ âŠ†-isProp B A) _ _))

-- Binary Intersections
infix 6 _âˆ©_

-- Unfortunately, simply importing Cubical.Data.Sum, Cubical.HITs.PropositionalTruncation and
-- Cubical.Functions.Logic indirectly imports Cubical.Functions.Embeddings which
-- creates a circular dependency
-- For now, we will have to repeat a few definitions
-- We mark all these as private to ensure that no name clashes take place

_âˆ©_ : â„™ X â†’ â„™ X â†’ â„™ X
A âˆ© B = Î» x â†’ A x âŠ“ B x where
              -- Repeated from Cubical.Functions.Logic
              _âŠ“_ : hProp â„“ â†’ hProp â„“ â†’ hProp â„“
              a âŠ“ b = âŸ¨ a âŸ© Ã— âŸ¨ b âŸ© , isOfHLevelÎ£ 1 (a .snd) Î» _ â†’ b .snd 

-- Repeated from Cubical.Data.Sum
private data _âŠ_ (A B : Type â„“) : Type â„“ where
  inl : A â†’ A âŠ B
  inr : B â†’ A âŠ B

-- Repeated from Cubical.Functions.Logic
âˆ¥_âˆ¥â‚š : (X : Type â„“) â†’ hProp â„“
âˆ¥ X âˆ¥â‚š = âˆ¥ X âˆ¥â‚ , squashâ‚

-- Binary Unions
infix 6 _âˆª_

_âˆª_ : â„™ X â†’ â„™ X â†’ â„™ X
A âˆª B = Î» x â†’ A x âŠ” B x where
              -- Repeated from Cubical.Functions.Logic
              _âŠ”_ : hProp â„“ â†’ hProp â„“ â†’ hProp â„“
              a âŠ” b = âˆ¥ âŸ¨ a âŸ© âŠ  âŸ¨ b âŸ© âˆ¥â‚š 

-- When defining union and intersection over families
-- we define an implicit argument X instead of using the module private metavariable
-- since Agda is unable to infer that â„“-max â„“ X.â„“ = â„“

-- Indexed Unions
â‹ƒ : {X I : Type â„“} â†’ (I â†’ â„™ X) â†’ â„™ X
â‹ƒ {I = I} family = Î» x â†’ âˆ¥ Î£[ i âˆˆ I ] (x âˆˆ family i) âˆ¥â‚š 

-- Indexed Intersections
â‹‚ : {X I : Type â„“} â†’ (I â†’ â„™ X) â†’ â„™ X
â‹‚ {I = I} family = Î» x â†’ âˆ¥ (âˆ€ i â†’ (x âˆˆ family i)) âˆ¥â‚š 

-- Empty subset
âˆ… : â„™ X
âˆ… x = âŠ¥* , isPropâŠ¥*

-- Total subset
ğ•‹ : â„™ X
ğ•‹ = Î» x â†’ âˆ¥ Lift Unit âˆ¥â‚š 

infixl 7 ï½_

ï½_ : â„™ X â†’ â„™ X
ï½ A = Î» x â†’ x âˆ‰â‚š A

-- The type of inhabited subsets
-- Inspired by the same definition in [martinescardo/TypeTopology](https://www.cs.bham.ac.uk/~mhe/TypeTopology/UF.Powerset.html#3251)

-- A subset A is inhabited if there merely exists an x such that x âˆˆ A
isInhabited : {X : Type â„“} â†’ â„™ X â†’ hProp â„“
isInhabited {X = X} A = âˆ¥ (Î£[ x âˆˆ X ] x âˆˆ A) âˆ¥â‚š

â„™âº : Type â„“ â†’ Type (â„“-suc â„“)
â„™âº X = Î£[ A âˆˆ â„™ X ] âŸ¨ isInhabited A âŸ©
