{-# OPTIONS --safe #-}
module Cubical.Algebra.Algebra.Base where

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.Equiv
open import Cubical.Foundations.Equiv.HalfAdjoint
open import Cubical.Foundations.HLevels
open import Cubical.Foundations.Isomorphism
open import Cubical.Foundations.Function
open import Cubical.Foundations.SIP

open import Cubical.Data.Sigma

open import Cubical.Displayed.Base
open import Cubical.Displayed.Auto
open import Cubical.Displayed.Record
open import Cubical.Displayed.Universe

open import Cubical.Reflection.RecordEquiv

open import Cubical.Algebra.Module
open import Cubical.Algebra.Ring
open import Cubical.Algebra.AbGroup
open import Cubical.Algebra.Group
open import Cubical.Algebra.Monoid

open Iso

private
  variable
    â„“ â„“' â„“'' â„“''' : Level

record IsAlgebra (R : Ring â„“) {A : Type â„“'}
                 (0a 1a : A) (_+_ _Â·_ : A â†’ A â†’ A) (-_ : A â†’ A)
                 (_â‹†_ : âŸ¨ R âŸ© â†’ A â†’ A) : Type (â„“-max â„“ â„“') where

  constructor isalgebra

  open RingStr (snd R) using (1r) renaming (_+_ to _+r_; _Â·_ to _Â·r_)

  field
    isLeftModule : IsLeftModule R 0a _+_ -_ _â‹†_
    Â·-isMonoid  : IsMonoid 1a _Â·_
    dist        : (x y z : A) â†’ (x Â· (y + z) â‰¡ (x Â· y) + (x Â· z))
                              Ã— ((x + y) Â· z â‰¡ (x Â· z) + (y Â· z))
    â‹†-lassoc     : (r : âŸ¨ R âŸ©) (x y : A) â†’ (r â‹† x) Â· y â‰¡ r â‹† (x Â· y)
    â‹†-rassoc     : (r : âŸ¨ R âŸ©) (x y : A) â†’ r â‹† (x Â· y) â‰¡ x Â· (r â‹† y)

  open IsLeftModule isLeftModule public

  isRing : IsRing _ _ _ _ _
  isRing = isring (IsLeftModule.+-isAbGroup isLeftModule) Â·-isMonoid dist
  open IsRing isRing public hiding (_-_; +Assoc; +Lid; +Linv; +Rid; +Rinv; +Comm)

unquoteDecl IsAlgebraIsoÎ£ = declareRecordIsoÎ£ IsAlgebraIsoÎ£ (quote IsAlgebra)

record AlgebraStr (R : Ring â„“) (A : Type â„“') : Type (â„“-max â„“ â„“') where

  constructor algebrastr

  field
    0a             : A
    1a             : A
    _+_            : A â†’ A â†’ A
    _Â·_            : A â†’ A â†’ A
    -_             : A â†’ A
    _â‹†_            : âŸ¨ R âŸ© â†’ A â†’ A
    isAlgebra      : IsAlgebra R 0a 1a _+_ _Â·_ -_ _â‹†_

  open IsAlgebra isAlgebra public

Algebra : (R : Ring â„“) â†’ âˆ€ â„“' â†’ Type (â„“-max â„“ (â„“-suc â„“'))
Algebra R â„“' = Î£[ A âˆˆ Type â„“' ] AlgebraStr R A

module commonExtractors {R : Ring â„“} where

  Algebraâ†’Module : (A : Algebra R â„“') â†’ LeftModule R â„“'
  Algebraâ†’Module (_ , algebrastr A _ _ _ _ _ (isalgebra isLeftModule _ _ _ _)) =
    _ , leftmodulestr A _ _ _ isLeftModule

  Algebraâ†’Ring : (A : Algebra R â„“') â†’ Ring â„“'
  Algebraâ†’Ring (_ , str) = _ , ringstr _ _ _ _ _ (IsAlgebra.isRing (AlgebraStr.isAlgebra str))

  Algebraâ†’AbGroup : (A : Algebra R â„“') â†’ AbGroup â„“'
  Algebraâ†’AbGroup A = LeftModuleâ†’AbGroup (Algebraâ†’Module A)

  Algebraâ†’Monoid : (A : Algebra R â„“') â†’ Monoid â„“'
  Algebraâ†’Monoid A = Ringâ†’Monoid (Algebraâ†’Ring A)

  isSetAlgebra : (A : Algebra R â„“') â†’ isSet âŸ¨ A âŸ©
  isSetAlgebra A = isSetAbGroup (Algebraâ†’AbGroup A)

  open RingStr (snd R) using (1r; Â·Ldist+) renaming (_+_ to _+r_; _Â·_ to _Â·s_)

  makeIsAlgebra : {A : Type â„“'} {0a 1a : A}
                  {_+_ _Â·_ : A â†’ A â†’ A} { -_ : A â†’ A} {_â‹†_ : âŸ¨ R âŸ© â†’ A â†’ A}
                  (isSet-A : isSet A)
                  (+-assoc :  (x y z : A) â†’ x + (y + z) â‰¡ (x + y) + z)
                  (+-rid : (x : A) â†’ x + 0a â‰¡ x)
                  (+-rinv : (x : A) â†’ x + (- x) â‰¡ 0a)
                  (+-comm : (x y : A) â†’ x + y â‰¡ y + x)
                  (Â·-assoc :  (x y z : A) â†’ x Â· (y Â· z) â‰¡ (x Â· y) Â· z)
                  (Â·-rid : (x : A) â†’ x Â· 1a â‰¡ x)
                  (Â·-lid : (x : A) â†’ 1a Â· x â‰¡ x)
                  (Â·-rdist-+ : (x y z : A) â†’ x Â· (y + z) â‰¡ (x Â· y) + (x Â· z))
                  (Â·-ldist-+ : (x y z : A) â†’ (x + y) Â· z â‰¡ (x Â· z) + (y Â· z))
                  (â‹†-assoc : (r s : âŸ¨ R âŸ©) (x : A) â†’ (r Â·s s) â‹† x â‰¡ r â‹† (s â‹† x))
                  (â‹†-ldist : (r s : âŸ¨ R âŸ©) (x : A) â†’ (r +r s) â‹† x â‰¡ (r â‹† x) + (s â‹† x))
                  (â‹†-rdist : (r : âŸ¨ R âŸ©) (x y : A) â†’ r â‹† (x + y) â‰¡ (r â‹† x) + (r â‹† y))
                  (â‹†-lid   : (x : A) â†’ 1r â‹† x â‰¡ x)
                  (â‹†-lassoc : (r : âŸ¨ R âŸ©) (x y : A) â†’ (r â‹† x) Â· y â‰¡ r â‹† (x Â· y))
                  (â‹†-rassoc : (r : âŸ¨ R âŸ©) (x y : A) â†’ r â‹† (x Â· y) â‰¡ x Â· (r â‹† y))
                â†’ IsAlgebra R 0a 1a _+_ _Â·_ -_ _â‹†_
  makeIsAlgebra isSet-A
                +-assoc +-rid +-rinv +-comm
                Â·-assoc Â·-rid Â·-lid Â·-rdist-+ Â·-ldist-+
                â‹†-assoc â‹†-ldist â‹†-rdist â‹†-lid â‹†-lassoc â‹†-rassoc =
                isalgebra
                  (makeIsLeftModule isSet-A
                                    +-assoc +-rid +-rinv +-comm
                                    â‹†-assoc â‹†-ldist â‹†-rdist â‹†-lid)
                  (makeIsMonoid isSet-A Â·-assoc Â·-rid Â·-lid)
                  (Î» x y z â†’ Â·-rdist-+ x y z , Â·-ldist-+ x y z)
                  â‹†-lassoc â‹†-rassoc


open commonExtractors public

record IsAlgebraHom {R : Ring â„“} {A : Type â„“'} {B : Type â„“''}
  (M : AlgebraStr R A) (f : A â†’ B) (N : AlgebraStr R B)
  : Type (â„“-max â„“ (â„“-max â„“' â„“''))
  where

  -- Shorter qualified names
  private
    module M = AlgebraStr M
    module N = AlgebraStr N

  field
    pres0 : f M.0a â‰¡ N.0a
    pres1 : f M.1a â‰¡ N.1a
    pres+ : (x y : A) â†’ f (x M.+ y) â‰¡ f x N.+ f y
    presÂ· : (x y : A) â†’ f (x M.Â· y) â‰¡ f x N.Â· f y
    pres- : (x : A) â†’ f (M.- x) â‰¡ N.- (f x)
    presâ‹† : (r : âŸ¨ R âŸ©) (y : A) â†’ f (r M.â‹† y) â‰¡ r N.â‹† f y

open IsAlgebraHom

AlgebraHom : {R : Ring â„“} (M : Algebra R â„“') (N : Algebra R â„“'') â†’ Type (â„“-max â„“ (â„“-max â„“' â„“''))
AlgebraHom M N = Î£[ f âˆˆ (âŸ¨ M âŸ© â†’ âŸ¨ N âŸ©) ] IsAlgebraHom (M .snd) f (N .snd)

IsAlgebraEquiv : {R : Ring â„“} {A B : Type â„“'}
  (M : AlgebraStr R A) (e : A â‰ƒ B) (N : AlgebraStr R B)
  â†’ Type (â„“-max â„“ â„“')
IsAlgebraEquiv M e N = IsAlgebraHom M (e .fst) N

AlgebraEquiv : {R : Ring â„“} (M N : Algebra R â„“') â†’ Type (â„“-max â„“ â„“')
AlgebraEquiv M N = Î£[ e âˆˆ âŸ¨ M âŸ© â‰ƒ âŸ¨ N âŸ© ] IsAlgebraEquiv (M .snd) e (N .snd)

_$a_ : {R : Ring â„“} {A : Algebra R â„“'} {B : Algebra R â„“''} â†’ AlgebraHom A B â†’ âŸ¨ A âŸ© â†’ âŸ¨ B âŸ©
f $a x = fst f x

isPropIsAlgebra : (R : Ring â„“) {A : Type â„“'}
  (0a 1a : A)
  (_+_ _Â·_ : A â†’ A â†’ A)
  (-_ : A â†’ A)
  (_â‹†_ : âŸ¨ R âŸ© â†’ A â†’ A)
  â†’ isProp (IsAlgebra R 0a 1a _+_ _Â·_ -_ _â‹†_)
isPropIsAlgebra R _ _ _ _ _ _ =
  isOfHLevelRetractFromIso 1 IsAlgebraIsoÎ£
    (isPropÎ£
      (isPropIsLeftModule _ _ _ _ _)
      (Î» mo â†’
        isPropÃ— (isPropIsMonoid _ _)
          (isPropÃ— (isPropÎ 3 Î» _ _ _ â†’ isPropÃ— (mo .is-set _ _) (mo .is-set _ _))
            (isPropÃ— (isPropÎ 3 Î» _ _ _ â†’ mo .is-set _ _)
              (isPropÎ 3 Î» _ _ _ â†’ mo .is-set _ _)))))
  where
  open IsLeftModule

ğ’®á´°-Algebra : (R : Ring â„“) â†’ DUARel (ğ’®-Univ â„“') (AlgebraStr R) (â„“-max â„“ â„“')
ğ’®á´°-Algebra R =
  ğ’®á´°-Record (ğ’®-Univ _) (IsAlgebraEquiv {R = R})
    (fields:
      data[ 0a âˆ£ nul âˆ£ pres0 ]
      data[ 1a âˆ£ nul âˆ£ pres1 ]
      data[ _+_ âˆ£ bin âˆ£ pres+ ]
      data[ _Â·_ âˆ£ bin âˆ£ presÂ· ]
      data[ -_ âˆ£ autoDUARel _ _ âˆ£ pres- ]
      data[ _â‹†_ âˆ£ autoDUARel _ _ âˆ£ presâ‹† ]
      prop[ isAlgebra âˆ£ (Î» _ _ â†’ isPropIsAlgebra _ _ _ _ _ _ _) ])
  where
  open AlgebraStr

  -- faster with some sharing
  nul = autoDUARel (ğ’®-Univ _) (Î» A â†’ A)
  bin = autoDUARel (ğ’®-Univ _) (Î» A â†’ A â†’ A â†’ A)

AlgebraPath : {R : Ring â„“} (A B : Algebra R â„“') â†’ (AlgebraEquiv A B) â‰ƒ (A â‰¡ B)
AlgebraPath {R = R} = âˆ« (ğ’®á´°-Algebra R) .UARel.ua

compIsAlgebraHom : {R : Ring â„“} {A : Algebra R â„“'} {B : Algebra R â„“''} {C : Algebra R â„“'''}
  {g : âŸ¨ B âŸ© â†’ âŸ¨ C âŸ©} {f : âŸ¨ A âŸ© â†’ âŸ¨ B âŸ©}
  â†’ IsAlgebraHom (B .snd) g (C .snd)
  â†’ IsAlgebraHom (A .snd) f (B .snd)
  â†’ IsAlgebraHom (A .snd) (g âˆ˜ f) (C .snd)
compIsAlgebraHom {g = g} {f} gh fh .pres0 = cong g (fh .pres0) âˆ™ gh .pres0
compIsAlgebraHom {g = g} {f} gh fh .pres1 = cong g (fh .pres1) âˆ™ gh .pres1
compIsAlgebraHom {g = g} {f} gh fh .pres+ x y = cong g (fh .pres+ x y) âˆ™ gh .pres+ (f x) (f y)
compIsAlgebraHom {g = g} {f} gh fh .presÂ· x y = cong g (fh .presÂ· x y) âˆ™ gh .presÂ· (f x) (f y)
compIsAlgebraHom {g = g} {f} gh fh .pres- x = cong g (fh .pres- x) âˆ™ gh .pres- (f x)
compIsAlgebraHom {g = g} {f} gh fh .presâ‹† r x = cong g (fh .presâ‹† r x) âˆ™ gh .presâ‹† r (f x)

_âˆ˜a_ : {R : Ring â„“} {A : Algebra R â„“'} {B : Algebra R â„“''} {C : Algebra R â„“'''}
       â†’ AlgebraHom B C â†’ AlgebraHom A B â†’ AlgebraHom A C
_âˆ˜a_  g f .fst = g .fst âˆ˜ f .fst
_âˆ˜a_  g f .snd = compIsAlgebraHom (g .snd) (f .snd)

module AlgebraTheory (R : Ring â„“) (A : Algebra R â„“') where
  open RingStr (snd R) renaming (_+_ to _+r_)
  open AlgebraStr (A .snd)

  0-actsNullifying : (x : âŸ¨ A âŸ©) â†’ 0r â‹† x â‰¡ 0a
  0-actsNullifying x =
    let idempotent-+ = 0r â‹† x              â‰¡âŸ¨ cong (Î» u â†’ u â‹† x) (sym (RingTheory.0Idempotent R)) âŸ©
                       (0r +r 0r) â‹† x      â‰¡âŸ¨ â‹†-ldist 0r 0r x âŸ©
                       (0r â‹† x) + (0r â‹† x) âˆ
    in RingTheory.+Idempotencyâ†’0 (Algebraâ†’Ring A) (0r â‹† x) idempotent-+
