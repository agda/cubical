{-# OPTIONS --safe #-}
{-
Module in which commutative monoids are defined.
-}
module Cubical.Algebra.CommMonoid.Base where

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.Equiv
open import Cubical.Foundations.HLevels
open import Cubical.Foundations.Isomorphism
open import Cubical.Foundations.SIP

open import Cubical.Data.Sigma

open import Cubical.Algebra.Semigroup
open import Cubical.Algebra.Monoid.Base

open import Cubical.Displayed.Base
open import Cubical.Displayed.Auto
open import Cubical.Displayed.Record
open import Cubical.Displayed.Universe

open import Cubical.Reflection.RecordEquiv

open Iso

private
  variable
    â„“ â„“' : Level

record IsCommMonoid {M : Type â„“}
                    (Îµ : M) (_Â·_ : M â†’ M â†’ M) : Type â„“ where
  constructor iscommmonoid

  field
    isMonoid : IsMonoid Îµ _Â·_
    comm     : (x y : M) â†’ x Â· y â‰¡ y Â· x

  open IsMonoid isMonoid public

unquoteDecl IsCommMonoidIsoÎ£ = declareRecordIsoÎ£ IsCommMonoidIsoÎ£ (quote IsCommMonoid)

record CommMonoidStr (M : Type â„“) : Type â„“ where
  constructor commmonoidstr

  field
    Îµ            : M
    _Â·_          : M â†’ M â†’ M
    isCommMonoid : IsCommMonoid Îµ _Â·_

  infixl 7 _Â·_

  open IsCommMonoid isCommMonoid public

CommMonoid : âˆ€ â„“ â†’ Type (â„“-suc â„“)
CommMonoid â„“ = TypeWithStr â„“ CommMonoidStr

makeIsCommMonoid : {M : Type â„“} {Îµ : M} {_Â·_ : M â†’ M â†’ M}
                 (is-setM : isSet M)
                 (assoc   : (x y z : M) â†’ x Â· (y Â· z) â‰¡ (x Â· y) Â· z)
                 (rid     : (x : M) â†’ x Â· Îµ â‰¡ x)
                 (comm    : (x y : M) â†’ x Â· y â‰¡ y Â· x)
               â†’ IsCommMonoid Îµ _Â·_
IsCommMonoid.isMonoid (makeIsCommMonoid is-setM assoc rid comm) =
  makeIsMonoid is-setM assoc rid (Î» x â†’ comm _ _ âˆ™ rid x)
IsCommMonoid.comm (makeIsCommMonoid is-setM assoc rid comm) = comm

makeCommMonoid : {M : Type â„“} (Îµ : M) (_Â·_ : M â†’ M â†’ M)
               (is-setM : isSet M)
               (assoc : (x y z : M) â†’ x Â· (y Â· z) â‰¡ (x Â· y) Â· z)
               (rid : (x : M) â†’ x Â· Îµ â‰¡ x)
               (comm    : (x y : M) â†’ x Â· y â‰¡ y Â· x)
             â†’ CommMonoid â„“
fst (makeCommMonoid Îµ _Â·_ is-setM assoc rid comm) = _
CommMonoidStr.Îµ (snd (makeCommMonoid Îµ _Â·_ is-setM assoc rid comm)) = Îµ
CommMonoidStr._Â·_ (snd (makeCommMonoid Îµ _Â·_ is-setM assoc rid comm)) = _Â·_
CommMonoidStr.isCommMonoid (snd (makeCommMonoid Îµ _Â·_ is-setM assoc rid comm)) =
  makeIsCommMonoid is-setM assoc rid comm

CommMonoidStrâ†’MonoidStr : {A : Type â„“} â†’ CommMonoidStr A â†’ MonoidStr A
CommMonoidStrâ†’MonoidStr (commmonoidstr _ _ H) = monoidstr _ _ (IsCommMonoid.isMonoid H)

CommMonoidâ†’Monoid : CommMonoid â„“ â†’ Monoid â„“
CommMonoidâ†’Monoid (_ , commmonoidstr  _ _ M) = _ , monoidstr _ _ (IsCommMonoid.isMonoid M)

CommMonoidHom : (L : CommMonoid â„“) (M : CommMonoid â„“') â†’ Type (â„“-max â„“ â„“')
CommMonoidHom L M = MonoidHom (CommMonoidâ†’Monoid L) (CommMonoidâ†’Monoid M)

IsCommMonoidEquiv : {A : Type â„“} {B : Type â„“'}
  (M : CommMonoidStr A) (e : A â‰ƒ B) (N : CommMonoidStr B) â†’ Type (â„“-max â„“ â„“')
IsCommMonoidEquiv M e N = IsMonoidHom (CommMonoidStrâ†’MonoidStr M) (e .fst) (CommMonoidStrâ†’MonoidStr N)

CommMonoidEquiv : (M : CommMonoid â„“) (N : CommMonoid â„“') â†’ Type (â„“-max â„“ â„“')
CommMonoidEquiv M N = Î£[ e âˆˆ (M .fst â‰ƒ N .fst) ] IsCommMonoidEquiv (M .snd) e (N .snd)

isPropIsCommMonoid : {M : Type â„“} (Îµ : M) (_Â·_ : M â†’ M â†’ M)
             â†’ isProp (IsCommMonoid Îµ _Â·_)
isPropIsCommMonoid Îµ _Â·_ (iscommmonoid MM MC) (iscommmonoid SM SC) =
  Î» i â†’ iscommmonoid (isPropIsMonoid _ _ MM SM i) (isPropComm MC SC i)
  where
  isSetM : isSet _
  isSetM = MM .IsMonoid.isSemigroup .IsSemigroup.is-set

  isPropComm : isProp ((x y : _) â†’ x Â· y â‰¡ y Â· x)
  isPropComm = isPropÎ 2 Î» _ _ â†’ isSetM _ _

ğ’®á´°-CommMonoid : DUARel (ğ’®-Univ â„“) CommMonoidStr â„“
ğ’®á´°-CommMonoid =
  ğ’®á´°-Record (ğ’®-Univ _) IsCommMonoidEquiv
    (fields:
      data[ Îµ âˆ£ autoDUARel _ _ âˆ£ presÎµ ]
      data[ _Â·_ âˆ£ autoDUARel _ _ âˆ£ presÂ· ]
      prop[ isCommMonoid âˆ£ (Î» _ _ â†’ isPropIsCommMonoid _ _) ])
  where
  open CommMonoidStr
  open IsMonoidHom

CommMonoidPath : (M N : CommMonoid â„“) â†’ CommMonoidEquiv M N â‰ƒ (M â‰¡ N)
CommMonoidPath = âˆ« ğ’®á´°-CommMonoid .UARel.ua
