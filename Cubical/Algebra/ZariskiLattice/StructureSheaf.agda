{-

   This module defines the basic opens of the Zariski lattice and proves that they're a basis of the lattice.
   It also contains the construction of the structure presheaf and a proof of the sheaf property on basic opens,
   using the theory developed in the module PreSheafFromUniversalProp and its toSheaf.lemma.
   Note that the structure sheaf is a functor into R-algebras and not just commutative rings.

-}

{-# OPTIONS --safe --lossy-unification #-}
module Cubical.Algebra.ZariskiLattice.StructureSheaf where


open import Cubical.Foundations.Prelude
open import Cubical.Foundations.Function
open import Cubical.Foundations.Equiv
open import Cubical.Foundations.Isomorphism
open import Cubical.Foundations.Univalence
open import Cubical.Foundations.HLevels
open import Cubical.Foundations.Transport
open import Cubical.Foundations.Powerset using (â„™ ; âŠ†-refl-consequence)
                                         renaming ( _âˆˆ_ to _âˆˆâ‚š_ ; subst-âˆˆ to subst-âˆˆâ‚š
                                                  ; âˆˆ-isProp to âˆˆâ‚š-isProp)

import Cubical.Data.Empty as âŠ¥
open import Cubical.Data.Bool hiding (_â‰¤_)
open import Cubical.Data.Nat renaming ( _+_ to _+â„•_ ; _Â·_ to _Â·â„•_ ; _^_ to _^â„•_
                                      ; +-comm to +â„•-comm ; +-assoc to +â„•-assoc
                                      ; Â·-assoc to Â·â„•-assoc ; Â·-comm to Â·â„•-comm
                                      ; Â·-identityÊ³ to Â·â„•-rid)
open import Cubical.Data.Sigma.Base
open import Cubical.Data.Sigma.Properties
open import Cubical.Data.FinData
open import Cubical.Data.Unit
open import Cubical.Relation.Nullary
open import Cubical.Relation.Binary
open import Cubical.Relation.Binary.Order.Poset

open import Cubical.Algebra.Ring
open import Cubical.Algebra.Ring.Properties
open import Cubical.Algebra.Ring.BigOps
open import Cubical.Algebra.Algebra
open import Cubical.Algebra.CommRing
open import Cubical.Algebra.CommRing.BinomialThm
open import Cubical.Algebra.CommRing.Ideal
open import Cubical.Algebra.CommRing.FGIdeal
open import Cubical.Algebra.CommRing.RadicalIdeal
open import Cubical.Algebra.CommRing.Localisation
open import Cubical.Algebra.CommRing.Instances.Unit
open import Cubical.Algebra.CommAlgebra.Base
open import Cubical.Algebra.CommAlgebra.Properties
open import Cubical.Algebra.CommAlgebra.Localisation
open import Cubical.Tactics.CommRingSolver
open import Cubical.Algebra.Semilattice
open import Cubical.Algebra.Lattice
open import Cubical.Algebra.DistLattice
open import Cubical.Algebra.DistLattice.Basis
open import Cubical.Algebra.DistLattice.BigOps
open import Cubical.Algebra.ZariskiLattice.Base
open import Cubical.Algebra.ZariskiLattice.UniversalProperty

open import Cubical.Categories.Category.Base hiding (_[_,_])
open import Cubical.Categories.Functor
open import Cubical.Categories.NaturalTransformation
open import Cubical.Categories.Limits.Limits
open import Cubical.Categories.Limits.Terminal
open import Cubical.Categories.Limits.RightKan

open import Cubical.Categories.Instances.CommRings
open import Cubical.Categories.Instances.CommAlgebras
open import Cubical.Categories.Instances.DistLattice
open import Cubical.Categories.Instances.Semilattice

open import Cubical.Categories.DistLatticeSheaf.Diagram
open import Cubical.Categories.DistLatticeSheaf.Base
open import Cubical.Categories.DistLatticeSheaf.Extension

open import Cubical.HITs.SetQuotients as SQ
open import Cubical.HITs.PropositionalTruncation as PT

open Iso
open BinaryRelation
open isEquivRel


module _ {â„“ : Level} (R' : CommRing â„“) where
 open CommRingStr â¦ƒ...â¦„
 open RingTheory (CommRingâ†’Ring R')
 open CommIdeal R'
 open isCommIdeal

 open ZarLat R'
 open ZarLatUniversalProp R'
 open IsZarMap

 open Join ZariskiLattice
 open JoinSemilattice (Latticeâ†’JoinSemilattice (DistLatticeâ†’Lattice ZariskiLattice))
 open IsBasis

 private
  R = fst R'
  instance
   _ = snd R'
  âŸ¨_âŸ©â‚› : R â†’ CommIdeal -- s is for singleton
  âŸ¨ f âŸ©â‚› = âŸ¨ replicateFinVec 1 f âŸ©[ R' ]

 BasicOpens : â„™ ZL
 BasicOpens ğ” = (âˆƒ[ f âˆˆ R ] (D f â‰¡ ğ”)) , isPropPropTrunc

 BO : Type â„“
 BO = Î£[ ğ” âˆˆ ZL ] (ğ” âˆˆâ‚š BasicOpens)

 basicOpensAreBasis : IsBasis ZariskiLattice BasicOpens
 contains1 basicOpensAreBasis = âˆ£ 1r , isZarMapD .pres1 âˆ£â‚
 âˆ§lClosed basicOpensAreBasis ğ” ğ”Ÿ = map2
            Î» (f , Dfâ‰¡ğ”) (g , Dgâ‰¡ğ”Ÿ) â†’ (f Â· g) , isZarMapD .Â·â‰¡âˆ§ f g âˆ™ congâ‚‚ (_âˆ§z_) Dfâ‰¡ğ” Dgâ‰¡ğ”Ÿ
 â‹Basis basicOpensAreBasis = elimProp (Î» _ â†’ isPropPropTrunc) Î£helper
  where
  Î£helper : (a : Î£[ n âˆˆ â„• ] FinVec R n)
          â†’ âˆƒ[ n âˆˆ â„• ] Î£[ Î± âˆˆ FinVec ZL n ] (âˆ€ i â†’ Î± i âˆˆâ‚š BasicOpens) Ã— (â‹ Î± â‰¡ [ a ])
  Î£helper (n , Î±) = âˆ£ n , (D âˆ˜ Î±) , (Î» i â†’ âˆ£ Î± i , refl âˆ£â‚) , â‹Dâ‰¡ Î± âˆ£â‚

 -- important fact that D(f)â‰¤D(g) â†’ isContr (R-Hom R[1/f] R[1/g])
 module _ where
   open InvertingElementsBase R'

   contrHoms : (f g : R)
             â†’ D f â‰¤ D g
             â†’ isContr (CommAlgebraHom R[1/ g ]AsCommAlgebra R[1/ f ]AsCommAlgebra)
   contrHoms f g Dfâ‰¤Dg = R[1/g]HasAlgUniversalProp R[1/ f ]AsCommAlgebra
     Î» s sâˆˆ[gâ¿|nâ‰¥0] â†’ subst-âˆˆâ‚š (R[1/ f ]AsCommRing Ë£)
       (sym (Â·IdR (s /1))) --can't apply the lemma directly as we get mult with 1 somewhere
         (RadicalLemma.toUnit R' f g fâˆˆâˆšâŸ¨gâŸ© s sâˆˆ[gâ¿|nâ‰¥0])
    where
    open AlgLoc R' [ g â¿|nâ‰¥0] (powersFormMultClosedSubset g)
         renaming (Sâ»Â¹RHasAlgUniversalProp to R[1/g]HasAlgUniversalProp)
    open Sâ»Â¹RUniversalProp R' [ f â¿|nâ‰¥0] (powersFormMultClosedSubset f) using (_/1)
    open RadicalIdeal R'

    private
     instance
      _ = snd R[1/ f ]AsCommRing

    fâˆˆâˆšâŸ¨gâŸ© : f âˆˆ âˆš âŸ¨ g âŸ©â‚›
    fâˆˆâˆšâŸ¨gâŸ© = isEquivRelâ†’effectiveIso âˆ¼PropValued âˆ¼EquivRel _ _ .fun Dfâ‰¤Dg .fst zero


 -- The structure presheaf on BO
 ZariskiCat = DistLatticeCategory ZariskiLattice

 BOCat : Category â„“ â„“
 BOCat = Î£PropCat ZariskiCat BasicOpens

 private
  P : ZL â†’ Type _
  P ğ” = Î£[ f âˆˆ R ] (D f â‰¡ ğ”) -- the untruncated defining property

  ğ“• : Î£ ZL P â†’ CommAlgebra R' _
  ğ“• (_ , f , _) = R[1/ f ]AsCommAlgebra -- D(f) â†¦ R[1/f]

  uniqueHom : âˆ€ (x y : Î£ ZL P) â†’ (fst x) â‰¤ (fst y) â†’ isContr (CommAlgebraHom (ğ“• y) (ğ“• x))
  uniqueHom (ğ” , f , p) (ğ”Ÿ , g , q) ğ”â‰¤ğ”Ÿ = contrHoms f g Dfâ‰¤Dg
    where
    Dfâ‰¤Dg : D f â‰¤ D g
    Dfâ‰¤Dg = subst2 _â‰¤_ (sym p) (sym q) ğ”â‰¤ğ”Ÿ



 open PreSheafFromUniversalProp ZariskiCat P ğ“• uniqueHom
 ğ“á´® : Functor (BOCat ^op) CommRingsCategory
 ğ“á´® = funcComp (ForgetfulCommAlgebraâ†’CommRing R') universalPShf

 -- The extension
 open Functor
 open PreSheafExtension ZariskiLattice CommRingsCategory LimitsCommRingsCategory BasicOpens
 ğ“ : Functor (ZariskiCat ^op) CommRingsCategory
 ğ“ = DLRan ğ“á´®

 toBasisPath : âˆ€ f â†’ ğ“ .F-ob (D f) â‰¡ ğ“á´® .F-ob (D f , âˆ£ f , refl âˆ£â‚)
 toBasisPath f = cong (Î» F â†’ F .F-ob (D f , âˆ£ f , refl âˆ£â‚))
                      (NatIsoToPath isUnivalentCommRingsCategory (DLRanNatIso ğ“á´®))


 open InvertingElementsBase R'
 private
   Forgetful = ForgetfulCommAlgebraâ†’CommRing R' {â„“' = â„“}

   ğ“á´®Obâ‰¡ : âˆ€ f â†’ ğ“á´® .F-ob (D f , âˆ£ f , refl âˆ£â‚) â‰¡ R[1/ f ]AsCommRing
   ğ“á´®Obâ‰¡ f = ğ“á´® .F-ob (D f , âˆ£ f , refl âˆ£â‚)     â‰¡âŸ¨ refl âŸ©
     -- all of this should hold by refl -----------------------------------------------------------
     -- but somehow Agda takes forever to type-check if you don't use -----------------------------
     -- the lemma funcCompObâ‰¡ (which is just refl itself) or if you leave out ---------------------
     -- any of the intermediate refl steps --------------------------------------------------------
       (funcComp (ForgetfulCommAlgebraâ†’CommRing R') universalPShf) .F-ob (D f , âˆ£ f , refl âˆ£â‚)
     â‰¡âŸ¨ funcCompObâ‰¡ Forgetful universalPShf _ âŸ©
       Forgetful .F-ob R[1/ f ]AsCommAlgebra
     â‰¡âŸ¨ refl âŸ©
     ----------------------------------------------------------------------------------------------
     CommAlgebraâ†’CommRing R[1/ f ]AsCommAlgebra â‰¡âŸ¨ invElCommAlgebraâ†’CommRingPath f âŸ©
     R[1/ f ]AsCommRing                         âˆ

 baseSections : âˆ€ f â†’ ğ“ .F-ob (D f) â‰¡ R[1/ f ]AsCommRing
 baseSections f = toBasisPath f âˆ™ ğ“á´®Obâ‰¡ f

 globalSection : ğ“ .F-ob (D 1r) â‰¡ R'
 globalSection = baseSections 1r âˆ™  invertingUnitsPath _ _ (Units.RË£ContainsOne _)


 open SheafOnBasis ZariskiLattice (CommRingsCategory {â„“ = â„“}) BasicOpens basicOpensAreBasis
 open DistLatticeStr â¦ƒ...â¦„
 private instance _ = snd ZariskiLattice

 isSheafğ“á´® : isDLBasisSheaf ğ“á´®
 isSheafğ“á´® {n = n} Î± = curriedHelper (fst âˆ˜ Î±) (snd âˆ˜ Î±)
  where
  curriedHelper : (ğ” : FinVec ZL n) (ğ”âˆˆBO : âˆ€ i â†’ ğ” i âˆˆâ‚š BasicOpens)
                  (â‹ğ”âˆˆBO : â‹ ğ” âˆˆâ‚š BasicOpens)
                â†’ isLimCone _ _ (F-cone ğ“á´®
                                (condCone.Bâ‹Cone (Î» i â†’ ğ” i , ğ”âˆˆBO i) â‹ğ”âˆˆBO))
  curriedHelper ğ” = PT.elimFin (Î» _ â†’ isPropÎ  (Î» _ â†’ isPropIsLimCone _ _ _))
                     Î» x â†’ PT.elim (Î» _ â†’ isPropIsLimCone _ _ _) (Î£helper x)
    where
    Î£helper : (x : âˆ€ i â†’ Î£[ f âˆˆ R ] D f â‰¡ ğ” i)
              (y : Î£[ g âˆˆ R ] D g â‰¡ â‹ ğ”)
            â†’ isLimCone _ _ (F-cone ğ“á´®
                            (condCone.Bâ‹Cone (Î» i â†’ ğ” i , âˆ£ x i âˆ£â‚) âˆ£ y âˆ£â‚))
    Î£helper x y = toSheaf.toLimCone theSheafCone doubleLocAlgCone
                                    algPaths isLimConeDoubleLocAlgCone
      where
      f = fst âˆ˜ x
      h = fst y
      Dfâ‰¡ğ” = snd âˆ˜ x
      Dhâ‰¡â‹ğ” = snd y

      open condCone (Î» i â†’ ğ” i , âˆ£ f i , Dfâ‰¡ğ” i âˆ£â‚)
      theSheafCone = Bâ‹Cone âˆ£ h , Dhâ‰¡â‹ğ” âˆ£â‚

      DHelper : D h â‰¡ [ n , f ] --â‹ (D âˆ˜ f)
      DHelper = Dhâ‰¡â‹ğ” âˆ™ â‹Ext (Î» i â†’ sym (Dfâ‰¡ğ” i)) âˆ™ â‹Dâ‰¡ f

      open Exponentiation R'
      open RadicalIdeal R'
      open DoubleLoc R' h
      open isMultClosedSubset (powersFormMultClosedSubset h)
      open Sâ»Â¹RUniversalProp R' [ h â¿|nâ‰¥0] (powersFormMultClosedSubset h)
      open CommIdeal R[1/ h ]AsCommRing using ()
                                        renaming (CommIdeal to CommIdealâ‚• ; _âˆˆ_ to _âˆˆâ‚•_)

      instance
       _ = snd R[1/ h ]AsCommRing

      -- crucial facts about radical ideals
      hâˆˆâˆšâŸ¨fâŸ© : h âˆˆ âˆš âŸ¨ f âŸ©[ R' ]
      hâˆˆâˆšâŸ¨fâŸ© = isEquivRelâ†’effectiveIso âˆ¼PropValued âˆ¼EquivRel _ _ .fun DHelper .fst zero

      fâˆˆâˆšâŸ¨hâŸ© : âˆ€ i â†’ f i âˆˆ âˆš âŸ¨ h âŸ©â‚›
      fâˆˆâˆšâŸ¨hâŸ© i = isEquivRelâ†’effectiveIso âˆ¼PropValued âˆ¼EquivRel _ _ .fun
                   (sym DHelper) .fst i

      ffâˆˆâˆšâŸ¨hâŸ© : âˆ€ i j â†’ f i Â· f j âˆˆ âˆš âŸ¨ h âŸ©â‚›
      ffâˆˆâˆšâŸ¨hâŸ© i j = âˆš âŸ¨ h âŸ©â‚› .snd .Â·Closed (f i) (fâˆˆâˆšâŸ¨hâŸ© j)

      f/1 : FinVec (R[1/ h ]) n
      f/1 i = (f i) /1

      1âˆˆâŸ¨f/1âŸ© : 1r âˆˆâ‚• âŸ¨ f/1 âŸ©[ R[1/ h ]AsCommRing ]
      1âˆˆâŸ¨f/1âŸ© = fromFact hâˆˆâˆšâŸ¨fâŸ©
       where
       fromFact : h âˆˆ âˆš âŸ¨ f âŸ©[ R' ] â†’ 1r âˆˆâ‚• âŸ¨ f/1 âŸ©[ R[1/ h ]AsCommRing ]
       fromFact = PT.rec isPropPropTrunc (uncurry helper1)
        where
        helper1 : (m : â„•) â†’ h ^ m âˆˆ âŸ¨ f âŸ©[ R' ] â†’ 1r âˆˆâ‚• âŸ¨ f/1 âŸ©[ R[1/ h ]AsCommRing ]
        helper1 m = PT.map helper2
         where
         helper2 : Î£[ Î± âˆˆ FinVec R n ]
                     h ^ m â‰¡ linearCombination R' Î± f
                 â†’ Î£[ Î² âˆˆ FinVec R[1/ h ] n ]
                     1r â‰¡ linearCombination R[1/ h ]AsCommRing Î² f/1
         helper2 (Î± , háµâ‰¡âˆ‘Î±f) = Î² , path
          where
          open Units R[1/ h ]AsCommRing
          open Sum (CommRingâ†’Ring R[1/ h ]AsCommRing)
          open IsRingHom (snd /1AsCommRingHom)
          open SumMap _ _ /1AsCommRingHom
          instance
           hâ»áµ : (h ^ m) /1 âˆˆâ‚š (R[1/ h ]AsCommRing Ë£)
           hâ»áµ = [ 1r , h ^ m , âˆ£ m , refl âˆ£â‚ ]
               , eq/ _ _ ((1r , containsOne) , solve! R')

          Î² : FinVec R[1/ h ] n
          Î² i = ((h ^ m) /1) â»Â¹ Â· Î± i /1

          /1Path : (h ^ m) /1 â‰¡ âˆ‘ (Î» i â†’ Î± i /1 Â· f i /1)
          /1Path = (h ^ m) /1
                 â‰¡âŸ¨ cong (_/1) háµâ‰¡âˆ‘Î±f âŸ©
                   (linearCombination R' Î± f) /1
                 â‰¡âŸ¨ âˆ‘Map (Î» i â†’ Î± i Â· f i) âŸ©
                   âˆ‘ (Î» i â†’ (Î± i Â· f i) /1)
                 â‰¡âŸ¨ âˆ‘Ext (Î» i â†’ presÂ· (Î± i) (f i)) âŸ©
                   âˆ‘ (Î» i â†’ Î± i /1 Â· f i /1) âˆ

          path : 1r â‰¡ âˆ‘ (Î» i â†’  Î² i Â· f/1 i)
          path = 1r
               â‰¡âŸ¨ sym (Â·-linv ((h ^ m) /1)) âŸ©
                 ((h ^ m) /1) â»Â¹ Â· (h ^ m) /1
               â‰¡âŸ¨ cong (((h ^ m) /1) â»Â¹ Â·_) /1Path âŸ©
                 ((h ^ m) /1) â»Â¹ Â· âˆ‘ (Î» i â†’ Î± i /1 Â· f i /1)
               â‰¡âŸ¨ âˆ‘Mulrdist (((h ^ m) /1) â»Â¹) (Î» i â†’ Î± i /1 Â· f i /1) âŸ©
                 âˆ‘ (Î» i â†’  ((h ^ m) /1) â»Â¹ Â· (Î± i /1 Â· f i /1))
               â‰¡âŸ¨ âˆ‘Ext (Î» i â†’ Â·Assoc (((h ^ m) /1) â»Â¹) (Î± i /1) (f i /1)) âŸ©
                 âˆ‘ (Î» i â†’  Î² i Â· f/1 i) âˆ


      -- Putting everything together:
      -- First, the diagram and limiting cone we get from our lemma
      -- in Cubical.Algebra.Localisation.Limit with R=R[1/h]
      --      âŸ¨ fâ‚/1 , ... , fâ‚™/1 âŸ© = R[1/h]
      --   â‡’  R[1/h] = lim { R[1/h][1/fáµ¢] â†’ R[1/h][1/fáµ¢fâ±¼] â† R[1/h][1/fâ±¼] }
      doubleLocDiag = locDiagram R[1/ h ]AsCommRing f/1
      doubleLocCone = locCone R[1/ h ]AsCommRing f/1
      isLimConeDoubleLocCone : isLimCone _ _ doubleLocCone
      isLimConeDoubleLocCone = isLimConeLocCone R[1/ h ]AsCommRing f/1 1âˆˆâŸ¨f/1âŸ©

      -- this gives a limiting cone in R-algebras via _/1/1 : R â†’ R[1/h][1/fáµ¢]
      -- note that the pair case looks more complicated as
      -- R[1/h][(fáµ¢fâ±¼)/1/1] =/= R[1/h][(fáµ¢/1 Â· fâ±¼/1)/1]
      -- definitionally
      open Cone
      open IsRingHom

      module D i = DoubleLoc R' h (f i)

      /1/1Cone : Cone doubleLocDiag R'
      coneOut /1/1Cone (sing i) = D./1/1AsCommRingHom i
      fst (coneOut /1/1Cone (pair i j i<j)) r =
          [ [ r , 1r , âˆ£ 0 , refl âˆ£â‚ ] , 1r , âˆ£ 0 , refl âˆ£â‚ ]
      pres0 (snd (coneOut /1/1Cone (pair i j i<j))) = refl
      pres1 (snd (coneOut /1/1Cone (pair i j i<j))) = refl
      pres+ (snd (coneOut /1/1Cone (pair i j i<j))) x y =
        cong [_] (â‰¡-Ã— (cong [_] (â‰¡-Ã—
                      (congâ‚‚ _+_ (solve! R') (solve! R'))
                      (Î£â‰¡Prop (Î» _ â†’ isPropPropTrunc) (solve! R'))))
                      (Î£â‰¡Prop (Î» _ â†’ isPropPropTrunc) (sym (Â·IdR 1r))))
      presÂ· (snd (coneOut /1/1Cone (pair i j i<j))) x y =
        cong [_] (â‰¡-Ã— (cong [_] (â‰¡-Ã— refl
                      (Î£â‰¡Prop (Î» _ â†’ isPropPropTrunc) (sym (Â·IdR 1r)))))
                      (Î£â‰¡Prop (Î» _ â†’ isPropPropTrunc) (sym (Â·IdR 1r))))
      pres- (snd (coneOut /1/1Cone (pair i j i<j))) _ = refl
      coneOutCommutes /1/1Cone idAr = idCompCommRingHom _
      coneOutCommutes /1/1Cone singPairL = RingHomâ‰¡ (funExt
        (Î» x â†’ cong [_] (â‰¡-Ã— (cong [_] (â‰¡-Ã— (cong (x Â·_) (transportRefl 1r) âˆ™ Â·IdR x)
        (Î£â‰¡Prop (Î» _ â†’ isPropPropTrunc) (cong (1r Â·_) (transportRefl 1r) âˆ™ Â·IdR 1r))))
        (Î£â‰¡Prop (Î» _ â†’ isPropPropTrunc) (cong (1r Â·_) (transportRefl 1r) âˆ™ Â·IdR 1r)))))
      coneOutCommutes /1/1Cone singPairR = RingHomâ‰¡ (funExt
        (Î» x â†’ cong [_] (â‰¡-Ã— (cong [_] (â‰¡-Ã— (cong (x Â·_) (transportRefl 1r) âˆ™ Â·IdR x)
        (Î£â‰¡Prop (Î» _ â†’ isPropPropTrunc) (cong (1r Â·_) (transportRefl 1r) âˆ™ Â·IdR 1r))))
        (Î£â‰¡Prop (Î» _ â†’ isPropPropTrunc) (cong (1r Â·_) (transportRefl 1r) âˆ™ Â·IdR 1r)))))

      open LimitFromCommRing R' R[1/ h ]AsCommRing (DLShfDiag n â„“)
                             doubleLocDiag doubleLocCone /1/1Cone

      -- get the desired cone in algebras:
      isConeMor/1 : isConeMor /1/1Cone doubleLocCone /1AsCommRingHom
      isConeMor/1 = isConeMorSingLemma /1/1Cone doubleLocCone
                      (Î» _ â†’ RingHomâ‰¡ (funExt (Î» _ â†’ refl)))

      doubleLocAlgCone = algCone /1AsCommRingHom isConeMor/1
      isLimConeDoubleLocAlgCone : isLimCone _ _ doubleLocAlgCone
      isLimConeDoubleLocAlgCone = reflectsLimits /1AsCommRingHom isConeMor/1
                                                 isLimConeDoubleLocCone

      -- we only give the paths on objects
      -- R[1/h][1/fáµ¢] â‰¡ [1/fáµ¢]
      -- R[1/h][1/fáµ¢fâ±¼] â‰¡ R[1/fáµ¢fâ±¼]
      algPaths : âˆ€ v â†’ F-ob algDiag v â‰¡ F-ob (funcComp universalPShf BDiag) v
      algPaths (sing i) = doubleLocCancel (fâˆˆâˆšâŸ¨hâŸ© i)
        where
        open DoubleAlgLoc R' h (f i)
      algPaths (pair i j i<j) = path âˆ™ doubleLocCancel (ffâˆˆâˆšâŸ¨hâŸ© i j)
        where
        open DoubleAlgLoc R' h (f i Â· f j)
        open CommAlgChar R'

        -- the naive def.
        R[1/h][1/fáµ¢fâ±¼]AsCommRingReg = InvertingElementsBase.R[1/_]AsCommRing
                                        R[1/ h ]AsCommRing ((f i Â· f j) /1)

        path : toCommAlg ( F-ob doubleLocDiag (pair i j i<j)
                         , coneOut /1/1Cone (pair i j i<j))
             â‰¡ toCommAlg (R[1/h][1/fáµ¢fâ±¼]AsCommRingReg , /1/1AsCommRingHom (f i Â· f j))
        path =  cong toCommAlg (Î£PathP (p , q))
          where
          eqInR[1/h] : (f i /1) Â· (f j /1) â‰¡ (f i Â· f j) /1
          eqInR[1/h] = sym (/1AsCommRingHom .snd .presÂ· (f i) (f j))

          p : F-ob doubleLocDiag (pair i j i<j) â‰¡ R[1/h][1/fáµ¢fâ±¼]AsCommRingReg
          p i = InvertingElementsBase.R[1/_]AsCommRing R[1/ h ]AsCommRing (eqInR[1/h] i)

          q : PathP (Î» i â†’ CommRingHom R' (p i)) (coneOut /1/1Cone (pair i j i<j))
                                                 (/1/1AsCommRingHom (f i Â· f j))
          q = toPathP (RingHomâ‰¡ (funExt (
                Î» r â†’ cong [_] (â‰¡-Ã— (cong [_] (â‰¡-Ã— (transportRefl _ âˆ™ transportRefl r)
                    (Î£â‰¡Prop (Î» _ â†’ isPropPropTrunc) (transportRefl 1r))))
                    (Î£â‰¡Prop (Î» _ â†’ isPropPropTrunc) (transportRefl 1r))))))


 -- our main result
 isSheafğ“ : isDLSheaf _ _ ğ“
 isSheafğ“ = isDLSheafDLRan _ _ isSheafğ“á´®
