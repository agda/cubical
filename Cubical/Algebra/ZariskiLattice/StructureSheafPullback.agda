{-

   This module defines the basic opens of the Zariski lattice and proves that
   they're a basis of the lattice. It also contains the construction of the
   structure presheaf and a proof of the sheaf property on basic opens,
   using the theory developed in the module PreSheafFromUniversalProp and its toSheaf.lemma.
   Note that the structure sheaf is a functor into R-algebras and not just commutative rings.

-}

{-# OPTIONS --safe --lossy-unification #-}
module Cubical.Algebra.ZariskiLattice.StructureSheafPullback where


open import Cubical.Foundations.Prelude
open import Cubical.Foundations.Function
open import Cubical.Foundations.Equiv
open import Cubical.Foundations.Isomorphism
open import Cubical.Foundations.Univalence
open import Cubical.Foundations.HLevels
open import Cubical.Foundations.Transport
open import Cubical.Foundations.Powerset using (â„™ ; âŠ†-refl-consequence)
                                         renaming (_âˆˆ_ to _âˆˆâ‚š_ ; subst-âˆˆ to subst-âˆˆâ‚š)

import Cubical.Data.Empty as âŠ¥
open import Cubical.Data.Bool hiding (_â‰¤_)
open import Cubical.Data.Nat renaming ( _+_ to _+â„•_ ; _Â·_ to _Â·â„•_ ; _^_ to _^â„•_
                                      ; +-comm to +â„•-comm ; +-assoc to +â„•-assoc
                                      ; Â·-assoc to Â·â„•-assoc ; Â·-comm to Â·â„•-comm
                                      ; Â·-identityÊ³ to Â·â„•-rid)
open import Cubical.Data.Sigma.Base
open import Cubical.Data.Sigma.Properties
open import Cubical.Data.FinData
open import Cubical.Data.Unit
open import Cubical.Relation.Nullary
open import Cubical.Relation.Binary
open import Cubical.Relation.Binary.Order.Poset

open import Cubical.Algebra.Ring
open import Cubical.Algebra.Ring.Properties
open import Cubical.Algebra.Ring.BigOps
open import Cubical.Algebra.Algebra
open import Cubical.Algebra.CommRing
open import Cubical.Algebra.CommRing.BinomialThm
open import Cubical.Algebra.CommRing.Ideal
open import Cubical.Algebra.CommRing.FGIdeal
open import Cubical.Algebra.CommRing.RadicalIdeal
open import Cubical.Algebra.CommRing.Localisation.Base
open import Cubical.Algebra.CommRing.Localisation.UniversalProperty
open import Cubical.Algebra.CommRing.Localisation.InvertingElements
open import Cubical.Algebra.CommRing.Localisation.PullbackSquare
open import Cubical.Algebra.CommAlgebra.Base
open import Cubical.Algebra.CommAlgebra.Properties
open import Cubical.Algebra.CommAlgebra.Localisation
open import Cubical.Algebra.CommAlgebra.Instances.Unit
open import Cubical.Tactics.CommRingSolver
open import Cubical.Algebra.Semilattice
open import Cubical.Algebra.Lattice
open import Cubical.Algebra.DistLattice
open import Cubical.Algebra.DistLattice.Basis
open import Cubical.Algebra.DistLattice.BigOps
open import Cubical.Algebra.ZariskiLattice.Base
open import Cubical.Algebra.ZariskiLattice.UniversalProperty

open import Cubical.Categories.Category.Base hiding (_[_,_])
open import Cubical.Categories.Functor
open import Cubical.Categories.Limits.Terminal
open import Cubical.Categories.Limits.Pullback
open import Cubical.Categories.Instances.CommAlgebras
open import Cubical.Categories.Instances.DistLattice
open import Cubical.Categories.Instances.Semilattice
open import Cubical.Categories.DistLatticeSheaf.Base

open import Cubical.HITs.SetQuotients as SQ
open import Cubical.HITs.PropositionalTruncation as PT

open Iso
open BinaryRelation
open isEquivRel

private
  variable
    â„“ â„“' : Level



module _ (R' : CommRing â„“) where
 open CommRingStr â¦ƒ...â¦„
 open RingTheory (CommRingâ†’Ring R')
 open CommIdeal R'
 open isCommIdeal

 open ZarLat R'
 open ZarLatUniversalProp R'
 open IsZarMap

 open Join ZariskiLattice
 open JoinSemilattice (Latticeâ†’JoinSemilattice (DistLatticeâ†’Lattice ZariskiLattice))
 open IsBasis

 private
  R = fst R'
  instance
   _ = snd R'
  âŸ¨_âŸ© : R â†’ CommIdeal
  âŸ¨ f âŸ© = âŸ¨ replicateFinVec 1 f âŸ©[ R' ]
  âŸ¨_âŸ©â‚š : R Ã— R â†’ CommIdeal -- p is for pair
  âŸ¨ f , g âŸ©â‚š = âŸ¨ replicateFinVec 1 f ++Fin replicateFinVec 1 g âŸ©[ R' ]


 BasicOpens : â„™ ZL
 BasicOpens ğ” = (âˆƒ[ f âˆˆ R ] (D f â‰¡ ğ”)) , isPropPropTrunc

 BO : Type â„“
 BO = Î£[ ğ” âˆˆ ZL ] (ğ” âˆˆâ‚š BasicOpens)

 basicOpensAreBasis : IsBasis ZariskiLattice BasicOpens
 contains1 basicOpensAreBasis = âˆ£ 1r , isZarMapD .pres1 âˆ£â‚
 âˆ§lClosed basicOpensAreBasis ğ” ğ”Ÿ = map2
            Î» (f , Dfâ‰¡ğ”) (g , Dgâ‰¡ğ”Ÿ) â†’ (f Â· g) , isZarMapD .Â·â‰¡âˆ§ f g âˆ™ congâ‚‚ (_âˆ§z_) Dfâ‰¡ğ” Dgâ‰¡ğ”Ÿ
 â‹Basis basicOpensAreBasis = elimProp (Î» _ â†’ isPropPropTrunc) Î£helper
  where
  Î£helper : (a : Î£[ n âˆˆ â„• ] FinVec R n)
          â†’ âˆƒ[ n âˆˆ â„• ] Î£[ Î± âˆˆ FinVec ZL n ] (âˆ€ i â†’ Î± i âˆˆâ‚š BasicOpens) Ã— (â‹ Î± â‰¡ [ a ])
  Î£helper (n , Î±) = âˆ£ n , (D âˆ˜ Î±) , (Î» i â†’ âˆ£ Î± i , refl âˆ£â‚) , path âˆ£â‚
   where
   path : â‹ (D âˆ˜ Î±) â‰¡ [ n , Î± ]
   path = funExtâ» (cong fst ZLUniversalPropCorollary) _


 -- The structure presheaf on BO
 ZariskiCat = DistLatticeCategory ZariskiLattice

 BOCat : Category â„“ â„“
 BOCat = Î£PropCat ZariskiCat BasicOpens

 private
  P : ZL â†’ Type _
  P ğ” = Î£[ f âˆˆ R ] (D f â‰¡ ğ”) -- the untruncated defining property

  ğ“• : Î£ ZL P â†’ CommAlgebra R' _
  ğ“• (_ , f , _) = R[1/ f ]AsCommAlgebra -- D(f) â†¦ R[1/f]

  uniqueHom : âˆ€ (x y : Î£ ZL P) â†’ (fst x) â‰¤ (fst y) â†’ isContr (CommAlgebraHom (ğ“• y) (ğ“• x))
  uniqueHom (ğ” , f , p) (ğ”Ÿ , g , q) = contrHoms ğ” ğ”Ÿ f g p q
   where
   open InvertingElementsBase R'

   contrHoms : (ğ” ğ”Ÿ : ZL) (f g : R) (p : D f â‰¡ ğ”) (q : D g â‰¡ ğ”Ÿ)
             â†’ ğ” â‰¤ ğ”Ÿ â†’ isContr (CommAlgebraHom R[1/ g ]AsCommAlgebra R[1/ f ]AsCommAlgebra)
   contrHoms ğ” ğ”Ÿ f g p q ğ”â‰¤ğ”Ÿ = R[1/g]HasAlgUniversalProp R[1/ f ]AsCommAlgebra
     Î» s sâˆˆ[gâ¿|nâ‰¥0] â†’ subst-âˆˆâ‚š (R[1/ f ]AsCommRing Ë£)
       (sym (Â·IdR (s /1))) --can't apply the lemma directly as we get mult with 1 somewhere
         (RadicalLemma.toUnit R' f g fâˆˆâˆšâŸ¨gâŸ© s sâˆˆ[gâ¿|nâ‰¥0])
    where
    open AlgLoc R' [ g â¿|nâ‰¥0] (powersFormMultClosedSubset g)
         renaming (Sâ»Â¹RHasAlgUniversalProp to R[1/g]HasAlgUniversalProp)
    open Sâ»Â¹RUniversalProp R' [ f â¿|nâ‰¥0] (powersFormMultClosedSubset f) using (_/1)
    open RadicalIdeal R'

    private
     instance
      _ = snd R[1/ f ]AsCommRing

    Dfâ‰¤Dg : D f â‰¤ D g
    Dfâ‰¤Dg = subst2 _â‰¤_ (sym p) (sym q) ğ”â‰¤ğ”Ÿ

    fâˆˆâˆšâŸ¨gâŸ© : f âˆˆ âˆš âŸ¨ g âŸ©
    fâˆˆâˆšâŸ¨gâŸ© = isEquivRelâ†’effectiveIso âˆ¼PropValued âˆ¼EquivRel _ _ .fun Dfâ‰¤Dg .fst zero


 open PreSheafFromUniversalProp ZariskiCat P ğ“• uniqueHom
 BasisStructurePShf : Functor (BOCat ^op) (CommAlgebrasCategory R')
 BasisStructurePShf = universalPShf


 -- now prove the sheaf properties
 open SheafOnBasis ZariskiLattice (CommAlgebrasCategory R' {â„“' = â„“})
                   BasicOpens basicOpensAreBasis

 -- only proof for weak notion of sheaf on a basis
 isSheafBasisStructurePShf : isDLBasisSheafPullback BasisStructurePShf
 fst isSheafBasisStructurePShf 0âˆˆBO = subst (isTerminal (CommAlgebrasCategory R'))
                                        (sym R[1/0]â‰¡0 âˆ™ Î» i â†’ F-ob (0z , canonical0âˆˆBOâ‰¡0âˆˆBO i))
                                          (TerminalCommAlgebra R' .snd)
   where
   open Functor â¦ƒ...â¦„
   instance
    _ = BasisStructurePShf

   canonical0âˆˆBO : 0z âˆˆâ‚š BasicOpens
   canonical0âˆˆBO = âˆ£ 0r , isZarMapD .pres0 âˆ£â‚

   canonical0âˆˆBOâ‰¡0âˆˆBO : canonical0âˆˆBO â‰¡ 0âˆˆBO
   canonical0âˆˆBOâ‰¡0âˆˆBO = BasicOpens 0z .snd _ _

   R[1/0]â‰¡0 : R[1/ 0r ]AsCommAlgebra â‰¡ UnitCommAlgebra R'
   R[1/0]â‰¡0 = uaCommAlgebra (e , eIsRHom)
    where
    open InvertingElementsBase R' using (isContrR[1/0])
    open IsAlgebraHom

    e : R[1/ 0r ]AsCommAlgebra .fst â‰ƒ UnitCommAlgebra R' .fst
    e = isContrâ†’Equiv isContrR[1/0] isContrUnit*

    eIsRHom : IsCommAlgebraEquiv (R[1/ 0r ]AsCommAlgebra .snd) e (UnitCommAlgebra R' .snd)
    pres0 eIsRHom = refl
    pres1 eIsRHom = refl
    pres+ eIsRHom _ _ = refl
    presÂ· eIsRHom _ _ = refl
    pres- eIsRHom _ = refl
    presâ‹† eIsRHom _ _ = refl

 snd isSheafBasisStructurePShf (ğ” , ğ”âˆˆBO) (ğ”Ÿ , ğ”ŸâˆˆBO) ğ”âˆ¨ğ”ŸâˆˆBO = curriedHelper ğ” ğ”Ÿ ğ”âˆˆBO ğ”ŸâˆˆBO ğ”âˆ¨ğ”ŸâˆˆBO
  where
  open condSquare
  {-
     here:
     BFsq (ğ” , ğ”âˆˆBO) (ğ”Ÿ , ğ”ŸâˆˆBO) ğ”âˆ¨ğ”ŸâˆˆBO BasisStructurePShf =

     ğ“ (ğ”âˆ¨ğ”Ÿ) â†’ ğ“ (ğ”)

       â†“         â†“

     ğ“ (ğ”Ÿ)  â†’  ğ“ (ğ”âˆ§ğ”Ÿ)

  -}
  curriedHelper : (ğ” ğ”Ÿ : ZL) (ğ”âˆˆBO : ğ” âˆˆâ‚š BasicOpens) (ğ”ŸâˆˆBO : ğ”Ÿ âˆˆâ‚š BasicOpens)
                  (ğ”âˆ¨ğ”ŸâˆˆBO : ğ” âˆ¨z ğ”Ÿ âˆˆâ‚š BasicOpens)
                â†’ isPullback (CommAlgebrasCategory R') _ _ _
                             (BFsq (ğ” , ğ”âˆˆBO) (ğ”Ÿ , ğ”ŸâˆˆBO) ğ”âˆ¨ğ”ŸâˆˆBO BasisStructurePShf)
  curriedHelper ğ” ğ”Ÿ = elim3 (Î» ğ”âˆˆBO ğ”ŸâˆˆBO ğ”âˆ¨ğ”ŸâˆˆBO â†’ isPropIsPullback _ _ _ _
                            (BFsq (ğ” , ğ”âˆˆBO) (ğ”Ÿ , ğ”ŸâˆˆBO) ğ”âˆ¨ğ”ŸâˆˆBO BasisStructurePShf))
                            Î£helper
   where
   -- write everything explicitly so things can type-check
   thePShfCospan : (a : Î£[ f âˆˆ R ] D f â‰¡ ğ”) (b : Î£[ g âˆˆ R ] D g â‰¡ ğ”Ÿ)
                 â†’ Cospan (CommAlgebrasCategory R')
   Cospan.l (thePShfCospan (f , Dfâ‰¡ğ”) (g , Dgâ‰¡ğ”Ÿ)) = BasisStructurePShf .Functor.F-ob (ğ”Ÿ , âˆ£ g , Dgâ‰¡ğ”Ÿ âˆ£â‚)
   Cospan.m (thePShfCospan (f , Dfâ‰¡ğ”) (g , Dgâ‰¡ğ”Ÿ)) = BasisStructurePShf .Functor.F-ob
            (ğ” âˆ§z ğ”Ÿ , basicOpensAreBasis .âˆ§lClosed ğ” ğ”Ÿ âˆ£ f , Dfâ‰¡ğ” âˆ£â‚ âˆ£ g , Dgâ‰¡ğ”Ÿ âˆ£â‚)
   Cospan.r (thePShfCospan (f , Dfâ‰¡ğ”) (g , Dgâ‰¡ğ”Ÿ)) = BasisStructurePShf .Functor.F-ob (ğ” , âˆ£ f , Dfâ‰¡ğ” âˆ£â‚)
   Cospan.sâ‚ (thePShfCospan (f , Dfâ‰¡ğ”) (g , Dgâ‰¡ğ”Ÿ)) = BasisStructurePShf .Functor.F-hom
             {x = (ğ”Ÿ , âˆ£ g , Dgâ‰¡ğ”Ÿ âˆ£â‚)}
             {y = (ğ” âˆ§z ğ”Ÿ , basicOpensAreBasis .âˆ§lClosed ğ” ğ”Ÿ âˆ£ f , Dfâ‰¡ğ” âˆ£â‚ âˆ£ g , Dgâ‰¡ğ”Ÿ âˆ£â‚)}
             (hom-âˆ§â‚‚  ZariskiLattice (CommAlgebrasCategory R' {â„“' = â„“}) ğ” ğ”Ÿ)
   Cospan.sâ‚‚ (thePShfCospan (f , Dfâ‰¡ğ”) (g , Dgâ‰¡ğ”Ÿ)) = BasisStructurePShf .Functor.F-hom
             {x = (ğ” , âˆ£ f , Dfâ‰¡ğ” âˆ£â‚)}
             {y = (ğ” âˆ§z ğ”Ÿ , basicOpensAreBasis .âˆ§lClosed ğ” ğ”Ÿ âˆ£ f , Dfâ‰¡ğ” âˆ£â‚ âˆ£ g , Dgâ‰¡ğ”Ÿ âˆ£â‚)}
             (hom-âˆ§â‚  ZariskiLattice (CommAlgebrasCategory R' {â„“' = â„“}) ğ” ğ”Ÿ)

   Î£helper : (a : Î£[ f âˆˆ R ] D f â‰¡ ğ”) (b : Î£[ g âˆˆ R ] D g â‰¡ ğ”Ÿ) (c : Î£[ h âˆˆ R ] D h â‰¡ ğ” âˆ¨z ğ”Ÿ)
           â†’ isPullback (CommAlgebrasCategory R') (thePShfCospan a b) _ _
                        (BFsq (ğ” , âˆ£ a âˆ£â‚) (ğ”Ÿ , âˆ£ b âˆ£â‚) âˆ£ c âˆ£â‚ BasisStructurePShf)
   Î£helper (f , Dfâ‰¡ğ”) (g , Dgâ‰¡ğ”Ÿ) (h , Dhâ‰¡ğ”âˆ¨ğ”Ÿ) = toSheafPB.lemma
           (ğ” âˆ¨z ğ”Ÿ , âˆ£ h , Dhâ‰¡ğ”âˆ¨ğ”Ÿ âˆ£â‚)
           (ğ” , âˆ£ f , Dfâ‰¡ğ” âˆ£â‚)
           (ğ”Ÿ , âˆ£ g , Dgâ‰¡ğ”Ÿ âˆ£â‚)
           (ğ” âˆ§z ğ”Ÿ , basicOpensAreBasis .âˆ§lClosed ğ” ğ”Ÿ âˆ£ f , Dfâ‰¡ğ” âˆ£â‚ âˆ£ g , Dgâ‰¡ğ”Ÿ âˆ£â‚)
           (Bsq (ğ” , âˆ£ f , Dfâ‰¡ğ” âˆ£â‚) (ğ”Ÿ , âˆ£ g , Dgâ‰¡ğ”Ÿ âˆ£â‚) âˆ£ h , Dhâ‰¡ğ”âˆ¨ğ”Ÿ âˆ£â‚)
           theAlgebraCospan theAlgebraPullback refl gPath fPath fgPath
    where
    open Exponentiation R'
    open RadicalIdeal R'
    open InvertingElementsBase R'
    open DoubleLoc R' h
    open Sâ»Â¹RUniversalProp R' [ h â¿|nâ‰¥0] (powersFormMultClosedSubset h)
    open CommIdeal R[1/ h ]AsCommRing using () renaming (CommIdeal to CommIdealâ‚• ; _âˆˆ_ to _âˆˆâ‚•_)

    instance
     _ = snd R[1/ h ]AsCommRing

    âŸ¨_âŸ©â‚• : R[1/ h ] Ã— R[1/ h ] â†’ CommIdealâ‚•
    âŸ¨ x , y âŸ©â‚• = âŸ¨ replicateFinVec 1 x ++Fin replicateFinVec 1 y âŸ©[ R[1/ h ]AsCommRing ]

    -- the crucial algebraic fact:
    DHelper : D h â‰¡ D f âˆ¨z D g
    DHelper = Dhâ‰¡ğ”âˆ¨ğ”Ÿ âˆ™ congâ‚‚ (_âˆ¨z_) (sym Dfâ‰¡ğ”) (sym Dgâ‰¡ğ”Ÿ)

    fâˆˆâˆšâŸ¨hâŸ© : f âˆˆ âˆš âŸ¨ h âŸ©
    fâˆˆâˆšâŸ¨hâŸ© = isEquivRelâ†’effectiveIso âˆ¼PropValued âˆ¼EquivRel _ _ .fun (sym DHelper) .fst zero

    gâˆˆâˆšâŸ¨hâŸ© : g âˆˆ âˆš âŸ¨ h âŸ©
    gâˆˆâˆšâŸ¨hâŸ© = isEquivRelâ†’effectiveIso âˆ¼PropValued âˆ¼EquivRel _ _ .fun (sym DHelper) .fst one

    fgâˆˆâˆšâŸ¨hâŸ© : (f Â· g) âˆˆ âˆš âŸ¨ h âŸ©
    fgâˆˆâˆšâŸ¨hâŸ© = âˆš âŸ¨ h âŸ© .snd .Â·Closed f gâˆˆâˆšâŸ¨hâŸ©

    1âˆˆfgIdeal : 1r âˆˆâ‚• âŸ¨ (f /1) , (g /1) âŸ©â‚•
    1âˆˆfgIdeal = helper1 (isEquivRelâ†’effectiveIso âˆ¼PropValued âˆ¼EquivRel _ _ .fun DHelper .fst zero)
     where
     helper1 : h âˆˆ âˆš âŸ¨ f , g âŸ©â‚š
             â†’ 1r âˆˆâ‚• âŸ¨ (f /1) , (g /1) âŸ©â‚•
     helper1 = PT.rec isPropPropTrunc (uncurry helper2)
      where
      helper2 : (n : â„•)
              â†’ h ^ n âˆˆ âŸ¨ f , g âŸ©â‚š
              â†’ 1r âˆˆâ‚• âŸ¨ (f /1) , (g /1) âŸ©â‚•
      helper2 n = map helper3
       where
       helper3 : Î£[ Î± âˆˆ FinVec R 2 ]
                  h ^ n â‰¡ linearCombination R' Î± (Î» { zero â†’ f ; (suc zero) â†’ g })
               â†’ Î£[ Î² âˆˆ FinVec R[1/ h ] 2 ]
                  1r â‰¡ linearCombination R[1/ h ]AsCommRing Î²
                                         Î» { zero â†’ f /1 ; (suc zero) â†’ g /1 }
       helper3 (Î± , p) = Î² , path
        where
        Î² : FinVec R[1/ h ] 2
        Î² zero = [ Î± zero , h ^ n , âˆ£ n , refl âˆ£â‚ ]
        Î² (suc zero) = [ Î± (suc zero) , h ^ n , âˆ£ n , refl âˆ£â‚ ]

        path : 1r â‰¡ linearCombination R[1/ h ]AsCommRing Î²
                                      Î» { zero â†’ f /1 ; (suc zero) â†’ g /1 }
        path = eq/ _ _ ((1r , âˆ£ 0 , refl âˆ£â‚) , bigPath)
             âˆ™ cong (Î² zero Â· (f /1) +_) (sym (+IdR (Î² (suc zero) Â· (g /1))))
         where
         bigPath : 1r Â· 1r Â· ((h ^ n Â· 1r) Â· (h ^ n Â· 1r))
                 â‰¡ 1r Â· (Î± zero Â· f Â· (h ^ n Â· 1r) + Î± (suc zero) Â· g Â· (h ^ n Â· 1r)) Â· 1r
         bigPath = solve! R' âˆ™ cong (h ^ n Â·_) p âˆ™ solve! R'

    {-

      We get the following pullback square in CommRings

        R[1/h]   â†’    R[1/h][1/f]
              âŒŸ
        â†“             â†“

        R[1/h][1/g] â†’ R[1/h][1/fg]

      this lifts to a pullback in R-Algebras using PullbackFromCommRing
      as all for rings have canonical morphisms coming from R
      and all the triangles commute.

      Then using toSheaf.lemma we get the desired square

        R[1/h]  â†’  R[1/f]
              âŒŸ
        â†“          â†“

        R[1/g]  â†’  R[1/fg]

      by only providing paths between the corresponding vertices of the square.
      These paths are constructed using Sâ»Â¹RAlgCharEquiv, which gives
      an equivalent characterization of the universal property of localization
      that can be found in e.g. Cor 3.2 of Atiyah-MacDonald

    -}

    theRingCospan = fgCospan R[1/ h ]AsCommRing (f /1) (g /1)
    theRingPullback = fgPullback R[1/ h ]AsCommRing (f /1) (g /1) 1âˆˆfgIdeal

    R[1/h][1/f] = InvertingElementsBase.R[1/_] R[1/ h ]AsCommRing (f /1)
    R[1/h][1/f]AsCommRing = InvertingElementsBase.R[1/_]AsCommRing R[1/ h ]AsCommRing (f /1)
    R[1/h][1/g] = InvertingElementsBase.R[1/_] R[1/ h ]AsCommRing (g /1)
    R[1/h][1/g]AsCommRing = InvertingElementsBase.R[1/_]AsCommRing R[1/ h ]AsCommRing (g /1)
    R[1/h][1/fg] = InvertingElementsBase.R[1/_] R[1/ h ]AsCommRing ((f /1) Â· (g /1))
    R[1/h][1/fg]AsCommRing = InvertingElementsBase.R[1/_]AsCommRing
                               R[1/ h ]AsCommRing ((f /1) Â· (g /1))

    open IsRingHom
    /1/1AsCommRingHomFG : CommRingHom R' R[1/h][1/fg]AsCommRing
    fst /1/1AsCommRingHomFG r = [ [ r , 1r , âˆ£ 0 , refl âˆ£â‚ ] , 1r , âˆ£ 0 , refl âˆ£â‚ ]
    pres0 (snd /1/1AsCommRingHomFG) = refl
    pres1 (snd /1/1AsCommRingHomFG) = refl
    pres+ (snd /1/1AsCommRingHomFG) x y = cong [_] (â‰¡-Ã— (cong [_] (â‰¡-Ã—
                                         (congâ‚‚ _+_ (solve! R') (solve! R'))
                                         (Î£â‰¡Prop (Î» _ â†’ isPropPropTrunc) (solve! R'))))
                                         (Î£â‰¡Prop (Î» _ â†’ isPropPropTrunc) (sym (Â·IdR 1r))))
    presÂ· (snd /1/1AsCommRingHomFG) x y = cong [_] (â‰¡-Ã— (cong [_] (â‰¡-Ã— refl
                                            (Î£â‰¡Prop (Î» _ â†’ isPropPropTrunc) (sym (Â·IdR 1r)))))
                                            (Î£â‰¡Prop (Î» _ â†’ isPropPropTrunc) (sym (Â·IdR 1r))))
    pres- (snd /1/1AsCommRingHomFG) x = refl

    open Cospan
    open Pullback
    open RingHoms
    isRHomR[1/h]â†’R[1/h][1/f] : theRingPullback .pbPrâ‚‚ âˆ˜r /1AsCommRingHom â‰¡ /1/1AsCommRingHom f
    isRHomR[1/h]â†’R[1/h][1/f] = RingHomâ‰¡ (funExt (Î» x â†’ refl))

    isRHomR[1/h]â†’R[1/h][1/g] : theRingPullback .pbPrâ‚ âˆ˜r /1AsCommRingHom â‰¡ /1/1AsCommRingHom g
    isRHomR[1/h]â†’R[1/h][1/g] = RingHomâ‰¡ (funExt (Î» x â†’ refl))

    isRHomR[1/h][1/f]â†’R[1/h][1/fg] : theRingCospan .sâ‚‚ âˆ˜r /1/1AsCommRingHom f â‰¡ /1/1AsCommRingHomFG
    isRHomR[1/h][1/f]â†’R[1/h][1/fg] = RingHomâ‰¡ (funExt
      (Î» x â†’ cong [_] (â‰¡-Ã— (cong [_] (â‰¡-Ã— (cong (x Â·_) (transportRefl 1r) âˆ™ Â·IdR x)
          (Î£â‰¡Prop (Î» _ â†’ isPropPropTrunc) (cong (1r Â·_) (transportRefl 1r) âˆ™ Â·IdR 1r))))
          (Î£â‰¡Prop (Î» _ â†’ isPropPropTrunc) (cong (1r Â·_) (transportRefl 1r) âˆ™ Â·IdR 1r)))))

    isRHomR[1/h][1/g]â†’R[1/h][1/fg] : theRingCospan .sâ‚ âˆ˜r /1/1AsCommRingHom g â‰¡ /1/1AsCommRingHomFG
    isRHomR[1/h][1/g]â†’R[1/h][1/fg] = RingHomâ‰¡ (funExt
      (Î» x â†’ cong [_] (â‰¡-Ã— (cong [_] (â‰¡-Ã— (cong (x Â·_) (transportRefl 1r) âˆ™ Â·IdR x)
          (Î£â‰¡Prop (Î» _ â†’ isPropPropTrunc) (cong (1r Â·_) (transportRefl 1r) âˆ™ Â·IdR 1r))))
          (Î£â‰¡Prop (Î» _ â†’ isPropPropTrunc) (cong (1r Â·_) (transportRefl 1r) âˆ™ Â·IdR 1r)))))


    open PullbackFromCommRing R' theRingCospan theRingPullback
         /1AsCommRingHom (/1/1AsCommRingHom f) (/1/1AsCommRingHom g) /1/1AsCommRingHomFG
    theAlgebraCospan = algCospan isRHomR[1/h]â†’R[1/h][1/f]
                                 isRHomR[1/h]â†’R[1/h][1/g]
                                 isRHomR[1/h][1/f]â†’R[1/h][1/fg]
                                 isRHomR[1/h][1/g]â†’R[1/h][1/fg]
    theAlgebraPullback = algPullback isRHomR[1/h]â†’R[1/h][1/f]
                                     isRHomR[1/h]â†’R[1/h][1/g]
                                     isRHomR[1/h][1/f]â†’R[1/h][1/fg]
                                     isRHomR[1/h][1/g]â†’R[1/h][1/fg]

    --and the three remaining paths
    fPath : theAlgebraCospan .r â‰¡ R[1/ f ]AsCommAlgebra
    fPath = doubleLocCancel fâˆˆâˆšâŸ¨hâŸ©
     where
     open DoubleAlgLoc R' h f

    gPath : theAlgebraCospan .l â‰¡ R[1/ g ]AsCommAlgebra
    gPath = doubleLocCancel gâˆˆâˆšâŸ¨hâŸ©
     where
     open DoubleAlgLoc R' h g

    fgPath : theAlgebraCospan .m â‰¡ R[1/ (f Â· g) ]AsCommAlgebra
    fgPath = path âˆ™ doubleLocCancel fgâˆˆâˆšâŸ¨hâŸ©
     where
     open DoubleAlgLoc R' h (f Â· g)
     open CommAlgChar R'

     R[1/h][1/fg]AsCommRing' = InvertingElementsBase.R[1/_]AsCommRing R[1/ h ]AsCommRing ((f Â· g) /1)

     path : toCommAlg (R[1/h][1/fg]AsCommRing , /1/1AsCommRingHomFG)
          â‰¡ toCommAlg (R[1/h][1/fg]AsCommRing' , /1/1AsCommRingHom (f Â· g))
     path = cong toCommAlg (Î£PathP (p , q))
      where
      eqInR[1/h] : (f /1) Â· (g /1) â‰¡ (f Â· g) /1
      eqInR[1/h] = sym (/1AsCommRingHom .snd .presÂ· f g)

      p : R[1/h][1/fg]AsCommRing â‰¡ R[1/h][1/fg]AsCommRing'
      p i = InvertingElementsBase.R[1/_]AsCommRing R[1/ h ]AsCommRing (eqInR[1/h] i)

      q : PathP (Î» i â†’ CommRingHom R' (p i)) /1/1AsCommRingHomFG (/1/1AsCommRingHom (f Â· g))
      q = toPathP (RingHomâ‰¡ (funExt (
            Î» x â†’ cong [_] (â‰¡-Ã— (cong [_] (â‰¡-Ã— (transportRefl _ âˆ™ transportRefl x)
                (Î£â‰¡Prop (Î» _ â†’ isPropPropTrunc) (transportRefl 1r))))
                (Î£â‰¡Prop (Î» _ â†’ isPropPropTrunc) (transportRefl 1r))))))
