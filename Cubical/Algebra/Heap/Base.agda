module Cubical.Algebra.Heap.Base where

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.Equiv
open import Cubical.Foundations.HLevels
open import Cubical.Foundations.Function
open import Cubical.Foundations.Isomorphism
open import Cubical.Foundations.SIP

open import Cubical.Reflection.RecordEquiv

open import Cubical.Displayed.Base
open import Cubical.Displayed.Auto
open import Cubical.Displayed.Record
open import Cubical.Displayed.Universe

open import Cubical.HITs.PropositionalTruncation

private variable
  â„“ â„“' : Level
  X Y : Type â„“

record IsHeap {H : Type â„“} ([_,_,_] : H â†’ H â†’ H â†’ H) : Type â„“ where
  no-eta-equality
  constructor isheap

  field
    is-set : isSet H
    assoc : âˆ€ a b c d e â†’ [ a , b , [ c , d , e ] ] â‰¡ [ [ a , b , c ] , d , e ]
    idl : âˆ€ a b â†’ [ a , a , b ] â‰¡ b
    idr : âˆ€ a b â†’ [ a , b , b ] â‰¡ a
    inhab : âˆ¥ H âˆ¥â‚

unquoteDecl IsHeapIsoÎ£ = declareRecordIsoÎ£ IsHeapIsoÎ£ (quote IsHeap)

record HeapStr (H : Type â„“) : Type â„“ where
  constructor heapstr

  field
    [_,_,_] : H â†’ H â†’ H â†’ H
    isHeap : IsHeap [_,_,_]

  open IsHeap isHeap public

Heap : âˆ€ â„“ â†’ Type (â„“-suc â„“)
Heap â„“ = TypeWithStr â„“ HeapStr

record IsHeapHom {X : Type â„“} {Y : Type â„“'} (H : HeapStr X) (f : X â†’ Y) (H' : HeapStr Y)
  : Type (â„“-max â„“ â„“') where

  constructor makeIsHeapHom

  private
    module H = HeapStr H
    module H' = HeapStr H'
  field
    pres-[] : (a b c : X) â†’ f H.[ a , b , c ] â‰¡ H'.[ f a , f b , f c ]

unquoteDecl IsHeapHomIsoÎ£ = declareRecordIsoÎ£ IsHeapHomIsoÎ£ (quote IsHeapHom)

isPropIsHeap : {H : Type â„“} ([_,_,_] : H â†’ H â†’ H â†’ H) â†’ isProp (IsHeap [_,_,_])
isPropIsHeap [_,_,_] = isOfHLevelRetractFromIso 1 IsHeapIsoÎ£ $ isPropÎ£ isPropIsSet Î» is-set â†’
   isPropÃ—3 (isPropÎ 5 Î» _ _ _ _ _ â†’ is-set _ _)
            (isPropÎ 2 Î» _ _ â†’ is-set _ _)
            (isPropÎ 2 Î» _ _ â†’ is-set _ _)
            isPropPropTrunc

isPropIsHeapHom : (H : HeapStr X) (f : X â†’ Y) (H' : HeapStr Y) â†’ isProp (IsHeapHom H f H')
isPropIsHeapHom H f H' = isOfHLevelRetractFromIso 1 IsHeapHomIsoÎ£ $
  isPropÎ 3 Î» _ _ _ â†’ H' .is-set _ _
  where open HeapStr

IsHeapEquiv : {X : Type â„“} {Y : Type â„“'} (H : HeapStr X) (e : X â‰ƒ Y) (H' : HeapStr Y) â†’ Type _
IsHeapEquiv H e H' = IsHeapHom H (e .fst) H'

HeapEquiv : (H : Heap â„“) (H' : Heap â„“') â†’ Type _
HeapEquiv H H' = Î£[ e âˆˆ âŸ¨ H âŸ© â‰ƒ âŸ¨ H' âŸ© ] IsHeapEquiv (str H) e (str H')

ğ’®á´°-Heap : DUARel (ğ’®-Univ â„“) HeapStr â„“
ğ’®á´°-Heap = ğ’®á´°-Record (ğ’®-Univ _) IsHeapEquiv
  (fields:
    data[ [_,_,_] âˆ£ autoDUARel _ _ âˆ£ pres-[] ]
    prop[ isHeap âˆ£ (Î» _ _ â†’ isPropIsHeap _) ])
  where
    open HeapStr
    open IsHeapHom

HeapPath : (H H' : Heap â„“) â†’ HeapEquiv H H' â‰ƒ (H â‰¡ H')
HeapPath = âˆ« ğ’®á´°-Heap .UARel.ua

uaHeap : {H H' : Heap â„“} â†’ HeapEquiv H H' â†’ H â‰¡ H'
uaHeap = HeapPath _ _ .fst
