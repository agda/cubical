
{-

This module introduces a way to represent elements of free groups as lists of pairs ([ğŸšÃ— A]), where A identifies generators and Bool differentiates a generator from its inverse.


The definition of `_â‡Š1g` encodes the concept of a word being equivalent to the identity element in a free group. It includes three constructors:
 - The trivial case `[]` represents the identity itself.
 - The `cj` constructor signifies conjugation by a generator, indicating that a word extended by a generator and its inverse is still equivalent to the identity.
 - Lastly, the `_Â·_` constructor asserts that if two words are each equivalent to the identity, their concatenation will also be equivalent to the identity, preserving the identity property under the group operation.

Then, a `_Â·_â»Â¹â‰¡Îµ relation`, defined as `Î» xs ys â†’ (xs ++ invLi ys) â‡Š1g`  captures the relationship where the concatenation of a word with the inverse of another equates to the group identity.

By quotienting by _Â·_â»Â¹â‰¡Îµ, resulting in the List/â†˜â†™group, a group structure on equivalence classes of lists is established. Here, concatenation acts as the group operation, hence forming the foundation for the free group constructed over A.

Given that _Â·_â»Â¹â‰¡Îµ functions is equivalence relation, it permits the retrieval of this relationship from paths in the quotient. This feature facilitates the proof of the uniqueness of the normal form in `Cubical.HITs.FreeGroup.NormalForm`.

In the Discrete module, the presence of decidable equality on the type of generators (A) enables the definition of groups without requiring truncation. This utility is used in `Cubical.HITs.Bouquet.Discrete` to demonstrate that a bouquet over a discrete type is an hGroupoid without truncation.

-}

module Cubical.Algebra.Group.Free where

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.Structure
open import Cubical.Foundations.Function
open import Cubical.Foundations.Equiv
open import Cubical.Foundations.Isomorphism
open import Cubical.Foundations.HLevels

open import Cubical.Data.Bool as ğŸš hiding (_â‰¤_)
open import Cubical.Data.Nat as â„• hiding (_Â·_)
open import Cubical.Data.Nat.Order.Recursive as OR
open import Cubical.Data.Empty as âŠ¥
open import Cubical.Data.Sum as âŠ
open import Cubical.Data.Sigma
open import Cubical.Data.List as Li

open import Cubical.Relation.Nullary
open import Cubical.Relation.Binary

open import Cubical.Functions.Involution

open import Cubical.Algebra.Group

open import Cubical.HITs.SetQuotients as SQ renaming (_/_ to _/â‚‚_ ; [_] to [_]/)



private
  variable
    â„“ : Level

[ğŸšÃ—_] : (A : Type â„“) â†’ Type â„“
[ğŸšÃ—_] = List âˆ˜ (Bool Ã—_)

module NormalForm (A : Type â„“) where

 -- Below we are defining predicate `HasRedex` over `List (Bool Ã— A)`
 -- basing it on presence of sublist - REDucible EXpression of form [(b , x) , (not b , x)]

 notâ‚ : (Bool Ã— A) â†’ (Bool Ã— A)
 notâ‚ = map-fst not

 notâ‚notâ‚ : âˆ€ x â†’ notâ‚ (notâ‚ x) â‰¡ x
 notâ‚notâ‚ (x , y) = cong (_, y) (notnot x)

 IsRedex : Bool Ã— A â†’ Bool Ã— A â†’ Type â„“
 IsRedex x x' = x â‰¡ notâ‚ x'

 symIsRedex : âˆ€ x y â†’ IsRedex x y â†’ IsRedex y x
 symIsRedex x y p = sym (notâ‚notâ‚ _) âˆ™ cong notâ‚ (sym p)

 WillReduce : Bool â†’ A â†’ (xs : [ğŸšÃ— A ]) â†’ Type â„“
 WillReduce _ _ [] = âŠ¥*
 WillReduce b x (h âˆ· _) = IsRedex (b , x) h

 HeadIsRedex : [ğŸšÃ— A ] â†’ Type â„“
 HeadIsRedex [] = âŠ¥*
 HeadIsRedex ((b , a) âˆ· xs) = WillReduce b a xs

 HasRedex : [ğŸšÃ— A ] â†’ Type â„“
 HasRedex [] = âŠ¥*
 HasRedex xs@(_ âˆ· xs') = HeadIsRedex xs âŠ HasRedex xs'

 HasRedexâˆ·Ê³ : âˆ€ {xs} {x} â†’ HasRedex xs â†’ HasRedex (xs âˆ·Ê³ x)
 HasRedexâˆ·Ê³ {xâ‚‚ âˆ· xs} (inr xâ‚) = inr (HasRedexâˆ·Ê³ xâ‚)
 HasRedexâˆ·Ê³ {xâ‚‚ âˆ· xâ‚ƒ âˆ· xs} (inl xâ‚) = inl xâ‚

 HasRedex++ : âˆ€ xs ys â†’ HasRedex xs â†’ HasRedex (xs ++ ys)
 HasRedex++ (xâ‚ âˆ· xs) ys (inr x) = inr (HasRedex++ xs ys x)
 HasRedex++ (xâ‚ âˆ· xâ‚‚ âˆ· xs) ys (inl x) = inl x

 ++HasRedex : âˆ€ xs ys â†’ HasRedex ys â†’ HasRedex (xs ++ ys)
 ++HasRedex [] ys x = x
 ++HasRedex (xâ‚ âˆ· xs) ys x =
   inr (++HasRedex xs ys x)

 -- We use absence of redexes as property of representing normalised term

 IsNormalised : [ğŸšÃ— A ] â†’ Type â„“
 IsNormalised xs = (Â¬ HasRedex xs)

 isPropIsNormalised : âˆ€ xs â†’ isProp (IsNormalised xs)
 isPropIsNormalised xs = isPropÂ¬ _


 IsNormalised[] : IsNormalised []
 IsNormalised[] = lower

 IsNormalised[x] : âˆ€ x â†’ IsNormalised [ x ]
 IsNormalised[x] _ = âŠ.rec lower lower

 IsNormalisedTail : âˆ€ xs â†’ IsNormalised xs â†’ IsNormalised (tail xs)
 IsNormalisedTail [] n = n
 IsNormalisedTail (x âˆ· xs) = _âˆ˜ inr

 Â¬IsRedexâ†’IsNormalisedPair :
   âˆ€ {x x'} â†’ Â¬ IsRedex x x' â†’ IsNormalised (x âˆ· [ x' ])
 Â¬IsRedexâ†’IsNormalisedPair {x' = x'} Â¬ir = âŠ.rec Â¬ir (IsNormalised[x] x')

 invLi : [ğŸšÃ— A ] â†’ [ğŸšÃ— A ]
 invLi = rev âˆ˜ Li.map (map-fst not)

 invLi++ : âˆ€ xs ys â†’ invLi (xs ++ ys) â‰¡
                 invLi ys ++ invLi xs
 invLi++ xs ys =
   sym (cong rev (map++ _ xs ys)) âˆ™
     rev-++ (Li.map _ xs) (Li.map _ ys)

 invol-invLi : isInvolution invLi
 invol-invLi xs =
  sym (rev-map-comm (map-fst not) (invLi xs)) âˆ™
    cong (Li.map (map-fst not))
      (rev-rev (Li.map (map-fst not) xs))
     âˆ™ map-âˆ˜ (map-fst not) (map-fst not) xs âˆ™
     (Î» i â†’ Li.map (map-fst (Î» x â†’ notnot x i) ) xs) âˆ™ map-id xs

 HasRedexInvLi : âˆ€ {xs} â†’ HasRedex xs â†’ HasRedex (invLi xs)
 HasRedexInvLi {_ âˆ· []} x = x
 HasRedexInvLi {xâ‚ âˆ· xâ‚‚ âˆ· xs} = âŠ.rec
  (subst HasRedex (sym (++-assoc (invLi xs) _ _))
     âˆ˜S ++HasRedex (invLi xs) (_ âˆ· _)
     âˆ˜S inl âˆ˜S cong notâ‚  âˆ˜S symIsRedex _ _ )
  (HasRedexâˆ·Ê³ âˆ˜S HasRedexInvLi)


 IsNormalisedInvLi : âˆ€ {xs} â†’ IsNormalised xs â‰ƒ IsNormalised (invLi xs)
 IsNormalisedInvLi = propBiimplâ†’Equiv (isPropIsNormalised _) (isPropIsNormalised _)
  (_âˆ˜S subst HasRedex (invol-invLi _) âˆ˜S HasRedexInvLi)
  (_âˆ˜S HasRedexInvLi)

 HasRedexSplit++ : âˆ€ {xs ys} â†’ HasRedex (xs ++ ys) â†’
        HasRedex (take 1 (rev xs) ++ take 1 ys) âŠ
            (HasRedex xs âŠ HasRedex ys)
 HasRedexSplit++ {[]} = inr âˆ˜ inr
 HasRedexSplit++ {x âˆ· []} {[]} r = âŠ¥.rec (IsNormalised[x] x r)
 HasRedexSplit++ {x âˆ· []} {xâ‚ âˆ· ys} = âŠ.rec (inl âˆ˜ inl) (inr âˆ˜ inr)
 HasRedexSplit++ {x âˆ· xâ‚ âˆ· xs} {ys} =
   âŠ.rec (inr âˆ˜ inl âˆ˜ inl)
    (âŠ.rec (inl âˆ˜ subst (Î» zz â†’ HasRedex (zz ++ take 1 ys))
     (w _ (subst (0 <_) (+-comm 1 _ âˆ™ sym (length++ (rev xs) _)) _)))
      (âŠ.rec (inr âˆ˜ inl âˆ˜ inr) (inr âˆ˜ inr) ) âˆ˜S HasRedexSplit++ {xâ‚ âˆ· xs} {ys})
  where
  w : âˆ€ xs â†’ 0 < length xs â†’ take 1 xs â‰¡ take 1 (xs ++ [ x ])
  w (x âˆ· xs) _ = refl

 -- This predicate, is encoding fact that given list reduces to [],
 -- meaning that if interpreted as term of free group it would be equall to 1g

 infixl 10 _â‡Š1g

 data _â‡Š1g : [ğŸšÃ— A ] â†’ Type â„“ where
  [] : [] â‡Š1g
  cj : âˆ€ x â†’ âˆ€ xs â†’ xs â‡Š1g â†’   (x âˆ· (xs âˆ·Ê³ notâ‚ x) ) â‡Š1g
  _Â·_ : âˆ€ xs ys â†’ xs â‡Š1g â†’ ys â‡Š1g â†’  (xs ++ ys) â‡Š1g

 _rÂ·_ : âˆ€ {xs ys} â†’ xs â‡Š1g â†’ ys â‡Š1g â†’ (xs ++ ys) â‡Š1g
 _rÂ·_ = _Â·_ _ _

 Â¬â‡Š1g[lenâ‰¡1] : âˆ€ xs â†’ length xs â‰¡ 1 â†’ Â¬ xs â‡Š1g
 Â¬â‡Š1g[lenâ‰¡1] .[] x [] = znots x
 Â¬â‡Š1g[lenâ‰¡1] .(_ âˆ· (_ âˆ·Ê³ _)) x (cj _ xs _) =
   snotz ((+-comm 1 _ âˆ™ sym (length++ xs _)) âˆ™ injSuc x)
 Â¬â‡Š1g[lenâ‰¡1] .(xs ++ ys) x ((xs Â· ys) xâ‚ xâ‚‚) =
  âŠ.rec (flip (Â¬â‡Š1g[lenâ‰¡1] ys) xâ‚‚ âˆ˜ snd)
        ((flip (Â¬â‡Š1g[lenâ‰¡1] xs) xâ‚ âˆ˜ fst))
    (m+nâ‰¡1â†’mâ‰¡0Ã—nâ‰¡1âŠmâ‰¡1nâ‰¡0 {length xs} {length ys} (sym (length++ xs ys) âˆ™ x))

 â‡Š1gWillReduceView : âˆ€ b a ys â†’ ys â‡Š1g â†’ WillReduce b a ys â†’
      Î£ ((Î£ _ _â‡Š1g) Ã— (Î£ _ _â‡Š1g))
        Î» ((rl , _) , (rr , _)) â†’
           rl ++ [ b , a ] ++ rr â‰¡ tail ys
 â‡Š1gWillReduceView b a .(x âˆ· (xs âˆ·Ê³ _)) (cj x xs xâ‚ƒ) xâ‚ =
   ((_ , xâ‚ƒ) , (_ , [])) , cong (xs âˆ·Ê³_) xâ‚
 â‡Š1gWillReduceView b a .([] ++ ys) (([] Â· ys) x xâ‚‚) xâ‚ =
   â‡Š1gWillReduceView b a ys xâ‚‚ xâ‚
 â‡Š1gWillReduceView b a .((_ âˆ· _) ++ ys) ((xs@(_ âˆ· _) Â· ys) x xâ‚‚) xâ‚ =
   let (((rl , rlR) , (rr , rrR)) , p) = â‡Š1gWillReduceView b a xs x xâ‚
   in ((_ , rlR) , (_ , (_ Â· _) rrR xâ‚‚)) ,
     sym (++-assoc rl _ _) âˆ™ cong (_++ ys) p

 â‡Š1gâ‡’HasRedex : âˆ€ xs â†’ 0 < length xs â†’ xs â‡Š1g â†’ HasRedex xs
 â‡Š1gâ‡’HasRedex .(xâ‚ âˆ· ([] âˆ·Ê³ notâ‚ xâ‚)) x (cj xâ‚ [] xâ‚‚) =
   inl (symIsRedex _ _ refl)
 â‡Š1gâ‡’HasRedex .(xâ‚ âˆ· ((xâ‚ƒ âˆ· xs) âˆ·Ê³ notâ‚ xâ‚)) x (cj xâ‚ (xâ‚ƒ âˆ· xs) xâ‚‚) =
   inr (HasRedexâˆ·Ê³ (â‡Š1gâ‡’HasRedex _ _ xâ‚‚))
 â‡Š1gâ‡’HasRedex .([] ++ ys) x (([] Â· ys) xâ‚ xâ‚‚) = â‡Š1gâ‡’HasRedex _ x xâ‚‚
 â‡Š1gâ‡’HasRedex .((xâ‚ƒ âˆ· xs) ++ ys) x (((xâ‚ƒ âˆ· xs) Â· ys) xâ‚ xâ‚‚) =
  HasRedex++ _ _ (â‡Š1gâ‡’HasRedex _ _ xâ‚)

 isNormalisedâ‡Š1g : âˆ€ xs â†’ IsNormalised xs â†’  xs â‡Š1g â†’ xs â‰¡ []
 isNormalisedâ‡Š1g [] _ _ = refl
 isNormalisedâ‡Š1g (xâ‚‚ âˆ· xs) x xâ‚ = âŠ¥.rec (x (â‡Š1gâ‡’HasRedex _ _ xâ‚))


 â‡Š1g-invLi : âˆ€ {xs} â†’ xs â‡Š1g â†’ (invLi xs) â‡Š1g
 â‡Š1g-invLi [] = []
 â‡Š1g-invLi (cj x xs xâ‚) =
   subst _â‡Š1g (cong (_âˆ· invLi (x âˆ· xs)) (sym (notâ‚notâ‚ _) )
    âˆ™ cong (_âˆ·Ê³ _) (sym (invLi++ xs [ notâ‚ x ]))) (cj x _ (â‡Š1g-invLi xâ‚))
 â‡Š1g-invLi ((xs Â· ys) x xâ‚) =
   subst _â‡Š1g (sym (invLi++ xs ys)) (â‡Š1g-invLi xâ‚ rÂ· â‡Š1g-invLi x)

 â‡Š1gRot : âˆ€ xs â†’ xs â‡Š1g â†’ _â‡Š1g (rot xs)
 â‡Š1gRot [] x = []
 â‡Š1gRot xs@(x'@(b , a) âˆ· xs') x =
  let (((rl , rlR) , (rr , rrR)) , p) = â‡Š1gWillReduceView (not b) a xs x refl
  in subst _â‡Š1g ((Î» i â†’ (++-assoc rl ([ not b , a ] ++ rr)
               [ notâ‚notâ‚ x' i ]) (~ i)) âˆ™ cong (_âˆ·Ê³ x') p)
       (rlR rÂ· cj (not b , a) _ rrR)

 â‡Š1g++comm : âˆ€ xs ys â†’ (xs ++ ys) â‡Š1g â†’ (ys ++ xs) â‡Š1g
 â‡Š1g++comm [] ys = subst _â‡Š1g (sym (++-unit-r ys))
 â‡Š1g++comm (xâ‚ âˆ· xs) ys x =
   subst _â‡Š1g (++-assoc ys _ _)
      (â‡Š1g++comm xs _ (subst _â‡Š1g (++-assoc xs _ _) (â‡Š1gRot _ x)))

 popâ‡Š1gHead : âˆ€ {xs} â†’ HeadIsRedex xs â†’ xs â‡Š1g â†’ _â‡Š1g (tail (tail xs))
 popâ‡Š1gHead x (cj xâ‚ [] r) = []
 popâ‡Š1gHead x (cj xâ‚ (xâ‚‚ âˆ· xs) r) =
   subst (_â‡Š1g âˆ˜ (xs âˆ·Ê³_)) (symIsRedex _ _ x) (â‡Š1gRot _ r)
 popâ‡Š1gHead x (([] Â· ys) r râ‚) = popâ‡Š1gHead x râ‚
 popâ‡Š1gHead x (((xâ‚ âˆ· []) Â· ys) r râ‚) = âŠ¥.rec (Â¬â‡Š1g[lenâ‰¡1] [ xâ‚ ] refl r)
 popâ‡Š1gHead x (((_ âˆ· _ âˆ· _) Â· ys) r râ‚) = popâ‡Š1gHead x r rÂ· râ‚

 â‡Š1gCJ : âˆ€ xs ys â†’ _â‡Š1g (ys ++ xs ++ invLi ys) â†’ xs â‡Š1g
 â‡Š1gCJ xs [] = subst _â‡Š1g (++-unit-r xs)
 â‡Š1gCJ xs (xâ‚ âˆ· ys) x =
  â‡Š1gCJ xs ys (popâ‡Š1gHead refl
   (â‡Š1g++comm (xâ‚ âˆ· ys ++ xs ++ invLi ys) [ notâ‚ xâ‚ ]
    (subst _â‡Š1g (cong (xâ‚ âˆ·_) (cong (ys ++_) (sym (++-assoc xs _ _))
           âˆ™ sym (++-assoc ys _ _))) x)))

 nf-uR : âˆ€ xs ys
    â†’ IsNormalised (invLi xs)
    â†’ IsNormalised ys
    â†’ (invLi xs ++ ys) â‡Š1g â†’ xs â‰¡ ys
 nf-uR xs [] nXs xâ‚ r = sym (invol-invLi xs) âˆ™ cong invLi
      (isNormalisedâ‡Š1g _ nXs (subst _â‡Š1g (++-unit-r _) r))
 nf-uR [] (xâ‚ƒ âˆ· ys) x xâ‚ xâ‚‚ = âŠ¥.rec (xâ‚ (â‡Š1gâ‡’HasRedex _ _ xâ‚‚))
 nf-uR xs@(_ âˆ· _) (xâ‚ƒ âˆ· ys) nXs nYs r =
   let ww = subst _â‡Š1g (cong (xâ‚ƒ âˆ·_) (sym (++-assoc ys _ _)))
              (â‡Š1g++comm (invLi xs) _ r)
       www = subst (0 <_)
           (sym (+-suc _ _)
             âˆ™ sym (length++ (invLi xs) _)) _
   in (âŠ.rec (âŠ.rec (Î» p â†’ congâ‚‚ _âˆ·_
          (sym (notâ‚notâ‚ _) âˆ™ sym (symIsRedex _ _ p))
          (nf-uR (tail xs) _ (fst IsNormalisedInvLi
             (invEq (IsNormalisedInvLi {xs}) nXs âˆ˜ inr) ) (nYs âˆ˜ inr)
               (â‡Š1g++comm _ (invLi (tail xs))
                  (popâ‡Š1gHead p (â‡Š1g++comm _ [ _ ] ww)))))
        (âŠ¥.rec âˆ˜ IsNormalised[x] xâ‚ƒ) âˆ˜S
       subst HasRedex (cong ((_++ _) âˆ˜ take 1)
         (rev-rev (Li.map notâ‚ xs))))
        (âŠ¥.rec âˆ˜ âŠ.rec nXs nYs)
    âˆ˜S HasRedexSplit++ {invLi xs}
    âˆ˜S â‡Š1gâ‡’HasRedex _ www) r

 infixr 5 _Â·_â»Â¹â‰¡Îµ

 -- We are defining this relation using record
 -- to help later definitions infer implicit arguments.
 -- With the help of earlier lemmas, we can prove that it is EquivRel,
 -- wich lets us recover witness of this relation from path in SetQuotent

 record _Â·_â»Â¹â‰¡Îµ (xs ys : _) : Type â„“ where
  constructor [_]â‰¡Îµ
  field
   â‰¡Îµ :  (xs ++ invLi ys) â‡Š1g

 open _Â·_â»Â¹â‰¡Îµ public

 open BinaryRelation
 open isEquivRel

 Â·â»Â¹â‰¡Îµ-refl : isRefl _Â·_â»Â¹â‰¡Îµ
 Â·â»Â¹â‰¡Îµ-refl [] = [ [] ]â‰¡Îµ
 â‰¡Îµ (Â·â»Â¹â‰¡Îµ-refl (x âˆ· xs)) =
   subst _â‡Š1g (sym (++-assoc [ x ] xs (invLi (x âˆ· xs)) âˆ™
         cong (x âˆ·_) (sym (++-assoc xs (invLi xs) _))))
     (cj x _ (â‰¡Îµ (Â·â»Â¹â‰¡Îµ-refl xs)))

 Â·â»Â¹â‰¡Îµ-sym : isSym _Â·_â»Â¹â‰¡Îµ
 â‰¡Îµ (Â·â»Â¹â‰¡Îµ-sym a b [ p ]â‰¡Îµ) =
    subst _â‡Š1g (invLi++ a (invLi b) âˆ™
       cong (_++ invLi a) (invol-invLi b)) (â‡Š1g-invLi p)

 Â·â»Â¹â‰¡Îµ-trans : isTrans _Â·_â»Â¹â‰¡Îµ
 â‰¡Îµ (Â·â»Â¹â‰¡Îµ-trans xs ys zs [ p ]â‰¡Îµ [ q ]â‰¡Îµ) =
    â‡Š1g++comm (invLi zs) xs
      (â‡Š1gCJ (invLi zs ++ xs) ys
        (subst _â‡Š1g (++-assoc ys _ _ âˆ™
         cong (ys ++_) (sym (++-assoc (invLi zs) _ _))) (q rÂ· p)))

 Â·â»Â¹â‰¡Îµ-isEquivRel : isEquivRel _Â·_â»Â¹â‰¡Îµ
 reflexive Â·â»Â¹â‰¡Îµ-isEquivRel = Â·â»Â¹â‰¡Îµ-refl
 symmetric Â·â»Â¹â‰¡Îµ-isEquivRel = Â·â»Â¹â‰¡Îµ-sym
 transitive Â·â»Â¹â‰¡Îµ-isEquivRel = Â·â»Â¹â‰¡Îµ-trans

 â‰¡â†’red : âˆ€ a b â†’ Iso ([ a ]/ â‰¡ [ b ]/) âˆ¥ a Â· b â»Â¹â‰¡Îµ âˆ¥â‚
 â‰¡â†’red = isEquivRelâ†’TruncIso Â·â»Â¹â‰¡Îµ-isEquivRel


 open Iso


 _â†˜â†™_ = _Â·_â»Â¹â‰¡Îµ

 List/â†˜â†™ : Type â„“
 List/â†˜â†™ = _ /â‚‚ _Â·_â»Â¹â‰¡Îµ


 -- concatenation of lists, respects `_Â·_â»Â¹â‰¡Îµ` on both sides,
 -- so we can give SetQuotiening by _Â·_â»Â¹â‰¡Îµ  structure of a group
 -- by taking concatenation as group operation

 _â†˜â†™++â†˜â†™_ : âˆ€ {xsL xsR ysL ysR} â†’
    xsL â†˜â†™ ysL â†’ xsR â†˜â†™ ysR â†’
      (xsL ++ xsR) â†˜â†™ (ysL ++ ysR)
 â‰¡Îµ (_â†˜â†™++â†˜â†™_ {xsL} {xsR} {ysL} {ysR} [ p ]â‰¡Îµ [ q ]â‰¡Îµ) =
   subst _â‡Š1g (sym (++-assoc xsL _ _))
     (â‡Š1g++comm _ xsL
       (subst _â‡Š1g (++-assoc xsR _ _ âˆ™âˆ™
           (Î» i â†’ xsR ++ ++-assoc (invLi ysR) (invLi ysL) xsL (~ i)) âˆ™âˆ™
           ( Î» i â†’ ++-assoc xsR (invLi++ ysL ysR (~ i)) xsL (~ i)))
         (q rÂ· â‡Š1g++comm xsL _ p)))

 List/â†˜â†™Â· : List/â†˜â†™ â†’ List/â†˜â†™ â†’ List/â†˜â†™
 List/â†˜â†™Â· =  SQ.rec2 squash/ (Î» a b â†’ [ a ++ b ]/)
    (Î» _ _ c â†’ eq/ _ _ âˆ˜ _â†˜â†™++â†˜â†™ (Â·â»Â¹â‰¡Îµ-refl c))
    (Î» a _ _ â†’ eq/ _ _ âˆ˜ (Â·â»Â¹â‰¡Îµ-refl a) â†˜â†™++â†˜â†™_ )

 List/â†˜â†™GroupStr : GroupStr List/â†˜â†™
 GroupStr.1g List/â†˜â†™GroupStr = [ [] ]/
 GroupStr._Â·_ List/â†˜â†™GroupStr = List/â†˜â†™Â·
 GroupStr.inv List/â†˜â†™GroupStr =
  SQ.rec squash/ ([_]/ âˆ˜ invLi)
     Î» xs ys â†’ sym âˆ˜S eq/ _ _ âˆ˜S [_]â‰¡Îµ
     âˆ˜S subst (_â‡Š1g âˆ˜ (invLi ys ++_)) (sym (invol-invLi xs))
     âˆ˜S â‡Š1g++comm xs (invLi ys) âˆ˜S â‰¡Îµ

 GroupStr.isGroup List/â†˜â†™GroupStr = makeIsGroup squash/
  (SQ.elimProp3 (Î» _ _ _ â†’ squash/ _ _)
      Î» xs _ _ â†’ cong [_]/ (sym (++-assoc xs _ _)))
  (SQ.elimProp (Î» _ â†’ squash/ _ _) Î» xs â†’ cong [_]/ (++-unit-r xs))
  (SQ.elimProp (Î» _ â†’ squash/ _ _) Î» _ â†’ refl)
  (SQ.elimProp (Î» _ â†’ squash/ _ _) Î» xs â†’ sym (eq/ _ _
     ([ â‡Š1g-invLi (â‰¡Îµ (Â·â»Â¹â‰¡Îµ-refl xs)) ]â‰¡Îµ)))
  (SQ.elimProp (Î» _ â†’ squash/ _ _) Î» xs â†’ eq/ _ _ [
     subst _â‡Š1g (cong (invLi xs ++_) (invol-invLi xs) âˆ™
       sym (++-unit-r _)) (â‰¡Îµ (Â·â»Â¹â‰¡Îµ-refl (invLi xs))) ]â‰¡Îµ)

 List/â†˜â†™group : Group â„“
 List/â†˜â†™group = _ , List/â†˜â†™GroupStr



 module Discrete (_â‰Ÿ_ : Discrete A) where

  -- For discrete set of generators, we can compute normal form,
  -- and proove `Iso (Î£ _  IsNormalised) List/â†˜â†™`

  isSetA = Discreteâ†’isSet _â‰Ÿ_

  isSet[ğŸšÃ—] = isOfHLevelList 0 (isSetÃ— isSetBool isSetA)

  IsRedex? : âˆ€ x x' â†’ Dec (IsRedex x x')
  IsRedex? _ _ = discreteÎ£ ğŸš._â‰Ÿ_ (Î» _ â†’ _â‰Ÿ_) _ _

  HeadIsRedex? : âˆ€ xs â†’ Dec (HeadIsRedex xs)
  HeadIsRedex? [] = no lower
  HeadIsRedex? (x âˆ· []) = no lower
  HeadIsRedex? (x âˆ· x' âˆ· _) = IsRedex? x x'

  preÎ·Â· : âˆ€ x xs â†’ Dec (HeadIsRedex (x âˆ· xs)) â†’ [ğŸšÃ— A ]
  preÎ·Â· _ xs (yes _) = tail xs
  preÎ·Â· x xs (no _) = x âˆ· xs

  preÎ·Â·-N : âˆ€ {x xs} hir? â†’ IsNormalised xs â†’ IsNormalised (preÎ·Â· x xs hir?)
  preÎ·Â·-N (yes _) = IsNormalisedTail _
  preÎ·Â·-N (no Â¬p) = âŠ.rec Â¬p

  sec-preÎ·Â· : âˆ€ x xs p q â†’ IsNormalised xs â†’ preÎ·Â· (notâ‚ x) (preÎ·Â· x xs p) q â‰¡ xs
  sec-preÎ·Â· x (xâ‚‚ âˆ· xs) (yes p) (no Â¬p) xâ‚ =
    cong (_âˆ· xs) (sym (symIsRedex _ _ p))
  sec-preÎ·Â· x (xâ‚‚ âˆ· xâ‚ƒ âˆ· xs) (yes p) (yes pâ‚) xâ‚ =
    âŠ¥.rec (xâ‚ (inl (symIsRedex _ _ p âˆ™ pâ‚)))
  sec-preÎ·Â· x xs (no Â¬p) (no Â¬pâ‚) xâ‚ = âŠ¥.rec (Â¬pâ‚ refl)
  sec-preÎ·Â· x xs (no Â¬p) (yes p) _ = refl

  Î·Â· : (Bool Ã— A) â†’ [ğŸšÃ— A ] â†’ [ğŸšÃ— A ]
  Î·Â· x xs = preÎ·Â· _ _ (HeadIsRedex? (x âˆ· xs))

  Î·Â·âˆ· : âˆ€ x xs â†’ (HeadIsRedex (x âˆ· xs) â†’ âŠ¥) â†’ Î·Â· x xs â‰¡ x âˆ· xs
  Î·Â·âˆ· x xs xâ‚ = cong (Î» u â†’ preÎ·Â· x xs u)
   (â‰¡no (HeadIsRedex? (x âˆ· xs)) xâ‚)

  nÎ·Â· : (Bool Ã— A) â†’ Î£ _ IsNormalised â†’ Î£ _ IsNormalised
  fst (nÎ·Â· x xâ‚) = Î·Â· x (fst xâ‚)
  snd (nÎ·Â· x xâ‚) = preÎ·Â·-N (HeadIsRedex? _) (snd xâ‚)


  Î·Â·iso : (Bool Ã— A) â†’ Iso (Î£ [ğŸšÃ— A ] IsNormalised) (Î£ [ğŸšÃ— A ] IsNormalised)
  Iso.fun (Î·Â·iso x) = nÎ·Â· x
  Iso.inv (Î·Â·iso x) = nÎ·Â· (notâ‚ x)
  Iso.rightInv (Î·Â·iso x) b =
    Î£â‰¡Prop isPropIsNormalised
     (funExtâ» (cong Î·Â· (sym (notâ‚notâ‚ x)) ) (Î·Â· (notâ‚ x) (fst b))
      âˆ™ sec-preÎ·Â· (notâ‚ x) _ (HeadIsRedex? _) (HeadIsRedex? _) (snd b))
  Iso.leftInv (Î·Â·iso x) a =
    Î£â‰¡Prop isPropIsNormalised
     (sec-preÎ·Â· x _ (HeadIsRedex? _) (HeadIsRedex? _) (snd a))

  Î·Â·â‰ƒ = isoToEquiv âˆ˜ Î·Â·iso

  normalise : âˆ€ xs â†’ Î£ _ Î» xs' â†’
    (xs' Â· xs â»Â¹â‰¡Îµ) Ã— IsNormalised xs'
  normalise = Li.elim ([] , [ [] ]â‰¡Îµ , lower )
   Î» {x} {xs} (xs' , [ u ]â‰¡Îµ , v) â†’
    let zz : âˆ€ xs' uu u â†’ (preÎ·Â· x xs' uu ++ invLi (x âˆ· xs)) â‡Š1g
        zz =
          Î» { xs' (no Â¬p) â†’ subst (_â‡Š1g âˆ˜S (x âˆ·_)) (++-assoc xs' _ _) âˆ˜ cj x _
             ; [] (yes ())
             ; (_ âˆ· xs') (yes p) â†’
                  subst _â‡Š1g (Î» i â†’ ++-assoc xs' (invLi xs)
                       [ symIsRedex _ _ p i ] i) âˆ˜ â‡Š1gRot _ }
        h = HeadIsRedex? _
    in  _ , [ zz xs' h u ]â‰¡Îµ , preÎ·Â·-N h v

  IsoNF : Iso (Î£ _  IsNormalised) List/â†˜â†™
  fun IsoNF = [_]/ âˆ˜ fst
  Iso.inv IsoNF =
   SQ.rec (isSetÎ£ isSet[ğŸšÃ—] (isPropâ†’isSet âˆ˜ isPropIsNormalised))
   ((Î» (_ , _ , u) â†’ _ , u) âˆ˜ normalise)
   Î» _ _ â†’
     let (a' , t  , u ) = normalise _ ; (b' , t' , u') = normalise _
     in  Î£â‰¡Prop isPropIsNormalised âˆ˜S sym
      âˆ˜S nf-uR _ _ (fst (IsNormalisedInvLi {b'}) u') u
      âˆ˜S â‡Š1g++comm a' (invLi b') âˆ˜S â‰¡Îµ
      âˆ˜S flip (Â·â»Â¹â‰¡Îµ-trans _ _ _) (Â·â»Â¹â‰¡Îµ-sym _ _ t')
      âˆ˜S Â·â»Â¹â‰¡Îµ-trans _ _ _ t
  rightInv IsoNF = SQ.elimProp (Î» _ â†’ squash/ _ _)
    (eq/ _ _ âˆ˜ fst âˆ˜ snd âˆ˜ normalise)
  leftInv IsoNF = Î£â‰¡Prop isPropIsNormalised âˆ˜ uncurry
   (Li.elim (Î» _ â†’ refl) Î» f v â†’
   let lem : âˆ€ uu â†’ preÎ·Â· _ _ uu â‰¡ _ âˆ· _
       lem =
        Î» { (yes p) â†’ âŠ¥.rec (v (inl (subst (WillReduce _ _) (f (v âˆ˜ inr)) p)))
          ; (no Â¬p) â†’ refl }
   in lem (HeadIsRedex? _) âˆ™ cong (_ âˆ·_) (f (v âˆ˜ inr)))

module NF {â„“'} {A : Type â„“} (G : Group â„“') (Î· : A â†’ âŸ¨ G âŸ©) where
 open NormalForm A

 open GroupStr (snd G) renaming (_Â·_ to _Â·fg_) public

 Î·* : Bool Ã— A â†’ âŸ¨ G âŸ©
 Î·* (b , a) = (if b then idfun _ else inv) (Î· a)

 fromList : [ğŸšÃ— A ] â†’ âŸ¨ G âŸ©
 fromList = foldr (_Â·fg_ âˆ˜ Î·*) 1g

 record NF (g : _) : Type (â„“-max â„“ â„“') where
  constructor _nf_,_
  field
   word : [ğŸšÃ— A ]
   fromListWordâ‰¡ : fromList word â‰¡ g
   isNormalisedWord : IsNormalised word


 fromListÂ· : âˆ€ xs ys â†’ fromList (xs ++ ys) â‰¡
                           fromList xs Â·fg fromList ys
 fromListÂ· [] _ = sym (Â·IdL _)
 fromListÂ· (_ âˆ· xs) _ =
  cong (_ Â·fg_) (fromListÂ· xs _) âˆ™
   Â·Assoc _ _ _

 redex-Îµ-Î·* : âˆ€ x x' â†’ IsRedex x x' â†’ Î·* x Â·fg Î·* x' â‰¡ 1g
 redex-Îµ-Î·* (false , _) (false , _) = âŠ¥.rec âˆ˜ falseâ‰¢true âˆ˜ cong fst
 redex-Îµ-Î·* (false , x) (true , _) q =
   cong (inv (Î· x) Â·fg_) (cong (Î·) (sym (cong snd q))) âˆ™ Â·InvL (Î· x)
 redex-Îµ-Î·* (true , x) (false , _) q =
   cong (Î· x Â·fg_) (cong (inv âˆ˜ Î·) (sym (cong snd q))) âˆ™ Â·InvR (Î· x)
 redex-Îµ-Î·* (true , _) (true , _) = âŠ¥.rec âˆ˜ trueâ‰¢false âˆ˜ cong fst
