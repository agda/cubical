{-

Elementary transformation specific to coefficient â„¤

-}
{-# OPTIONS --safe #-}
module Cubical.Algebra.IntegerMatrix.Elementaries where

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.Function

open import Cubical.Data.Nat hiding (_+_ ; _Â·_)
open import Cubical.Data.FinData

open import Cubical.Relation.Nullary

open import Cubical.Tactics.CommRingSolver
open import Cubical.Algebra.Ring.BigOps
open import Cubical.Algebra.CommRing
open import Cubical.Algebra.CommRing.Instances.Int

open import Cubical.Algebra.Matrix
open import Cubical.Algebra.Matrix.CommRingCoefficient
open import Cubical.Algebra.Matrix.RowTransformation

private
  variable
    â„“ : Level
    m n : â„•

-- It seems there are bugs when applying ring solver to integers.
-- The following is a work-around.
private
  module Helper {â„“ : Level} (ğ“¡ : CommRing â„“) where
    open CommRingStr (ğ“¡ .snd)

    helper1 : (a b x y g : ğ“¡ .fst) â†’ (a Â· x - b Â· - y) Â· g â‰¡ a Â· (x Â· g) + b Â· (y Â· g)
    helper1 _ _ _ _ _ = solve! ğ“¡

    helper2 : (a b : ğ“¡ .fst) â†’ a â‰¡ 1r Â· a + 0r Â· b
    helper2 _ _ = solve! ğ“¡

open Helper â„¤CommRing

module ElemTransformationâ„¤ where

  open import Cubical.Foundations.Powerset

  open import Cubical.Data.Int using (Â·rCancel)
  open import Cubical.Data.Int.Divisibility

  private
    â„¤ = â„¤CommRing .fst

  open CommRingStr      (â„¤CommRing .snd)
  open Sum              (CommRingâ†’Ring â„¤CommRing)

  open Coefficient â„¤CommRing
  open LinearTransformation â„¤CommRing
  open BÃ©zout

  open SimRel
  open Sim

  open isLinear
  open isLinear2Ã—2

  -- The BÃ©zout step to simplify one row

  module _
    (x y : â„¤)(b : BÃ©zout x y) where

    bÃ©zout2Mat : Mat 2 2
    bÃ©zout2Mat zero zero = b .coefâ‚
    bÃ©zout2Mat zero one  = b .coefâ‚‚
    bÃ©zout2Mat one  zero = - (divâ‚‚ b)
    bÃ©zout2Mat one  one  = divâ‚ b

    module _
      (p : Â¬ x â‰¡ 0) where

      open Units â„¤CommRing

      private
        detEq : det2Ã—2 bÃ©zout2Mat Â· b .gcd â‰¡ b .gcd
        detEq =
            helper1 (b .coefâ‚) (b .coefâ‚‚) _ _ _
          âˆ™ (Î» t â†’ b .coefâ‚ Â· divideEq (b .isCD .fst) t + b .coefâ‚‚ Â· divideEq (b .isCD .snd) t)
          âˆ™ b .identity

        detâ‰¡1 : det2Ã—2 bÃ©zout2Mat â‰¡ 1
        detâ‰¡1 = Â·rCancel _ _ _ (detEq âˆ™ sym (Â·IdL _)) (Â¬mâ‰¡0â†’Â¬gcdâ‰¡0 b p)

      isInvBÃ©zout2Mat : isInv bÃ©zout2Mat
      isInvBÃ©zout2Mat = isInvMat2x2 bÃ©zout2Mat (subst (Î» r â†’ r âˆˆ RË£) (sym detâ‰¡1) RË£ContainsOne)

  module _
    (M : Mat 2 (suc n)) where

    private
      b = bÃ©zout (M zero zero) (M one zero)

    bÃ©zout2Rows : Mat 2 (suc n)
    bÃ©zout2Rows zero i =   b .coefâ‚ Â· M zero i +  b .coefâ‚‚ Â· M one i
    bÃ©zout2Rows one  i = - (divâ‚‚ b) Â· M zero i +    divâ‚ b Â· M one i

    bÃ©zout2Rows-vanish : bÃ©zout2Rows one zero â‰¡ 0
    bÃ©zout2Rows-vanish = divÂ·- b

    bÃ©zout2Rows-divâ‚ : (n : â„¤) â†’ M zero zero âˆ£ n â†’ bÃ©zout2Rows zero zero âˆ£ n
    bÃ©zout2Rows-divâ‚ n p = subst (Î» a â†’ a âˆ£ n) (sym (b .identity)) (âˆ£-trans (b .isCD .fst) p)

    bÃ©zout2Rows-divâ‚‚ : (n : â„¤) â†’ M one  zero âˆ£ n â†’ bÃ©zout2Rows zero zero âˆ£ n
    bÃ©zout2Rows-divâ‚‚ n p = subst (Î» a â†’ a âˆ£ n) (sym (b .identity)) (âˆ£-trans (b .isCD .snd) p)

    bÃ©zout2Rows-nonZero : Â¬ M zero zero â‰¡ 0 â†’ Â¬ bÃ©zout2Rows zero zero â‰¡ 0
    bÃ©zout2Rows-nonZero p r =
      p (sym (âˆ£-zeroË¡ (subst (Î» a â†’ a âˆ£ M zero zero) r (bÃ©zout2Rows-divâ‚ (M zero zero) (âˆ£-refl refl)))))

    bÃ©zout2Rows-inv : Â¬ M zero zero â‰¡ 0 â†’ M zero zero âˆ£ M one zero â†’ M zero â‰¡ bÃ©zout2Rows zero
    bÃ©zout2Rows-inv p q t j =
      let (câ‚â‰¡1 , câ‚‚â‰¡0) = bÃ©zoutâˆ£ _ _ p q in
      (helper2 (M zero j) (M one j) âˆ™ (Î» t â†’ câ‚â‰¡1 (~ t) Â· M zero j + câ‚‚â‰¡0 (~ t) Â· M one j)) t

    bÃ©zout2Rows-commonDiv : (a : â„¤)
      â†’ ((j : Fin (suc n)) â†’ a âˆ£ M zero j)
      â†’ ((j : Fin (suc n)) â†’ a âˆ£ M one  j)
      â†’  (i : Fin 2)(j : Fin (suc n)) â†’ a âˆ£ bÃ©zout2Rows i j
    bÃ©zout2Rows-commonDiv a p q zero j = âˆ£-+ (âˆ£-rightÂ· {n =   b .coefâ‚} (p j)) (âˆ£-rightÂ· {n = b .coefâ‚‚} (q j))
    bÃ©zout2Rows-commonDiv a p q one  j = âˆ£-+ (âˆ£-rightÂ· {n = - (divâ‚‚ b)} (p j)) (âˆ£-rightÂ· {n =   divâ‚ b} (q j))

  module _
    (M : Mat (suc m) (suc n)) where

    bÃ©zoutRows : Mat (suc m) (suc n)
    bÃ©zoutRows = transRows bÃ©zout2Rows M

    bÃ©zoutRows-vanish : (i : Fin m) â†’ bÃ©zoutRows (suc i) zero â‰¡ 0
    bÃ©zoutRows-vanish = transRowsIndP' _ (Î» v â†’ v zero â‰¡ 0) bÃ©zout2Rows-vanish M

    bÃ©zoutRows-divâ‚-helper : (n : â„¤) â†’ M zero zero âˆ£ n â†’ bÃ©zoutRows zero zero âˆ£ n
    bÃ©zoutRows-divâ‚-helper n = transRowsIndP _ (Î» v â†’ v zero âˆ£ n) (Î» M â†’ bÃ©zout2Rows-divâ‚ M n) M

    bÃ©zoutRows-divâ‚‚-helper : (n : â„¤) â†’ (i : Fin m) â†’ M (suc i) zero âˆ£ n â†’ bÃ©zoutRows zero zero âˆ£ n
    bÃ©zoutRows-divâ‚‚-helper n =
      transRowsIndPQ' _ (Î» v â†’ v zero âˆ£ n) (Î» v â†’ v zero âˆ£ n)
        (Î» M â†’ bÃ©zout2Rows-divâ‚ M n) (Î» M â†’ bÃ©zout2Rows-divâ‚‚ M n) M

    bÃ©zoutRows-div : (i : Fin (suc m)) â†’ bÃ©zoutRows zero zero âˆ£ M i zero
    bÃ©zoutRows-div zero    = bÃ©zoutRows-divâ‚-helper _   (âˆ£-refl refl)
    bÃ©zoutRows-div (suc i) = bÃ©zoutRows-divâ‚‚-helper _ i (âˆ£-refl refl)

    bÃ©zoutRows-nonZero : Â¬ M zero zero â‰¡ 0 â†’ Â¬ bÃ©zoutRows zero zero â‰¡ 0
    bÃ©zoutRows-nonZero p r = p (sym (âˆ£-zeroË¡ (subst (Î» a â†’ a âˆ£ M zero zero) r (bÃ©zoutRows-div zero))))

    bÃ©zoutRows-inv : Â¬ M zero zero â‰¡ 0 â†’ ((i : Fin m) â†’ M zero zero âˆ£ M (suc i) zero) â†’ M zero â‰¡ bÃ©zoutRows zero
    bÃ©zoutRows-inv = transRowsIndPRelInv _ (Î» V â†’ Â¬ V zero â‰¡ 0) (Î» U V â†’ U zero âˆ£ V zero) bÃ©zout2Rows-inv M

    bÃ©zoutRows-commonDivâ‚€ : (a : â„¤)
      â†’ ((j : Fin (suc n)) â†’ a âˆ£ M zero j)
      â†’ ((i : Fin m)(j : Fin (suc n)) â†’ a âˆ£ M (suc i) j)
      â†’  (j : Fin (suc n)) â†’ a âˆ£ bÃ©zoutRows zero j
    bÃ©zoutRows-commonDivâ‚€ a =
      transRowsIndPâ‚€ _ (Î» V â†’ ((j : Fin (suc n)) â†’ a âˆ£ V j))
        (Î» N s s' â†’ bÃ©zout2Rows-commonDiv N a s s' zero)
        (Î» N s s' â†’ bÃ©zout2Rows-commonDiv N a s s' one) _

    bÃ©zoutRows-commonDivâ‚ : (a : â„¤)
      â†’ ((j : Fin (suc n)) â†’ a âˆ£ M zero j)
      â†’ ((i : Fin m)(j : Fin (suc n)) â†’ a âˆ£ M (suc i) j)
      â†’  (i : Fin m)(j : Fin (suc n)) â†’ a âˆ£ bÃ©zoutRows (suc i) j
    bÃ©zoutRows-commonDivâ‚ a =
      transRowsIndPâ‚ _ (Î» V â†’ ((j : Fin (suc n)) â†’ a âˆ£ V j))
        (Î» N s s' â†’ bÃ©zout2Rows-commonDiv N a s s' zero)
        (Î» N s s' â†’ bÃ©zout2Rows-commonDiv N a s s' one) _

    bÃ©zoutRows-commonDiv :
        ((i : Fin (suc m))(j : Fin (suc n)) â†’ M zero zero âˆ£ M i j)
      â†’  (i : Fin (suc m))(j : Fin (suc n)) â†’ M zero zero âˆ£ bÃ©zoutRows i j
    bÃ©zoutRows-commonDiv p zero    = bÃ©zoutRows-commonDivâ‚€ _ (p zero) (p âˆ˜ suc)
    bÃ©zoutRows-commonDiv p (suc i) = bÃ©zoutRows-commonDivâ‚ _ (p zero) (p âˆ˜ suc) i

    bÃ©zoutRows-commonDivInv :
        Â¬ M zero zero â‰¡ 0
      â†’ ((i : Fin (suc m))(j : Fin (suc n)) â†’ M zero zero âˆ£ M i j)
      â†’  (i : Fin (suc m))(j : Fin (suc n)) â†’ bÃ©zoutRows zero zero âˆ£ bÃ©zoutRows i j
    bÃ©zoutRows-commonDivInv h p i j =
      let inv = (Î» t â†’ bÃ©zoutRows-inv h (Î» i â†’ p (suc i) zero) t zero) in
      subst (_âˆ£ bÃ©zoutRows i j) inv (bÃ©zoutRows-commonDiv p i j)

  isLinear2BÃ©zout2Rows : isLinear2Ã—2 (bÃ©zout2Rows {n = n})
  isLinear2BÃ©zout2Rows .transMat M = bÃ©zout2Mat _ _ (bÃ©zout (M zero zero) (M one zero))
  isLinear2BÃ©zout2Rows .transEq  M t zero j = mul2 (isLinear2BÃ©zout2Rows .transMat M) M zero j (~ t)
  isLinear2BÃ©zout2Rows .transEq  M t one  j = mul2 (isLinear2BÃ©zout2Rows .transMat M) M one  j (~ t)

  isLinearBÃ©zoutRows : isLinear (bÃ©zoutRows {m = m} {n = n})
  isLinearBÃ©zoutRows = isLinearTransRows _ isLinear2BÃ©zout2Rows _

  isInv2BÃ©zout2Rows : (M : Mat 2 (suc n))(p : Â¬ M zero zero â‰¡ 0) â†’ isInv (isLinear2BÃ©zout2Rows .transMat M)
  isInv2BÃ©zout2Rows _ p = isInvBÃ©zout2Mat _ _ _ p

  isInvBÃ©zout2Rows : (M : Mat (suc m) (suc n))(p : Â¬ M zero zero â‰¡ 0) â†’ isInv (isLinearBÃ©zoutRows .transMat M)
  isInvBÃ©zout2Rows = isInvTransRowsInd _ _ (Î» V â†’ Â¬ V zero â‰¡ 0) bÃ©zout2Rows-nonZero isInv2BÃ©zout2Rows

  -- Using BÃ©zout identity to eliminate the first column/row

  record RowsImproved (M : Mat (suc m) (suc n)) : Type where
    field
      sim : Sim M

      div     : (i : Fin (suc m)) â†’ sim .result zero zero âˆ£ M i zero
      vanish  : (i : Fin m) â†’ sim .result (suc i) zero â‰¡ 0
      nonZero : Â¬ sim .result zero zero â‰¡ 0

  record ColsImproved (M : Mat (suc m) (suc n)) : Type where
    field
      sim : Sim M

      div     : (j : Fin (suc n)) â†’ sim .result zero zero âˆ£ M zero j
      vanish  : (j : Fin n) â†’ sim .result zero (suc j) â‰¡ 0
      nonZero : Â¬ sim .result zero zero â‰¡ 0

  open RowsImproved
  open ColsImproved

  improveRows : (M : Mat (suc m) (suc n))(p : Â¬ M zero zero â‰¡ 0) â†’ RowsImproved M
  improveRows M _ .sim .result   = bÃ©zoutRows M
  improveRows M _ .sim .simrel .transMatL = isLinearBÃ©zoutRows .transMat M
  improveRows _ _ .sim .simrel .transMatR = ğŸ™
  improveRows _ _ .sim .simrel .transEq   = isLinearBÃ©zoutRows .transEq _ âˆ™ sym (â‹†rUnit _)
  improveRows _ p .sim .simrel .isInvTransL = isInvBÃ©zout2Rows _ p
  improveRows _ p .sim .simrel .isInvTransR = isInvğŸ™
  improveRows _ _ .div     = bÃ©zoutRows-div     _
  improveRows _ _ .vanish  = bÃ©zoutRows-vanish  _
  improveRows M p .nonZero = bÃ©zoutRows-nonZero M p

  improveCols : (M : Mat (suc m) (suc n))(p : Â¬ M zero zero â‰¡ 0) â†’ ColsImproved M
  improveCols M _ .sim .result    = (bÃ©zoutRows (M áµ—))áµ—
  improveCols _ _ .sim .simrel .transMatL = ğŸ™
  improveCols M _ .sim .simrel .transMatR = (isLinearBÃ©zoutRows .transMat (M áµ—))áµ—
  improveCols M _ .sim .simrel .transEq     =
    let P = isLinearBÃ©zoutRows .transMat (M áµ—) in
      (Î» t â†’ (isLinearBÃ©zoutRows .transEq (M áµ—) t)áµ—)
    âˆ™ compáµ— P (M áµ—)
    âˆ™ (Î» t â†’ idemáµ— M t â‹† P áµ—)
    âˆ™ (Î» t â†’ â‹†lUnit M (~ t) â‹† P áµ—)
  improveCols _ _ .sim .simrel .isInvTransL = isInvğŸ™
  improveCols M p .sim .simrel .isInvTransR =
    isInváµ— {M = isLinearBÃ©zoutRows .transMat (M áµ—)} (isInvBÃ©zout2Rows (M áµ—) p)
  improveCols _ _ .div     = bÃ©zoutRows-div     _
  improveCols _ _ .vanish  = bÃ©zoutRows-vanish  _
  improveCols M p .nonZero = bÃ©zoutRows-nonZero (M áµ—) p
