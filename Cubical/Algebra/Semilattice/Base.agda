{-
 following Johnstone's book "Stone Spaces" we define semilattices
 to be commutative monoids such that every element is idempotent.
 In particular, we take every semilattice to have a neutral element
 that is either the maximal or minimal element depending on whether
 we have a join or meet semilattice.
-}

{-# OPTIONS --safe #-}
module Cubical.Algebra.Semilattice.Base where

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.Equiv
open import Cubical.Foundations.Equiv.HalfAdjoint
open import Cubical.Foundations.Function
open import Cubical.Foundations.HLevels
open import Cubical.Foundations.Isomorphism
open import Cubical.Foundations.Univalence
open import Cubical.Foundations.Transport
open import Cubical.Foundations.SIP

open import Cubical.Data.Sigma
open import Cubical.Data.Nat using (â„• ; zero ; suc)
open import Cubical.Data.FinData

open import Cubical.Algebra.Semigroup
open import Cubical.Algebra.Monoid
open import Cubical.Algebra.Monoid.BigOp
open import Cubical.Algebra.CommMonoid

open import Cubical.Displayed.Base
open import Cubical.Displayed.Auto
open import Cubical.Displayed.Record
open import Cubical.Displayed.Universe

open import Cubical.Relation.Binary
open import Cubical.Relation.Binary.Poset

open import Cubical.Reflection.RecordEquiv

open Iso

private
  variable
    â„“ â„“' : Level

record IsSemilattice {A : Type â„“} (Îµ : A) (_Â·_ : A â†’ A â†’ A) : Type â„“ where
  constructor issemilattice

  field
   isCommMonoid : IsCommMonoid Îµ _Â·_
   idem : (x : A) â†’ x Â· x â‰¡ x

  open IsCommMonoid isCommMonoid public

unquoteDecl IsSemilatticeIsoÎ£ = declareRecordIsoÎ£ IsSemilatticeIsoÎ£ (quote IsSemilattice)

record SemilatticeStr (A : Type â„“) : Type â„“ where
  constructor semilatticestr

  field
    Îµ        : A
    _Â·_      : A â†’ A â†’ A
    isSemilattice : IsSemilattice Îµ _Â·_

  infixl 7 _Â·_

  open IsSemilattice isSemilattice public

Semilattice : âˆ€ â„“ â†’ Type (â„“-suc â„“)
Semilattice â„“ = TypeWithStr â„“ SemilatticeStr

semilattice : (A : Type â„“) (Îµ : A) (_Â·_ : A â†’ A â†’ A) (h : IsSemilattice Îµ _Â·_) â†’ Semilattice â„“
semilattice A Îµ _Â·_ h = A , semilatticestr Îµ _Â·_ h

-- Easier to use constructors
makeIsSemilattice : {L : Type â„“} {Îµ : L} {_Â·_ : L â†’ L â†’ L}
               (is-setL : isSet L)
               (assoc : (x y z : L) â†’ x Â· (y Â· z) â‰¡ (x Â· y) Â· z)
               (rid : (x : L) â†’ x Â· Îµ â‰¡ x)
               (comm : (x y : L) â†’ x Â· y â‰¡ y Â· x)
               (idem : (x : L) â†’ x Â· x â‰¡ x)
             â†’ IsSemilattice Îµ _Â·_
IsSemilattice.isCommMonoid (makeIsSemilattice is-setL assoc rid comm idem) =
                                        makeIsCommMonoid is-setL assoc rid comm
IsSemilattice.idem (makeIsSemilattice is-setL assoc rid comm idem) = idem

makeSemilattice : {L : Type â„“} (Îµ : L) (_Â·_ : L â†’ L â†’ L)
             (is-setL : isSet L)
             (assoc : (x y z : L) â†’ x Â· (y Â· z) â‰¡ (x Â· y) Â· z)
             (rid : (x : L) â†’ x Â· Îµ â‰¡ x)
             (comm : (x y : L) â†’ x Â· y â‰¡ y Â· x)
             (idem : (x : L) â†’ x Â· x â‰¡ x)
             â†’ Semilattice â„“
makeSemilattice Îµ _Â·_ is-setL assoc rid comm idem =
  semilattice _ Îµ _Â·_ (makeIsSemilattice is-setL assoc rid comm idem)


SemilatticeStrâ†’MonoidStr : {A : Type â„“} â†’ SemilatticeStr A â†’ MonoidStr A
SemilatticeStrâ†’MonoidStr (semilatticestr _ _ H) =
                          monoidstr _ _ (H .IsSemilattice.isCommMonoid .IsCommMonoid.isMonoid)

Semilatticeâ†’Monoid : Semilattice â„“ â†’ Monoid â„“
Semilatticeâ†’Monoid (_ , semilatticestr _ _ H) =
                    _ , monoidstr _ _ (H .IsSemilattice.isCommMonoid .IsCommMonoid.isMonoid)

Semilatticeâ†’CommMonoid : Semilattice â„“ â†’ CommMonoid â„“
Semilatticeâ†’CommMonoid (_ , semilatticestr _ _ H) =
                        _ , commmonoidstr _ _ (H .IsSemilattice.isCommMonoid)

SemilatticeHom : (L : Semilattice â„“) (M : Semilattice â„“') â†’ Type (â„“-max â„“ â„“')
SemilatticeHom L M = MonoidHom (Semilatticeâ†’Monoid L) (Semilatticeâ†’Monoid M)

IsSemilatticeEquiv : {A : Type â„“} {B : Type â„“'}
  (M : SemilatticeStr A) (e : A â‰ƒ B) (N : SemilatticeStr B) â†’ Type (â„“-max â„“ â„“')
IsSemilatticeEquiv M e N =
                   IsMonoidHom (SemilatticeStrâ†’MonoidStr M) (e .fst) (SemilatticeStrâ†’MonoidStr N)

SemilatticeEquiv : (M : Semilattice â„“) (N : Semilattice â„“') â†’ Type (â„“-max â„“ â„“')
SemilatticeEquiv M N = Î£[ e âˆˆ (M .fst â‰ƒ N .fst) ] IsSemilatticeEquiv (M .snd) e (N .snd)

isPropIsSemilattice : {L : Type â„“} (Îµ : L) (_Â·_ : L â†’ L â†’ L)
             â†’ isProp (IsSemilattice Îµ _Â·_)
isPropIsSemilattice Îµ _Â·_ (issemilattice LL LC) (issemilattice SL SC) =
  Î» i â†’ issemilattice (isPropIsCommMonoid _ _ LL SL i) (isPropIdem LC SC i)
  where
  isSetL : isSet _
  isSetL = LL .IsCommMonoid.isMonoid .IsMonoid.isSemigroup .IsSemigroup.is-set

  isPropIdem : isProp ((x : _) â†’ x Â· x â‰¡ x)
  isPropIdem = isPropÎ  Î» _ â†’ isSetL _ _

ğ’®á´°-Semilattice : DUARel (ğ’®-Univ â„“) SemilatticeStr â„“
ğ’®á´°-Semilattice =
  ğ’®á´°-Record (ğ’®-Univ _) IsSemilatticeEquiv
    (fields:
      data[ Îµ âˆ£ autoDUARel _ _ âˆ£ presÎµ ]
      data[ _Â·_ âˆ£ autoDUARel _ _ âˆ£ presÂ· ]
      prop[ isSemilattice âˆ£ (Î» _ _ â†’ isPropIsSemilattice _ _) ])
  where
  open SemilatticeStr
  open IsMonoidHom

SemilatticePath : (L K : Semilattice â„“) â†’ SemilatticeEquiv L K â‰ƒ (L â‰¡ K)
SemilatticePath = âˆ« ğ’®á´°-Semilattice .UARel.ua


-- TODO: decide if that's the right approach
module JoinSemilattice (L' : Semilattice â„“) where
 private L = fst L'
 open SemilatticeStr (snd L') renaming (_Â·_ to _âˆ¨l_ ; Îµ to 0l)
 open CommMonoidTheory (Semilatticeâ†’CommMonoid L')
 open MonoidBigOp (Semilatticeâ†’Monoid L')

 _â‰¤_ : L â†’ L â†’ Type â„“
 x â‰¤ y = x âˆ¨l y â‰¡ y

 infix 4 _â‰¤_

 IndPoset : Poset â„“ â„“
 fst IndPoset = L
 PosetStr._â‰¤_ (snd IndPoset) = _â‰¤_
 IsPoset.is-set (PosetStr.isPoset (snd IndPoset)) = is-set
 IsPoset.is-prop-valued (PosetStr.isPoset (snd IndPoset)) = Î» _ _ â†’ is-set _ _
 IsPoset.is-refl (PosetStr.isPoset (snd IndPoset)) = idem
 IsPoset.is-trans (PosetStr.isPoset (snd IndPoset)) = path
  where
  path : (a b c : L) â†’ a âˆ¨l b â‰¡ b â†’ b âˆ¨l c â‰¡ c â†’ a âˆ¨l c â‰¡ c
  path a b c aâˆ¨bâ‰¡b bâˆ¨câ‰¡c = a âˆ¨l c â‰¡âŸ¨ cong (a âˆ¨l_) (sym bâˆ¨câ‰¡c) âŸ©
                            a âˆ¨l (b âˆ¨l c) â‰¡âŸ¨ Â·Assoc _ _ _ âŸ©
                            (a âˆ¨l b) âˆ¨l c â‰¡âŸ¨ cong (_âˆ¨l c) aâˆ¨bâ‰¡b âŸ©
                            b âˆ¨l c â‰¡âŸ¨ bâˆ¨câ‰¡c âŸ©
                            c âˆ
 IsPoset.is-antisym (PosetStr.isPoset (snd IndPoset)) =
   Î» _ _ aâˆ¨bâ‰¡b bâˆ¨aâ‰¡a â†’ sym bâˆ¨aâ‰¡a âˆ™âˆ™ Â·Comm _ _ âˆ™âˆ™ aâˆ¨bâ‰¡b

 âˆ¨lIsMax : âˆ€ x y z â†’ x â‰¤ z â†’ y â‰¤ z â†’ x âˆ¨l y â‰¤ z
 âˆ¨lIsMax x y z xâ‰¤z yâ‰¤z = cong ((x âˆ¨l y) âˆ¨l_) (sym (idem z)) âˆ™ commAssocSwap x y z z
                                                            âˆ™ congâ‚‚ (_âˆ¨l_) xâ‰¤z yâ‰¤z
                                                            âˆ™ idem z

 âˆ¨â‰¤LCancel : âˆ€ x y â†’ y â‰¤ x âˆ¨l y
 âˆ¨â‰¤LCancel x y = commAssocl y x y âˆ™ cong (x âˆ¨l_) (idem y)

 âˆ¨â‰¤RCancel : âˆ€ x y â†’ x â‰¤ x âˆ¨l y
 âˆ¨â‰¤RCancel x y = Â·Assoc _ _ _ âˆ™ cong (_âˆ¨l y) (idem x)

 â‰¤-âˆ¨Pres : âˆ€ x y u w â†’ x â‰¤ y â†’ u â‰¤ w â†’ x âˆ¨l u â‰¤ y âˆ¨l w
 â‰¤-âˆ¨Pres x y u w xâ‰¤y uâ‰¤w = commAssocSwap x u y w âˆ™ congâ‚‚ (_âˆ¨l_) xâ‰¤y uâ‰¤w

 â‰¤-âˆ¨LPres : âˆ€ x y z â†’ x â‰¤ y â†’ z âˆ¨l x â‰¤ z âˆ¨l y
 â‰¤-âˆ¨LPres x y z xâ‰¤y = â‰¤-âˆ¨Pres _ _ _ _ (idem z) xâ‰¤y

 â‰¤-âˆ¨RPres : âˆ€ x y z â†’ x â‰¤ y â†’ x âˆ¨l z â‰¤ y âˆ¨l z
 â‰¤-âˆ¨RPres x y z xâ‰¤y = â‰¤-âˆ¨Pres _ _ _ _ xâ‰¤y (idem z)

 -- inequalities of bigOps
 open IsPoset (PosetStr.isPoset (snd IndPoset))
 open PosetReasoning IndPoset


 indâ‰¤bigOp : {n : â„•} (U : FinVec L n) (i : Fin n) â†’ U i â‰¤ bigOp U
 indâ‰¤bigOp {n = suc n} U zero = âˆ¨â‰¤RCancel _ _
 indâ‰¤bigOp {n = suc n} U (suc i) = is-trans _ (bigOp (U âˆ˜ suc)) _ (indâ‰¤bigOp (U âˆ˜ suc) i)
                                                                  (âˆ¨â‰¤LCancel _ _)

 bigOpIsMax : {n : â„•} (U : FinVec L n) (x : L) â†’ (âˆ€ i â†’ U i â‰¤ x) â†’ bigOp U â‰¤ x
 bigOpIsMax {n = zero} _ _ _ = Â·IdL _
 bigOpIsMax {n = suc n} U x Uâ‰¤x =
   bigOp U                   â‰¤âŸ¨ is-refl _ âŸ©
   U zero âˆ¨l bigOp (U âˆ˜ suc) â‰¤âŸ¨ â‰¤-âˆ¨LPres _ _ _ (bigOpIsMax _ _ (Uâ‰¤x âˆ˜ suc)) âŸ©
   U zero âˆ¨l x               â‰¤âŸ¨ âˆ¨lIsMax _ _ _ (Uâ‰¤x zero) (is-refl x) âŸ©
   x â—¾

 â‰¤-bigOpExt : {n : â„•} (U W : FinVec L n) â†’ (âˆ€ i â†’ U i â‰¤ W i) â†’ bigOp U â‰¤ bigOp W
 â‰¤-bigOpExt {n = zero} U W Uâ‰¤W = is-refl 0l
 â‰¤-bigOpExt {n = suc n} U W Uâ‰¤W = â‰¤-âˆ¨Pres _ _ _ _ (Uâ‰¤W zero) (â‰¤-bigOpExt _ _ (Uâ‰¤W âˆ˜ suc))

module MeetSemilattice (L' : Semilattice â„“) where
 private L = fst L'
 open SemilatticeStr (snd L') renaming (_Â·_ to _âˆ§l_ ; Îµ to 1l)
 open CommMonoidTheory (Semilatticeâ†’CommMonoid L')

 _â‰¤_ : L â†’ L â†’ Type â„“
 x â‰¤ y = x âˆ§l y â‰¡ x

 infix 4 _â‰¤_

 IndPoset : Poset â„“ â„“
 fst IndPoset = L
 PosetStr._â‰¤_ (snd IndPoset) = _â‰¤_
 IsPoset.is-set (PosetStr.isPoset (snd IndPoset)) = is-set
 IsPoset.is-prop-valued (PosetStr.isPoset (snd IndPoset)) = Î» _ _ â†’ is-set _ _
 IsPoset.is-refl (PosetStr.isPoset (snd IndPoset)) = idem
 IsPoset.is-trans (PosetStr.isPoset (snd IndPoset)) = path
  where
  path : (a b c : L) â†’ a âˆ§l b â‰¡ a â†’ b âˆ§l c â‰¡ b â†’ a âˆ§l c â‰¡ a
  path a b c aâˆ§bâ‰¡a bâˆ§câ‰¡b = a âˆ§l c â‰¡âŸ¨ cong (_âˆ§l c) (sym aâˆ§bâ‰¡a) âŸ©
                            (a âˆ§l b) âˆ§l c â‰¡âŸ¨ sym (Â·Assoc _ _ _) âŸ©
                            a âˆ§l (b âˆ§l c) â‰¡âŸ¨ cong (a âˆ§l_) bâˆ§câ‰¡b âŸ©
                            a âˆ§l b â‰¡âŸ¨ aâˆ§bâ‰¡a âŸ©
                            a âˆ
 IsPoset.is-antisym (PosetStr.isPoset (snd IndPoset)) =
   Î» _ _ aâˆ§bâ‰¡a bâˆ§aâ‰¡b â†’ sym aâˆ§bâ‰¡a âˆ™âˆ™ Â·Comm _ _ âˆ™âˆ™ bâˆ§aâ‰¡b

 â‰¤-âˆ§LPres : âˆ€ x y z â†’ x â‰¤ y â†’ z âˆ§l x â‰¤ z âˆ§l y
 â‰¤-âˆ§LPres x y z xâ‰¤y = commAssocSwap z x z y âˆ™âˆ™ cong (_âˆ§l (x âˆ§l y)) (idem z) âˆ™âˆ™ cong (z âˆ§l_) xâ‰¤y

 â‰¤-âˆ§RPres : âˆ€ x y z â†’ x â‰¤ y â†’ x âˆ§l z â‰¤ y âˆ§l z
 â‰¤-âˆ§RPres x y z xâ‰¤y = commAssocSwap x z y z âˆ™âˆ™ cong (x âˆ§l y âˆ§l_) (idem z) âˆ™âˆ™ cong (_âˆ§l z) xâ‰¤y

 â‰¤-âˆ§Pres : âˆ€ x y z w â†’ x â‰¤ y â†’ z â‰¤ w â†’ x âˆ§l z â‰¤ y âˆ§l w
 â‰¤-âˆ§Pres x y z w xâ‰¤y zâ‰¤w = commAssocSwap x z y w âˆ™ congâ‚‚ _âˆ§l_ xâ‰¤y zâ‰¤w

 âˆ§â‰¤LCancel : âˆ€ x y â†’ x âˆ§l y â‰¤ y
 âˆ§â‰¤LCancel x y = sym (Â·Assoc _ _ _) âˆ™ cong (x âˆ§l_) (idem y)

 âˆ§â‰¤RCancel : âˆ€ x y â†’ x âˆ§l y â‰¤ x
 âˆ§â‰¤RCancel x y = commAssocr x y x âˆ™ cong (_âˆ§l y) (idem x)

 âˆ§lIsMin : âˆ€ x y z â†’ z â‰¤ x â†’ z â‰¤ y â†’ z â‰¤ x âˆ§l y
 âˆ§lIsMin x y z zâ‰¤x zâ‰¤y = cong (_âˆ§l (x âˆ§l y)) (sym (idem z)) âˆ™ commAssocSwap z z x y
                                                            âˆ™ congâ‚‚ (_âˆ§l_) zâ‰¤x zâ‰¤y
                                                            âˆ™ idem z
