{-# OPTIONS --safe #-}
module Cubical.Algebra.Ring.Base where

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.Equiv
open import Cubical.Foundations.Equiv.HalfAdjoint
open import Cubical.Foundations.Function
open import Cubical.Foundations.HLevels
open import Cubical.Foundations.Isomorphism
open import Cubical.Foundations.Univalence
open import Cubical.Foundations.Transport
open import Cubical.Foundations.SIP

open import Cubical.Data.Sigma

open import Cubical.Algebra.Semigroup
open import Cubical.Algebra.Monoid
open import Cubical.Algebra.Group
open import Cubical.Algebra.AbGroup

open import Cubical.Displayed.Base
open import Cubical.Displayed.Auto
open import Cubical.Displayed.Record
open import Cubical.Displayed.Universe

open import Cubical.Reflection.RecordEquiv

open Iso

private
  variable
    â„“ â„“' : Level

record IsRing {R : Type â„“}
              (0r 1r : R) (_+_ _Â·_ : R â†’ R â†’ R) (-_ : R â†’ R) : Type â„“ where

  constructor isring

  field
    +IsAbGroup : IsAbGroup 0r _+_ -_
    Â·IsMonoid  : IsMonoid 1r _Â·_
    dist        : (x y z : R) â†’ (x Â· (y + z) â‰¡ (x Â· y) + (x Â· z))
                              Ã— ((x + y) Â· z â‰¡ (x Â· z) + (y Â· z))
    -- This is in the Agda stdlib, but it's redundant
    -- zero             : (x : R) â†’ (x Â· 0r â‰¡ 0r) Ã— (0r Â· x â‰¡ 0r)

  open IsAbGroup +IsAbGroup public
    renaming
      ( assoc       to +Assoc
      ; identity    to +Identity
      ; lid         to +Lid
      ; rid         to +Rid
      ; inverse     to +Inv
      ; invl        to +Linv
      ; invr        to +Rinv
      ; comm        to +Comm
      ; isSemigroup to +IsSemigroup
      ; isMonoid    to +IsMonoid
      ; isGroup     to +IsGroup )

  open IsMonoid Â·IsMonoid public
    renaming
      ( assoc       to Â·Assoc
      ; identity    to Â·Identity
      ; lid         to Â·Lid
      ; rid         to Â·Rid
      ; isSemigroup to Â·IsSemigroup )
    hiding
      ( is-set ) -- We only want to export one proof of this

  Â·Rdist+ : (x y z : R) â†’ x Â· (y + z) â‰¡ (x Â· y) + (x Â· z)
  Â·Rdist+ x y z = dist x y z .fst

  Â·Ldist+ : (x y z : R) â†’ (x + y) Â· z â‰¡ (x Â· z) + (y Â· z)
  Â·Ldist+ x y z = dist x y z .snd

record RingStr (A : Type â„“) : Type (â„“-suc â„“) where

  constructor ringstr

  field
    0r      : A
    1r      : A
    _+_     : A â†’ A â†’ A
    _Â·_     : A â†’ A â†’ A
    -_      : A â†’ A
    isRing  : IsRing 0r 1r _+_ _Â·_ -_

  infix  8 -_
  infixl 7 _Â·_
  infixl 6 _+_

  open IsRing isRing public

Ring : âˆ€ â„“ â†’ Type (â„“-suc â„“)
Ring â„“ = TypeWithStr â„“ RingStr

isSetRing : (R : Ring â„“) â†’ isSet âŸ¨ R âŸ©
isSetRing R = R .snd .RingStr.isRing .IsRing.Â·IsMonoid .IsMonoid.isSemigroup .IsSemigroup.is-set

makeIsRing : {R : Type â„“} {0r 1r : R} {_+_ _Â·_ : R â†’ R â†’ R} { -_ : R â†’ R}
             (is-setR : isSet R)
             (+-assoc : (x y z : R) â†’ x + (y + z) â‰¡ (x + y) + z)
             (+-rid : (x : R) â†’ x + 0r â‰¡ x)
             (+-rinv : (x : R) â†’ x + (- x) â‰¡ 0r)
             (+-comm : (x y : R) â†’ x + y â‰¡ y + x)
             (r+-assoc : (x y z : R) â†’ x Â· (y Â· z) â‰¡ (x Â· y) Â· z)
             (Â·-rid : (x : R) â†’ x Â· 1r â‰¡ x)
             (Â·-lid : (x : R) â†’ 1r Â· x â‰¡ x)
             (Â·-rdist-+ : (x y z : R) â†’ x Â· (y + z) â‰¡ (x Â· y) + (x Â· z))
             (Â·-ldist-+ : (x y z : R) â†’ (x + y) Â· z â‰¡ (x Â· z) + (y Â· z))
           â†’ IsRing 0r 1r _+_ _Â·_ -_
makeIsRing is-setR assoc +-rid +-rinv +-comm Â·-assoc Â·-rid Â·-lid Â·-rdist-+ Â·-ldist-+ =
  isring (makeIsAbGroup is-setR assoc +-rid +-rinv +-comm)
         (makeIsMonoid is-setR Â·-assoc Â·-rid Â·-lid)
         Î» x y z â†’ Â·-rdist-+ x y z , Â·-ldist-+ x y z

makeRing : {R : Type â„“} (0r 1r : R) (_+_ _Â·_ : R â†’ R â†’ R) (-_ : R â†’ R)
           (is-setR : isSet R)
           (+-assoc : (x y z : R) â†’ x + (y + z) â‰¡ (x + y) + z)
           (+-rid : (x : R) â†’ x + 0r â‰¡ x)
           (+-rinv : (x : R) â†’ x + (- x) â‰¡ 0r)
           (+-comm : (x y : R) â†’ x + y â‰¡ y + x)
           (+-assoc : (x y z : R) â†’ x Â· (y Â· z) â‰¡ (x Â· y) Â· z)
           (Â·-rid : (x : R) â†’ x Â· 1r â‰¡ x)
           (Â·-lid : (x : R) â†’ 1r Â· x â‰¡ x)
           (Â·-rdist-+ : (x y z : R) â†’ x Â· (y + z) â‰¡ (x Â· y) + (x Â· z))
           (Â·-ldist-+ : (x y z : R) â†’ (x + y) Â· z â‰¡ (x Â· z) + (y Â· z))
         â†’ Ring â„“
makeRing 0r 1r _+_ _Â·_ -_ is-setR assoc +-rid +-rinv +-comm Â·-assoc Â·-rid Â·-lid Â·-rdist-+ Â·-ldist-+ =
  _ , ringstr 0r 1r _+_ _Â·_ -_
       (makeIsRing is-setR assoc +-rid +-rinv +-comm
                   Â·-assoc Â·-rid Â·-lid Â·-rdist-+ Â·-ldist-+ )

record IsRingHom {A : Type â„“} {B : Type â„“'} (R : RingStr A) (f : A â†’ B) (S : RingStr B)
  : Type (â„“-max â„“ â„“')
  where

  -- Shorter qualified names
  private
    module R = RingStr R
    module S = RingStr S

  field
    pres0 : f R.0r â‰¡ S.0r
    pres1 : f R.1r â‰¡ S.1r
    pres+ : (x y : A) â†’ f (x R.+ y) â‰¡ f x S.+ f y
    presÂ· : (x y : A) â†’ f (x R.Â· y) â‰¡ f x S.Â· f y
    pres- : (x : A) â†’ f (R.- x) â‰¡ S.- (f x)

unquoteDecl IsRingHomIsoÎ£ = declareRecordIsoÎ£ IsRingHomIsoÎ£ (quote IsRingHom)

RingHom : (R : Ring â„“) (S : Ring â„“') â†’ Type (â„“-max â„“ â„“')
RingHom R S = Î£[ f âˆˆ (âŸ¨ R âŸ© â†’ âŸ¨ S âŸ©) ] IsRingHom (R .snd) f (S .snd)

IsRingEquiv : {A : Type â„“} {B : Type â„“'} (M : RingStr A) (e : A â‰ƒ B) (N : RingStr B)
  â†’ Type (â„“-max â„“ â„“')
IsRingEquiv M e N = IsRingHom M (e .fst) N

RingEquiv : (R : Ring â„“) (S : Ring â„“') â†’ Type (â„“-max â„“ â„“')
RingEquiv R S = Î£[ e âˆˆ (âŸ¨ R âŸ© â‰ƒ âŸ¨ S âŸ©) ] IsRingEquiv (R .snd) e (S .snd)

_$_ : {R S : Ring â„“} â†’ (Ï† : RingHom R S) â†’ (x : âŸ¨ R âŸ©) â†’ âŸ¨ S âŸ©
Ï† $ x = Ï† .fst x

RingEquivâ†’RingHom : {A B : Ring â„“} â†’ RingEquiv A B â†’ RingHom A B
RingEquivâ†’RingHom (e , eIsHom) = e .fst , eIsHom

isPropIsRing : {R : Type â„“} (0r 1r : R) (_+_ _Â·_ : R â†’ R â†’ R) (-_ : R â†’ R)
             â†’ isProp (IsRing 0r 1r _+_ _Â·_ -_)
isPropIsRing 0r 1r _+_ _Â·_ -_ (isring RG RM RD) (isring SG SM SD) =
  Î» i â†’ isring (isPropIsAbGroup _ _ _ RG SG i)
               (isPropIsMonoid _ _ RM SM i)
               (isPropDistr RD SD i)
  where
  isSetR : isSet _
  isSetR = RM .IsMonoid.isSemigroup .IsSemigroup.is-set

  isPropDistr : isProp ((x y z : _) â†’ ((x Â· (y + z)) â‰¡ ((x Â· y) + (x Â· z)))
                                    Ã— (((x + y) Â· z) â‰¡ ((x Â· z) + (y Â· z))))
  isPropDistr = isPropÎ 3 Î» _ _ _ â†’ isPropÃ— (isSetR _ _) (isSetR _ _)

isPropIsRingHom : {A : Type â„“} {B : Type â„“'} (R : RingStr A) (f : A â†’ B) (S : RingStr B)
  â†’ isProp (IsRingHom R f S)
isPropIsRingHom R f S = isOfHLevelRetractFromIso 1 IsRingHomIsoÎ£
                        (isPropÃ—4 (isSetRing (_ , S) _ _)
                                  (isSetRing (_ , S) _ _)
                                  (isPropÎ 2 Î» _ _ â†’ isSetRing (_ , S) _ _)
                                  (isPropÎ 2 Î» _ _ â†’ isSetRing (_ , S) _ _)
                                  (isPropÎ  Î» _ â†’ isSetRing (_ , S) _ _))

RingHomPathP : (R S T : Ring â„“) (p : S â‰¡ T) (Ï† : RingHom R S) (Ïˆ : RingHom R T)
             â†’ PathP (Î» i â†’ R .fst â†’ p i .fst) (Ï† .fst) (Ïˆ .fst)
             â†’ PathP (Î» i â†’ RingHom R (p i)) Ï† Ïˆ
RingHomPathP R S T p Ï† Ïˆ q = Î£PathP (q , isPropâ†’PathP (Î» _ â†’ isPropIsRingHom _ _ _) _ _)

RingHomâ‰¡ : {R S : Ring â„“} {Ï† Ïˆ : RingHom R S} â†’ fst Ï† â‰¡ fst Ïˆ â†’ Ï† â‰¡ Ïˆ
RingHomâ‰¡ = Î£â‰¡Prop Î» f â†’ isPropIsRingHom _ f _

ğ’®á´°-Ring : DUARel (ğ’®-Univ â„“) RingStr â„“
ğ’®á´°-Ring =
  ğ’®á´°-Record (ğ’®-Univ _) IsRingEquiv
    (fields:
      data[ 0r âˆ£ null âˆ£ pres0 ]
      data[ 1r âˆ£ null âˆ£ pres1 ]
      data[ _+_ âˆ£ bin âˆ£ pres+ ]
      data[ _Â·_ âˆ£ bin âˆ£ presÂ· ]
      data[ -_ âˆ£ un âˆ£ pres- ]
      prop[ isRing âˆ£ (Î» _ _ â†’ isPropIsRing _ _ _ _ _) ])
 where
  open RingStr
  open IsRingHom

  -- faster with some sharing
  null = autoDUARel (ğ’®-Univ _) (Î» A â†’ A)
  un = autoDUARel (ğ’®-Univ _) (Î» A â†’ A â†’ A)
  bin = autoDUARel (ğ’®-Univ _) (Î» A â†’ A â†’ A â†’ A)

RingPath : (R S : Ring â„“) â†’ RingEquiv R S â‰ƒ (R â‰¡ S)
RingPath = âˆ« ğ’®á´°-Ring .UARel.ua

-- Rings have an abelian group and a monoid

Ringâ†’AbGroup : Ring â„“ â†’ AbGroup â„“
Ringâ†’AbGroup (A , ringstr _ _ _ _ _ R) = A , abgroupstr _ _ _ (IsRing.+IsAbGroup R)

Ringâ†’Group : Ring â„“ â†’ Group â„“
Ringâ†’Group = AbGroupâ†’Group âˆ˜ Ringâ†’AbGroup

Ringâ†’AddMonoid : Ring â„“ â†’ Monoid â„“
Ringâ†’AddMonoid = Groupâ†’Monoid âˆ˜ Ringâ†’Group

Ringâ†’MultMonoid : Ring â„“ â†’ Monoid â„“
Ringâ†’MultMonoid (A , ringstr _ _ _ _ _ R) = monoid _ _ _ (IsRing.Â·IsMonoid R)

-- Smart constructor for ring homomorphisms
-- that infers the other equations from pres1, pres+, and presÂ·

module _ {R : Ring â„“} {S : Ring â„“'} {f : âŸ¨ R âŸ© â†’ âŸ¨ S âŸ©} where

  private
    module R = RingStr (R .snd)
    module S = RingStr (S .snd)

  module _
    (p1 : f R.1r â‰¡ S.1r)
    (p+ : (x y : âŸ¨ R âŸ©) â†’ f (x R.+ y) â‰¡ f x S.+ f y)
    (pÂ· : (x y : âŸ¨ R âŸ©) â†’ f (x R.Â· y) â‰¡ f x S.Â· f y)
    where

    open IsRingHom
    private
      isGHom : IsGroupHom (Ringâ†’Group R .snd) f (Ringâ†’Group S .snd)
      isGHom = makeIsGroupHom p+

    makeIsRingHom : IsRingHom (R .snd) f (S .snd)
    makeIsRingHom .pres0 = isGHom .IsGroupHom.pres1
    makeIsRingHom .pres1 = p1
    makeIsRingHom .pres+ = p+
    makeIsRingHom .presÂ· = pÂ·
    makeIsRingHom .pres- = isGHom .IsGroupHom.presinv
