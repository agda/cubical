{-# OPTIONS --safe #-}
module Cubical.Algebra.AbGroup.Base where

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.Equiv
open import Cubical.Foundations.Equiv.HalfAdjoint
open import Cubical.Foundations.HLevels
open import Cubical.Foundations.Isomorphism
open import Cubical.Foundations.Univalence
open import Cubical.Foundations.Transport
open import Cubical.Foundations.SIP

open import Cubical.Data.Sigma
open import Cubical.Data.Unit

open import Cubical.Algebra.Semigroup
open import Cubical.Algebra.Monoid
open import Cubical.Algebra.CommMonoid
open import Cubical.Algebra.Group
open import Cubical.Algebra.Group.Morphisms
open import Cubical.Algebra.Group.MorphismProperties
open import Cubical.Algebra.Group.DirProd

open import Cubical.Displayed.Base
open import Cubical.Displayed.Auto
open import Cubical.Displayed.Record
open import Cubical.Displayed.Universe

open Iso

private
  variable
    â„“ â„“' : Level

record IsAbGroup {G : Type â„“}
                 (0g : G) (_+_ : G â†’ G â†’ G) (-_ : G â†’ G) : Type â„“ where

  constructor isabgroup

  field
    isGroup : IsGroup 0g _+_ -_
    +Comm    : (x y : G) â†’ x + y â‰¡ y + x

  open IsGroup isGroup public
    renaming
      ( Â·Assoc      to +Assoc
      ; identity    to +Identity
      ; Â·IdL        to +IdL
      ; Â·IdR        to +IdR
      ; inverse     to +Inv
      ; Â·InvL       to +InvL
      ; Â·InvR       to +InvR)

record AbGroupStr (A : Type â„“) : Type (â„“-suc â„“) where

  constructor abgroupstr

  field
    0g        : A
    _+_       : A â†’ A â†’ A
    -_        : A â†’ A
    isAbGroup : IsAbGroup 0g _+_ -_

  infix  8 -_
  infixr 7 _+_

  open IsAbGroup isAbGroup public

AbGroup : âˆ€ â„“ â†’ Type (â„“-suc â„“)
AbGroup â„“ = TypeWithStr â„“ AbGroupStr

makeIsAbGroup : {G : Type â„“} {0g : G} {_+_ : G â†’ G â†’ G} { -_ : G â†’ G}
              (is-setG : isSet G)
              (+Assoc  : (x y z : G) â†’ x + (y + z) â‰¡ (x + y) + z)
              (+IdR    : (x : G) â†’ x + 0g â‰¡ x)
              (+InvR   : (x : G) â†’ x + (- x) â‰¡ 0g)
              (+Comm   : (x y : G) â†’ x + y â‰¡ y + x)
            â†’ IsAbGroup 0g _+_ -_
makeIsAbGroup is-setG +Assoc Â·IdR +InvR +Comm =
  isabgroup (makeIsGroup is-setG +Assoc Â·IdR (Î» x â†’ +Comm _ _ âˆ™ Â·IdR x) +InvR (Î» x â†’ +Comm _ _ âˆ™ +InvR x)) +Comm

makeAbGroup : {G : Type â„“} (0g : G) (_+_ : G â†’ G â†’ G) (-_ : G â†’ G)
            (is-setG : isSet G)
            (+Assoc  : (x y z : G) â†’ x + (y + z) â‰¡ (x + y) + z)
            (+IdR    : (x : G) â†’ x + 0g â‰¡ x)
            (+InvR   : (x : G) â†’ x + (- x) â‰¡ 0g)
            (+Comm   : (x y : G) â†’ x + y â‰¡ y + x)
          â†’ AbGroup â„“
makeAbGroup 0g _+_ -_ is-setG +Assoc Â·IdR +InvR +Comm =
  _ , abgroupstr 0g _+_ -_ (makeIsAbGroup is-setG +Assoc Â·IdR +InvR +Comm)

open GroupStr
open AbGroupStr
open IsAbGroup

AbGroupStrâ†’GroupStr : {G : Type â„“} â†’ AbGroupStr G â†’ GroupStr G
AbGroupStrâ†’GroupStr A .1g = A .0g
AbGroupStrâ†’GroupStr A ._Â·_ = A ._+_
AbGroupStrâ†’GroupStr A .inv = A .-_
AbGroupStrâ†’GroupStr A .isGroup = A .isAbGroup .isGroup

AbGroupâ†’Group : AbGroup â„“ â†’ Group â„“
fst (AbGroupâ†’Group A) = fst A
snd (AbGroupâ†’Group A) = AbGroupStrâ†’GroupStr (snd A)

Groupâ†’AbGroup : (G : Group â„“) â†’ ((x y : fst G) â†’ _Â·_ (snd G) x y â‰¡ _Â·_ (snd G) y x) â†’ AbGroup â„“
fst (Groupâ†’AbGroup G +Comm) = fst G
AbGroupStr.0g (snd (Groupâ†’AbGroup G +Comm)) = 1g (snd G)
AbGroupStr._+_ (snd (Groupâ†’AbGroup G +Comm)) = _Â·_ (snd G)
AbGroupStr.- snd (Groupâ†’AbGroup G +Comm) = inv (snd G)
IsAbGroup.isGroup (AbGroupStr.isAbGroup (snd (Groupâ†’AbGroup G +Comm))) = isGroup (snd G)
IsAbGroup.+Comm (AbGroupStr.isAbGroup (snd (Groupâ†’AbGroup G +Comm))) = +Comm

AbGroupâ†’CommMonoid : AbGroup â„“ â†’ CommMonoid â„“
AbGroupâ†’CommMonoid (_ , abgroupstr  _ _ _ G) =
  _ , commmonoidstr _ _ (iscommmonoid (IsAbGroup.isMonoid G) (IsAbGroup.+Comm G))

isSetAbGroup : (A : AbGroup â„“) â†’ isSet âŸ¨ A âŸ©
isSetAbGroup A = isSetGroup (AbGroupâ†’Group A)

AbGroupHom : (G : AbGroup â„“) (H : AbGroup â„“') â†’ Type (â„“-max â„“ â„“')
AbGroupHom G H = GroupHom (AbGroupâ†’Group G) (AbGroupâ†’Group H)

IsAbGroupEquiv : {A : Type â„“} {B : Type â„“'}
  (G : AbGroupStr A) (e : A â‰ƒ B) (H : AbGroupStr B) â†’ Type (â„“-max â„“ â„“')
IsAbGroupEquiv G e H = IsGroupHom (AbGroupStrâ†’GroupStr G) (e .fst) (AbGroupStrâ†’GroupStr H)

AbGroupEquiv : (G : AbGroup â„“) (H : AbGroup â„“') â†’ Type (â„“-max â„“ â„“')
AbGroupEquiv G H = Î£[ e âˆˆ (G .fst â‰ƒ H .fst) ] IsAbGroupEquiv (G .snd) e (H .snd)

isPropIsAbGroup : {G : Type â„“} (0g : G) (_+_ : G â†’ G â†’ G) (- : G â†’ G)
                â†’ isProp (IsAbGroup 0g _+_ -)
isPropIsAbGroup 0g _+_ -_ (isabgroup GG GC) (isabgroup HG HC) =
  Î» i â†’ isabgroup (isPropIsGroup _ _ _ GG HG i) (isPropComm GC HC i)
  where
  isSetG : isSet _
  isSetG = GG .IsGroup.isMonoid .IsMonoid.isSemigroup .IsSemigroup.is-set

  isPropComm : isProp ((x y : _) â†’ x + y â‰¡ y + x)
  isPropComm = isPropÎ 2 Î» _ _ â†’ isSetG _ _

ð’®á´°-AbGroup : DUARel (ð’®-Univ â„“) AbGroupStr â„“
ð’®á´°-AbGroup =
  ð’®á´°-Record (ð’®-Univ _) IsAbGroupEquiv
    (fields:
      data[ _+_ âˆ£ autoDUARel _ _ âˆ£ presÂ· ]
      data[ 0g âˆ£ autoDUARel _ _ âˆ£ pres1 ]
      data[ -_ âˆ£ autoDUARel _ _ âˆ£ presinv ]
      prop[ isAbGroup âˆ£ (Î» _ _ â†’ isPropIsAbGroup _ _ _) ])
  where
  open AbGroupStr
  open IsGroupHom

-- Extract the characterization of equality of groups
AbGroupPath : (G H : AbGroup â„“) â†’ (AbGroupEquiv G H) â‰ƒ (G â‰¡ H)
AbGroupPath = âˆ« ð’®á´°-AbGroup .UARel.ua

-- TODO: Induced structure results are temporarily inconvenient while we transition between algebra
-- representations
module _ (G : AbGroup â„“) {A : Type â„“} (m : A â†’ A â†’ A)
  (e : âŸ¨ G âŸ© â‰ƒ A)
  (pÂ· : âˆ€ x y â†’ e .fst (G .snd ._+_ x y) â‰¡ m (e .fst x) (e .fst y))
  where

  private
    module G = AbGroupStr (G .snd)

    FamilyÎ£ : Î£[ B âˆˆ Type â„“ ] (B â†’ B â†’ B) â†’ Type â„“
    FamilyÎ£ (B , n) =
      Î£[ e âˆˆ B ]
      Î£[ i âˆˆ (B â†’ B) ]
      IsAbGroup e n i

    inducedÎ£ : FamilyÎ£ (A , m)
    inducedÎ£ =
      subst FamilyÎ£
        (UARel.â‰…â†’â‰¡ (autoUARel (Î£[ B âˆˆ Type â„“ ] (B â†’ B â†’ B))) (e , pÂ·))
        (G.0g , G.-_ , G.isAbGroup)

  InducedAbGroup : AbGroup â„“
  InducedAbGroup .fst = A
  InducedAbGroup .snd ._+_ = m
  InducedAbGroup .snd .0g = inducedÎ£ .fst
  InducedAbGroup .snd .-_ = inducedÎ£ .snd .fst
  InducedAbGroup .snd .isAbGroup = inducedÎ£ .snd .snd

  InducedAbGroupPath : G â‰¡ InducedAbGroup
  InducedAbGroupPath = AbGroupPath _ _ .fst (e , makeIsGroupHom pÂ·)

open IsMonoid
open IsSemigroup
open IsGroup

dirProdAb : AbGroup â„“ â†’ AbGroup â„“' â†’ AbGroup (â„“-max â„“ â„“')
dirProdAb A B =
  Groupâ†’AbGroup (DirProd (AbGroupâ†’Group A) (AbGroupâ†’Group B))
                 Î» p q â†’ Î£PathP (+Comm (isAbGroup (snd A)) _ _
                                , +Comm (isAbGroup (snd B)) _ _)

trivialAbGroup : âˆ€ {â„“} â†’ AbGroup â„“
fst trivialAbGroup = Unit*
0g (snd trivialAbGroup) = tt*
_+_ (snd trivialAbGroup) _ _ = tt*
(- snd trivialAbGroup) _ = tt*
is-set (isSemigroup (isMonoid (isGroup (isAbGroup (snd trivialAbGroup))))) =
  isPropâ†’isSet isPropUnit*
Â·Assoc (isSemigroup (isMonoid (isGroup (isAbGroup (snd trivialAbGroup))))) _ _ _ = refl
identity (isMonoid (isGroup (isAbGroup (snd trivialAbGroup)))) _ = refl , refl
inverse (isGroup (isAbGroup (snd trivialAbGroup))) _ = refl , refl
+Comm (isAbGroup (snd trivialAbGroup)) _ _ = refl

-- useful lemma
-- duplicate propeerties => this file should be split !
move4 : âˆ€ {â„“} {A : Type â„“} (x y z w : A) (_+_ : A â†’ A â†’ A)
       â†’ ((x y z : A) â†’ x + (y + z) â‰¡ (x + y) + z)
       â†’ ((x y : A) â†’ x + y â‰¡ y + x)
      â†’ (x + y) + (z + w) â‰¡ ((x + z) + (y + w))
move4 x y z w _+_ assoc +Comm =
     sym (assoc x y (z + w))
  âˆ™âˆ™ cong (x +_) (assoc y z w âˆ™âˆ™ cong (_+ w) (+Comm y z) âˆ™âˆ™ sym (assoc z y w))
  âˆ™âˆ™ assoc x z (y + w)

---- The type of homomorphisms A â†’ B is an AbGroup if B is -----
module _ {â„“ â„“' : Level} (AGr : Group â„“) (BGr : AbGroup â„“') where
  private
    strA = snd AGr
    strB = snd BGr

    _* = AbGroupâ†’Group

    A = fst AGr
    B = fst BGr
    open IsGroupHom

    open AbGroupStr strB
      renaming (_+_ to _+B_ ; -_ to -B_ ; 0g to 0B
              ; +IdR to +IdRB ; +IdL to +IdLB
              ; +Assoc to +AssocB ; +Comm to +CommB
              ; +InvR to +InvRB ; +InvL to +InvLB)
    open GroupStr strA
      renaming (_Â·_ to _âˆ™A_ ; inv to -A_
                ; 1g to 1A ; Â·IdR to Â·IdRA)

  trivGroupHom : GroupHom AGr (BGr *)
  fst trivGroupHom x = 0B
  snd trivGroupHom = makeIsGroupHom Î» _ _ â†’ sym (+IdRB 0B)

  compHom : GroupHom AGr (BGr *) â†’ GroupHom AGr (BGr *) â†’ GroupHom AGr (BGr *)
  fst (compHom f g) x = fst f x +B fst g x
  snd (compHom f g) =
      makeIsGroupHom Î» x y
      â†’ congâ‚‚ _+B_ (presÂ· (snd f) x y) (presÂ· (snd g) x y)
      âˆ™ move4 (fst f x) (fst f y) (fst g x) (fst g y)
              _+B_ +AssocB +CommB

  invHom : GroupHom AGr (BGr *) â†’ GroupHom AGr (BGr *)
  fst (invHom (f , p)) x = -B f x
  snd (invHom (f , p)) =
    makeIsGroupHom
      Î» x y â†’ cong -B_ (presÂ· p x y)
            âˆ™âˆ™ GroupTheory.invDistr (BGr *) (f x) (f y)
            âˆ™âˆ™ +CommB _ _

  open AbGroupStr
  open IsAbGroup
  open IsGroup
  open IsMonoid
  open IsSemigroup

  HomGroup : AbGroup (â„“-max â„“ â„“')
  fst HomGroup = GroupHom AGr (BGr *)
  0g (snd HomGroup) = trivGroupHom
  AbGroupStr._+_ (snd HomGroup) = compHom
  AbGroupStr.- snd HomGroup = invHom
  is-set (isSemigroup (isMonoid (isGroup (isAbGroup (snd HomGroup))))) =
    isSetGroupHom
  Â·Assoc (isSemigroup (isMonoid (isGroup (isAbGroup (snd HomGroup))))) (f , p) (g , q) (h , r) =
    Î£â‰¡Prop (Î» _ â†’ isPropIsGroupHom _ _)
      (funExt Î» x â†’ +AssocB _ _ _)
  fst (identity (isMonoid (isGroup (isAbGroup (snd HomGroup)))) (f , p)) =
    Î£â‰¡Prop (Î» _ â†’ isPropIsGroupHom _ _) (funExt Î» y â†’ +IdRB _)
  snd (identity (isMonoid (isGroup (isAbGroup (snd HomGroup)))) (f , p)) =
    Î£â‰¡Prop (Î» _ â†’ isPropIsGroupHom _ _) (funExt Î» x â†’ +IdLB _)
  fst (inverse (isGroup (isAbGroup (snd HomGroup))) (f , p)) =
    Î£â‰¡Prop (Î» _ â†’ isPropIsGroupHom _ _) (funExt Î» x â†’ +InvRB (f x))
  snd (inverse (isGroup (isAbGroup (snd HomGroup))) (f , p)) =
    Î£â‰¡Prop (Î» _ â†’ isPropIsGroupHom _ _) (funExt Î» x â†’ +InvLB (f x))
  +Comm (isAbGroup (snd HomGroup)) (f , p) (g , q) =
    Î£â‰¡Prop (Î» _ â†’ isPropIsGroupHom _ _)
      (funExt Î» x â†’ +CommB _ _)
