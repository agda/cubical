module Cubical.Algebra.BooleanRing.Base where

open import Cubical.Foundations.Prelude hiding (_âˆ§_;_âˆ¨_)
open import Cubical.Foundations.Structure
open import Cubical.Data.Empty as âŠ¥
open import Cubical.Algebra.Ring
open import Cubical.Algebra.AbGroup.Base
open import Cubical.Algebra.CommRing
open import Cubical.Tactics.CommRingSolver
open RingTheory

private
  variable
   â„“ â„“' : Level

record IsBooleanRing {B : Type â„“}
  (ğŸ˜ ğŸ™ : B) (_+_ _Â·_ : B â†’ B â†’ B) (-_ : B â†’ B) : Type â„“ where
  no-eta-equality

  field
    isCommRing   : IsCommRing ğŸ˜ ğŸ™ _+_ _Â·_ -_
    Â·Idem : (x : B) â†’ x Â· x â‰¡ x

  open IsCommRing isCommRing public

record BooleanRingStr (A : Type â„“) : Type (â„“-suc â„“) where
  field
    ğŸ˜          : A
    ğŸ™          : A
    _+_        : A â†’ A â†’ A
    _Â·_        : A â†’ A â†’ A
    -_         : A â†’ A
    isBooleanRing : IsBooleanRing ğŸ˜ ğŸ™ _+_ _Â·_ -_

  infix  8 -_
  infixl 7 _Â·_
  infixl 6 _+_

  open IsBooleanRing isBooleanRing public

BooleanRing : âˆ€ â„“ â†’ Type (â„“-suc â„“)
BooleanRing â„“ = TypeWithStr â„“ BooleanRingStr

module _ {A : Type â„“} (BRStr : BooleanRingStr A) where
  open CommRingStr
  open BooleanRingStr( BRStr)
  BooleanRingStrâ†’CommRingStr : CommRingStr A
  0r  BooleanRingStrâ†’CommRingStr = _
  1r  BooleanRingStrâ†’CommRingStr = _
  _+_ BooleanRingStrâ†’CommRingStr = _
  _Â·_ BooleanRingStrâ†’CommRingStr = _
  -   BooleanRingStrâ†’CommRingStr = _
  isCommRing BooleanRingStrâ†’CommRingStr = isCommRing BRStr

BooleanRingâ†’CommRing : BooleanRing â„“ â†’ CommRing â„“
BooleanRingâ†’CommRing (carrier , structure ) = carrier , BooleanRingStrâ†’CommRingStr structure

BooleanRingStrâ†’RingStr : { A : Type â„“ } â†’ BooleanRingStr A â†’ RingStr A
BooleanRingStrâ†’RingStr S = CommRingStrâ†’RingStr (BooleanRingStrâ†’CommRingStr S)

BooleanRingâ†’Ring : BooleanRing â„“ â†’ Ring â„“
BooleanRingâ†’Ring (carrier , structure ) = carrier , BooleanRingStrâ†’RingStr structure

isIdemRing : {â„“ : Level} â†’ CommRing â„“ â†’ Type â„“
isIdemRing R = âˆ€ (r : âŸ¨ R âŸ©) â†’ (str R) .CommRingStr._Â·_ r r â‰¡ r

module _ {â„“ : Level} (R : CommRing â„“) (idem : isIdemRing R) where
  open BooleanRingStr
  open IsBooleanRing
  idemCommRingâ†’BR : BooleanRing â„“
  fst idemCommRingâ†’BR = âŸ¨ R âŸ©
  ğŸ˜ (snd idemCommRingâ†’BR)   = _
  ğŸ™ (snd idemCommRingâ†’BR)   = _
  _+_ (snd idemCommRingâ†’BR) = _
  _Â·_ (snd idemCommRingâ†’BR) = _
  - snd idemCommRingâ†’BR     = _
  isCommRing (isBooleanRing (snd idemCommRingâ†’BR)) = (str R) .CommRingStr.isCommRing
  Â·Idem (isBooleanRing (snd idemCommRingâ†’BR))      = idem

BoolHom : {â„“ â„“' : Level} â†’ (A : BooleanRing â„“) â†’ (B : BooleanRing â„“') â†’ Type _
BoolHom A B = CommRingHom (BooleanRingâ†’CommRing A) (BooleanRingâ†’CommRing B)

module BooleanAlgebraStr (A : BooleanRing â„“)  where
  open BooleanRingStr (A . snd)
  _âˆ¨_ : âŸ¨ A âŸ© â†’ âŸ¨ A âŸ© â†’ âŸ¨ A âŸ©
  a âˆ¨ b = (a + b) + (a Â· b)
  _âˆ§_ : âŸ¨ A âŸ© â†’ âŸ¨ A âŸ© â†’ âŸ¨ A âŸ©
  a âˆ§ b = a Â· b
  Â¬_ : âŸ¨ A âŸ© â†’ âŸ¨ A âŸ©
  Â¬ a = ğŸ™ + a

  infix  8 Â¬_
  infixl 7 _âˆ§_
  infixl 6 _âˆ¨_

  variable x y z : âŸ¨ A âŸ©

  âˆ§Idem : x âˆ§ x â‰¡ x
  âˆ§Idem = Â·Idem _

  âˆ§Assoc : x âˆ§ ( y âˆ§ z ) â‰¡ ( x âˆ§ y ) âˆ§ z
  âˆ§Assoc = Â·Assoc _ _ _

  âˆ§Comm :  x âˆ§ y â‰¡ y âˆ§ x
  âˆ§Comm = Â·Comm _ _

  âˆ¨Assoc : (x âˆ¨ ( y âˆ¨ z ) â‰¡ ( x âˆ¨ y ) âˆ¨ z )
  âˆ¨Assoc =  solve! (BooleanRingâ†’CommRing A)

  âˆ¨Comm : x âˆ¨ y â‰¡ y âˆ¨ x
  âˆ¨Comm  = solve! (BooleanRingâ†’CommRing A)

  âˆ¨IdR : x âˆ¨ ğŸ˜ â‰¡ x
  âˆ¨IdR = solve! (BooleanRingâ†’CommRing A)

  âˆ¨IdL : ğŸ˜ âˆ¨ x â‰¡ x
  âˆ¨IdL = solve! (BooleanRingâ†’CommRing A)

  âˆ§IdR : x âˆ§ ğŸ™ â‰¡ x
  âˆ§IdR = Â·IdR _

  âˆ§IdL : ğŸ™ âˆ§ x â‰¡ x
  âˆ§IdL = Â·IdL _

  âˆ§AnnihilR : x âˆ§ ğŸ˜ â‰¡ ğŸ˜
  âˆ§AnnihilR = RingTheory.0RightAnnihilates (CommRingâ†’Ring (BooleanRingâ†’CommRing A)) _

  âˆ§AnnihilL : ğŸ˜ âˆ§ x â‰¡ ğŸ˜
  âˆ§AnnihilL = RingTheory.0LeftAnnihilates (CommRingâ†’Ring (BooleanRingâ†’CommRing A)) _

  characteristic2 : x + x â‰¡ ğŸ˜
  characteristic2 {x = x} =  RingTheory.+Idempotencyâ†’0 (CommRingâ†’Ring (BooleanRingâ†’CommRing A)) (x + x) 2xâ‰¡4x
    where
      2xâ‰¡4x : x + x â‰¡ (x + x) + (x + x)
      2xâ‰¡4x =
        x + x
          â‰¡âŸ¨ sym (Â·Idem (x + x)) âŸ©
        (x + x) Â· (x + x)
          â‰¡âŸ¨ solve! (BooleanRingâ†’CommRing A) âŸ©
        ((x Â· x) + (x Â· x)) + ((x Â· x) + (x Â· x))
          â‰¡âŸ¨ congâ‚‚ _+_ (congâ‚‚ _+_ (Â·Idem x) (Â·Idem x)) (congâ‚‚ _+_ (Â·Idem x) (Â·Idem x)) âŸ©
        (x + x) + (x + x) âˆ

  -IsId : x â‰¡ - x
  -IsId {x = x} = implicitInverse (BooleanRingâ†’Ring A) x x characteristic2

  âˆ¨Idem   : x âˆ¨ x â‰¡ x
  âˆ¨Idem { x = x } =
    x + x + x Â· x
      â‰¡âŸ¨ cong (Î» y â†’ y + x Â· x) characteristic2 âŸ©
    ğŸ˜  + x Â· x
      â‰¡âŸ¨ +IdL (x Â· x) âŸ©
    x Â· x
      â‰¡âŸ¨ Â·Idem x âŸ©
    x âˆ

  1Absorbsâˆ¨R : x âˆ¨ ğŸ™ â‰¡ ğŸ™
  1Absorbsâˆ¨R {x = x} =
    (x + ğŸ™) + (x Â· ğŸ™)
      â‰¡âŸ¨ solve! (BooleanRingâ†’CommRing A) âŸ©
    ğŸ™ + (x + x)
      â‰¡âŸ¨ cong (Î» y â†’ ğŸ™ + y) characteristic2 âŸ©
    ğŸ™ + ğŸ˜
      â‰¡âŸ¨ +IdR ğŸ™ âŸ©
    ğŸ™ âˆ

  1Absorbsâˆ¨L : ğŸ™ âˆ¨ x â‰¡ ğŸ™
  1Absorbsâˆ¨L {x = x} = âˆ¨Comm âˆ™ 1Absorbsâˆ¨R

  âˆ§DistRâˆ¨ : x âˆ§ ( y âˆ¨ z) â‰¡ (x âˆ§ y) âˆ¨ (x âˆ§ z)
  âˆ§DistRâˆ¨ {x = x} {y = y} { z = z} =
    x Â· ((y + z) + (y Â· z))
      â‰¡âŸ¨ solve! (BooleanRingâ†’CommRing A) âŸ©
    x Â· y + x Â· z +   x   Â· (y Â· z)
      â‰¡âŸ¨ cong (Î» a â†’ x Â· y + x Â· z + a Â· (y Â· z)) (sym (Â·Idem x)) âŸ©
    x Â· y + x Â· z + x Â· x Â· (y Â· z)
      â‰¡âŸ¨  solve! (BooleanRingâ†’CommRing A) âŸ©
    x Â· y + x Â· z + (x Â· y) Â· (x Â· z) âˆ

  âˆ§DistLâˆ¨ : (x âˆ¨ y) âˆ§ z â‰¡ (x âˆ§ z) âˆ¨ (y âˆ§ z)
  âˆ§DistLâˆ¨ = âˆ§Comm âˆ™ âˆ§DistRâˆ¨ âˆ™ congâ‚‚ _âˆ¨_ âˆ§Comm âˆ§Comm

  âˆ¨DistRâˆ§ :  x âˆ¨ (y âˆ§ z) â‰¡ (x âˆ¨ y) âˆ§ (x âˆ¨ z)
  âˆ¨DistRâˆ§ {x = x} {y = y} {z = z} =
    x + (y Â· z) + x Â· (y Â· z)
      â‰¡âŸ¨ solve! (BooleanRingâ†’CommRing A) âŸ©
    x + ğŸ˜ + ğŸ˜ + y Â· z + ğŸ˜ + x Â· y Â· z
      â‰¡âŸ¨ cong (Î» a â†’ a + ğŸ˜ + ğŸ˜ + y Â· z + ğŸ˜ + a Â· y Â· z) (sym (Â·Idem x)) âŸ©
    x Â· x + ğŸ˜  + ğŸ˜  + y Â· z + ğŸ˜ + x Â· x Â· y Â· z
      â‰¡âŸ¨ cong (Î» a â†’ x Â· x + ğŸ˜ + ğŸ˜ + y Â· z + a + x Â· x Â· y Â· z) (sym (characteristic2 {x = (x Â· y) Â· z})) âŸ©
    x Â· x + ğŸ˜ + ğŸ˜ + y Â· z + (x Â· y Â· z + x Â· y Â· z) + x Â· x Â· y Â· z
      â‰¡âŸ¨ (congâ‚‚ (Î» a b â†’ x Â· x + a + b + y Â· z + (x Â· y Â· z + x Â· y Â· z) + x Â· x Â· y Â· z)) (xa-xxaâ‰¡0 z) (xa-xxaâ‰¡0 y) âŸ©
    x Â· x + (x Â· z + x Â· x Â· z) + (x Â· y + x Â· x Â· y) + y Â· z + (x Â· y Â· z + x Â· y Â· z) + x Â· x Â· y Â· z
      â‰¡âŸ¨ solve! (BooleanRingâ†’CommRing A) âŸ©
    (x + y + x Â· y) Â· (x + z + x Â· z) âˆ where
      xaâ‰¡xxa : (a : âŸ¨ A âŸ©) â†’ x Â· a â‰¡ (x Â· x ) Â· a
      xaâ‰¡xxa a = cong (Î» y â†’ y Â· a) (sym (Â·Idem x))
      xa-xxaâ‰¡0 : (a : âŸ¨ A âŸ©) â†’ ğŸ˜ â‰¡ x Â· a + x Â· x Â· a
      xa-xxaâ‰¡0 a =
       ğŸ˜
         â‰¡âŸ¨ sym characteristic2 âŸ©
       x Â· a + x Â· a
         â‰¡âŸ¨ cong (Î» y â†’ x Â· a + y Â· a) (sym (Â·Idem x)) âŸ©
       x Â· a + x Â· x Â· a âˆ

  âˆ¨Distrâˆ§R :  (x âˆ§ y) âˆ¨ z â‰¡ (x âˆ¨ z) âˆ§ (y âˆ¨ z)
  âˆ¨Distrâˆ§R = âˆ¨Comm âˆ™ âˆ¨DistRâˆ§ âˆ™ congâ‚‚ _âˆ§_ âˆ¨Comm âˆ¨Comm

  âˆ§AbsorbLâˆ¨ : x âˆ§ (x âˆ¨ y) â‰¡ x
  âˆ§AbsorbLâˆ¨ {x = x} {y = y} =
    x Â· ((x + y) + (x Â· y))
      â‰¡âŸ¨ solve! (BooleanRingâ†’CommRing A) âŸ©
    x Â· x + (x Â· y + x Â· x Â· y)
      â‰¡âŸ¨ cong (Î» z â†’ z + ((x Â· y) + (z Â· y))) (Â·Idem x) âŸ©
    x + (x Â· y + x Â· y)
      â‰¡âŸ¨ cong (_+_ x) characteristic2 âŸ©
    x + ğŸ˜
      â‰¡âŸ¨ +IdR x âŸ©
    x âˆ

  âˆ¨AbsorbLâˆ§ :  x âˆ¨ (x âˆ§ y) â‰¡ x
  âˆ¨AbsorbLâˆ§ {x = x} { y = y}  =
    x + x Â· y + x Â· (x Â· y)
      â‰¡âŸ¨ solve! (BooleanRingâ†’CommRing A)  âŸ©
    x + (x Â· y + x Â· x Â· y)
      â‰¡âŸ¨ cong (Î» z â†’ x + (x Â· y + z Â· y)) (Â·Idem x) âŸ©
    x + (x Â· y + x Â· y)
      â‰¡âŸ¨ cong (_+_ x) characteristic2 âŸ©
    x + ğŸ˜
      â‰¡âŸ¨ +IdR x âŸ©
    x âˆ

  Â¬Cancelsâˆ§R : x âˆ§ Â¬ x â‰¡ ğŸ˜
  Â¬Cancelsâˆ§R {x = x} =
    x Â· (ğŸ™ + x)
      â‰¡âŸ¨ solve! (BooleanRingâ†’CommRing A) âŸ©
    x + x Â· x
      â‰¡âŸ¨ cong (Î» y â†’ x + y) (Â·Idem x) âŸ©
    x + x
      â‰¡âŸ¨ characteristic2 âŸ©
    ğŸ˜ âˆ

  Â¬Cancelsâˆ§L : Â¬ x âˆ§ x â‰¡ ğŸ˜
  Â¬Cancelsâˆ§L = âˆ§Comm âˆ™ Â¬Cancelsâˆ§R

  Â¬Completesâˆ¨R : x âˆ¨ Â¬ x â‰¡ ğŸ™
  Â¬Completesâˆ¨R {x = x} =
    x + Â¬ x + (x âˆ§ Â¬ x)
      â‰¡âŸ¨ cong (Î» z â†’ x + Â¬ x + z) Â¬Cancelsâˆ§R âŸ©
    x + Â¬ x + ğŸ˜
      â‰¡âŸ¨ solve! (BooleanRingâ†’CommRing A) âŸ©
    x âˆ¨ ğŸ™
      â‰¡âŸ¨ 1Absorbsâˆ¨R âŸ©
    ğŸ™ âˆ

  Â¬Completesâˆ¨L : (Â¬ x) âˆ¨ x â‰¡ ğŸ™
  Â¬Completesâˆ¨L = âˆ¨Comm âˆ™ Â¬Completesâˆ¨R

  Â¬Invol : Â¬ Â¬ x â‰¡ x
  Â¬Invol {x = x} =
    ğŸ™ + (ğŸ™ + x)
      â‰¡âŸ¨ +Assoc ğŸ™ ğŸ™ x âŸ©
    (ğŸ™ + ğŸ™) + x
      â‰¡âŸ¨ cong (Î» y â†’ y + x) ( characteristic2 {x = ğŸ™}) âŸ©
    ğŸ˜ + x
      â‰¡âŸ¨ +IdL x âŸ©
    x âˆ

  Â¬0â‰¡1 : Â¬ ğŸ˜ â‰¡ ğŸ™
  Â¬0â‰¡1 = +IdR ğŸ™

  Â¬1â‰¡0 : Â¬ ğŸ™ â‰¡ ğŸ˜
  Â¬1â‰¡0 = characteristic2 {x = ğŸ™}

  DeMorganÂ¬âˆ¨ : Â¬ (x âˆ¨ y) â‰¡ Â¬ x âˆ§ Â¬ y
  DeMorganÂ¬âˆ¨ = solve! (BooleanRingâ†’CommRing A)

  DeMorganÂ¬âˆ§ : Â¬ (x âˆ§ y) â‰¡ Â¬ x âˆ¨ Â¬ y
  DeMorganÂ¬âˆ§ {x = x} {y = y} =
    ğŸ™ + x Â· y
      â‰¡âŸ¨ solve! (BooleanRingâ†’CommRing A) âŸ©
    ğŸ˜ + ğŸ˜ + ğŸ™ + x Â· y
      â‰¡âŸ¨ congâ‚‚ (Î» a b â†’ ((a + b) + ğŸ™) + (x Â· y)) (sym (characteristic2 {x = ğŸ™ + x})) (sym (characteristic2 {x = y})) âŸ©
    ((ğŸ™ + x)  + (ğŸ™ + x)) + (y + y)  + ğŸ™ + x Â· y
      â‰¡âŸ¨ solve! (BooleanRingâ†’CommRing A) âŸ©
    Â¬ x âˆ¨ Â¬ y âˆ

