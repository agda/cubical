{-

   The impredicative way to define functorial qcqs-schemes (over Spec(â„¤))

-}
{-# OPTIONS --safe --lossy-unification #-}
module Cubical.Categories.Instances.ZFunctors where

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.Equiv
open import Cubical.Foundations.Isomorphism
open import Cubical.Foundations.Function
open import Cubical.Foundations.Powerset
open import Cubical.Foundations.HLevels


open import Cubical.Functions.FunExtEquiv

open import Cubical.Data.Sigma
open import Cubical.Data.Nat using (â„•)

open import Cubical.Data.FinData

open import Cubical.Algebra.Ring
open import Cubical.Algebra.CommRing
open import Cubical.Algebra.Semilattice
open import Cubical.Algebra.Lattice
open import Cubical.Algebra.DistLattice
open import Cubical.Algebra.DistLattice.BigOps
open import Cubical.Algebra.ZariskiLattice.Base
open import Cubical.Algebra.ZariskiLattice.UniversalProperty

open import Cubical.Categories.Category
open import Cubical.Categories.Functor
open import Cubical.Categories.Instances.Sets
open import Cubical.Categories.Instances.CommRings
open import Cubical.Categories.Instances.DistLattice
open import Cubical.Categories.Instances.DistLattices
open import Cubical.Categories.Instances.Functors
open import Cubical.Categories.Site.Coverage
open import Cubical.Categories.Site.Sheaf
open import Cubical.Categories.Site.Instances.ZariskiCommRing
open import Cubical.Categories.NaturalTransformation
open import Cubical.Categories.Yoneda


open import Cubical.HITs.PropositionalTruncation as PT
open import Cubical.HITs.SetQuotients as SQ

open import Cubical.Relation.Binary.Order.Poset

open Category hiding (_âˆ˜_) renaming (_â‹†_ to _â‹†c_)

private
 variable
  â„“ â„“' â„“'' : Level


module _ {â„“ : Level} where

  open Functor
  open NatTrans
  open CommRingStr â¦ƒ...â¦„
  open IsRingHom

  -- using the naming conventions of Demazure & Gabriel
  â„¤Functor = Functor (CommRingsCategory {â„“ = â„“}) (SET â„“)
  â„¤FUNCTOR = FUNCTOR (CommRingsCategory {â„“ = â„“}) (SET â„“)

  -- Yoneda in the notation of Demazure & Gabriel,
  -- uses that double op is original category definitionally
  Sp : Functor (CommRingsCategory {â„“ = â„“} ^op) â„¤FUNCTOR
  Sp = YO {C = (CommRingsCategory {â„“ = â„“} ^op)}

  -- the forgetful functor
  -- aka the affine line
  -- (aka the representable of â„¤[x])
  ğ”¸Â¹ : â„¤Functor
  ğ”¸Â¹ = ForgetfulCommRingâ†’Set

  -- the global sections functor
  ğ“ : Functor â„¤FUNCTOR (CommRingsCategory {â„“ = â„“-suc â„“} ^op)
  fst (F-ob ğ“ X) = X â‡’ ğ”¸Â¹

  -- ring struncture induced by internal ring object ğ”¸Â¹
  N-ob (CommRingStr.0r (snd (F-ob ğ“ X))) A _ = 0r
    where instance _ = A .snd
  N-hom (CommRingStr.0r (snd (F-ob ğ“ X))) Ï† = funExt Î» _ â†’ sym (Ï† .snd .pres0)

  N-ob (CommRingStr.1r (snd (F-ob ğ“ X))) A _ = 1r
    where instance _ = A .snd
  N-hom (CommRingStr.1r (snd (F-ob ğ“ X))) Ï† = funExt Î» _ â†’ sym (Ï† .snd .pres1)

  N-ob ((snd (F-ob ğ“ X) CommRingStr.+ Î±) Î²) A x = Î± .N-ob A x + Î² .N-ob A x
    where instance _ = A .snd
  N-hom ((snd (F-ob ğ“ X) CommRingStr.+ Î±) Î²) {x = A} {y = B} Ï† = funExt path
    where
    instance
      _ = A .snd
      _ = B .snd
    path : âˆ€ x â†’ Î± .N-ob B (X .F-hom Ï† x) + Î² .N-ob B (X .F-hom Ï† x)
               â‰¡ Ï† .fst (Î± .N-ob A x + Î² .N-ob A x)
    path x = Î± .N-ob B (X .F-hom Ï† x) + Î² .N-ob B (X .F-hom Ï† x)
           â‰¡âŸ¨ congâ‚‚ _+_ (funExtâ» (Î± .N-hom Ï†) x) (funExtâ» (Î² .N-hom Ï†) x) âŸ©
             Ï† .fst (Î± .N-ob A x) + Ï† .fst (Î² .N-ob A x)
           â‰¡âŸ¨ sym (Ï† .snd .pres+ _ _) âŸ©
             Ï† .fst (Î± .N-ob A x + Î² .N-ob A x) âˆ

  N-ob ((snd (F-ob ğ“ X) CommRingStr.Â· Î±) Î²) A x = Î± .N-ob A x Â· Î² .N-ob A x
    where instance _ = A .snd
  N-hom ((snd (F-ob ğ“ X) CommRingStr.Â· Î±) Î²) {x = A} {y = B} Ï† = funExt path
    where
    instance
      _ = A .snd
      _ = B .snd
    path : âˆ€ x â†’ Î± .N-ob B (X .F-hom Ï† x) Â· Î² .N-ob B (X .F-hom Ï† x)
               â‰¡ Ï† .fst (Î± .N-ob A x Â· Î² .N-ob A x)
    path x = Î± .N-ob B (X .F-hom Ï† x) Â· Î² .N-ob B (X .F-hom Ï† x)
           â‰¡âŸ¨ congâ‚‚ _Â·_ (funExtâ» (Î± .N-hom Ï†) x) (funExtâ» (Î² .N-hom Ï†) x) âŸ©
             Ï† .fst (Î± .N-ob A x) Â· Ï† .fst (Î² .N-ob A x)
           â‰¡âŸ¨ sym (Ï† .snd .presÂ· _ _) âŸ©
             Ï† .fst (Î± .N-ob A x Â· Î² .N-ob A x) âˆ

  N-ob ((CommRingStr.- snd (F-ob ğ“ X)) Î±) A x = - Î± .N-ob A x
    where instance _ = A .snd
  N-hom ((CommRingStr.- snd (F-ob ğ“ X)) Î±) {x = A} {y = B} Ï† = funExt path
    where
    instance
      _ = A .snd
      _ = B .snd
    path : âˆ€ x â†’ - Î± .N-ob B (X .F-hom Ï† x) â‰¡ Ï† .fst (- Î± .N-ob A x)
    path x = - Î± .N-ob B (X .F-hom Ï† x) â‰¡âŸ¨ cong -_ (funExtâ» (Î± .N-hom Ï†) x) âŸ©
             - Ï† .fst (Î± .N-ob A x)     â‰¡âŸ¨ sym (Ï† .snd .pres- _) âŸ©
             Ï† .fst (- Î± .N-ob A x)     âˆ

  CommRingStr.isCommRing (snd (F-ob ğ“ X)) = makeIsCommRing
    isSetNatTrans
    (Î» _ _ _ â†’ makeNatTransPath (funExtâ‚‚ Î» A _ â†’ A .snd .CommRingStr.+Assoc _ _ _))
    (Î» _ â†’ makeNatTransPath (funExtâ‚‚ Î» A _ â†’ A .snd .CommRingStr.+IdR _))
    (Î» _ â†’ makeNatTransPath (funExtâ‚‚ Î» A _ â†’ A .snd .CommRingStr.+InvR _))
    (Î» _ _ â†’ makeNatTransPath (funExtâ‚‚ Î» A _ â†’ A .snd .CommRingStr.+Comm _ _))
    (Î» _ _ _ â†’ makeNatTransPath (funExtâ‚‚ Î» A _ â†’ A .snd .CommRingStr.Â·Assoc _ _ _))
    (Î» _ â†’ makeNatTransPath (funExtâ‚‚ Î» A _ â†’ A .snd .CommRingStr.Â·IdR _))
    (Î» _ _ _ â†’ makeNatTransPath (funExtâ‚‚ Î» A _ â†’ A .snd .CommRingStr.Â·DistR+ _ _ _))
    (Î» _ _ â†’ makeNatTransPath (funExtâ‚‚ Î» A _ â†’ A .snd .CommRingStr.Â·Comm _ _))

  -- action on natural transformations
  fst (F-hom ğ“ Î±) = Î± â—áµ›_
  pres0 (snd (F-hom ğ“ Î±)) = makeNatTransPath (funExtâ‚‚ Î» _ _ â†’ refl)
  pres1 (snd (F-hom ğ“ Î±)) = makeNatTransPath (funExtâ‚‚ Î» _ _ â†’ refl)
  pres+ (snd (F-hom ğ“ Î±)) _ _ = makeNatTransPath (funExtâ‚‚ Î» _ _ â†’ refl)
  presÂ· (snd (F-hom ğ“ Î±)) _ _ = makeNatTransPath (funExtâ‚‚ Î» _ _ â†’ refl)
  pres- (snd (F-hom ğ“ Î±)) _ = makeNatTransPath (funExtâ‚‚ Î» _ _ â†’ refl)

  -- functoriality of ğ“
  F-id ğ“ = RingHomâ‰¡ (funExt Î» _ â†’ makeNatTransPath (funExtâ‚‚ Î» _ _ â†’ refl))
  F-seq ğ“ _ _ = RingHomâ‰¡ (funExt Î» _ â†’ makeNatTransPath (funExtâ‚‚ Î» _ _ â†’ refl))



-- There is an adjunction ğ“ âŠ£áµ¢ Sp
-- (relative to the inclusion i : CommRing â„“ â†’ CommRing (â„“+1))
-- between the "global sections functor" ğ“
-- and the fully-faithful embedding of affines Sp,
-- whose counit is a natural isomorphism
module AdjBij where

  open Functor
  open NatTrans
  open Iso
  open IsRingHom

  private module _ {A : CommRing â„“} {X : â„¤Functor {â„“}} where
    _â™­ : CommRingHom A (ğ“ .F-ob X) â†’ X â‡’ Sp .F-ob A
    fst (N-ob (Ï† â™­) B x) a = Ï† .fst a .N-ob B x

    pres0 (snd (N-ob (Ï† â™­) B x)) = cong (Î» y â†’ y .N-ob B x) (Ï† .snd .pres0)
    pres1 (snd (N-ob (Ï† â™­) B x)) = cong (Î» y â†’ y .N-ob B x) (Ï† .snd .pres1)
    pres+ (snd (N-ob (Ï† â™­) B x)) _ _ = cong (Î» y â†’ y .N-ob B x) (Ï† .snd .pres+ _ _)
    presÂ· (snd (N-ob (Ï† â™­) B x)) _ _ = cong (Î» y â†’ y .N-ob B x) (Ï† .snd .presÂ· _ _)
    pres- (snd (N-ob (Ï† â™­) B x)) _ = cong (Î» y â†’ y .N-ob B x) (Ï† .snd .pres- _)

    N-hom (Ï† â™­) Ïˆ = funExt (Î» x â†’ RingHomâ‰¡ (funExt Î» a â†’ funExtâ» (Ï† .fst a .N-hom Ïˆ) x))


    -- the other direction is just precomposition modulo Yoneda
    _â™¯ : X â‡’ Sp .F-ob A â†’ CommRingHom A (ğ“ .F-ob X)
    fst (Î± â™¯) a = Î± â—áµ› yonedaá´¾ ğ”¸Â¹ A .inv a

    pres0 (snd (Î± â™¯)) = makeNatTransPath (funExtâ‚‚ Î» B x â†’ Î± .N-ob B x .snd .pres0)
    pres1 (snd (Î± â™¯)) = makeNatTransPath (funExtâ‚‚ Î» B x â†’ Î± .N-ob B x .snd .pres1)
    pres+ (snd (Î± â™¯)) _ _ = makeNatTransPath (funExtâ‚‚ Î» B x â†’ Î± .N-ob B x .snd .pres+ _ _)
    presÂ· (snd (Î± â™¯)) _ _ = makeNatTransPath (funExtâ‚‚ Î» B x â†’ Î± .N-ob B x .snd .presÂ· _ _)
    pres- (snd (Î± â™¯)) _ = makeNatTransPath (funExtâ‚‚ Î» B x â†’ Î± .N-ob B x .snd .pres- _)


    -- the two maps are inverse to each other
    â™­â™¯Id : âˆ€ (Î±  : X â‡’ Sp .F-ob A) â†’ ((Î± â™¯) â™­) â‰¡ Î±
    â™­â™¯Id _ = makeNatTransPath (funExtâ‚‚ Î» _ _ â†’ RingHomâ‰¡ (funExt (Î» _ â†’ refl)))

    â™¯â™­Id : âˆ€ (Ï† : CommRingHom A (ğ“ .F-ob X)) â†’ ((Ï† â™­) â™¯) â‰¡ Ï†
    â™¯â™­Id _ = RingHomâ‰¡ (funExt Î» _ â†’ makeNatTransPath (funExtâ‚‚ Î» _ _ â†’ refl))


  -- we get a relative adjunction ğ“ âŠ£áµ¢ Sp
  -- with respect to the inclusion i : CommRing â„“ â†’ CommRing (â„“+1)
  ğ“âŠ£SpIso : {A : CommRing â„“} {X : â„¤Functor {â„“}}
          â†’ Iso (CommRingHom A (ğ“ .F-ob X)) (X â‡’ Sp .F-ob A)
  fun ğ“âŠ£SpIso = _â™­
  inv ğ“âŠ£SpIso = _â™¯
  rightInv ğ“âŠ£SpIso = â™­â™¯Id
  leftInv ğ“âŠ£SpIso = â™¯â™­Id

  ğ“âŠ£SpNatâ„¤Functor : {A : CommRing â„“} {X Y : â„¤Functor {â„“}} (Î± : X â‡’ Sp .F-ob A) (Î² : Y â‡’ X)
                  â†’ (Î² â—áµ› Î±) â™¯ â‰¡ (ğ“ .F-hom Î²) âˆ˜cr (Î± â™¯)
  ğ“âŠ£SpNatâ„¤Functor _ _ = RingHomâ‰¡ (funExt (Î» _ â†’ makeNatTransPath (funExtâ‚‚ (Î» _ _ â†’ refl))))

  ğ“âŠ£SpNatCommRing : {X : â„¤Functor {â„“}} {A B : CommRing â„“}
                    (Ï† : CommRingHom A (ğ“ .F-ob X)) (Ïˆ : CommRingHom B A)
                  â†’ (Ï† âˆ˜cr Ïˆ) â™­ â‰¡ (Ï† â™­) â—áµ› Sp .F-hom Ïˆ
  ğ“âŠ£SpNatCommRing _ _ = makeNatTransPath (funExtâ‚‚ Î» _ _ â†’ RingHomâ‰¡ (funExt (Î» _ â†’ refl)))

  -- the counit is an equivalence
  private
    Îµ : (A : CommRing â„“) â†’ CommRingHom A ((ğ“ âˆ˜F Sp) .F-ob A)
    Îµ A = (idTrans (Sp .F-ob A)) â™¯

  ğ“âŠ£SpCounitEquiv : (A : CommRing â„“) â†’ CommRingEquiv A ((ğ“ âˆ˜F Sp) .F-ob A)
  fst (ğ“âŠ£SpCounitEquiv A) = isoToEquiv theIso
    where
    theIso : Iso (A .fst) ((ğ“ âˆ˜F Sp) .F-ob A .fst)
    fun theIso = Îµ A .fst
    inv theIso = yonedaá´¾ ğ”¸Â¹ A .fun
    rightInv theIso Î± = â„¤FUNCTOR .â‹†IdL _ âˆ™ yonedaá´¾ ğ”¸Â¹ A .leftInv Î±
    leftInv theIso a = path -- I get yellow otherwise
      where
      path : yonedaá´¾ ğ”¸Â¹ A .fun ((idTrans (Sp .F-ob A)) â—áµ› yonedaá´¾ ğ”¸Â¹ A .inv a) â‰¡ a
      path = cong (yonedaá´¾ ğ”¸Â¹ A .fun) (â„¤FUNCTOR .â‹†IdL _) âˆ™ yonedaá´¾ ğ”¸Â¹ A .rightInv a
  snd (ğ“âŠ£SpCounitEquiv A) = Îµ A .snd


-- Affine schemes
module _ {â„“ : Level} where
  open Functor

  isAffine : (X : â„¤Functor) â†’ Type (â„“-suc â„“)
  isAffine X = âˆƒ[ A âˆˆ CommRing â„“ ] NatIso (Sp .F-ob A) X

  -- TODO: ğ”¸Â¹ â‰… Sp â„¤[x] and ğ”¾â‚˜ â‰… Sp â„¤[x,xâ»Â¹] as first examples of affine schemes


-- The unit is an equivalence iff the â„¤-functor is affine.
-- Unfortunately, we can't give a natural transformation
-- X â‡’ Sp (ğ“ X), because the latter â„¤-functor lives in a higher universe.
-- We can however give terms that look just like the unit,
-- giving us an alternative def. of affine â„¤-functors
private module AffineDefs {â„“ : Level} where
  open Functor
  open NatTrans
  open Iso
  open IsRingHom
  Î· : (X : â„¤Functor) (A : CommRing â„“) â†’ X .F-ob A .fst â†’ CommRingHom (ğ“ .F-ob X) A
  fst (Î· X A x) Î± = Î± .N-ob A x
  pres0 (snd (Î· X A x)) = refl
  pres1 (snd (Î· X A x)) = refl
  pres+ (snd (Î· X A x)) _ _ = refl
  presÂ· (snd (Î· X A x)) _ _ = refl
  pres- (snd (Î· X A x)) _ = refl

  -- this is basically a natural transformation
  Î·ObHom : (X : â„¤Functor) {A B : CommRing â„“} (Ï† : CommRingHom A B)
           â†’ Î· X B âˆ˜ (X .F-hom Ï†) â‰¡ (Ï† âˆ˜cr_) âˆ˜ Î· X A
  Î·ObHom X Ï† = funExt (Î» x â†’ RingHomâ‰¡ (funExt Î» Î± â†’ funExtâ» (Î± .N-hom Ï†) x))

  -- can only state equality on object part, but that would be enough
  Î·Hom : {X Y : â„¤Functor} (Î± : X â‡’ Y) (A : CommRing â„“) (x : X .F-ob A .fst)
         â†’ Î· Y A (Î± .N-ob A x) â‰¡ Î· X A x âˆ˜cr ğ“ .F-hom Î±
  Î·Hom _ _ _ = RingHomâ‰¡ refl

  isAffine' : (X : â„¤Functor) â†’ Type (â„“-suc â„“)
  isAffine' X = âˆ€ (A : CommRing â„“) â†’ isEquiv (Î· X A)
  -- TODO: isAffine â†’ isAffine'


-- compact opens and affine covers
module _ {â„“ : Level} where

  open Iso
  open Functor
  open NatTrans
  open NatIso
  open DistLatticeStr â¦ƒ...â¦„
  open CommRingStr â¦ƒ...â¦„
  open IsRingHom
  open IsLatticeHom
  open ZarLat
  open ZarLatUniversalProp

  -- the Zariski lattice classifying compact open subobjects
  ZarLatFun : â„¤Functor {â„“ = â„“}
  F-ob ZarLatFun A = ZL A , SQ.squash/
  F-hom ZarLatFun Ï† = inducedZarLatHom Ï† .fst
  F-id ZarLatFun {A} = cong fst (inducedZarLatHomId A)
  F-seq ZarLatFun Ï† Ïˆ = cong fst (inducedZarLatHomSeq Ï† Ïˆ)

  CompactOpen : â„¤Functor â†’ Type (â„“-suc â„“)
  CompactOpen X = X â‡’ ZarLatFun

  -- the induced subfunctor
  âŸ¦_âŸ§á¶œáµ’ : {X : â„¤Functor} (U : CompactOpen X) â†’ â„¤Functor
  F-ob (âŸ¦_âŸ§á¶œáµ’ {X = X} U) A = (Î£[ x âˆˆ X .F-ob A .fst  ] U .N-ob A x â‰¡ D A 1r)
                                , isSetÎ£SndProp (X .F-ob A .snd) Î» _ â†’ squash/ _ _
   where instance _ = snd A
  F-hom (âŸ¦_âŸ§á¶œáµ’ {X = X} U) {x = A} {y = B} Ï† (x , Uxâ‰¡D1) = (X .F-hom Ï† x) , path
    where
    instance
      _ = A .snd
      _ = B .snd
    open IsLatticeHom
    path : U .N-ob B (X .F-hom Ï† x) â‰¡ D B 1r
    path = U .N-ob B (X .F-hom Ï† x)         â‰¡âŸ¨ funExtâ» (U .N-hom Ï†) x âŸ©
           ZarLatFun .F-hom Ï† (U .N-ob A x) â‰¡âŸ¨ cong (ZarLatFun .F-hom Ï†) Uxâ‰¡D1 âŸ©
           ZarLatFun .F-hom Ï† (D A 1r)      â‰¡âŸ¨ inducedZarLatHom Ï† .snd .pres1 âŸ©
           D B 1r âˆ
  F-id (âŸ¦_âŸ§á¶œáµ’ {X = X} U) = funExt (Î» x â†’ Î£â‰¡Prop (Î» _ â†’ squash/ _ _)
                                     (funExtâ» (X .F-id) (x .fst)))
  F-seq (âŸ¦_âŸ§á¶œáµ’ {X = X} U) Ï† Ïˆ = funExt (Î» x â†’ Î£â‰¡Prop (Î» _ â†’ squash/ _ _)
                                          (funExtâ» (X .F-seq Ï† Ïˆ) (x .fst)))


  isAffineCompactOpen : {X : â„¤Functor} (U : CompactOpen X) â†’ Type (â„“-suc â„“)
  isAffineCompactOpen U = isAffine âŸ¦ U âŸ§á¶œáµ’

  -- TODO: define basic opens D(f) â†ª Sp A and prove âŸ¦ D(f) âŸ§á¶œáµ’ â‰… Sp A[1/f]

  -- the (big) dist. lattice of compact opens
  CompOpenDistLattice : Functor â„¤FUNCTOR (DistLatticesCategory {â„“ = â„“-suc â„“} ^op)
  fst (F-ob CompOpenDistLattice X) = CompactOpen X

  -- lattice structure induce by internal lattice object ZarLatFun
  N-ob (DistLatticeStr.0l (snd (F-ob CompOpenDistLattice X))) A _ = 0l
    where instance _ = ZariskiLattice A .snd
  N-hom (DistLatticeStr.0l (snd (F-ob CompOpenDistLattice X))) _ = funExt Î» _ â†’ refl

  N-ob (DistLatticeStr.1l (snd (F-ob CompOpenDistLattice X))) A _ = 1l
    where instance _ = ZariskiLattice A .snd
  N-hom (DistLatticeStr.1l (snd (F-ob CompOpenDistLattice X))) {x = A} {y = B} Ï† = funExt Î» _ â†’ path
    where
    instance
      _ = A .snd
      _ = B .snd
      _ = ZariskiLattice B .snd
    path : D B 1r â‰¡ D B (Ï† .fst 1r) âˆ¨l 0l
    path = cong (D B) (sym (Ï† .snd .pres1)) âˆ™ sym (âˆ¨lRid _)

  N-ob ((snd (F-ob CompOpenDistLattice X) DistLatticeStr.âˆ¨l U) V) A x = U .N-ob A x âˆ¨l V .N-ob A x
    where instance _ = ZariskiLattice A .snd
  N-hom ((snd (F-ob CompOpenDistLattice X) DistLatticeStr.âˆ¨l U) V)  {x = A} {y = B} Ï† = funExt path
    where
    instance
      _ = ZariskiLattice A .snd
      _ = ZariskiLattice B .snd
    path : âˆ€ x â†’ U .N-ob B (X .F-hom Ï† x) âˆ¨l V .N-ob B (X .F-hom Ï† x)
               â‰¡ ZarLatFun .F-hom Ï† (U .N-ob A x âˆ¨l V .N-ob A x)
    path x = U .N-ob B (X .F-hom Ï† x) âˆ¨l V .N-ob B (X .F-hom Ï† x)
           â‰¡âŸ¨ congâ‚‚ _âˆ¨l_ (funExtâ» (U .N-hom Ï†) x) (funExtâ» (V .N-hom Ï†) x) âŸ©
             ZarLatFun .F-hom Ï† (U .N-ob A x) âˆ¨l ZarLatFun .F-hom Ï† (V .N-ob A x)
           â‰¡âŸ¨ sym (inducedZarLatHom Ï† .snd .presâˆ¨l _ _) âŸ©
             ZarLatFun .F-hom Ï† (U .N-ob A x âˆ¨l V .N-ob A x) âˆ

  N-ob ((snd (F-ob CompOpenDistLattice X) DistLatticeStr.âˆ§l U) V) A x = U .N-ob A x âˆ§l V .N-ob A x
    where instance _ = ZariskiLattice A .snd
  N-hom ((snd (F-ob CompOpenDistLattice X) DistLatticeStr.âˆ§l U) V)  {x = A} {y = B} Ï† = funExt path
    where
    instance
      _ = ZariskiLattice A .snd
      _ = ZariskiLattice B .snd
    path : âˆ€ x â†’ U .N-ob B (X .F-hom Ï† x) âˆ§l V .N-ob B (X .F-hom Ï† x)
               â‰¡ ZarLatFun .F-hom Ï† (U .N-ob A x âˆ§l V .N-ob A x)
    path x = U .N-ob B (X .F-hom Ï† x) âˆ§l V .N-ob B (X .F-hom Ï† x)
           â‰¡âŸ¨ congâ‚‚ _âˆ§l_ (funExtâ» (U .N-hom Ï†) x) (funExtâ» (V .N-hom Ï†) x) âŸ©
             ZarLatFun .F-hom Ï† (U .N-ob A x) âˆ§l ZarLatFun .F-hom Ï† (V .N-ob A x)
           â‰¡âŸ¨ sym (inducedZarLatHom Ï† .snd .presâˆ§l _ _) âŸ©
             ZarLatFun .F-hom Ï† (U .N-ob A x âˆ§l V .N-ob A x) âˆ

  DistLatticeStr.isDistLattice (snd (F-ob CompOpenDistLattice X)) = makeIsDistLatticeâˆ§lOverâˆ¨l
    isSetNatTrans
    (Î» _ _ _ â†’ makeNatTransPath (funExtâ‚‚
                 (Î» A _ â†’ ZariskiLattice A .snd .DistLatticeStr.âˆ¨lAssoc _ _ _)))
    (Î» _ â†’ makeNatTransPath (funExtâ‚‚ (Î» A _ â†’ ZariskiLattice A .snd .DistLatticeStr.âˆ¨lRid _)))
    (Î» _ _ â†’ makeNatTransPath (funExtâ‚‚ (Î» A _ â†’ ZariskiLattice A .snd .DistLatticeStr.âˆ¨lComm _ _)))
    (Î» _ _ _ â†’ makeNatTransPath (funExtâ‚‚
                 (Î» A _ â†’ ZariskiLattice A .snd .DistLatticeStr.âˆ§lAssoc _ _ _)))
    (Î» _ â†’ makeNatTransPath (funExtâ‚‚ (Î» A _ â†’ ZariskiLattice A .snd .DistLatticeStr.âˆ§lRid _)))
    (Î» _ _ â†’ makeNatTransPath (funExtâ‚‚ (Î» A _ â†’ ZariskiLattice A .snd .DistLatticeStr.âˆ§lComm _ _)))
    (Î» _ _ â†’ makeNatTransPath (funExtâ‚‚ -- don't know why âˆ§lAbsorbâˆ¨l doesn't work
               (Î» A _ â†’ ZariskiLattice A .snd .DistLatticeStr.absorb _ _ .snd)))
    (Î» _ _ _ â†’ makeNatTransPath (funExtâ‚‚ -- same here
                 (Î» A _ â†’ ZariskiLattice A .snd .DistLatticeStr.âˆ§l-dist-âˆ¨l _ _ _ .fst)))

  -- (contravariant) action on morphisms
  fst (F-hom CompOpenDistLattice Î±) = Î± â—áµ›_
  pres0 (snd (F-hom CompOpenDistLattice Î±)) = makeNatTransPath (funExtâ‚‚ Î» _ _ â†’ refl)
  pres1 (snd (F-hom CompOpenDistLattice Î±)) = makeNatTransPath (funExtâ‚‚ Î» _ _ â†’ refl)
  presâˆ¨l (snd (F-hom CompOpenDistLattice Î±)) _ _ = makeNatTransPath (funExtâ‚‚ Î» _ _ â†’ refl)
  presâˆ§l (snd (F-hom CompOpenDistLattice Î±)) _ _ = makeNatTransPath (funExtâ‚‚ Î» _ _ â†’ refl)

  -- functoriality
  F-id CompOpenDistLattice = LatticeHomâ‰¡f _ _
                               (funExt Î» _ â†’ makeNatTransPath (funExtâ‚‚ Î» _ _ â†’ refl))
  F-seq CompOpenDistLattice _ _ = LatticeHomâ‰¡f _ _
                                    (funExt Î» _ â†’ makeNatTransPath (funExtâ‚‚ Î» _ _ â†’ refl))


  module _ (X : â„¤Functor) where
    open isIso
    private instance _ = (CompOpenDistLattice .F-ob X) .snd

    compOpenTopNatIso : NatIso X âŸ¦ 1l âŸ§á¶œáµ’
    N-ob (trans compOpenTopNatIso) _ Ï† = Ï† , refl
    N-hom (trans compOpenTopNatIso) _ = funExt Î» _ â†’ Î£â‰¡Prop (Î» _ â†’ squash/ _ _) refl
    inv (nIso compOpenTopNatIso B) = fst
    sec (nIso compOpenTopNatIso B) = funExt Î» _ â†’ Î£â‰¡Prop (Î» _ â†’ squash/ _ _) refl
    ret (nIso compOpenTopNatIso B) = funExt Î» _ â†’ refl


  module _ (X : â„¤Functor) where
    open Join (CompOpenDistLattice .F-ob X)
    open JoinSemilattice (Latticeâ†’JoinSemilattice (DistLatticeâ†’Lattice (CompOpenDistLattice .F-ob X)))
    open PosetStr (IndPoset .snd) hiding (_â‰¤_)
    open LatticeTheory â¦ƒ...â¦„ -- ((DistLatticeâ†’Lattice (CompOpenDistLattice .F-ob X)))
    private instance _ = (CompOpenDistLattice .F-ob X) .snd

    record AffineCover : Type (â„“-suc â„“) where
      field
        n : â„•
        U : FinVec (CompactOpen X) n
        covers : â‹ U â‰¡ 1l -- TODO: equivalent to X â‰¡ âŸ¦ â‹ U âŸ§á¶œáµ’
        isAffineU : âˆ€ i â†’ isAffineCompactOpen (U i)

    hasAffineCover : Type (â„“-suc â„“)
    hasAffineCover = âˆ¥ AffineCover âˆ¥â‚

    -- the structure sheaf
    private COáµ’áµ– = (DistLatticeCategory (CompOpenDistLattice .F-ob X)) ^op

    compOpenIncl : {U V : CompactOpen X} â†’ V â‰¤ U â†’ âŸ¦ V âŸ§á¶œáµ’ â‡’ âŸ¦ U âŸ§á¶œáµ’
    N-ob (compOpenIncl {U = U} {V = V} Vâ‰¤U) A (x , Vxâ‰¡D1) = x , path
      where
      instance
        _ = A .snd
        _ = ZariskiLattice A .snd
        _ = DistLatticeâ†’Lattice (ZariskiLattice A)
      path : U .N-ob A x â‰¡ D A 1r
      path = U .N-ob A x                â‰¡âŸ¨ funExtâ» (funExtâ» (cong N-ob (sym Vâ‰¤U)) A) x âŸ©
             V .N-ob A x âˆ¨l U .N-ob A x â‰¡âŸ¨ cong (_âˆ¨l U .N-ob A x) Vxâ‰¡D1 âŸ©
             D A 1r âˆ¨l U .N-ob A x      â‰¡âŸ¨ 1lLeftAnnihilatesâˆ¨l _ âŸ©
             D A 1r âˆ
    N-hom (compOpenIncl Vâ‰¤U) Ï† = funExt Î» x â†’ Î£â‰¡Prop (Î» _ â†’ squash/ _ _) refl

    compOpenInclId : âˆ€ {U : CompactOpen X} â†’ compOpenIncl (is-refl U) â‰¡ idTrans âŸ¦ U âŸ§á¶œáµ’
    compOpenInclId = makeNatTransPath (funExtâ‚‚ (Î» _ _ â†’ Î£â‰¡Prop (Î» _ â†’ squash/ _ _) refl))

    compOpenInclSeq : âˆ€ {U V W : CompactOpen X} (Uâ‰¤V : U â‰¤ V) (Vâ‰¤W : V â‰¤ W)
                    â†’ compOpenIncl (is-trans _ _ _ Uâ‰¤V Vâ‰¤W)
                    â‰¡ compOpenIncl Uâ‰¤V â—áµ› compOpenIncl Vâ‰¤W
    compOpenInclSeq _ _ = makeNatTransPath
                            (funExtâ‚‚ (Î» _ _ â†’ Î£â‰¡Prop (Î» _ â†’ squash/ _ _) refl))

    strDLSh : Functor COáµ’áµ– (CommRingsCategory {â„“ = â„“-suc â„“})
    F-ob strDLSh  U = ğ“ .F-ob âŸ¦ U âŸ§á¶œáµ’
    F-hom strDLSh Uâ‰¥V = ğ“ .F-hom (compOpenIncl Uâ‰¥V)
    F-id strDLSh = cong (ğ“ .F-hom) compOpenInclId âˆ™ ğ“ .F-id
    F-seq strDLSh _ _ = cong (ğ“ .F-hom) (compOpenInclSeq _ _) âˆ™ ğ“ .F-seq _ _

  -- the canonical one element affine cover of a representable
  module _ (A : CommRing â„“) where
    open AffineCover
    private instance _ = (CompOpenDistLattice âŸ… Sp âŸ… A âŸ† âŸ†) .snd

    singlAffineCover : AffineCover (Sp .F-ob A)
    n singlAffineCover = 1
    U singlAffineCover zero = 1l
    covers singlAffineCover = âˆ¨lRid _
    isAffineU singlAffineCover zero = âˆ£ A , compOpenTopNatIso (Sp âŸ… A âŸ†) âˆ£â‚


  -- qcqs-schemes as Zariski sheaves (local â„¤-functors) with an affine cover in the sense above
  isLocal : â„¤Functor â†’ Type (â„“-suc â„“)
  isLocal X = isSheaf zariskiCoverage X

  isQcQsScheme : â„¤Functor â†’ Type (â„“-suc â„“)
  isQcQsScheme X = isLocal X Ã— hasAffineCover X

  -- affine schemes are qcqs-schemes
  isQcQsSchemeAffine : âˆ€ (A : CommRing â„“) â†’ isQcQsScheme (Sp .F-ob A)
  fst (isQcQsSchemeAffine A) = isSubcanonicalZariskiCoverage A
  snd (isQcQsSchemeAffine A) = âˆ£ singlAffineCover A âˆ£â‚
