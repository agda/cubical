{-# OPTIONS --safe #-}

module Cubical.Categories.Instances.Setoids where

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.Structure
open import Cubical.Foundations.HLevels
open import Cubical.Foundations.Equiv
open import Cubical.Foundations.Equiv.Properties
open import Cubical.Foundations.Isomorphism
open import Cubical.Foundations.Univalence
open import Cubical.Foundations.Transport hiding (pathToIso)
open import Cubical.Foundations.Function
open import Cubical.Foundations.Path
open import Cubical.Functions.FunExtEquiv
open import Cubical.Functions.Logic hiding (_â‡’_)
open import Cubical.Data.Unit
open import Cubical.Data.Maybe
open import Cubical.Data.Sigma
open import Cubical.Data.Bool
open import Cubical.Data.Empty as âŠ¥
open import Cubical.Categories.Category
open import Cubical.Categories.Monoidal.Enriched
open import Cubical.Categories.Morphism
open import Cubical.Categories.Functor
open import Cubical.Categories.NaturalTransformation
open import Cubical.Categories.Equivalence.WeakEquivalence
open import Cubical.Categories.Equivalence.AdjointEquivalence
open import Cubical.Categories.Adjoint
open import Cubical.Categories.Functors.HomFunctor
open import Cubical.Categories.Instances.Sets
open import Cubical.Categories.Constructions.BinProduct
open import Cubical.Categories.Constructions.Slice
open import Cubical.Categories.Constructions.FullSubcategory
open import Cubical.Categories.Instances.Functors
open import Cubical.Categories.Instances.Functors.Currying
open import Cubical.Relation.Binary
open import Cubical.Relation.Nullary
open import Cubical.Relation.Binary.Setoid

open import Cubical.Categories.Instances.Cat

open import Cubical.Categories.Monoidal

open import Cubical.Categories.Limits.Pullback
open import Cubical.Categories.Constructions.Slice.Functor

open import Cubical.HITs.SetQuotients as /
open import Cubical.HITs.PropositionalTruncation

open Category hiding (_âˆ˜_)
open Functor


module _ â„“ where
  SETOID : Category (â„“-max (â„“-suc â„“) (â„“-suc â„“)) (â„“-max â„“ â„“)
  ob SETOID = Setoid â„“ â„“
  Hom[_,_] SETOID = SetoidMor
  fst (id SETOID) _ = _
  snd (id SETOID) r = r
  fst ((SETOID â‹† _) _) = _
  snd ((SETOID â‹† (_ , f)) (_ , g)) = g âˆ˜ f
  â‹†IdL SETOID _ = refl
  â‹†IdR SETOID _ = refl
  â‹†Assoc SETOID _ _ _ = refl
  isSetHom SETOID {y = (_ , isSetB) , ((_ , isPropR) , _)} =
   isSetÎ£ (isSetâ†’ isSetB) (isPropâ†’isSet âˆ˜ isPropRespects isPropR )

  SETOID' : Category (â„“-max (â„“-suc â„“) (â„“-suc â„“)) (â„“-max â„“ â„“)
  SETOID' = Î£PropCat (Cat â„“ â„“) ((Î» C â†’ _ , isPropÃ— (isPropIsThin C) (isPropIsGroupoidCat C)) âˆ˜ fst)


  SETOIDâ†’SETOID' : Functor SETOID SETOID'
  F-ob SETOIDâ†’SETOID' ((X , isSetX) , ((_âˆ¼_ , isPropâˆ¼) , isEquivRelâˆ¼))  = (w , isSetX)
      , isPropâˆ¼ , Î» f â†’ isiso (symmetric _ _ f) (isPropâˆ¼ _ _ _ _) (isPropâˆ¼ _ _ _ _)
    where
    open BinaryRelation.isEquivRel isEquivRelâˆ¼
    w : (Category â„“ â„“)
    ob w = X
    Hom[_,_] w = _âˆ¼_
    id w = reflexive _
    _â‹†_ w = transitive'
    â‹†IdL w _ = isPropâˆ¼ _ _ _ _
    â‹†IdR w _ = isPropâˆ¼ _ _ _ _
    â‹†Assoc w _ _ _ = isPropâˆ¼ _ _ _ _
    isSetHom w = isPropâ†’isSet (isPropâˆ¼ _ _)

  F-hom SETOIDâ†’SETOID' {y = (_ , ((_ , isPropâˆ¼') , _))} (f , f-resp) = w
    where
    w : Functor _ _
    F-ob w = f
    F-hom w = f-resp
    F-id w = isPropâˆ¼' _ _ _ _
    F-seq w _ _ = isPropâˆ¼' _ _ _ _
  F-id SETOIDâ†’SETOID' = Functorâ‰¡ (Î» _ â†’ refl) Î» _ â†’ refl
  F-seq SETOIDâ†’SETOID' _ _ = Functorâ‰¡ (Î» _ â†’ refl) Î» _ â†’ refl

  SETOID'â†’SETOID : Functor SETOID' SETOID
  F-ob SETOID'â†’SETOID ((C , isSetCob) , thin , isGrpCat) =
    (_ , isSetCob) , (C [_,_] , thin) ,
      BinaryRelation.equivRel (Î» _ â†’ C .id) (Î» _ _ â†’ isIso.inv âˆ˜ isGrpCat)
        Î» _ _ _ â†’ C ._â‹†_

  F-hom SETOID'â†’SETOID F = _ , F-hom F
  F-id SETOID'â†’SETOID = refl
  F-seq SETOID'â†’SETOID _ _ = refl

  open Iso

  IsoPathCatIsoSETOID : âˆ€ {x y} â†’ Iso (x â‰¡ y) (CatIso SETOID x y)
  fun (IsoPathCatIsoSETOID) = pathToIso
  inv (IsoPathCatIsoSETOID {y = _ , ((y , _) , _) }) ((_ , r) , ci) =
    congâ‚‚ _,_
     (TypeOfHLevelâ‰¡ 2 (isoToPath i))
     (toPathP (EquivPropRelâ‰¡
       ( substRel y ((cong fst c.sec â‰¡$ _) âˆ™_ âˆ˜ transportRefl) âˆ˜ r
       , snd c.inv âˆ˜ substRel y (sym âˆ˜ transportRefl))
       ))
   where
   module c = isIso ci
   i : Iso _ _
   fun i = _ ; inv i = fst c.inv
   rightInv i = cong fst c.sec â‰¡$_ ; leftInv i = cong fst c.ret â‰¡$_

  rightInv (IsoPathCatIsoSETOID {x = x} {y = y}) ((f , _) , _) =
    CatIsoâ‰¡ _ _ (SetoidMorâ‰¡ x y
       (funExt Î» _ â†’ transportRefl _ âˆ™ cong f (transportRefl _)))
  leftInv (IsoPathCatIsoSETOID) a =
    Î£SquareSet (Î» _ â†’ isSetEquivPropRel)
      (TypeOfHLevelPathâ‰¡ 2 (Î» _ â†’
       transportRefl _ âˆ™ cong (subst (fst âˆ˜ fst) a) (transportRefl _)))

  isUnivalentSETOID : isUnivalent SETOID
  isUnivalent.univ isUnivalentSETOID _ _ =
   isoToIsEquiv IsoPathCatIsoSETOID


  Quot Forget : Functor SETOID (SET â„“)
  IdRel FullRel : Functor (SET â„“) SETOID


  F-ob Quot (_ , ((R , _) , _)) = (_ / R) , squash/
  F-hom Quot (f , r) = /.rec squash/ ([_] âˆ˜  f) Î» _ _ â†’ eq/ _ _ âˆ˜ r
  F-id Quot = funExt (/.elim (Î» _ â†’ isPropâ†’isSet (squash/ _ _))
    (Î» _ â†’ refl) Î» _ _ _ _ â†’ refl)
  F-seq Quot _ _ = funExt (/.elim (Î» _ â†’ isPropâ†’isSet (squash/ _ _))
    (Î» _ â†’ refl) Î» _ _ _ _ â†’ refl)

  F-ob IdRel A = A , (_ , snd A) , isEquivRelIdRel
  F-hom IdRel = _, cong _
  F-id IdRel = refl
  F-seq IdRel _ _ = refl

  F-ob Forget = fst
  F-hom Forget = fst
  F-id Forget = refl
  F-seq Forget _ _ = refl

  F-ob FullRel = _, fullEquivPropRel
  F-hom FullRel = _, _
  F-id FullRel = refl
  F-seq FullRel _ _ = refl

  isFullyFaithfulIdRel : isFullyFaithful IdRel
  isFullyFaithfulIdRel x y = isoToIsEquiv
    (iso _ fst
     (Î» _ â†’ SetoidMorâ‰¡ (IdRel âŸ… x âŸ†) (IdRel âŸ… y âŸ†) refl)
      Î» _ â†’ refl)

  isFullyFaithfulFullRel : isFullyFaithful FullRel
  isFullyFaithfulFullRel x y = isoToIsEquiv
    (iso _ fst (Î» _ â†’ refl) Î» _ â†’ refl)

  IdRelâ‡’FullRel : IdRel â‡’ FullRel
  NatTrans.N-ob IdRelâ‡’FullRel x = idfun _ , _
  NatTrans.N-hom IdRelâ‡’FullRel f = refl


  open Cubical.Categories.Adjoint.NaturalBijection
  open _âŠ£_

  QuotâŠ£IdRel : Quot âŠ£ IdRel
  adjIso QuotâŠ£IdRel {d = (_ , isSetD)} = setQuotUniversalIso isSetD
  adjNatInD QuotâŠ£IdRel {c = c} {d' = d'} f k = SetoidMorâ‰¡ c (IdRel âŸ… d' âŸ†) refl
  adjNatInC QuotâŠ£IdRel {d = d} g h =
   funExt (/.elimProp (Î» _ â†’ snd d _ _) Î» _ â†’ refl)

  IdRelâŠ£Forget : IdRel âŠ£ Forget
  fun (adjIso IdRelâŠ£Forget) = fst
  inv (adjIso IdRelâŠ£Forget {d = d}) f =
    f , J (Î» x' p â†’ fst (fst (snd d)) (f _) (f x'))
      (BinaryRelation.isEquivRel.reflexive (snd (snd d)) (f _))
  rightInv (adjIso IdRelâŠ£Forget) _ = refl
  leftInv (adjIso IdRelâŠ£Forget {c = c} {d = d}) _ =
    SetoidMorâ‰¡ (IdRel âŸ… c âŸ†) d refl
  adjNatInD IdRelâŠ£Forget _ _ = refl
  adjNatInC IdRelâŠ£Forget _ _ = refl

  ForgetâŠ£FullRel : Forget âŠ£ FullRel
  fun (adjIso ForgetâŠ£FullRel) = _
  inv (adjIso ForgetâŠ£FullRel) = fst
  rightInv (adjIso ForgetâŠ£FullRel) _ = refl
  leftInv (adjIso ForgetâŠ£FullRel) _ = refl
  adjNatInD ForgetâŠ£FullRel _ _ = refl
  adjNatInC ForgetâŠ£FullRel _ _ = refl


  pieces : Functor SETOID SETOID
  pieces = IdRel âˆ˜F Quot

  points : Functor SETOID SETOID
  points = IdRel âˆ˜F Forget

  piecesâŠ£points : pieces âŠ£ points
  piecesâŠ£points = Compose.LFâŠ£GR QuotâŠ£IdRel IdRelâŠ£Forget

  pointsâ†’pieces : points â‡’ pieces
  pointsâ†’pieces =
      Îµ (adj'â†’adj _ _ IdRelâŠ£Forget)
   â—áµ› Î· (adj'â†’adj _ _ QuotâŠ£IdRel)
   where open UnitCounit._âŠ£_

  piecesHavePoints : âˆ€ A â†’
    isEpic SETOID {points âŸ… A âŸ† } {pieces âŸ… A âŸ†}
     (pointsâ†’pieces âŸ¦ A âŸ§)
  piecesHavePoints  A {z = z@((_ , isSetZ) , _) } =
    SetoidMorâ‰¡ (pieces âŸ… A âŸ†) z âˆ˜
      (funExt âˆ˜ /.elimProp (Î» _ â†’ isSetZ _ _) âˆ˜ funExtâ» âˆ˜ cong fst)

  piecesâ†’â‰ƒâ†’points : (A B : Setoid â„“ â„“) â†’
         SetoidMor (pieces âŸ… A âŸ†) B
       â‰ƒ SetoidMor A (points âŸ… B âŸ†)
  piecesâ†’â‰ƒâ†’points A B =
     NaturalBijection._âŠ£_.adjEquiv
       (piecesâŠ£points)
       {c = A} {d = B}

  -âŠ—- : Functor (SETOID Ã—C SETOID) SETOID
  F-ob -âŠ—- = uncurry _âŠ—_
  fst (F-hom -âŠ—- _) = _
  snd (F-hom -âŠ—- (f , g)) (x , y) = snd f x , snd g y
  F-id -âŠ—- = refl
  F-seq -âŠ—- _ _ = refl

  InternalHomFunctor : Functor (SETOID ^op Ã—C SETOID) SETOID
  F-ob InternalHomFunctor = uncurry _âŸ¶_
  fst (F-hom InternalHomFunctor (f , g )) (_ , y) = _ , snd g âˆ˜ y âˆ˜ (snd f)
  snd (F-hom InternalHomFunctor (f , g)) q = snd g âˆ˜ q âˆ˜ fst f
  F-id InternalHomFunctor = refl
  F-seq InternalHomFunctor _ _ = refl

  -^_ : âˆ€ X â†’ Functor SETOID SETOID
  -^_ X = Î»F SETOID _ (SETOID ^op) InternalHomFunctor âŸ… X âŸ†

  -âŠ—_ : âˆ€ X â†’ Functor SETOID SETOID
  -âŠ—_ X = Î»F SETOID _ (SETOID) (-âŠ—- âˆ˜F fst (Swap SETOID SETOID)) âŸ… X âŸ†

  âŠ—âŠ£^ : âˆ€ X â†’ (-âŠ— X) âŠ£ (-^ X)
  adjIso (âŠ—âŠ£^ X) {A} {B} = setoidCurryIso X A B
  adjNatInD (âŠ—âŠ£^ X) {A} {d' = C} _ _ = SetoidMorâ‰¡ A (X âŸ¶ C) refl
  adjNatInC (âŠ—âŠ£^ X) {A} {d = C} _ _ = SetoidMorâ‰¡ (A âŠ— X) C refl


  -- works but slow!
  SetoidsMonoidalStrÎ± :
      -âŠ—- âˆ˜F (ğŸ™âŸ¨ SETOID âŸ© Ã—F -âŠ—-) â‰…á¶œ
      -âŠ—- âˆ˜F (-âŠ—- Ã—F ğŸ™âŸ¨ SETOID âŸ©) âˆ˜F Ã—C-assoc SETOID SETOID SETOID
  NatTrans.N-ob (NatIso.trans SetoidsMonoidalStrÎ±) _ =
    invEq Î£-assoc-â‰ƒ , invEq Î£-assoc-â‰ƒ
  NatTrans.N-hom (NatIso.trans SetoidsMonoidalStrÎ±) {x} {y} _ =
    SetoidMorâ‰¡
     (F-ob (-âŠ—- âˆ˜F (ğŸ™âŸ¨ SETOID âŸ© Ã—F -âŠ—-)) x)
      ((-âŠ—- âˆ˜F (-âŠ—- Ã—F ğŸ™âŸ¨ SETOID âŸ©) âˆ˜F Ã—C-assoc SETOID SETOID SETOID)
       .F-ob y)
     refl
  isIso.inv (NatIso.nIso SetoidsMonoidalStrÎ± _) =
    fst Î£-assoc-â‰ƒ , fst Î£-assoc-â‰ƒ
  isIso.sec (NatIso.nIso SetoidsMonoidalStrÎ± x) = refl
  isIso.ret (NatIso.nIso SetoidsMonoidalStrÎ± x) = refl

  SetoidsMonoidalStrÎ· : -âŠ—- âˆ˜F rinj SETOID SETOID setoidUnit â‰…á¶œ ğŸ™âŸ¨ SETOID âŸ©
  NatIso.trans SetoidsMonoidalStrÎ· =
    natTrans (Î» _ â†’ Iso.fun lUnit*Ã—Iso , Iso.fun lUnit*Ã—Iso)
             Î» {x} {y} _ â†’
              SetoidMorâ‰¡ (F-ob (-âŠ—- âˆ˜F rinj SETOID SETOID setoidUnit) x) y refl
  NatIso.nIso SetoidsMonoidalStrÎ· x =
   isiso (Iso.inv lUnit*Ã—Iso , Iso.inv lUnit*Ã—Iso) refl refl

  SetoidsMonoidalStrÏ :  -âŠ—- âˆ˜F linj SETOID SETOID setoidUnit â‰…á¶œ ğŸ™âŸ¨ SETOID âŸ©
  NatIso.trans SetoidsMonoidalStrÏ =
    natTrans (Î» _ â†’ Iso.fun rUnit*Ã—Iso , Iso.fun rUnit*Ã—Iso)
             Î» {x} {y} _ â†’
              SetoidMorâ‰¡ (F-ob (-âŠ—- âˆ˜F linj SETOID SETOID setoidUnit) x) y refl
  NatIso.nIso SetoidsMonoidalStrÏ x =
   isiso (Iso.inv rUnit*Ã—Iso , Iso.inv rUnit*Ã—Iso) refl refl


  SetoidsMonoidalStr : MonoidalStr SETOID
  TensorStr.â”€âŠ—â”€ (MonoidalStr.tenstr SetoidsMonoidalStr) = -âŠ—-
  TensorStr.unit (MonoidalStr.tenstr SetoidsMonoidalStr) = setoidUnit
  MonoidalStr.Î± SetoidsMonoidalStr = SetoidsMonoidalStrÎ±
  MonoidalStr.Î· SetoidsMonoidalStr = SetoidsMonoidalStrÎ·
  MonoidalStr.Ï SetoidsMonoidalStr = SetoidsMonoidalStrÏ
  MonoidalStr.pentagon SetoidsMonoidalStr w x y z = refl
  MonoidalStr.triangle SetoidsMonoidalStr x y = refl

  pullbacks : Pullbacks SETOID
  pullbacks cspn = w
   where
   open Cospan cspn
   open Pullback
   w : Pullback (SETOID) cspn
   pbOb w = PullbackSetoid l r m sâ‚ sâ‚‚
   pbPrâ‚ w = fst âˆ˜ fst , fst
   pbPrâ‚‚ w = snd âˆ˜ fst , snd
   pbCommutes w = SetoidMorâ‰¡ (PullbackSetoid l r m sâ‚ sâ‚‚) m (funExt snd)
   fst (fst (univProp w h k H')) = (Î» x â†’ (fst h x , fst k x) ,
     (cong fst H' â‰¡$ x)) ,
      Î» {a} {b} x â†’ (snd h x) , (snd k x)
   snd (fst (univProp w {d} h k H')) = SetoidMorâ‰¡ d l refl , SetoidMorâ‰¡ d r refl
   snd (univProp w {d} h k H') y = Î£â‰¡Prop
     (Î» _ â†’ isPropÃ— (isSetHom (SETOID) {d} {l} _ _)
                    (isSetHom (SETOID) {d} {r} _ _))
    (SetoidMorâ‰¡ d (PullbackSetoid l r m sâ‚ sâ‚‚)
     (funExt Î» x â†’ Î£â‰¡Prop (Î» _ â†’ snd (fst m) _ _)
             (congâ‚‚ _,_ (Î» i â†’ fst (fst (snd y) i) x)
                        (Î» i â†’ fst (snd (snd y) i) x))))


  open WeakEquivalence
  open isWeakEquivalence


  -- SET is subcategory of SETOID in two ways:

  --  1. As as subcategory of SETOIDs with FullRelations

  module FullRelationsSubcategory = FullSubcategory SETOID
    (BinaryRelation.isFull âˆ˜ EquivPropRelâ†’Rel âˆ˜ snd)


  FullRelationsSubcategory : Category _ _
  FullRelationsSubcategory = FullRelationsSubcategory.FullSubcategory

  FullRelationsSubcategoryâ‰…SET : WeakEquivalence FullRelationsSubcategory (SET â„“)
  func FullRelationsSubcategoryâ‰…SET = Forget âˆ˜F FullRelationsSubcategory.FullInclusion
  fullfaith (isWeakEquiv FullRelationsSubcategoryâ‰…SET) x y@(_ , is-full-rel) =
     isoToIsEquiv (iso _ (_, Î» _ â†’ is-full-rel _ _) (Î» _ â†’ refl)
       Î» _ â†’ SetoidMorâ‰¡ (fst x) (fst y) refl)
  esssurj (isWeakEquiv FullRelationsSubcategoryâ‰…SET) d =
    âˆ£ (FullRel âŸ… d âŸ† , _)  , idCatIso âˆ£â‚

  --  2. As as subcategory of SETOIDs with Identity relations

  module IdRelationsSubcategory = FullSubcategory SETOID
    (BinaryRelation.impliesIdentity âˆ˜ EquivPropRelâ†’Rel âˆ˜ snd)

  IdRelationsSubcategory : Category _ _
  IdRelationsSubcategory = IdRelationsSubcategory.FullSubcategory

  IdRelationsSubcategoryâ‰…SET : WeakEquivalence IdRelationsSubcategory (SET â„“)
  func IdRelationsSubcategoryâ‰…SET = Forget âˆ˜F IdRelationsSubcategory.FullInclusion
  fullfaith (isWeakEquiv IdRelationsSubcategoryâ‰…SET)
    x@(_ , implies-id) y@((_ , ((rel , _) , is-equiv-rel)) , _) =
     isoToIsEquiv (iso _ (Î» f â†’ f , Î» z â†’
      isReflâ†’impliedByIdentity rel reflexive (cong f (implies-id z))) (Î» _ â†’ refl)
       Î» _ â†’ SetoidMorâ‰¡ (fst x) (fst y) refl)
   where open BinaryRelation ; open isEquivRel is-equiv-rel

  esssurj (isWeakEquiv IdRelationsSubcategoryâ‰…SET) d =
    âˆ£ (IdRel âŸ… d âŸ† , idfun _)  , idCatIso âˆ£â‚


  -- base change functor does not have right adjoint (so SETOID cannot be LCCC)
  -- implementation of `Setoids are not an LCCC` by Thorsten Altenkirch and Nicolai Kraus
  -- (https://www.cs.nott.ac.uk/~psznk/docs/setoids.pdf)

  open BaseChange pullbacks public


  Â¬BaseChangeâŠ£SetoidÎ  : ({X Y : ob SETOID} (f : SETOID .Hom[_,_] X Y) â†’
     Î£ (Functor (SliceCat SETOID X) (SliceCat SETOID Y))
      (Î» Î f â†’ (_ï¼Š {c = X} {d = Y} f) âŠ£ Î f)) â†’ âŠ¥.âŠ¥
  Â¬BaseChangeâŠ£SetoidÎ  isLCCC = Î ob-full-rel Î ob-full

   where

   ğ•€ : Setoid â„“ â„“
   ğ•€ = (Lift Bool , isOfHLevelLift 2 isSetBool) , fullEquivPropRel

   ğŸš : Setoid â„“ â„“
   ğŸš = (Lift Bool , isOfHLevelLift 2 isSetBool) ,
         ((_ , isOfHLevelLift 2 isSetBool) , isEquivRelIdRel)

   ğ‘¨ : SetoidMor (setoidUnit {â„“} {â„“}) ğ•€
   ğ‘¨ = (Î» _ â†’ lift true) , Î» _ â†’ _

   ğ‘¨ï¼Š = _ï¼Š {c = (setoidUnit {â„“} {â„“})} {ğ•€} ğ‘¨

   ğŸš/ = sliceob {S-ob = ğŸš} ((Î» _ â†’ tt*) , Î» {x} {x'} _ â†’ tt*)


   open Î£ (isLCCC {(setoidUnit {â„“} {â„“})} {ğ•€} ğ‘¨) renaming (fst to Î A ; snd to Î âŠ£A*)
   open _âŠ£_ Î âŠ£A* renaming (adjIso to aIso)

   module lem2 where
    G = sliceob {S-ob = setoidUnit} ((Î» x â†’ lift false) , _)

    bcf =  ğ‘¨ï¼Š âŸ… G âŸ†

    isPropFiberFalse : isProp (fiber (fst (S-arr (Î A âŸ… ğŸš/ âŸ†))) (lift false))
    isPropFiberFalse (x , p) (y , q) =
      Î£â‰¡Prop (Î» _ _ _ â†’ cong (cong lift) (isSetBool _ _ _ _))
       ((cong (fst âˆ˜ S-hom) (isoInvInjective (aIso {G} {ğŸš/})
          (slicehom
            ((Î» _ â†’ x) , Î» _ â†’ BinaryRelation.isEquivRel.reflexive (snd (snd
                (S-ob (F-ob Î A ğŸš/)))) _)
                 ( SetoidMorâ‰¡ (S-ob G) ğ•€ (funExt Î» _ â†’ p)))
          ((slicehom
            ((Î» _ â†’ y) , Î» _ â†’ BinaryRelation.isEquivRel.reflexive (snd (snd
                (S-ob (F-ob Î A ğŸš/)))) _)
                 ( SetoidMorâ‰¡ (S-ob G) ğ•€ (funExt Î» _ â†’ q))))
          (SliceHom-â‰¡-intro' _ _
             (SetoidMorâ‰¡ (bcf .S-ob) (ğŸš/ .S-ob)
               (funExt Î» z â†’ âŠ¥.rec (trueâ‰¢false (cong lower (snd z)))
               ))))) â‰¡$ _ )


   module lem3 where
    G = sliceob {S-ob = ğ•€} (SETOID .id {ğ•€})

    aL : Iso
           (fst (fst (S-ob ğŸš/)))
           (SliceHom SETOID setoidUnit ( ğ‘¨ ï¼Š âŸ… G âŸ†) ğŸš/)

    fun aL h =
      slicehom ((Î» _ â†’ h)
        , Î» _ â†’ BinaryRelation.isEquivRel.reflexive (snd (snd
                (S-ob ğŸš/))) _) refl
    inv aL (slicehom (f , _) _) = f (_ , refl)
    rightInv aL b =
      SliceHom-â‰¡-intro' _ _
       (SetoidMorâ‰¡
      ((ğ‘¨ï¼Š âŸ… G âŸ†)  .S-ob)
      (ğŸš/ .S-ob) (funExt Î» x' â†’
         cong (Î» (x , y) â†’ fst (S-hom b) ((tt* , x) , y))
           (isPropSingl _ _)))
    leftInv aL _ = refl

    lem3 : Iso (fst (fst (S-ob ğŸš/))) (SliceHom SETOID ğ•€ G (Î A âŸ… ğŸš/ âŸ†))
    lem3 = compIso aL (aIso {G} {ğŸš/})



   module zzz3 = Iso (compIso LiftIso (lem3.lem3))


   open BinaryRelation.isEquivRel (snd (snd (S-ob (Î A âŸ… ğŸš/ âŸ†))))


   piPt : Bool â†’ fiber
                    (fst
                     (S-arr
                      (Î A âŸ… ğŸš/ âŸ†))) (lift true)
   piPt b =  (fst (S-hom (zzz3.fun b)) (lift true)) ,
     (cong fst (S-comm (zzz3.fun b)) â‰¡$ lift true)



   finLLem : fst (piPt true) â‰¡ fst (piPt false)
              â†’ âŠ¥.âŠ¥
   finLLem p =
     trueâ‰¢false (isoFunInjective (compIso LiftIso (lem3.lem3)) _ _
           $ SliceHom-â‰¡-intro' _ _
             $ SetoidMorâ‰¡
              ((lem3.G) .S-ob)
              ((Î A âŸ… ğŸš/ âŸ†) .S-ob)
              (funExt (
          Î» where (lift false) â†’ (congS fst (lem2.isPropFiberFalse
                        (_ , ((cong fst (S-comm (fun (lem3.lem3) (lift true))) â‰¡$ lift false)))
                        (_ , (cong fst (S-comm (fun (lem3.lem3) (lift false))) â‰¡$ lift false))))
                  (lift true) â†’ p)))


   Î ob-full : fst (fst (snd (S-ob (Î A âŸ… ğŸš/ âŸ†))))
                      (fst (piPt false))
                      (fst (piPt true))

   Î ob-full =
      ((transitive' ((snd (S-hom (zzz3.fun false)) {lift true} {lift false} _))
            (transitive'
              ((BinaryRelation.isReflâ†’impliedByIdentity
                    (fst (fst (snd (S-ob (Î A âŸ… ğŸš/ âŸ†))))) reflexive
                      (congS fst (lem2.isPropFiberFalse
                        (_ , ((cong fst (S-comm (fun (lem3.lem3) (lift false))) â‰¡$ lift false)))
                        (_ , (cong fst (S-comm (fun (lem3.lem3) (lift true))) â‰¡$ lift false))))
                        ))
              (snd (S-hom (zzz3.fun true)) {lift false} {lift true}  _))))

   Î ob-full-rel : fst (fst (snd (S-ob (Î A âŸ… ğŸš/ âŸ†))))
                      (fst (piPt false))
                      (fst (piPt true))
      â†’ âŠ¥.âŠ¥
   Î ob-full-rel rr = elimğŸš<fromIso ((invIso
           (compIso aL (aIso {sliceob ((Î» _ â†’ lift true) , _)} {ğŸš/}))))
          mT mF mMix
         (finLLem âˆ˜S cong Î» x â†’ fst (S-hom x) (lift false))
         (finLLem âˆ˜S cong Î» x â†’ fst (S-hom x) (lift false))
         (finLLem âˆ˜S (sym âˆ˜S cong Î» x â†’ fst (S-hom x) (lift true)))

     where

     aL : Iso Bool
        ((SliceHom SETOID setoidUnit _  ğŸš/))
     fun aL b = slicehom ((Î» _ â†’ lift b) , Î» _ â†’ refl) refl
     inv aL (slicehom f _) = lower (fst f ((_ , lift true) , refl ))
     rightInv aL b = SliceHom-â‰¡-intro' _ _
        (SetoidMorâ‰¡
          (S-ob ((ğ‘¨ï¼Š)
            âŸ… sliceob {S-ob = ğ•€} ((Î» _ â†’ lift true) , _) âŸ†))  ğŸš
              (funExt Î» x â†’ snd (S-hom b) {(_ , lift true) , refl} {x} _))
     leftInv aL _ = refl

     mB : Bool â†’ (SliceHom SETOID ğ•€
                 (sliceob {S-ob = ğ•€} ((Î» _ â†’ lift true) , (Î» {x} {x'} _ â†’ tt*))) (Î A âŸ… ğŸš/ âŸ†))
     mB b = slicehom ((Î» _ â†’ fst (piPt b)) ,
            Î» _ â†’ BinaryRelation.isEquivRel.reflexive (snd (snd (S-ob (Î A âŸ… ğŸš/ âŸ†)))) _)
          (Î£PathP ((funExt Î» _ â†’ snd (piPt b)) , refl) )


     mF mT mMix : _
     mF = mB false
     mT = mB true
     mMix = slicehom ((fst âˆ˜ piPt) âˆ˜ lower ,
         Î» where {lift false} {lift false} _ â†’ reflexive _
                 {lift true} {lift true} _ â†’ reflexive _
                 {lift false} {lift true} _ â†’ rr
                 {lift true} {lift false} _ â†’ symmetric _ _ rr)
                      ((Î£PathP ((funExt Î» b â†’ snd (piPt (lower b))) , refl) ))

