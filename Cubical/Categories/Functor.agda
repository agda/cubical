{-# OPTIONS --cubical --no-import-sorts --safe #-}

module Cubical.Categories.Functor where

open import Cubical.Foundations.Prelude
open import Cubical.Data.Sigma
open import Cubical.Categories.Category

private
  variable
    â„“C â„“C' â„“D â„“D' : Level

record Functor (C : Precategory â„“C â„“C') (D : Precategory â„“D â„“D') : Type (â„“-max (â„“-max â„“C â„“C') (â„“-max â„“D â„“D')) where
  no-eta-equality
  open Precategory

  field
    F-ob : C .ob â†’ D .ob
    F-hom : {x y : C .ob} â†’ C [ x , y ] â†’ D [(F-ob x) , (F-ob y)]
    F-id : {x : C .ob} â†’ F-hom (C .id x) â‰¡ D .id (F-ob x)
    F-seq : {x y z : C .ob} (f : C [ x , y ]) (g : C [ y , z ]) â†’ F-hom (f â‹†âŸ¨ C âŸ© g) â‰¡ (F-hom f) â‹†âŸ¨ D âŸ© (F-hom g)

  isFull = (x y : _) (F[f] : D [(F-ob x) , (F-ob y)]) â†’ âˆƒ (C [ x , y ]) (Î» f â†’ F-hom f â‰¡ F[f])
  isFaithful = (x y : _) (f g : C [ x , y ]) â†’ F-hom f â‰¡ F-hom g â†’ f â‰¡ g

private
  variable
    â„“ â„“' : Level
    â„“E â„“E' : Level
    C : Precategory â„“C â„“C'
    D : Precategory â„“D â„“D'
    E : Precategory â„“E â„“E'

open Precategory
open Functor

-- Helpful notation

-- action on objects
infix 30 _âŸ…_âŸ†
_âŸ…_âŸ† : (F : Functor C D)
     â†’ C .ob
     â†’ D .ob
_âŸ…_âŸ† = F-ob

-- action on morphisms
infix 30 _âŸª_âŸ« -- same infix level as on objects since these will never be used in the same context
_âŸª_âŸ« : (F : Functor C D)
     â†’ âˆ€ {x y}
     â†’ C [ x , y ]
     â†’ D [(F âŸ… x âŸ†) , (F âŸ… y âŸ†)]
_âŸª_âŸ« = F-hom


-- Functor constructions

ğŸ™âŸ¨_âŸ© : âˆ€ (C : Precategory â„“ â„“') â†’ Functor C C
ğŸ™âŸ¨ C âŸ© .F-ob x = x
ğŸ™âŸ¨ C âŸ© .F-hom f = f
ğŸ™âŸ¨ C âŸ© .F-id = refl
ğŸ™âŸ¨ C âŸ© .F-seq _ _ = refl

-- functor composition
funcComp : âˆ€ (G : Functor D E) (F : Functor C D) â†’ Functor C E
(funcComp G F) .F-ob c = G âŸ… F âŸ… c âŸ† âŸ†
(funcComp G F) .F-hom f = G âŸª F âŸª f âŸ« âŸ«
(funcComp {D = D} {E = E} {C = C} G F) .F-id {c}
  = (G âŸª F âŸª C .id c âŸ« âŸ«)
  â‰¡âŸ¨ cong (G âŸª_âŸ«) (F .F-id) âŸ©
    (G âŸª D .id (F âŸ… c âŸ†) âŸ«)
  â‰¡âŸ¨ G .F-id âŸ©
    E .id (G âŸ… F âŸ… c âŸ† âŸ†)
  âˆ
(funcComp {D = D} {E = E} {C = C} G F) .F-seq {x} {y} {z} f g
  = (G âŸª F âŸª f â‹†âŸ¨ C âŸ© g âŸ« âŸ«)
  â‰¡âŸ¨ cong (G âŸª_âŸ«) (F .F-seq _ _) âŸ©
    (G âŸª (F âŸª f âŸ«) â‹†âŸ¨ D âŸ© (F âŸª g âŸ«) âŸ«)
  â‰¡âŸ¨ G .F-seq _ _ âŸ©
    (G âŸª F âŸª f âŸ« âŸ«) â‹†âŸ¨ E âŸ© (G âŸª F âŸª g âŸ« âŸ«)
  âˆ

infixr 30 funcComp
syntax funcComp G F = G âˆ˜F F

-- reasoning about path connected functors
Ï€ObF : {F F' : Functor C D} â†’ F â‰¡ F' â†’ (F .F-ob) â‰¡ (F' .F-ob)
Ï€ObF p i = F-ob (p i)


-- composition is associative

-- Results about functors

module _ {C : Precategory â„“C â„“C'} {D : Precategory â„“D â„“D'} {F : Functor C D} where

  -- functors preserve commutative diagrams (specificallysqures here)
  preserveCommF : âˆ€ {x y z w} {f : C [ x , y ]} {g : C [ y , w ]} {h : C [ x , z ]} {k : C [ z , w ]}
                â†’ f â‹†âŸ¨ C âŸ© g â‰¡ h â‹†âŸ¨ C âŸ© k
                â†’ (F âŸª f âŸ«) â‹†âŸ¨ D âŸ© (F âŸª g âŸ«) â‰¡ (F âŸª h âŸ«) â‹†âŸ¨ D âŸ© (F âŸª k âŸ«)
  preserveCommF {f = f} {g = g} {h = h} {k = k} eq
    = (F âŸª f âŸ«) â‹†âŸ¨ D âŸ© (F âŸª g âŸ«)
    â‰¡âŸ¨ sym (F .F-seq _ _) âŸ©
      F âŸª f â‹†âŸ¨ C âŸ© g âŸ«
    â‰¡âŸ¨ cong (F âŸª_âŸ«) eq âŸ©
      F âŸª h â‹†âŸ¨ C âŸ© k âŸ«
    â‰¡âŸ¨ F .F-seq _ _ âŸ©
      (F âŸª h âŸ«) â‹†âŸ¨ D âŸ© (F âŸª k âŸ«)
    âˆ

  -- functors preserve isomorphisms
  preserveIsosF : âˆ€ {x y} â†’ CatIso {C = C} x y â†’ CatIso {C = D} (F âŸ… x âŸ†) (F âŸ… y âŸ†)
  preserveIsosF {x} {y} (catiso f fâ»Â¹ sec' ret') =
    catiso
      g gâ»Â¹
      -- sec
      ( (gâ»Â¹ â‹†âŸ¨ D âŸ© g)
      â‰¡âŸ¨ sym (F .F-seq fâ»Â¹ f) âŸ©
        F âŸª fâ»Â¹ â‹†âŸ¨ C âŸ© f âŸ«
      â‰¡âŸ¨ cong (F .F-hom) sec' âŸ©
        F âŸª C .id y âŸ«
      â‰¡âŸ¨ F .F-id âŸ©
        D .id y'
      âˆ )
      -- ret
      ( (g â‹†âŸ¨ D âŸ© gâ»Â¹)
        â‰¡âŸ¨ sym (F .F-seq f fâ»Â¹) âŸ©
      F âŸª f â‹†âŸ¨ C âŸ© fâ»Â¹ âŸ«
        â‰¡âŸ¨ cong (F .F-hom) ret' âŸ©
      F âŸª C .id x âŸ«
      â‰¡âŸ¨ F .F-id âŸ©
        D .id x'
      âˆ )

      where
        x' : D .ob
        x' = F âŸ… x âŸ†
        y' : D .ob
        y' = F âŸ… y âŸ†

        g : D [ x' , y' ]
        g = F âŸª f âŸ«
        gâ»Â¹ : D [ y' , x' ]
        gâ»Â¹ = F âŸª fâ»Â¹ âŸ«
