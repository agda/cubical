{-# OPTIONS --safe #-}

module Cubical.Categories.Presheaf.Properties where

open import Cubical.Categories.Category renaming (isIso to isIsoC)
open import Cubical.Categories.NaturalTransformation
open import Cubical.Categories.Instances.Sets
open import Cubical.Categories.Instances.Functors
open import Cubical.Categories.Functor
open import Cubical.Categories.Presheaf.Base
open import Cubical.Categories.Equivalence
open import Cubical.Foundations.Prelude
open import Cubical.Foundations.HLevels
open import Cubical.Foundations.Isomorphism
open import Cubical.Foundations.Equiv using (fiber)
open import Cubical.Data.Sigma

import Cubical.Categories.Morphism as Morphism
import Cubical.Categories.Constructions.Slice as Slice
import Cubical.Categories.Constructions.Elements as Elements
import Cubical.Functions.Fibration as Fibration

private
  variable
    â„“ â„“' : Level
    e e' : Level


-- (PresheafCategory C) / F â‰ƒá¶œ PresheafCategory (âˆ«á´¾ F)
module _ {â„“S : Level} (C : Category â„“ â„“') (F : Functor (C ^op) (SET â„“S)) where
  open Category
  open Functor
  open _â‰ƒá¶œ_
  open isEquivalence
  open NatTrans
  open NatIso
  open Slice (PresheafCategory C â„“S) F
  open Elements.Contravariant {C = C}

  open Fibration.ForSets

  -- specific case of fiber under natural transformation
  fibersEqIfRepsEqNatTrans : âˆ€ {A} (Ï• : A â‡’ F) {c x x'} {px : x â‰¡ x'} {a' : fiber (Ï• âŸ¦ c âŸ§) x} {b' : fiber (Ï• âŸ¦ c âŸ§) x'}
                  â†’ fst a' â‰¡ fst b'
                  â†’ PathP (Î» i â†’ fiber (Ï• âŸ¦ c âŸ§) (px i)) a' b'
  fibersEqIfRepsEqNatTrans Ï• {c} {x} {x'} {px} {a , fiba} {b , fibb} p
    = fibersEqIfRepsEq {isSetB = snd (F âŸ… c âŸ†)} (Ï• âŸ¦ c âŸ§) p

  -- ========================================
  --            K : Slice â†’ PresheafCategory
  -- ========================================

  -- action on (slice) objects
  K-ob : (s : SliceCat .ob) â†’ (PresheafCategory (âˆ«á´¾ F) â„“S .ob)
  -- we take (c , x) to the fiber in A of Ï• over x
  K-ob (sliceob {A} Ï•) .F-ob (c , x)
    = (fiber (Ï• âŸ¦ c âŸ§) x)
    , isOfHLevelÎ£ 2 (snd (A âŸ… c âŸ†)) Î» _ â†’ isSetâ†’isGroupoid (snd (F âŸ… c âŸ†)) _ _
  -- for morphisms, we just apply A âŸª h âŸ« (plus equality proof)
  K-ob (sliceob {A} Ï•) .F-hom {d , y} {c , x} (h , com) (b , eq)
    = ((A âŸª h âŸ«) b)
    , ((Ï• âŸ¦ c âŸ§) ((A âŸª h âŸ«) b)
    â‰¡[ i ]âŸ¨ (Ï• .N-hom h) i b âŸ©
      (F âŸª h âŸ«) ((Ï• âŸ¦ d âŸ§) b)
    â‰¡[ i ]âŸ¨ (F âŸª h âŸ«) (eq i) âŸ©
      (F âŸª h âŸ«) y
    â‰¡âŸ¨ sym com âŸ©
      x
    âˆ)
  -- functoriality follows from functoriality of A
  K-ob (sliceob {A} Ï•) .F-id {x = (c , x)}
    = funExt Î» { (a , fibp)
                â†’ fibersEqIfRepsEqNatTrans Ï• (Î» i â†’ A .F-id i a) }
  K-ob (sliceob {A} Ï•) .F-seq {x = (c , x)} {(d , y)} {(e , z)} (f' , eq1) (g' , eq2)
    = funExt Î» { ( a , fibp )
                  â†’ fibersEqIfRepsEqNatTrans Ï• (Î» i â†’ (A .F-seq f' g') i a) }


  -- action on morphisms (in this case, natural transformation)
  K-hom : {sA sB : SliceCat .ob}
        â†’ (Îµ : SliceCat [ sA , sB ])
        â†’ (K-ob sA) â‡’ (K-ob sB)
  K-hom {sA = s1@(sliceob {A} Ï•)} {s2@(sliceob {B} Ïˆ)} (slicehom Îµ com) = natTrans Î·-ob (Î» h â†’ funExt (Î·-hom h))
    where
      P = K-ob s1
      Q = K-ob s2

      -- just apply the natural transformation (Îµ) we're given
      -- this ensures that we stay in the fiber over x due to the commutativity given by slicenesss
      Î·-ob : (el : (âˆ«á´¾ F) .ob) â†’ (fst (P âŸ… el âŸ†) â†’ fst (Q âŸ… el âŸ†) )
      Î·-ob (c , x) (a , Ï•aâ‰¡x) = ((Îµ âŸ¦ c âŸ§) a) , ÎµÏˆâ‰¡Ï• âˆ™ Ï•aâ‰¡x
        where
          ÎµÏˆâ‰¡Ï• : (Ïˆ âŸ¦ c âŸ§) ((Îµ âŸ¦ c âŸ§) a) â‰¡ (Ï• âŸ¦ c âŸ§) a
          ÎµÏˆâ‰¡Ï• i = ((com i) âŸ¦ c âŸ§) a

      Î·-hom : âˆ€ {el1 el2} (h : (âˆ«á´¾ F) [ el1 , el2 ]) (ae : fst (P âŸ… el2 âŸ†)) â†’ Î·-ob el1 ((P âŸª h âŸ«) ae) â‰¡ (Q âŸª h âŸ«) (Î·-ob el2 ae)
      Î·-hom {el1 = (c , x)} {d , y} (h , eqh) (a , eqa)
        = fibersEqIfRepsEqNatTrans Ïˆ (Î» i â†’ Îµ .N-hom h i a)


  K : Functor SliceCat (PresheafCategory (âˆ«á´¾ F) â„“S)
  K .F-ob = K-ob
  K .F-hom = K-hom
  K .F-id = makeNatTransPath
                          (funExt Î» cx@(c , x)
                                  â†’ funExt Î» aeq@(a , eq)
                                            â†’ fibersEqIfRepsEq {isSetB = snd (F âŸ… c âŸ†)} _ refl)
  K .F-seq (slicehom Î± eqa) (slicehom Î² eqb)
    = makeNatTransPath
        (funExt Î» cx@(c , x)
        â†’ funExt Î» aeq@(a , eq)
        â†’ fibersEqIfRepsEq {isSetB = snd (F âŸ… c âŸ†)} _ refl)


  -- ========================================
  --            L : PresheafCategory â†’ Slice
  -- ========================================

  -- action on objects (presheaves)
  L-ob : (P : PresheafCategory (âˆ«á´¾ F) â„“S .ob)
        â†’ SliceCat .ob
  L-ob P = sliceob {S-ob = L-ob-ob} L-ob-hom
    where
      -- sends c to the disjoint union of all the images under P
      LF-ob : (c : C .ob) â†’ (SET _) .ob
      LF-ob c = (Î£[ x âˆˆ fst (F âŸ… c âŸ†) ] fst (P âŸ… c , x âŸ†)) , isSetÎ£ (snd (F âŸ… c âŸ†)) (Î» x â†’ snd (P âŸ… c , x âŸ†))

      -- defines a function piecewise over the fibers by applying P
      LF-hom : âˆ€ {x y}
              â†’ (f : C [ y , x ])
              â†’ (SET _) [ LF-ob x , LF-ob y ]
      LF-hom {x = c} {d} f (x , a) = ((F âŸª f âŸ«) x) , (P âŸª f , refl âŸ«) a

      L-ob-ob : Functor (C ^op) (SET _)
      L-ob-ob .F-ob = LF-ob
      L-ob-ob .F-hom = LF-hom
      L-ob-ob .F-id {x = c}
        = funExt idFunExt
          where
            idFunExt : âˆ€ (un : fst (LF-ob c))
                      â†’ (LF-hom (C .id) un) â‰¡ un
            idFunExt (x , X) = Î£PathP (leftEq , rightEq)
              where
                leftEq : (F âŸª C .id âŸ«) x â‰¡ x
                leftEq i = F .F-id i x

                rightEq : PathP (Î» i â†’ fst (P âŸ… c , leftEq i âŸ†))
                          ((P âŸª C .id , refl âŸ«) X) X
                rightEq = left â–· right
                  where
                    -- the id morphism in (âˆ«á´¾ F)
                    âˆ«id = C .id , sym (funExtâ» (F .F-id) x âˆ™ refl)

                    -- functoriality of P gives us close to what we want
                    right : (P âŸª âˆ«id âŸ«) X â‰¡ X
                    right i = P .F-id i X

                    -- but need to do more work to show that (C .id , refl) â‰¡ âˆ«id
                    left : PathP (Î» i â†’ fst (P âŸ… c , leftEq i âŸ†))
                                  ((P âŸª C .id , refl âŸ«) X)
                                  ((P âŸª âˆ«id âŸ«) X)
                    left i = (P âŸª âˆ«á´¾homEq {F = F} (C .id , refl) âˆ«id (Î» i â†’ (c , leftEq i)) refl refl i âŸ«) X
      L-ob-ob .F-seq {x = c} {d} {e} f g
        = funExt seqFunEq
          where
            seqFunEq : âˆ€ (un : fst (LF-ob c))
                      â†’ (LF-hom (g â‹†âŸ¨ C âŸ© f) un) â‰¡ (LF-hom g) (LF-hom f un)
            seqFunEq un@(x , X) = Î£PathP (leftEq , rightEq)
              where
                -- the left component is comparing the action of F on x
                -- equality follows from functoriality of F
                -- leftEq : fst (LF-hom (g â‹†âŸ¨ C âŸ© f) un) â‰¡ fst ((LF-hom g) (LF-hom f un))
                leftEq : (F âŸª g â‹†âŸ¨ C âŸ© f âŸ«) x â‰¡ (F âŸª g âŸ«) ((F âŸª f âŸ«) x)
                leftEq i = F .F-seq f g i x

                -- on the right, equality also follows from functoriality of P
                -- but it's more complicated because of heterogeneity
                -- since leftEq is not a definitional equality
                rightEq : PathP (Î» i â†’ fst (P âŸ… e , leftEq i âŸ†))
                                ((P âŸª g â‹†âŸ¨ C âŸ© f , refl âŸ«) X)
                                ((P âŸª g , refl âŸ«) ((P âŸª f , refl âŸ«) X))
                rightEq = left â–· right
                  where
                    -- functoriality of P only gets us to this weird composition on the left
                    right : (P âŸª (g , refl) â‹†âŸ¨ (âˆ«á´¾ F) âŸ© (f , refl) âŸ«) X â‰¡ (P âŸª g , refl âŸ«) ((P âŸª f , refl âŸ«) X)
                    right i = P .F-seq (f , refl) (g , refl) i X

                    -- so we need to show that this composition is actually equal to the one we want
                    left : PathP (Î» i â†’ fst (P âŸ… e , leftEq i âŸ†))
                                  ((P âŸª g â‹†âŸ¨ C âŸ© f , refl âŸ«) X)
                                  ((P âŸª (g , refl) â‹†âŸ¨ (âˆ«á´¾ F) âŸ© (f , refl) âŸ«) X)
                    left i = (P âŸª âˆ«á´¾homEq {F = F} (g â‹†âŸ¨ C âŸ© f , refl) ((g , refl) â‹†âŸ¨ (âˆ«á´¾ F) âŸ© (f , refl)) (Î» i â†’ (e , leftEq i)) refl refl i âŸ«) X
      L-ob-hom : L-ob-ob â‡’ F
      L-ob-hom .N-ob c (x , _) = x
      L-ob-hom .N-hom f = funExt Î» (x , _) â†’ refl

  -- action on morphisms (aka natural transformations between presheaves)
  -- is essentially the identity (plus equality proofs for naturality and slice commutativity)
  L-hom : âˆ€ {P Q} â†’ PresheafCategory (âˆ«á´¾ F) â„“S [ P , Q ] â†’
        SliceCat [ L-ob P , L-ob Q ]
  L-hom {P} {Q} Î· = slicehom arr com
    where
      A = S-ob (L-ob P)
      Ï• = S-arr (L-ob P)
      B = S-ob (L-ob Q)
      Ïˆ = S-arr (L-ob Q)
      arr : A â‡’ B
      arr .N-ob c (x , X) = x , ((Î· âŸ¦ c , x âŸ§) X)
      arr .N-hom {c} {d} f = funExt natu
        where
          natuType : fst (A âŸ… c âŸ†) â†’ Type _
          natuType xX@(x , X) = ((F âŸª f âŸ«) x , (Î· âŸ¦ d , (F âŸª f âŸ«) x âŸ§) ((P âŸª f , refl âŸ«) X)) â‰¡ ((F âŸª f âŸ«) x , (Q âŸª f , refl âŸ«) ((Î· âŸ¦ c , x âŸ§) X))
          natu : âˆ€ (xX : fst (A âŸ… c âŸ†)) â†’ natuType xX
          natu (x , X) = Î£PathP (refl , Î» i â†’ (Î· .N-hom (f , refl) i) X)

      com : arr â‹†âŸ¨ PresheafCategory C â„“S âŸ© Ïˆ â‰¡ Ï•
      com = makeNatTransPath (funExt comFunExt)
        where
          comFunExt : âˆ€ (c : C .ob)
                    â†’ (arr â—áµ› Ïˆ) âŸ¦ c âŸ§ â‰¡ Ï• âŸ¦ c âŸ§
          comFunExt c = funExt Î» x â†’ refl

  L : Functor (PresheafCategory (âˆ«á´¾ F) â„“S) SliceCat
  L .F-ob = L-ob
  L .F-hom = L-hom
  L .F-id {cx} = SliceHom-â‰¡-intro' (makeNatTransPath (funExt Î» c â†’ refl))
  L .F-seq {cx} {dy} P Q = SliceHom-â‰¡-intro' (makeNatTransPath (funExt Î» c â†’ refl))

  -- ========================================
  --              Î· : ğŸ™ â‰… LK
  -- ========================================

  module _ where
    open Iso
    -- the iso we need
    -- a type is isomorphic to the disjoint union of all its fibers
    typeSectionIso : âˆ€ {A B : Type â„“S} {isSetB : isSet B} â†’ (Ï• : A â†’ B)
                  â†’ Iso A (Î£[ b âˆˆ B ] fiber Ï• b)
    typeSectionIso Ï• .fun a = (Ï• a) , (a , refl)
    typeSectionIso Ï• .inv (b , (a , eq)) = a
    typeSectionIso {isSetB = isSetB} Ï• .rightInv (b , (a , eq))
      = Î£PathP (eq
                , Î£PathP (refl
                        , isOfHLevelâ†’isOfHLevelDep 1 (Î» b' â†’ isSetB _ _) refl eq eq))
    typeSectionIso Ï• .leftInv a = refl

    -- the natural transformation
    -- just applies typeSectionIso
    Î·Trans : ğŸ™âŸ¨ SliceCat âŸ© â‡’ (L âˆ˜F K)
    Î·Trans .N-ob sob@(sliceob {A} Ï•) = slicehom Aâ‡’LK comm
      where
        LKA = S-ob  (L âŸ… K âŸ… sob âŸ† âŸ†)
        Ïˆ = S-arr  (L âŸ… K âŸ… sob âŸ† âŸ†)

        Aâ‡’LK : A â‡’ LKA
        Aâ‡’LK .N-ob c = typeSectionIso {isSetB = snd (F âŸ… c âŸ†)} (Ï• âŸ¦ c âŸ§) .fun
        Aâ‡’LK .N-hom {c} {d} f = funExt homFunExt
          where
            homFunExt : (x : fst (A âŸ… c âŸ†))
                      â†’ (((Ï• âŸ¦ d âŸ§) ((A âŸª f âŸ«) x)) , ((A âŸª f âŸ«) x , refl))  â‰¡ ((F âŸª f âŸ«) ((Ï• âŸ¦ c âŸ§) x) , (A âŸª f âŸ«) x , _)
            homFunExt x = Î£PathP ((Î» i â†’ (Ï• .N-hom f i) x) , fibersEqIfRepsEqNatTrans Ï• refl)

        comm : (Aâ‡’LK) â—áµ› Ïˆ â‰¡ Ï•
        comm = makeNatTransPath (funExt Î» x â†’ refl)
    Î·Trans .N-hom {sliceob {A} Î±} {sliceob {B} Î²} (slicehom Ï• eq)
      = SliceHom-â‰¡-intro' (makeNatTransPath (funExt (Î» c â†’ funExt Î» a â†’ natFunExt c a)))
      where
        natFunExt : âˆ€ (c : C .ob) (a : fst (A âŸ… c âŸ†))
                  â†’ ((Î² âŸ¦ c âŸ§) ((Ï• âŸ¦ c âŸ§) a) , (Ï• âŸ¦ c âŸ§) a , _) â‰¡ ((Î± âŸ¦ c âŸ§) a , (Ï• âŸ¦ c âŸ§) a , _)
        natFunExt c a = Î£PathP ((Î» i â†’ ((eq i) âŸ¦ c âŸ§) a) , fibersEqIfRepsEqNatTrans Î² refl)

    -- isomorphism follows from typeSectionIso
    Î·Iso : âˆ€ (sob : SliceCat .ob)
          â†’ isIsoC SliceCat (Î·Trans âŸ¦ sob âŸ§)
    Î·Iso sob@(sliceob Ï•) = sliceIso _ _ (FUNCTORIso _ _ _ isIsoCf)
      where
        isIsoCf : âˆ€ (c : C .ob)
                â†’ isIsoC _ (Î·Trans .N-ob sob .S-hom âŸ¦ c âŸ§)
        isIsoCf c = Morphism.CatIsoâ†’isIso (Isoâ†’CatIso (typeSectionIso {isSetB = snd (F âŸ… c âŸ†)} (Ï• âŸ¦ c âŸ§)))


  -- ========================================
  --              Îµ : KL â‰… ğŸ™
  -- ========================================

  module _ where
    open Iso
    -- the iso we deserve
    -- says that a type family at x is isomorphic to the fiber over x of that type family packaged up
    typeFiberIso : âˆ€ {â„“ â„“'} {A : Type â„“} {isSetA : isSet A} {x} (B : A â†’ Type â„“')
                  â†’ Iso (B x) (fiber {A = Î£[ a âˆˆ A ] B a} (Î» (x , _) â†’ x) x)
    typeFiberIso {x = x} _ .fun b = (x , b) , refl
    typeFiberIso _ .inv ((a , b) , eq) = subst _ eq b
    typeFiberIso {isSetA = isSetA} {x = x} B .rightInv ((a , b) , eq)
      = fibersEqIfRepsEq {isSetB = isSetA} (Î» (x , _) â†’ x) (Î£PathP (sym eq , symP (transport-filler (Î» i â†’ B (eq i)) b)))
    typeFiberIso {x = x} _ .leftInv b = sym (transport-filler refl b)

    -- the natural isomorphism
    -- applies typeFiberIso (inv)
    ÎµTrans : (K âˆ˜F L) â‡’ ğŸ™âŸ¨ PresheafCategory (âˆ«á´¾ F) â„“S âŸ©
    ÎµTrans .N-ob P = natTrans Î³-ob (Î» f â†’ funExt (Î» a â†’ Î³-homFunExt f a))
      where
        KLP = K âŸ… L âŸ… P âŸ† âŸ†

        Î³-ob : (el : (âˆ«á´¾ F) .ob)
            â†’ (fst (KLP âŸ… el âŸ†) â†’ fst (P âŸ… el âŸ†) )
        Î³-ob el@(c , _) = typeFiberIso {isSetA = snd (F âŸ… c âŸ†)} (Î» x â†’ fst (P âŸ… c , x âŸ†)) .inv

        -- naturality
        -- the annoying part is all the substs
        Î³-homFunExt : âˆ€ {el2 el1} â†’ (f' : (âˆ«á´¾ F) [ el2 , el1 ])
              â†’ (âˆ€ (a : fst (KLP âŸ… el1 âŸ†)) â†’ Î³-ob el2 ((KLP âŸª f' âŸ«) a) â‰¡ (P âŸª f' âŸ«) (Î³-ob el1 a))
        Î³-homFunExt {d , y} {c , x} f'@(f , comm) a@((x' , X') , eq) i
          = comp (Î» j â†’ fst (P âŸ… d , eq' j âŸ†)) (Î» j â†’ Î» { (i = i0) â†’ left j
                                                        ; (i = i1) â†’ right j }) ((P âŸª f , refl âŸ«) X')
            where
              -- fiber equality proof that we get from an application of KLP
              eq' = snd ((KLP âŸª f' âŸ«) a)

              -- top right of the commuting diagram
              -- "remove" the subst from the inside
              right : PathP (Î» i â†’ fst (P âŸ… d , eq' i âŸ†)) ((P âŸª f , refl âŸ«) X') ((P âŸª f , comm âŸ«) (subst _ eq X'))
              right i = (P âŸª f , reflâ‰¡comm i âŸ«) (X'â‰¡subst i)
                where
                  reflâ‰¡comm : PathP (Î» i â†’ (eq' i) â‰¡ (F âŸª f âŸ«) (eq i)) refl comm
                  reflâ‰¡comm = isOfHLevelâ†’isOfHLevelDep 1 (Î» (v , w) â†’ snd (F âŸ… d âŸ†) v ((F âŸª f âŸ«) w)) refl comm Î» i â†’ (eq' i , eq i)

                  X'â‰¡subst : PathP (Î» i â†’ fst (P âŸ… c , eq i âŸ†)) X' (subst _ eq X')
                  X'â‰¡subst = transport-filler (Î» i â†’ fst (P âŸ… c , eq i âŸ†)) X'

              -- bottom left of the commuting diagram
              -- "remove" the subst from the outside
              left : PathP (Î» i â†’ fst (P âŸ… d , eq' i âŸ†)) ((P âŸª f , refl âŸ«) X') (subst (Î» v â†’ fst (P âŸ… d , v âŸ†)) eq' ((P âŸª f , refl âŸ«) X'))
              left = transport-filler (Î» i â†’ fst (P âŸ… d , eq' i âŸ†)) ((P âŸª f , refl âŸ«) X')
    ÎµTrans .N-hom {P} {Q} Î± = makeNatTransPath (funExt Î» cx â†’ funExt Î» xX' â†’ Îµ-homFunExt cx xX')
      where
        KLP = K âŸ… L âŸ… P âŸ† âŸ†

        -- naturality of the above construction applies a similar argument as in `Î³-homFunExt`
        Îµ-homFunExt : âˆ€ (cx@(c , x) : (âˆ«á´¾ F) .ob) (xX'@((x' , X') , eq) : fst (KLP âŸ… cx âŸ†))
                    â†’ subst (Î» v â†’ fst (Q âŸ… c , v âŸ†)) (snd ((K âŸª L âŸª Î± âŸ« âŸ« âŸ¦ cx âŸ§) xX')) ((Î± âŸ¦ c , x' âŸ§) X')
                    â‰¡ (Î± âŸ¦ c , x âŸ§) (subst _ eq X')
        Îµ-homFunExt cx@(c , x) xX'@((x' , X') , eq) i
          = comp (Î» j â†’ fst (Q âŸ… c , eq j âŸ†)) (Î» j â†’ Î» { (i = i0) â†’ left j
                                                        ; (i = i1) â†’ right j }) ((Î± âŸ¦ c , x' âŸ§) X')
          where
            eq' : x' â‰¡ x
            eq' = snd ((K âŸª L âŸª Î± âŸ« âŸ« âŸ¦ cx âŸ§) xX')

            right : PathP (Î» i â†’ fst (Q âŸ… c , eq i âŸ†)) ((Î± âŸ¦ c , x' âŸ§) X') ((Î± âŸ¦ c , x âŸ§) (subst _ eq X'))
            right i = (Î± âŸ¦ c , eq i âŸ§) (X'â‰¡subst i)
              where
                -- this is exactly the same as the one from before, can refactor?
                X'â‰¡subst : PathP (Î» i â†’ fst (P âŸ… c , eq i âŸ†)) X' (subst _ eq X')
                X'â‰¡subst = transport-filler _ _

            -- extracted out type since need to use in in 'left' body as well
            leftTy : (x' â‰¡ x) â†’ Type _
            leftTy eq* = PathP (Î» i â†’ fst (Q âŸ… c , eq* i âŸ†)) ((Î± âŸ¦ c , x' âŸ§) X') (subst (Î» v â†’ fst (Q âŸ… c , v âŸ†)) eq' ((Î± âŸ¦ c , x' âŸ§) X'))

            left : leftTy eq
            left = subst
                    (Î» eq* â†’ leftTy eq*)
                    eq'â‰¡eq
                    (transport-filler _ _)
              where
                eq'â‰¡eq : eq' â‰¡ eq
                eq'â‰¡eq = snd (F âŸ… c âŸ†) _ _ eq' eq

    ÎµIso : âˆ€ (P : PresheafCategory (âˆ«á´¾ F) â„“S .ob)
          â†’ isIsoC (PresheafCategory (âˆ«á´¾ F) â„“S) (ÎµTrans âŸ¦ P âŸ§)
    ÎµIso P = FUNCTORIso _ _ _ isIsoC'
      where
        isIsoC' : âˆ€ (cx : (âˆ«á´¾ F) .ob)
                â†’ isIsoC (SET _) ((ÎµTrans âŸ¦ P âŸ§) âŸ¦ cx âŸ§)
        isIsoC' cx@(c , _) = Morphism.CatIsoâ†’isIso (Isoâ†’CatIso (invIso (typeFiberIso {isSetA = snd (F âŸ… c âŸ†)} _)))


  -- putting it all together

  preshSliceâ‰ƒpreshElem : SliceCat â‰ƒá¶œ PresheafCategory (âˆ«á´¾ F) â„“S
  preshSliceâ‰ƒpreshElem .func = K
  preshSliceâ‰ƒpreshElem .isEquiv .invFunc = L
  preshSliceâ‰ƒpreshElem .isEquiv .Î· .trans = Î·Trans
  preshSliceâ‰ƒpreshElem .isEquiv .Î· .nIso = Î·Iso
  preshSliceâ‰ƒpreshElem .isEquiv .Îµ .trans = ÎµTrans
  preshSliceâ‰ƒpreshElem .isEquiv .Îµ .nIso = ÎµIso
