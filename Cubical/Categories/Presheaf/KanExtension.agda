{-# OPTIONS --safe --lossy-unification #-}

{-
  Kan extension of a functor C â†’ D to a functor PresheafCategory C â„“ â†’ PresheafCategory D â„“
  left or right adjoint to precomposition.
-}

module Cubical.Categories.Presheaf.KanExtension where

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.Function
open import Cubical.Foundations.HLevels
open import Cubical.Functions.FunExtEquiv

open import Cubical.HITs.SetQuotients

open import Cubical.Categories.Category
open import Cubical.Categories.Functor
open import Cubical.Categories.NaturalTransformation
open import Cubical.Categories.Adjoint
open import Cubical.Categories.Presheaf.Base
open import Cubical.Categories.Instances.Functors
open import Cubical.Categories.Instances.Sets

{-
  Left Kan extension of a functor C â†’ D to a functor PresheafCategory C â„“ â†’ PresheafCategory D â„“
  left adjoint to precomposition.
-}

module Lan {â„“C â„“C' â„“D â„“D'} â„“S
  {C : Category â„“C â„“C'} {D : Category â„“D â„“D'}
  (F : Functor C D)
  where

  open Functor
  open NatTrans

  private
    module C = Category C
    module D = Category D

    {-
      We want the category SET â„“ we're mapping into to be large enough that the coend will take presheaves
      Cáµ’áµ– â†’ Set â„“ to presheaves Dáµ’áµ– â†’ Set â„“, otherwise we get no adjunction with precomposition.
      So we must have â„“C,â„“C',â„“D' â‰¤ â„“; the parameter â„“S allows â„“ to be larger than their maximum.
    -}
    â„“ = â„“-max (â„“-max (â„“-max â„“C â„“C') â„“D') â„“S

  module _ (G : Functor (C ^op) (SET â„“)) where

    -- Definition of the coend

    module _ (d : D.ob) where

      Raw : Type â„“
      Raw = Î£[ c âˆˆ C.ob ] Î£[ g âˆˆ D.Hom[ d , F âŸ… c âŸ† ] ] G .F-ob c .fst

      data _â‰ˆ_ : (u v : Raw) â†’ Type â„“ where
        shift : {c c' : C.ob} (g : D.Hom[ d , F âŸ… c âŸ† ]) (f : C.Hom[ c , c' ]) (a : (G âŸ… c' âŸ†) .fst)
          â†’ (c' , (g D.â‹† F âŸª f âŸ«) , a) â‰ˆ (c , g , (G âŸª f âŸ«) a)

      Quo = Raw / _â‰ˆ_

    pattern shift/ g f a i = eq/ _ _ (shift g f a) i

    -- Action of Quo on arrows in D

    mapR : {d d' : D.ob} (h : D.Hom[ d' , d ]) â†’ Quo d â†’ Quo d'
    mapR h [ c , g , a ] = [ c , h D.â‹† g , a ]
    mapR h (shift/ g f a i) =
      hcomp
        (Î» j â†’ Î»
          { (i = i0) â†’ [ _ , D.â‹†Assoc h g (F âŸª f âŸ«) j , a ]
          ; (i = i1) â†’ [ _ , h D.â‹† g , (G âŸª f âŸ«) a ]
          })
        (shift/ (h D.â‹† g) f a i)
    mapR h (squash/ t u p q i j) =
      squash/ (mapR h t) (mapR h u) (cong (mapR h) p) (cong (mapR h) q) i j

    mapRId : (d : D.ob) â†’ mapR (D.id {x = d}) â‰¡ idfun (Quo d)
    mapRId d =
      funExt (elimProp (Î» _ â†’ squash/ _ _) (Î» (c , g , a) i â†’ [ c , D.â‹†IdL g i , a ]))

    mapRâˆ˜ : {d d' d'' : D.ob}
      (h' : D.Hom[ d'' , d' ]) (h : D.Hom[ d' , d ])
      â†’ mapR (h' D.â‹† h) â‰¡ mapR h' âˆ˜ mapR h
    mapRâˆ˜ h' h =
      funExt (elimProp (Î» _ â†’ squash/ _ _) (Î» (c , g , a) i â†’ [ c , D.â‹†Assoc h' h g i , a ]))

  LanOb : Functor (C ^op) (SET â„“) â†’ Functor (D ^op) (SET _)
  LanOb G .F-ob d .fst = Quo G d
  LanOb G .F-ob d .snd = squash/
  LanOb G .F-hom = mapR G
  LanOb G .F-id {d} = mapRId G d
  LanOb G .F-seq h h' = mapRâˆ˜ G h' h

  -- Action of Quo on arrows in Cáµ’áµ– â†’ Set

  module _ {G G' : Functor (C ^op) (SET â„“)} (Î± : NatTrans G G') where

    mapL : (d : D.ob) â†’ Quo G d â†’ Quo G' d
    mapL d [ c , g , a ] = [ c , g , Î± .N-ob c a ]
    mapL d (shift/ g f a i) =
      hcomp
        (Î» j â†’ Î»
          { (i = i0) â†’ [ _ , (g D.â‹† F âŸª f âŸ«) , Î± .N-ob _ a ]
          ; (i = i1) â†’ [ _ , g , funExtâ» (Î± .N-hom f) a (~ j) ]
          })
        (shift/ g f ((Î± âŸ¦ _ âŸ§) a) i)
    mapL d (squash/ t u p q i j) =
      squash/ (mapL d t) (mapL d u) (cong (mapL d) p) (cong (mapL d) q) i j

    mapLR : {d d' : D.ob} (h : D.Hom[ d' , d ])
      â†’ mapL d' âˆ˜ mapR G h â‰¡ mapR G' h âˆ˜ mapL d
    mapLR h = funExt (elimProp (Î» _ â†’ squash/ _ _) (Î» _ â†’ refl))

  mapLId : (G : Functor (C ^op) (SET â„“))
    (d : D.ob) â†’ mapL (idTrans G) d â‰¡ idfun (Quo G d)
  mapLId G d = funExt (elimProp (Î» _ â†’ squash/ _ _) (Î» _ â†’ refl))

  mapLâˆ˜ : {G G' G'' : Functor (C ^op) (SET â„“)}
    (Î² : NatTrans G' G'') (Î± : NatTrans G G')
    (d : D.ob) â†’ mapL (seqTrans Î± Î²) d â‰¡ mapL Î² d âˆ˜ mapL Î± d
  mapLâˆ˜ Î² Î± d = funExt (elimProp (Î» _ â†’ squash/ _ _) (Î» _ â†’ refl))

  LanHom : {G G' : Functor (C ^op) (SET â„“)}
    â†’ NatTrans G G' â†’ NatTrans (LanOb G) (LanOb G')
  LanHom Î± .N-ob = mapL Î±
  LanHom Î± .N-hom = mapLR Î±

  -- Definition of the left Kan extension functor

  Lan : Functor (FUNCTOR (C ^op) (SET â„“)) (FUNCTOR (D ^op) (SET â„“))
  Lan .F-ob = LanOb
  Lan .F-hom = LanHom
  Lan .F-id {G} = makeNatTransPath (funExt (mapLId G))
  Lan .F-seq Î± Î² = makeNatTransPath (funExt (mapLâˆ˜ Î² Î±))

  -- Adjunction between the left Kan extension and precomposition

  private
    F* = precomposeF (SET â„“) (F ^opF)

  open UnitCounit

  Î· : ğŸ™âŸ¨ FUNCTOR (C ^op) (SET â„“) âŸ© â‡’ funcComp F* Lan
  Î· .N-ob G .N-ob c a = [ c , D.id , a ]
  Î· .N-ob G .N-hom {c'} {c} f =
    funExt Î» a â†’
    [ c , D.id , (G âŸª f âŸ«) a ]
      â‰¡âŸ¨ sym (shift/ D.id f a) âŸ©
    [ c' , (D.id D.â‹† F âŸª f âŸ«) , a ]
      â‰¡[ i ]âŸ¨ [ c' , lem i , a ] âŸ©
    [ c' , (F âŸª f âŸ« D.â‹† D.id) , a ]
    âˆ
    where
    lem : D.id D.â‹† F âŸª f âŸ« â‰¡ F âŸª f âŸ« D.â‹† D.id
    lem = D.â‹†IdL (F âŸª f âŸ«) âˆ™ sym (D.â‹†IdR (F âŸª f âŸ«))
  Î· .N-hom f = makeNatTransPath refl

  Îµ : funcComp Lan F* â‡’ ğŸ™âŸ¨ FUNCTOR (D ^op) (SET â„“) âŸ©
  Îµ .N-ob H .N-ob d =
    elim
      (Î» _ â†’ (H âŸ… d âŸ†) .snd)
      (Î» (c , g , a) â†’ (H âŸª g âŸ«) a)
      (Î» {_ _ (shift g f a) i â†’ H .F-seq (F âŸª f âŸ«) g i a})
  Îµ .N-ob H .N-hom g' =
    funExt (elimProp (Î» _ â†’ (H âŸ… _ âŸ†) .snd _ _) (Î» (c , g , a) â†’ funExtâ» (H .F-seq g g') a))
  Îµ .N-hom {H} {H'} Î± =
    makeNatTransPath
      (funExtâ‚‚ Î» d â†’
         elimProp (Î» _ â†’ (H' âŸ… _ âŸ†) .snd _ _)
          (Î» (c , g , a) â†’ sym (funExtâ» (Î± .N-hom g) a)))

  Î”â‚ : âˆ€ G â†’ seqTrans (Lan âŸª Î· âŸ¦ G âŸ§ âŸ«) (Îµ âŸ¦ Lan âŸ… G âŸ† âŸ§) â‰¡ idTrans _
  Î”â‚ G =
    makeNatTransPath
      (funExtâ‚‚ Î» d â†’
        elimProp (Î» _ â†’ squash/ _ _)
          (Î» (c , g , a) â†’
            [ c , g D.â‹† D.id , a ]
              â‰¡[ i ]âŸ¨ [ c , (g D.â‹† F .F-id (~ i)) , a ] âŸ©
            [ c , g D.â‹† (F âŸª C.id âŸ«) , a ]
              â‰¡âŸ¨ shift/ g C.id a âŸ©
            [ c , g , (G âŸª C.id âŸ«) a ]
              â‰¡[ i ]âŸ¨ [ c , g , G .F-id i a ] âŸ©
            [ c , g , a ]
            âˆ))

  Î”â‚‚ : âˆ€ H â†’ seqTrans (Î· âŸ¦ F* âŸ… H âŸ† âŸ§) (F* âŸª Îµ âŸ¦ H âŸ§ âŸ«) â‰¡ idTrans _
  Î”â‚‚ H = makeNatTransPath (funExt Î» c â†’ H .F-id)

  adj : Lan âŠ£ F*
  adj ._âŠ£_.Î· = Î·
  adj ._âŠ£_.Îµ = Îµ
  adj ._âŠ£_.Î”â‚ = Î”â‚
  adj ._âŠ£_.Î”â‚‚ = Î”â‚‚

{-
  Right Kan extension of a functor C â†’ D to a functor PresheafCategory C â„“ â†’ PresheafCategory D â„“
  right adjoint to precomposition.
-}

module Ran {â„“C â„“C' â„“D â„“D'} â„“S
  {C : Category â„“C â„“C'} {D : Category â„“D â„“D'}
  (F : Functor C D)
  where

  open Functor
  open NatTrans

  private
    module C = Category C
    module D = Category D

    {-
      We want the category SET â„“ we're mapping into to be large enough that the coend will take presheaves
      Cáµ’áµ– â†’ Set â„“ to presheaves Dáµ’áµ– â†’ Set â„“, otherwise we get no adjunction with precomposition.
      So we must have â„“C,â„“C',â„“D' â‰¤ â„“; the parameter â„“S allows â„“ to be larger than their maximum.
    -}
    â„“ = â„“-max (â„“-max (â„“-max â„“C â„“C') â„“D') â„“S

  module _ (G : Functor (C ^op) (SET â„“)) where

    -- Definition of the end

    record End (d : D.ob) : Type â„“ where
      field
        fun : (c : C.ob) (g : D.Hom[ F âŸ… c âŸ† , d ]) â†’ G .F-ob c .fst
        coh : {c c' : C.ob} (f : C.Hom[ c , c' ]) (g : D.Hom[ F âŸ… c' âŸ† , d ])
          â†’ fun c (F âŸª f âŸ« â‹†âŸ¨ D âŸ© g) â‰¡ (G âŸª f âŸ«) (fun c' g)

    open End

    endâ‰¡ : {d : D.ob} {x x' : End d} â†’ (âˆ€ c g â†’ x .fun c g â‰¡ x' .fun c g) â†’ x â‰¡ x'
    endâ‰¡ h i .fun c g = h c g i
    endâ‰¡ {_} {x} {x'} h i .coh f g =
      isSetâ†’isSet' (G .F-ob _ .snd)
        (x .coh f g)
        (x' .coh f g)
        (h _ (F âŸª f âŸ« â‹†âŸ¨ D âŸ© g))
        (cong (G âŸª f âŸ«) (h _ g))
        i

    -- Action of End on arrows in D

    mapR : {d d' : D.ob} (h : D.Hom[ d' , d ]) â†’ End d â†’ End d'
    mapR h x .fun c g = x .fun c (g â‹†âŸ¨ D âŸ© h)
    mapR h x .coh f g = cong (x .fun _) (D.â‹†Assoc (F âŸª f âŸ«) g h) âˆ™ x .coh f (g â‹†âŸ¨ D âŸ© h)

    mapRId : (d : D.ob) â†’ mapR (D.id {x = d}) â‰¡ idfun (End d)
    mapRId h = funExt Î» x â†’ endâ‰¡ Î» c g â†’ cong (x .fun c) (D.â‹†IdR g)

    mapRâˆ˜ : {d d' d'' : D.ob}
      (h' : D.Hom[ d'' , d' ]) (h : D.Hom[ d' , d ])
      â†’ mapR (h' D.â‹† h) â‰¡ mapR h' âˆ˜ mapR h
    mapRâˆ˜ h' h = funExt Î» x â†’ endâ‰¡ Î» c g â†’ cong (x .fun c) (sym (D.â‹†Assoc g h' h))

  open End

  RanOb : Functor (C ^op) (SET â„“) â†’ Functor (D ^op) (SET _)
  RanOb G .F-ob d .fst = End G d
  RanOb G .F-ob d .snd =
    -- We use that End is equivalent to a Î£-type to prove its HLevel more easily
    isOfHLevelRetract 2
      {B =
        Î£[ z âˆˆ ((c : C.ob) (g : D.Hom[ F âŸ… c âŸ† , d ]) â†’ G .F-ob c .fst) ]
        ({c c' : C.ob} (f : C.Hom[ c , c' ]) (g : D.Hom[ F âŸ… c' âŸ† , d ])
          â†’ z c (F âŸª f âŸ« â‹†âŸ¨ D âŸ© g) â‰¡ (G âŸª f âŸ«) (z c' g))
      }
      (Î» x â†’ Î» where .fst â†’ x .fun; .snd â†’ x .coh)
      (Î» Ïƒ â†’ Î» where .fun â†’ Ïƒ .fst; .coh â†’ Ïƒ .snd)
      (Î» _ â†’ refl)
      (isSetÎ£
        (isSetÎ 2 Î» _ _ â†’ G .F-ob _ .snd)
        (Î» _ â†’ isPropâ†’isSet
          (isPropImplicitÎ  Î» _ â†’ isPropImplicitÎ  Î» _ â†’ isPropÎ 2 Î» _ _ â†’ G .F-ob _ .snd _ _)))
  RanOb G .F-hom = mapR G
  RanOb G .F-id {d} = mapRId G d
  RanOb G .F-seq h h' = mapRâˆ˜ G h' h

  -- Action of End on arrows in Cáµ’áµ– â†’ Set

  module _ {G G' : Functor (C ^op) (SET â„“)} (Î± : NatTrans G G') where

    mapL : (d : D.ob) â†’ End G d â†’ End G' d
    mapL d x .fun c g = (Î± âŸ¦ c âŸ§) (x .fun c g)
    mapL d x .coh f g =
      cong (Î± âŸ¦ _ âŸ§) (x .coh f g)
      âˆ™ funExtâ» (Î± .N-hom f) (x .fun _ g)

    mapLR : {d d' : D.ob} (h : D.Hom[ d' , d ])
      â†’ mapL d' âˆ˜ mapR G h â‰¡ mapR G' h âˆ˜ mapL d
    mapLR h = funExt Î» _ â†’ endâ‰¡ _ Î» _ _ â†’ refl

  mapLId : (G : Functor (C ^op) (SET â„“))
    (d : D.ob) â†’ mapL (idTrans G) d â‰¡ idfun (End G d)
  mapLId G d = funExt Î» _ â†’ endâ‰¡ _ Î» _ _ â†’ refl

  mapLâˆ˜ : {G G' G'' : Functor (C ^op) (SET â„“)}
    (Î² : NatTrans G' G'') (Î± : NatTrans G G')
    (d : D.ob) â†’ mapL (seqTrans Î± Î²) d â‰¡ mapL Î² d âˆ˜ mapL Î± d
  mapLâˆ˜ Î² Î± d = funExt Î» _ â†’ endâ‰¡ _ Î» _ _ â†’ refl

  RanHom : {G G' : Functor (C ^op) (SET â„“)}
    â†’ NatTrans G G' â†’ NatTrans (RanOb G) (RanOb G')
  RanHom Î± .N-ob = mapL Î±
  RanHom Î± .N-hom = mapLR Î±

  -- Definition of the right Kan extension functor

  Ran : Functor (FUNCTOR (C ^op) (SET â„“)) (FUNCTOR (D ^op) (SET â„“))
  Ran .F-ob = RanOb
  Ran .F-hom = RanHom
  Ran .F-id {G} = makeNatTransPath (funExt (mapLId G))
  Ran .F-seq Î± Î² = makeNatTransPath (funExt (mapLâˆ˜ Î² Î±))

  -- Adjunction between precomposition and right Kan extension

  private
    F* = precomposeF (SET â„“) (F ^opF)

  open UnitCounit

  Î· : ğŸ™âŸ¨ FUNCTOR (D ^op) (SET â„“) âŸ© â‡’ (funcComp Ran F*)
  Î· .N-ob G .N-ob d a .fun c g = (G âŸª g âŸ«) a
  Î· .N-ob G .N-ob d a .coh f g = funExtâ» (G .F-seq g (F âŸª f âŸ«)) a
  Î· .N-ob G .N-hom h = funExt Î» a â†’ endâ‰¡ _ Î» c g â†’ sym (funExtâ» (G .F-seq h g) a)
  Î· .N-hom {G} {G'} Î± =
    makeNatTransPath (funExtâ‚‚ Î» d a â†’ endâ‰¡ _ Î» c g â†’ sym (funExtâ» (Î± .N-hom g) a))

  Îµ : funcComp F* Ran â‡’ ğŸ™âŸ¨ FUNCTOR (C ^op) (SET â„“) âŸ©
  Îµ .N-ob H .N-ob c x = x .fun c D.id
  Îµ .N-ob H .N-hom {c} {c'} g =
    funExt Î» x â†’
    cong (x .fun c') (D.â‹†IdL _ âˆ™ sym (D.â‹†IdR _)) âˆ™ x .coh g D.id
  Îµ .N-hom {H} {H'} Î± = makeNatTransPath refl

  Î”â‚ : âˆ€ G â†’ seqTrans (F* âŸª Î· âŸ¦ G âŸ§ âŸ«) (Îµ âŸ¦ F* âŸ… G âŸ† âŸ§) â‰¡ idTrans _
  Î”â‚ G = makeNatTransPath (funExtâ‚‚ Î» c a â†’ funExtâ» (G .F-id) a)

  Î”â‚‚ : âˆ€ H â†’ seqTrans (Î· âŸ¦ Ran âŸ… H âŸ† âŸ§) (Ran âŸª Îµ âŸ¦ H âŸ§ âŸ«) â‰¡ idTrans _
  Î”â‚‚ H = makeNatTransPath (funExtâ‚‚ Î» c x â†’ endâ‰¡ _ Î» c' g â†’ cong (x .fun c') (D.â‹†IdL g))

  adj : F* âŠ£ Ran
  adj ._âŠ£_.Î· = Î·
  adj ._âŠ£_.Îµ = Îµ
  adj ._âŠ£_.Î”â‚ = Î”â‚
  adj ._âŠ£_.Î”â‚‚ = Î”â‚‚
