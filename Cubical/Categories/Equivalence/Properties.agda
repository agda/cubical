{-# OPTIONS --safe #-}

module Cubical.Categories.Equivalence.Properties where

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.Equiv
  renaming (isEquiv to isEquivMap)
open import Cubical.Foundations.Equiv.Dependent
open import Cubical.Foundations.HLevels
open import Cubical.Foundations.Isomorphism
open import Cubical.Foundations.Powerset
open import Cubical.Data.Sigma
open import Cubical.Categories.Category
open import Cubical.Categories.Functor
open import Cubical.Categories.NaturalTransformation
open import Cubical.Categories.Morphism
open import Cubical.Categories.Equivalence.Base
open import Cubical.HITs.PropositionalTruncation

open Category
open Functor
open NatIso
open isIso
open WeakInverse

private
  variable
    â„“C â„“C' â„“D â„“D' : Level

-- Equivalence implies Full, Faithul, and Essentially Surjective

module _ {C : Category â„“C â„“C'} {D : Category â„“D â„“D'} where
  symWeakInverse : âˆ€ {F : Functor C D} â†’ (e : WeakInverse F) â†’ WeakInverse (e .invFunc)
  symWeakInverse {F} record { invFunc = G ; Î· = Î· ; Îµ = Îµ } = record { invFunc = F ; Î· = symNatIso Îµ ; Îµ = symNatIso Î· }

  isEquivâ†’Faithful : âˆ€ {F : Functor C D} â†’ isEquivalence F â†’ isFaithful F
  isEquivâ†’Faithful {F} = rec (isPropÎ 5 (Î» _ _ _ _ _ â†’ C .isSetHom _ _)) lifted
    where
      lifted : WeakInverse F â†’ isFaithful F
      lifted record { invFunc = G
                              ; Î· = Î·
                              ; Îµ = _ }
                   c c' f g p = f
                              â‰¡âŸ¨ sqRL Î· âŸ©
                                cIso .fst â‹†âŸ¨ C âŸ© G âŸª F âŸª f âŸ« âŸ« â‹†âŸ¨ C âŸ© c'Iso .snd .inv
                              â‰¡âŸ¨ cong (Î» v â†’ cIso .fst â‹†âŸ¨ C âŸ© (G âŸª v âŸ«) â‹†âŸ¨ C âŸ© c'Iso .snd .inv) p âŸ©
                                cIso .fst â‹†âŸ¨ C âŸ© G âŸª F âŸª g âŸ« âŸ« â‹†âŸ¨ C âŸ© c'Iso .snd .inv
                              â‰¡âŸ¨ sym (sqRL Î·) âŸ©
                                g
                              âˆ
         where
           -- isomorphism between c and GFc
          cIso = isIsoâ†’CatIso (Î· .nIso c)
          -- isomorphism between c' and GFc'
          c'Iso = isIsoâ†’CatIso (Î· .nIso c')

module _ {C : Category â„“C â„“C'} {D : Category â„“D â„“D'} where
  isEquivâ†’Full : âˆ€ {F : Functor C D} â†’ isEquivalence F â†’ isFull F
  isEquivâ†’Full {F} = rec (isPropÎ 3 (Î» _ _ _ â†’ isPropPropTrunc)) lifted
    where
      lifted : WeakInverse F â†’ isFull F
      lifted eq@record { invFunc = G
                             ; Î· = Î·
                             ; Îµ = _ }
        c c' g = âˆ£ h , isEquivâ†’Faithful âˆ£ symWeakInverse eq âˆ£â‚ _ _ _ _ GFhâ‰¡Gg âˆ£â‚ -- apply faithfulness of G
        where
          -- isomorphism between c and GFc
          cIso = isIsoâ†’CatIso (Î· .nIso c)
          -- isomorphism between c' and GFc'
          c'Iso = isIsoâ†’CatIso (Î· .nIso c')

          -- reverses
          cIsoâ» = symCatIso cIso
          c'Isoâ» = symCatIso c'Iso

          h = cIso .fst â‹†âŸ¨ C âŸ© G âŸª g âŸ« â‹†âŸ¨ C âŸ© c'Iso .snd .inv

          -- we show that both `G âŸª g âŸ«` and `G âŸª F âŸª h âŸ« âŸ«`
          -- are equal to the same thing
          -- namely : cIso .inv â‹†âŸ¨ C âŸ© h â‹†âŸ¨ C âŸ© c'Iso .mor
          Ggâ‰¡Î·hÎ· : G âŸª g âŸ« â‰¡ cIso .snd .inv â‹†âŸ¨ C âŸ© h â‹†âŸ¨ C âŸ© c'Iso .fst
          Ggâ‰¡Î·hÎ· = invMoveL cAreInv move-c' âˆ™ sym (C .â‹†Assoc _ _ _)
            where
              cAreInv : areInv _ (cIso .fst) (cIso .snd .inv)
              cAreInv = CatIsoâ†’areInv cIso

              c'AreInv : areInv _ (c'Iso .fst) (c'Iso .snd .inv)
              c'AreInv = CatIsoâ†’areInv c'Iso

              move-c' : cIso .fst â‹†âŸ¨ C âŸ© G âŸª g âŸ« â‰¡ h â‹†âŸ¨ C âŸ© c'Iso .fst
              move-c' = invMoveR (symAreInv c'AreInv) refl

          GFhâ‰¡Gg : G âŸª F âŸª h âŸ« âŸ« â‰¡ G âŸª g âŸ«
          GFhâ‰¡Gg = G âŸª F âŸª h âŸ« âŸ«
                 â‰¡âŸ¨ sqLR Î· âŸ©
                   cIso .snd .inv â‹†âŸ¨ C âŸ© h â‹†âŸ¨ C âŸ© c'Iso .fst
                 â‰¡âŸ¨ sym Ggâ‰¡Î·hÎ· âŸ©
                   G âŸª g âŸ«
                 âˆ

  isEquivâ†’FullyFaithful :  âˆ€ {F : Functor C D} â†’ isEquivalence F â†’ isFullyFaithful F
  isEquivâ†’FullyFaithful {F = F} h = isFull+Faithfulâ†’isFullyFaithful {F = F} (isEquivâ†’Full h) (isEquivâ†’Faithful h)

  isEquivâ†’Surj : âˆ€ {F : Functor C D} â†’ isEquivalence F â†’ isEssentiallySurj F
  isEquivâ†’Surj = rec (isPropÎ  (Î» _ â†’ isPropPropTrunc))
    (Î» wkInv d â†’ âˆ£ (wkInv .invFunc âŸ… d âŸ†) , isIsoâ†’CatIso ((wkInv .Îµ .nIso) d) âˆ£â‚)


-- A fully-faithful functor that induces equivalence on objects is an equivalence

module _ {C : Category â„“C â„“C'} {D : Category â„“D â„“D'}
  {F : Functor C D} where

  isFullyFaithful+isEquivF-obâ†’isEquiv : isFullyFaithful F â†’ isEquivMap (F .F-ob) â†’ isEquivalence F
  isFullyFaithful+isEquivF-obâ†’isEquiv fullfaith isequiv = âˆ£ w âˆ£â‚
    where
    open Iso
    open IsoOver

    MorC : C .ob Ã— C .ob â†’ Type _
    MorC (x , y) = C [ x , y ]

    MorD : D .ob Ã— D .ob â†’ Type _
    MorD (x , y) = D [ x , y ]

    F-Mor : ((x , y) : C .ob Ã— C .ob) â†’ C [ x , y ] â†’ D [ F .F-ob x , F .F-ob y ]
    F-Mor _ = F .F-hom

    equiv-obÂ² : C .ob Ã— C .ob â‰ƒ D .ob Ã— D .ob
    equiv-obÂ² = â‰ƒ-Ã— (_ , isequiv) (_ , isequiv)

    iso-ob  = equivToIso (_ , isequiv)
    iso-hom = equivOverâ†’IsoOver {P = MorC} {Q = MorD} equiv-obÂ² F-Mor (Î» (x , y) â†’ fullfaith x y)

    w-inv : Functor D C
    w-inv .F-ob = iso-ob .inv
    w-inv .F-hom = iso-hom .inv _
    w-inv .F-id {x = x} = isFullyFaithfulâ†’Faithful {F = F} fullfaith _ _ _ _ (p âˆ™ sym (F .F-id))
      where
      p : _
      p i =
        comp
        (Î» j â†’ D [ iso-ob .rightInv x (~ j) , iso-ob .rightInv x (~ j) ])
        (Î» j â†’ Î»
          { (i = i0) â†’ iso-hom .rightInv _ (D .id {x = x}) (~ j)
          ; (i = i1) â†’ D .id {x = iso-ob .rightInv x (~ j)} })
        (D .id {x = x})
    w-inv .F-seq {x = x} {z = z} f g = isFullyFaithfulâ†’Faithful {F = F} fullfaith _ _ _ _ (p âˆ™ sym (F .F-seq _ _))
      where
      p : _
      p i =
        comp
        (Î» j â†’ D [ iso-ob .rightInv x (~ j) , iso-ob .rightInv z (~ j) ])
        (Î» j â†’ Î»
          { (i = i0) â†’ iso-hom .rightInv _ (f â‹†âŸ¨ D âŸ© g) (~ j)
          ; (i = i1) â†’ iso-hom .rightInv _ f (~ j) â‹†âŸ¨ D âŸ© iso-hom .rightInv _ g (~ j) })
        (f â‹†âŸ¨ D âŸ© g)

    w-Î·-path : ğŸ™âŸ¨ C âŸ© â‰¡ w-inv âˆ˜F F
    w-Î·-path = Functorâ‰¡ (Î» x â†’ sym (retIsEq isequiv x)) (Î» {x} {y} f â†’ (Î» i â†’ iso-hom .leftInv (x , y) f (~ i)))

    w-Îµ-path : F âˆ˜F w-inv â‰¡ ğŸ™âŸ¨ D âŸ©
    w-Îµ-path = Functorâ‰¡ (Î» x â†’ secIsEq isequiv x) (Î» {x} {y} f i â†’ iso-hom .rightInv (x , y) f i)

    w : WeakInverse F
    w .invFunc = w-inv
    w .Î· = pathToNatIso w-Î·-path
    w .Îµ = pathToNatIso w-Îµ-path



-- equivalence on full subcategories defined by propositions
module _ {C : Category â„“C â„“C'} {D : Category â„“D â„“D'} (F : Functor C D) (invF : WeakInverse F) where

  open NatTrans
  open _â‰ƒá¶œ_

  private
    Fâ»Â¹ = invF .invFunc
    Î·á´± = invF .Î·
    Îµá´± = invF .Îµ


  Î£PropCatEquiv : {P : â„™ (ob C)} {Q : â„™ (ob D)}
                â†’ (presF : âˆ€ c â†’ c âˆˆ P â†’ F .F-ob c âˆˆ Q)
                â†’ (âˆ€ d â†’ d âˆˆ Q â†’ Fâ»Â¹ .F-ob d âˆˆ P)
                â†’ WeakInverse (Î£PropCatFunc {P = P} {Q = Q} F presF)

  invFunc (Î£PropCatEquiv {P} {Q} _ presFâ»Â¹) = Î£PropCatFunc {P = Q} {Q = P} Fâ»Â¹ presFâ»Â¹

  N-ob (trans (Î· (Î£PropCatEquiv _ _))) (x , _) = Î·á´± .trans .N-ob x
  N-hom (trans (Î· (Î£PropCatEquiv _ _))) f = Î·á´± .trans .N-hom f
  inv (nIso (Î· (Î£PropCatEquiv _ _)) (x , _)) = Î·á´± .nIso x .inv
  sec (nIso (Î· (Î£PropCatEquiv _ _)) (x , _)) = Î·á´± .nIso x .sec
  ret (nIso (Î· (Î£PropCatEquiv _ _)) (x , _)) = Î·á´± .nIso x .ret

  N-ob (trans (Îµ (Î£PropCatEquiv _ _))) (x , _) = Îµá´± .trans .N-ob x
  N-hom (trans (Îµ (Î£PropCatEquiv _ _))) f = Îµá´± .trans .N-hom f
  inv (nIso (Îµ (Î£PropCatEquiv _ _)) (x , _)) = Îµá´± .nIso x .inv
  sec (nIso (Îµ (Î£PropCatEquiv _ _)) (x , _)) = Îµá´± .nIso x .sec
  ret (nIso (Îµ (Î£PropCatEquiv _ _)) (x , _)) = Îµá´± .nIso x .ret
