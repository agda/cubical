-- Free category over a directed graph/quiver
-- This time without any assumptions on the HLevels of the graph
{-# OPTIONS --safe #-}

module Cubical.Categories.Constructions.Free.Category.Base where

open import Cubical.Categories.Morphism
open import Cubical.Foundations.Prelude
open import Cubical.Foundations.Path
open import Cubical.Foundations.HLevels
open import Cubical.Categories.Category.Base
open import Cubical.Categories.Functor.Base
open import Cubical.Categories.NaturalTransformation hiding (_âŸ¦_âŸ§)
open import Cubical.Categories.UnderlyingGraph
open import Cubical.Data.Graph.Base
open import Cubical.Data.Sigma

private
  variable
    â„“c â„“c' â„“d â„“d' â„“g â„“g' : Level

open Category
open Functor
open NatIso hiding (sqRL; sqLL)
open NatTrans

module FreeCategory (G : Graph â„“g â„“g') where
    data Exp : G .Node â†’ G .Node â†’ Type (â„“-max â„“g â„“g') where
      â†‘_   : âˆ€ {A B} â†’ G .Edge A B â†’ Exp A B
      idâ‚‘  : âˆ€ {A} â†’ Exp A A
      _â‹†â‚‘_ : âˆ€ {A B C} â†’ Exp A B â†’ Exp B C â†’ Exp A C
      â‹†â‚‘IdL : âˆ€ {A B} (e : Exp A B) â†’ idâ‚‘ â‹†â‚‘ e â‰¡ e
      â‹†â‚‘IdR : âˆ€ {A B} (e : Exp A B) â†’ e â‹†â‚‘ idâ‚‘ â‰¡ e
      â‹†â‚‘Assoc : âˆ€ {A B C D} (e : Exp A B)(f : Exp B C)(g : Exp C D)
              â†’ (e â‹†â‚‘ f) â‹†â‚‘ g â‰¡ e â‹†â‚‘ (f â‹†â‚‘ g)
      isSetExp : âˆ€ {A B} â†’ isSet (Exp A B)

    FreeCat : Category â„“g (â„“-max â„“g â„“g')
    FreeCat .ob = G .Node
    FreeCat .Hom[_,_] = Exp
    FreeCat .id = idâ‚‘
    FreeCat ._â‹†_ = _â‹†â‚‘_
    FreeCat .â‹†IdL = â‹†â‚‘IdL
    FreeCat .â‹†IdR = â‹†â‚‘IdR
    FreeCat .â‹†Assoc = â‹†â‚‘Assoc
    FreeCat .isSetHom = isSetExp

    Î· : Interp G FreeCat
    Î· ._$g_ = Î» z â†’ z
    Î· ._<$g>_ = â†‘_

    module Semantics {â„“c â„“c'} (ğ“’ : Category â„“c â„“c') (Ä± : GraphHom G (Ugr ğ“’)) where
      âŸ¦_âŸ§ : âˆ€ {A B} â†’ Exp A B â†’ ğ“’ [ Ä± $g A , Ä± $g B ]
      âŸ¦ â†‘ x âŸ§ = Ä± <$g> x
      âŸ¦ idâ‚‘ âŸ§ = ğ“’ .id
      âŸ¦ e â‹†â‚‘ e' âŸ§ = âŸ¦ e âŸ§ â‹†âŸ¨ ğ“’ âŸ© âŸ¦ e' âŸ§
      âŸ¦ â‹†â‚‘IdL e i âŸ§ = ğ“’ .â‹†IdL âŸ¦ e âŸ§ i
      âŸ¦ â‹†â‚‘IdR e i âŸ§ = ğ“’ .â‹†IdR âŸ¦ e âŸ§ i
      âŸ¦ â‹†â‚‘Assoc e e' e'' i âŸ§ = ğ“’ .â‹†Assoc âŸ¦ e âŸ§ âŸ¦ e' âŸ§ âŸ¦ e'' âŸ§ i
      âŸ¦ isSetExp e e' p q i j âŸ§ = ğ“’ .isSetHom âŸ¦ e âŸ§ âŸ¦ e' âŸ§ (cong âŸ¦_âŸ§ p) (cong âŸ¦_âŸ§ q) i j

      sem : Functor FreeCat ğ“’
      sem .Functor.F-ob v = Ä± $g v
      sem .Functor.F-hom e = âŸ¦ e âŸ§
      sem .Functor.F-id = refl
      sem .Functor.F-seq e e' = refl

      sem-extends-Ä± : (Î· â‹†Interp sem) â‰¡ Ä±
      sem-extends-Ä± = refl

      sem-uniq : âˆ€ {F : Functor FreeCat ğ“’} â†’ ((Uhom F âˆ˜GrHom Î·) â‰¡ Ä±) â†’ F â‰¡ sem
      sem-uniq {F} agree-on-generators = Functorâ‰¡ agree-on-objects agree-on-morphisms where
        agree-on-objects : âˆ€ v â†’ F âŸ… v âŸ† â‰¡ Ä± $g v
        agree-on-objects v i = agree-on-generators i $g v

        aom-type : âˆ€ {v w} â†’ (f : FreeCat [ v , w ]) â†’ Type _
        aom-type {v}{w} f = PathP (Î» i â†’ ğ“’ [ agree-on-objects v i , agree-on-objects w i ]) (F âŸª f âŸ«) âŸ¦ f âŸ§

        aom-id : âˆ€ {v} â†’ aom-type {v} idâ‚‘
        aom-id {v} = toPathPâ» (F .F-id âˆ™ fromPathPâ» (Î» i â†’ ğ“’ .id {agree-on-objects v i}))

        aom-seq : âˆ€ {v w x} â†’ (f : FreeCat [ v , w ]) (g : FreeCat [ w , x ])
                            â†’ aom-type f
                            â†’ aom-type g
                            â†’ aom-type (f â‹†â‚‘ g)
        aom-seq f g pf pg = toPathPâ» (F .F-seq f g âˆ™ fromPathPâ» (Î» i â†’ pf i â‹†âŸ¨ ğ“’ âŸ© pg i))

        agree-on-morphisms : âˆ€ {v w} â†’ (f : FreeCat [ v , w ]) â†’ aom-type f
        agree-on-morphisms (â†‘ x) = Î» i â†’ agree-on-generators i <$g> x
        agree-on-morphisms idâ‚‘ = aom-id
        agree-on-morphisms (f â‹†â‚‘ g) = aom-seq f g (agree-on-morphisms f) (agree-on-morphisms g)
        agree-on-morphisms (â‹†â‚‘IdL f i) j = isSetâ†’SquareP (Î» i j â†’ ğ“’ .isSetHom) (aom-seq idâ‚‘ f aom-id (agree-on-morphisms f)) (agree-on-morphisms f) (Î» i â†’ F âŸª â‹†â‚‘IdL f i âŸ«) (Î» i â†’ ğ“’ .â‹†IdL âŸ¦ f âŸ§ i) i j
        agree-on-morphisms (â‹†â‚‘IdR f i) j = isSetâ†’SquareP (Î» i j â†’ ğ“’ .isSetHom) (aom-seq f idâ‚‘ (agree-on-morphisms f) aom-id) (agree-on-morphisms f) (Î» i â†’ F âŸª â‹†â‚‘IdR f i âŸ«) (ğ“’ .â‹†IdR âŸ¦ f âŸ§) i j
        agree-on-morphisms (â‹†â‚‘Assoc f fâ‚ fâ‚‚ i) j = isSetâ†’SquareP (Î» i j â†’ ğ“’ .isSetHom) (aom-seq (f â‹†â‚‘ fâ‚) fâ‚‚ (aom-seq f fâ‚ (agree-on-morphisms f) (agree-on-morphisms fâ‚)) (agree-on-morphisms fâ‚‚)) (aom-seq f (fâ‚ â‹†â‚‘ fâ‚‚) (agree-on-morphisms f) (aom-seq fâ‚ fâ‚‚ (agree-on-morphisms fâ‚) (agree-on-morphisms fâ‚‚))) (Î» i â†’ F âŸª â‹†â‚‘Assoc f fâ‚ fâ‚‚ i âŸ«) (ğ“’ .â‹†Assoc âŸ¦ f âŸ§ âŸ¦ fâ‚ âŸ§ âŸ¦ fâ‚‚ âŸ§) i j
        agree-on-morphisms (isSetExp f g p q i j) k =
          isSetâ†’SquareP {A = Î» i j â†’ PathP (Î» k â†’ ğ“’ [ agree-on-objects _ k , agree-on-objects _ k ]) (F âŸª (isSetExp f g p q i j) âŸ«) (âŸ¦ (isSetExp f g p q i j) âŸ§)}
            (Î» i j â†’ isOfHLevelPathP
                       {A = Î» k â†’ ğ“’ [ agree-on-objects _ k , agree-on-objects _ k ]}
                       2 (ğ“’ .isSetHom) (F âŸª isSetExp f g p q i j âŸ«) âŸ¦ isSetExp f g p q i j âŸ§)
            (Î» j k â†’ agree-on-morphisms (p j) k)
            (Î» j k â†’ agree-on-morphisms (q j) k)
            (Î» i k â†’ agree-on-morphisms f k)
            (Î» i k â†’ agree-on-morphisms g k)
            i j k

      sem-contr : âˆƒ![ F âˆˆ Functor FreeCat ğ“’ ] Uhom F âˆ˜GrHom Î· â‰¡ Ä±
      sem-contr .fst = sem , sem-extends-Ä±
      sem-contr .snd (sem' , sem'-extends-Ä±) = Î£PathP paths
        where
          paths : Î£[ p âˆˆ sem â‰¡ sem' ] PathP (Î» i â†’ Uhom (p i) âˆ˜GrHom Î· â‰¡ Ä±) sem-extends-Ä± sem'-extends-Ä±
          paths .fst = sym (sem-uniq sem'-extends-Ä±)
          paths .snd i j = sem'-extends-Ä± ((~ i) âˆ¨ j)

    free-cat-functor-Î·-expansion : {ğ“’ : Category â„“c â„“c'} (F : Functor FreeCat ğ“’)
      â†’ F â‰¡ Semantics.sem ğ“’ (F âˆ˜Interp Î·)
    free-cat-functor-Î·-expansion F = Semantics.sem-uniq _ (F âˆ˜Interp Î·) refl

    free-cat-functor-ind : {ğ“’ : Category â„“c â„“c'} (F F' : Functor FreeCat ğ“’)
      â†’ (F âˆ˜Interp Î·) â‰¡ (F' âˆ˜Interp Î·)
      â†’ F â‰¡ F'
    free-cat-functor-ind {ğ“’ = ğ“’} F F' p =
      free-cat-functor-Î·-expansion F
      âˆ™ (cong (Semantics.sem ğ“’) p)
      âˆ™ sym (free-cat-functor-Î·-expansion F')

-- co-unit of the 2-adjunction
module _ {ğ“’ : Category â„“c â„“c'} where
  open FreeCategory (Ugr ğ“’)
  Îµ : Functor FreeCat ğ“’
  Îµ = Semantics.sem ğ“’ (Uhom {ğ““ = ğ“’} Id)

  Îµ-reasoning : {ğ““ : Category â„“d â„“d'}
            â†’ (ğ“• : Functor ğ“’ ğ““)
            â†’ ğ“• âˆ˜F Îµ â‰¡ Semantics.sem ğ““ (Uhom ğ“•)
  Îµ-reasoning {ğ““ = ğ““} ğ“• = Semantics.sem-uniq ğ““ (Uhom ğ“•) refl
