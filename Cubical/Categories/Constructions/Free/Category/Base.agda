-- Free category over a directed graph, along with (most of) its
-- universal property.

-- This differs from the implementation in Free.Category, which
-- assumes the vertices of the input graph form a Set.
{-# OPTIONS --safe #-}

module Cubical.Categories.Constructions.Free.Category.Base where

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.HLevels
open import Cubical.Foundations.Path

open import Cubical.Data.Graph.Base
open import Cubical.Data.Sigma

open import Cubical.Categories.Category.Base
open import Cubical.Categories.Functor.Base
open import Cubical.Categories.Morphism
open import Cubical.Categories.NaturalTransformation hiding (_âŸ¦_âŸ§)
open import Cubical.Categories.UnderlyingGraph

private
  variable
    â„“c â„“c' â„“d â„“d' â„“g â„“g' : Level

open Category
open Functor
open NatIso hiding (sqRL; sqLL)
open NatTrans

module FreeCategory (G : Graph â„“g â„“g') where
    data Exp : G .Node â†’ G .Node â†’ Type (â„“-max â„“g â„“g') where
      â†‘_   : âˆ€ {A B} â†’ G .Edge A B â†’ Exp A B
      idâ‚‘  : âˆ€ {A} â†’ Exp A A
      _â‹†â‚‘_ : âˆ€ {A B C} â†’ Exp A B â†’ Exp B C â†’ Exp A C
      â‹†â‚‘IdL : âˆ€ {A B} (e : Exp A B) â†’ idâ‚‘ â‹†â‚‘ e â‰¡ e
      â‹†â‚‘IdR : âˆ€ {A B} (e : Exp A B) â†’ e â‹†â‚‘ idâ‚‘ â‰¡ e
      â‹†â‚‘Assoc : âˆ€ {A B C D} (e : Exp A B)(f : Exp B C)(g : Exp C D)
              â†’ (e â‹†â‚‘ f) â‹†â‚‘ g â‰¡ e â‹†â‚‘ (f â‹†â‚‘ g)
      isSetExp : âˆ€ {A B} â†’ isSet (Exp A B)

    FreeCat : Category â„“g (â„“-max â„“g â„“g')
    FreeCat .ob = G .Node
    FreeCat .Hom[_,_] = Exp
    FreeCat .id = idâ‚‘
    FreeCat ._â‹†_ = _â‹†â‚‘_
    FreeCat .â‹†IdL = â‹†â‚‘IdL
    FreeCat .â‹†IdR = â‹†â‚‘IdR
    FreeCat .â‹†Assoc = â‹†â‚‘Assoc
    FreeCat .isSetHom = isSetExp

    Î· : Interp G FreeCat
    Î· ._$g_ = Î» z â†’ z
    Î· ._<$g>_ = â†‘_

    module _ {â„“c â„“c'} {ğ“’ : Category â„“c â„“c'} (F F' : Functor FreeCat ğ“’) where
      -- Formulating uniqueness this way works out best definitionally.

      -- If you prove induction from the alternative below of
      --   sem-uniq : (F âˆ˜Interp Î· â‰¡ Ä±) â†’ F â‰¡ sem Ä±
      -- then you have to use path comp which has bad definitional behavior
      module _  (agree-on-Î· : F âˆ˜Interp Î· â‰¡ F' âˆ˜Interp Î·) where
        private
          aoo : âˆ€ c â†’ F âŸ… c âŸ† â‰¡ F' âŸ… c âŸ†
          aoo = (Î» c i â†’ agree-on-Î· i $g c)

          aom-t : âˆ€ {c c'} (e : Exp c c') â†’ Type _
          aom-t {c}{c'} e =
            PathP (Î» i â†’ ğ“’ [ aoo c i , aoo c' i ]) (F âŸª e âŸ«) (F' âŸª e âŸ«)

          aom-id : âˆ€ {c} â†’ aom-t (idâ‚‘ {c})
          aom-id = F .F-id â— (Î» i â†’ ğ“’ .id) â–· sym (F' .F-id)

          aom-seq : âˆ€ {c c' c''} (e : Exp c c')(e' : Exp c' c'')
                  â†’ aom-t e â†’ aom-t e' â†’ aom-t (e â‹†â‚‘ e')
          aom-seq e e' ihe ihe' =
            F .F-seq e e' â— (Î» i â†’ ihe i â‹†âŸ¨ ğ“’ âŸ© ihe' i) â–· sym (F' .F-seq e e')

          aom : âˆ€ {c c'} (e : Exp c c') â†’ aom-t e
          aom (â†‘ x) = Î» i â†’ agree-on-Î· i <$g> x
          aom idâ‚‘ = aom-id
          aom (e â‹†â‚‘ e') = aom-seq e e' (aom e) (aom e')
          aom (â‹†â‚‘IdL e i) =
            isSetâ†’SquareP
              (Î» i j â†’ ğ“’ .isSetHom)
              (aom-seq idâ‚‘ e aom-id (aom e))
              (aom e)
              (Î» i â†’ F âŸª â‹†â‚‘IdL e i âŸ«) ((Î» i â†’ F' âŸª â‹†â‚‘IdL e i âŸ«)) i
          aom (â‹†â‚‘IdR e i) =
            isSetâ†’SquareP
            (Î» i j â†’ ğ“’ .isSetHom)
            (aom-seq e idâ‚‘ (aom e) aom-id)
            (aom e)
            (Î» i â†’ F âŸª â‹†â‚‘IdR e i âŸ«) ((Î» i â†’ F' âŸª â‹†â‚‘IdR e i âŸ«)) i
          aom (â‹†â‚‘Assoc e e' e'' i) =
            isSetâ†’SquareP
            (Î» _ _ â†’ ğ“’ .isSetHom)
            (aom-seq _ _ (aom-seq _ _ (aom e) (aom e')) (aom e''))
            (aom-seq _ _ (aom e) (aom-seq _ _ (aom e') (aom e'')))
            ((Î» i â†’ F âŸª â‹†â‚‘Assoc e e' e'' i âŸ«))
            (Î» i â†’ F' âŸª â‹†â‚‘Assoc e e' e'' i âŸ«)
            i
          aom (isSetExp e e' x y i j) =
            isSetâ†’SquareP
            {A = Î» i j â†’ aom-t (isSetExp e e' x y i j)}
            (Î» i j â†’ isOfHLevelPathP 2 (ğ“’ .isSetHom)
                       (F âŸª isSetExp e e' x y i j âŸ«)
                       (F' âŸª isSetExp e e' x y i j âŸ«))
            (Î» j â†’ aom (x j))
            (Î» j â†’ aom (y j))
            (Î» i â†’ aom e)
            (Î» i â†’ aom e')
            i
            j
        induction : F â‰¡ F'
        induction = Functorâ‰¡ aoo aom

    module Semantics {â„“c â„“c'}
                     (ğ“’ : Category â„“c â„“c')
                     (Ä± : GraphHom G (Catâ†’Graph ğ“’)) where
      âŸ¦_âŸ§ : âˆ€ {A B} â†’ Exp A B â†’ ğ“’ [ Ä± $g A , Ä± $g B ]
      âŸ¦ â†‘ x âŸ§ = Ä± <$g> x
      âŸ¦ idâ‚‘ âŸ§ = ğ“’ .id
      âŸ¦ e â‹†â‚‘ e' âŸ§ = âŸ¦ e âŸ§ â‹†âŸ¨ ğ“’ âŸ© âŸ¦ e' âŸ§
      âŸ¦ â‹†â‚‘IdL e i âŸ§ = ğ“’ .â‹†IdL âŸ¦ e âŸ§ i
      âŸ¦ â‹†â‚‘IdR e i âŸ§ = ğ“’ .â‹†IdR âŸ¦ e âŸ§ i
      âŸ¦ â‹†â‚‘Assoc e e' e'' i âŸ§ = ğ“’ .â‹†Assoc âŸ¦ e âŸ§ âŸ¦ e' âŸ§ âŸ¦ e'' âŸ§ i
      âŸ¦ isSetExp e e' p q i j âŸ§ =
        ğ“’ .isSetHom âŸ¦ e âŸ§ âŸ¦ e' âŸ§ (cong âŸ¦_âŸ§ p) (cong âŸ¦_âŸ§ q) i j

      sem : Functor FreeCat ğ“’
      sem .Functor.F-ob v = Ä± $g v
      sem .Functor.F-hom e = âŸ¦ e âŸ§
      sem .Functor.F-id = refl
      sem .Functor.F-seq e e' = refl

      sem-extends-Ä± : (Î· â‹†Interp sem) â‰¡ Ä±
      sem-extends-Ä± = refl

      sem-uniq : âˆ€ {F : Functor FreeCat ğ“’} â†’ ((Functorâ†’GraphHom F âˆ˜GrHom Î·) â‰¡ Ä±) â†’ F â‰¡ sem
      sem-uniq {F} aog = induction F sem aog

      sem-contr : âˆƒ![ F âˆˆ Functor FreeCat ğ“’ ] Functorâ†’GraphHom F âˆ˜GrHom Î· â‰¡ Ä±
      sem-contr .fst = sem , sem-extends-Ä±
      sem-contr .snd (sem' , sem'-extends-Ä±) = Î£PathP paths
        where
          paths : Î£[ p âˆˆ sem â‰¡ sem' ]
                  PathP (Î» i â†’ Functorâ†’GraphHom (p i) âˆ˜GrHom Î· â‰¡ Ä±)
                        sem-extends-Ä±
                        sem'-extends-Ä±
          paths .fst = sym (sem-uniq sem'-extends-Ä±)
          paths .snd i j = sem'-extends-Ä± ((~ i) âˆ¨ j)

    Î·-expansion : {ğ“’ : Category â„“c â„“c'} (F : Functor FreeCat ğ“’)
      â†’ F â‰¡ Semantics.sem ğ“’ (F âˆ˜Interp Î·)
    Î·-expansion {ğ“’ = ğ“’} F = induction F (Semantics.sem ğ“’ (F âˆ˜Interp Î·)) refl

-- co-unit of the 2-adjunction
module _ {ğ“’ : Category â„“c â„“c'} where
  open FreeCategory (Catâ†’Graph ğ“’)
  Îµ : Functor FreeCat ğ“’
  Îµ = Semantics.sem ğ“’ (Functorâ†’GraphHom {ğ““ = ğ“’} Id)

  Îµ-reasoning : {ğ““ : Category â„“d â„“d'}
            â†’ (ğ“• : Functor ğ“’ ğ““)
            â†’ ğ“• âˆ˜F Îµ â‰¡ Semantics.sem ğ““ (Functorâ†’GraphHom ğ“•)
  Îµ-reasoning {ğ““ = ğ““} ğ“• = Semantics.sem-uniq ğ““ (Functorâ†’GraphHom ğ“•) refl
