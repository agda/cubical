{-# OPTIONS --safe #-}
module Cubical.Categories.Constructions.FullSubcategory where
-- Full subcategory (not necessarily injective on objects)

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.Function

open import Cubical.Categories.Category
open import Cubical.Categories.Functor renaming (ğŸ™âŸ¨_âŸ© to funcId)

private
  variable
    â„“C â„“C' â„“D â„“D' â„“E â„“E' â„“P â„“Q â„“R : Level

module _ (C : Category â„“C â„“C') (P : Category.ob C â†’ Type â„“P) where
  private
    module C = Category C
  open Category
  open Functor

  FullSubcategory : Category (â„“-max â„“C â„“P) â„“C'
  ob FullSubcategory = Î£[ x âˆˆ C.ob ] P x
  Hom[_,_] FullSubcategory (x , p) (y , q) = C.Hom[ x , y ]
  id FullSubcategory = C.id
  _â‹†_ FullSubcategory = C._â‹†_
  â‹†IdL FullSubcategory = C.â‹†IdL
  â‹†IdR FullSubcategory = C.â‹†IdR
  â‹†Assoc FullSubcategory = C.â‹†Assoc
  isSetHom FullSubcategory = C.isSetHom

  FullInclusion : Functor FullSubcategory C
  F-ob FullInclusion = fst
  F-hom FullInclusion = idfun _
  F-id FullInclusion = refl
  F-seq FullInclusion f g = refl

module _ (C : Category â„“C â„“C')
         (D : Category â„“D â„“D') (Q : Category.ob D â†’ Type â„“Q) where
  private
    module C = Category C
    module D = Category D
  open Category
  open Functor

  ToFullSubcategory  : (F : Functor C D) â†’ ((c : C.ob) â†’ Q (F-ob F c)) â†’ Functor C (FullSubcategory D Q)
  F-ob (ToFullSubcategory F f) c = F-ob F c , f c
  F-hom (ToFullSubcategory F f) = F-hom F
  F-id (ToFullSubcategory F f) = F-id F
  F-seq (ToFullSubcategory F f) = F-seq F

module _ (C : Category â„“C â„“C') (P : Category.ob C â†’ Type â„“P)
         (D : Category â„“D â„“D') (Q : Category.ob D â†’ Type â„“Q) where
  private
    module C = Category C
    module D = Category D
  open Category
  open Functor

  MapFullSubcategory : (F : Functor C D) â†’ ((c : C.ob) â†’ P c â†’ Q (F-ob F c))
    â†’ Functor (FullSubcategory C P) (FullSubcategory D Q)
  MapFullSubcategory F f = ToFullSubcategory (FullSubcategory C P) D Q
    (funcComp F (FullInclusion C P) )
    Î» (c , p) â†’ f c p

module _ (C : Category â„“C â„“C') (P : Category.ob C â†’ Type â„“P) where
  private
    module C = Category C
  open Category
  open Functor

  MapFullSubcategory-id :
    MapFullSubcategory C P C P (funcId C) (Î» c p â†’ p) â‰¡ funcId (FullSubcategory C P)
  MapFullSubcategory-id = Functorâ‰¡
    (Î» (c , p) â†’ refl)
    (Î» Î³ â†’ refl)

module _ (C : Category â„“C â„“C') (P : Category.ob C â†’ Type â„“P)
         (D : Category â„“D â„“D') (Q : Category.ob D â†’ Type â„“Q)
         (E : Category â„“E â„“E') (R : Category.ob E â†’ Type â„“R) where
  private
    module C = Category C
    module D = Category D
    module E = Category E
  open Category
  open Functor

  MapFullSubcategory-seq :
    (F : Functor C D) â†’ (f : (c : C.ob) â†’ P c â†’ Q (F-ob F c)) â†’
    (G : Functor D E) â†’ (g : (d : D.ob) â†’ Q d â†’ R (F-ob G d)) â†’
    MapFullSubcategory C P E R (funcComp G F) (Î» c p â†’ g (F-ob F c) (f c p)) â‰¡
    funcComp
      (MapFullSubcategory D Q E R G g)
      (MapFullSubcategory C P D Q F f)
  MapFullSubcategory-seq F f G g = Functorâ‰¡
    (Î» (c , p) â†’ refl)
    (Î» Î³ â†’ refl)
