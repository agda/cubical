{-# OPTIONS --safe #-}
module Cubical.Categories.Functor.Base where

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.Equiv
open import Cubical.Foundations.HLevels

open import Cubical.Data.Sigma

open import Cubical.Categories.Category

private
  variable
    â„“C â„“C' â„“D â„“D' : Level

record Functor (C : Category â„“C â„“C') (D : Category â„“D â„“D') :
         Type (â„“-max (â„“-max â„“C â„“C') (â„“-max â„“D â„“D')) where
  no-eta-equality

  open Category

  field
    F-ob  : C .ob â†’ D .ob
    F-hom : {x y : C .ob} â†’ C [ x , y ] â†’ D [ F-ob x , F-ob y ]
    F-id  : {x : C .ob} â†’ F-hom (C .id) â‰¡ D .id {x = F-ob x}
    F-seq : {x y z : C .ob} (f : C [ x , y ]) (g : C [ y , z ])
          â†’ F-hom (f â‹†âŸ¨ C âŸ© g) â‰¡ (F-hom f) â‹†âŸ¨ D âŸ© (F-hom g)

  isFull = (x y : _) (F[f] : D [ F-ob x , F-ob y ]) â†’ âˆƒ[ f âˆˆ C [ x , y ] ] F-hom f â‰¡ F[f]
  isFaithful = (x y : _) (f g : C [ x , y ]) â†’ F-hom f â‰¡ F-hom g â†’ f â‰¡ g
  isFullyFaithful = (x y : _) â†’ isEquiv (F-hom {x = x} {y = y})
  isEssentiallySurj = (d : D .ob) â†’ âˆƒ[ c âˆˆ C .ob ] CatIso D (F-ob c) d

  -- preservation of commuting squares and triangles
  F-square : {x y u v : C .ob}
             {f : C [ x , y ]} {g : C [ x , u ]}
             {h : C [ u , v ]} {k : C [ y , v ]}
           â†’ f â‹†âŸ¨ C âŸ© k â‰¡ g â‹†âŸ¨ C âŸ© h
           â†’ (F-hom f) â‹†âŸ¨ D âŸ© (F-hom k) â‰¡ (F-hom g) â‹†âŸ¨ D âŸ© (F-hom h)
  F-square Csquare = sym (F-seq _ _) âˆ™âˆ™ cong F-hom Csquare âˆ™âˆ™ F-seq _ _

  F-triangle : {x y z : C .ob}
               {f : C [ x , y ]} {g : C [ y , z ]} {h : C [ x , z ]}
             â†’ f â‹†âŸ¨ C âŸ© g â‰¡ h
             â†’ (F-hom f) â‹†âŸ¨ D âŸ© (F-hom g) â‰¡ (F-hom h)
  F-triangle Ctriangle = sym (F-seq _ _) âˆ™ cong F-hom Ctriangle

private
  variable
    â„“ â„“' : Level
    B C D E : Category â„“ â„“'

open Category
open Functor

Functorâ‰¡ : {F G : Functor C D}
         â†’ (h : âˆ€ (c : ob C) â†’ F-ob F c â‰¡ F-ob G c)
         â†’ (âˆ€ {c c' : ob C} (f : C [ c , c' ]) â†’ PathP (Î» i â†’ D [ h c i , h c' i ]) (F-hom F f) (F-hom G f))
         â†’ F â‰¡ G
F-ob (Functorâ‰¡ hOb hHom i) c = hOb c i
F-hom (Functorâ‰¡ hOb hHom i) f = hHom f i
F-id (Functorâ‰¡ {C = C} {D = D} {F = F} {G = G} hOb hHom i) =
  isPropâ†’PathP (Î» j â†’ isSetHom D (hHom (C .id) j) (D .id)) (F-id F) (F-id G) i
F-seq (Functorâ‰¡ {C = C} {D = D} {F = F} {G = G} hOb hHom i) f g =
  isPropâ†’PathP (Î» j â†’ isSetHom D (hHom (f â‹†âŸ¨ C âŸ© g) j) ((hHom f j) â‹†âŸ¨ D âŸ© (hHom g j))) (F-seq F f g) (F-seq G f g) i

FunctorSquare :
  {Fâ‚€â‚€ Fâ‚€â‚ Fâ‚â‚€ Fâ‚â‚ : Functor C D}
  (Fâ‚€â‚‹ : Fâ‚€â‚€ â‰¡ Fâ‚€â‚) (Fâ‚â‚‹ : Fâ‚â‚€ â‰¡ Fâ‚â‚)
  (Fâ‚‹â‚€ : Fâ‚€â‚€ â‰¡ Fâ‚â‚€) (Fâ‚‹â‚ : Fâ‚€â‚ â‰¡ Fâ‚â‚)
  â†’ Square (cong F-ob Fâ‚€â‚‹) (cong F-ob Fâ‚â‚‹) (cong F-ob Fâ‚‹â‚€) (cong F-ob Fâ‚‹â‚)
  â†’ Square Fâ‚€â‚‹ Fâ‚â‚‹ Fâ‚‹â‚€ Fâ‚‹â‚
FunctorSquare {C = C} {D = D} Fâ‚€â‚‹ Fâ‚â‚‹ Fâ‚‹â‚€ Fâ‚‹â‚ r = sqr
  where
  sqr : _
  sqr i j .F-ob = r i j
  sqr i j .F-hom {x = x} {y = y} f =
    isSetâ†’SquareP (Î» i j â†’ D .isSetHom {x = sqr i j .F-ob x} {y = sqr i j .F-ob y})
    (Î» i â†’ Fâ‚€â‚‹ i .F-hom f) (Î» i â†’ Fâ‚â‚‹ i .F-hom f) (Î» i â†’ Fâ‚‹â‚€ i .F-hom f) (Î» i â†’ Fâ‚‹â‚ i .F-hom f) i j
  sqr i j .F-id {x = x} =
    isSetâ†’SquareP (Î» i j â†’ isPropâ†’isSet (D .isSetHom (sqr i j .F-hom (C .id)) (D .id)))
    (Î» i â†’ Fâ‚€â‚‹ i .F-id) (Î» i â†’ Fâ‚â‚‹ i .F-id) (Î» i â†’ Fâ‚‹â‚€ i .F-id) (Î» i â†’ Fâ‚‹â‚ i .F-id) i j
  sqr i j .F-seq f g =
    isSetâ†’SquareP (Î» i j â†’
      isPropâ†’isSet (D .isSetHom (sqr i j .F-hom (f â‹†âŸ¨ C âŸ© g)) ((sqr i j .F-hom f) â‹†âŸ¨ D âŸ© (sqr i j .F-hom g))))
    (Î» i â†’ Fâ‚€â‚‹ i .F-seq f g) (Î» i â†’ Fâ‚â‚‹ i .F-seq f g) (Î» i â†’ Fâ‚‹â‚€ i .F-seq f g) (Î» i â†’ Fâ‚‹â‚ i .F-seq f g) i j

FunctorPathâ‰¡ : {F G : Functor C D}{p q : F â‰¡ G} â†’ cong F-ob p â‰¡ cong F-ob q â†’ p â‰¡ q
FunctorPathâ‰¡ {p = p} {q = q} = FunctorSquare p q refl refl


-- Helpful notation

-- action on objects
infix 30 _âŸ…_âŸ†
_âŸ…_âŸ† : (F : Functor C D)
     â†’ C .ob
     â†’ D .ob
_âŸ…_âŸ† = F-ob

-- action on morphisms
infix 30 _âŸª_âŸ« -- same infix level as on objects since these will never be used in the same context
_âŸª_âŸ« : (F : Functor C D)
     â†’ âˆ€ {x y}
     â†’ C [ x , y ]
     â†’ D [(F âŸ… x âŸ†) , (F âŸ… y âŸ†)]
_âŸª_âŸ« = F-hom


-- Functor constructions

ğŸ™âŸ¨_âŸ© : âˆ€ (C : Category â„“ â„“') â†’ Functor C C
ğŸ™âŸ¨ C âŸ© .F-ob x    = x
ğŸ™âŸ¨ C âŸ© .F-hom f   = f
ğŸ™âŸ¨ C âŸ© .F-id      = refl
ğŸ™âŸ¨ C âŸ© .F-seq _ _ = refl

Id : {C : Category â„“ â„“'} â†’ Functor C C
Id = ğŸ™âŸ¨ _ âŸ©


-- functor composition
funcComp : âˆ€ (G : Functor D E) (F : Functor C D) â†’ Functor C E
(funcComp G F) .F-ob c    = G âŸ… F âŸ… c âŸ† âŸ†
(funcComp G F) .F-hom f   = G âŸª F âŸª f âŸ« âŸ«
(funcComp G F) .F-id      = cong (G âŸª_âŸ«) (F .F-id) âˆ™ G .F-id
(funcComp G F) .F-seq f g = cong (G âŸª_âŸ«) (F .F-seq _ _) âˆ™ G .F-seq _ _

infixr 30 funcComp
syntax funcComp G F = G âˆ˜F F

-- hacky lemma to stop Agda from computing too much
funcCompObâ‰¡ : âˆ€ (G : Functor D E) (F : Functor C D) (c : ob C)
            â†’ funcComp G F .F-ob c â‰¡ G .F-ob (F .F-ob c)
funcCompObâ‰¡ G F c = refl

_^opF : Functor C D â†’ Functor (C ^op) (D ^op)
(F ^opF) .F-ob      = F .F-ob
(F ^opF) .F-hom     = F .F-hom
(F ^opF) .F-id      = F .F-id
(F ^opF) .F-seq f g = F .F-seq g f
