{-
  Given a type A with a UARel and a family B over A,
  a SubstRel on B is a family of functions a â‰… a' â†’ B a â‰ƒ B a' path-equal to transport in that family.

  Any SubstRel gives rise to a DUARel in which b and b' are related over p when the transport of b along p is
  equial to b'.
-}

{-# OPTIONS --safe #-}
module Cubical.Displayed.Subst where

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.Equiv
open import Cubical.Foundations.Isomorphism
open import Cubical.Foundations.Path
open import Cubical.Foundations.Transport
open import Cubical.Foundations.Univalence

open import Cubical.Displayed.Base

private
  variable
    â„“A â„“â‰…A â„“B : Level

record SubstRel {A : Type â„“A} {â„“â‰…A : Level} (ğ’®-A : UARel A â„“â‰…A) (B : A â†’ Type â„“B)
  : Type (â„“-max (â„“-max â„“A â„“B) â„“â‰…A)
  where

  no-eta-equality
  constructor substrel
  open UARel ğ’®-A

  field
    act : {a a' : A} â†’ a â‰… a' â†’ B a â‰ƒ B a'
    uaË¢ : {a a' : A} (p : a â‰… a') (b : B a) â†’ subst B (â‰…â†’â‰¡ p) b â‰¡ equivFun (act p) b

  uaË¢â» : {a a' : A} (p : a â‰… a') (b : B a') â†’ subst B (sym (â‰…â†’â‰¡ p)) b â‰¡ invEq (act p) b
  uaË¢â» p b =
    subst B (sym (â‰…â†’â‰¡ p)) b
      â‰¡âŸ¨ cong (subst B (sym (â‰…â†’â‰¡ p))) (sym (secEq (act p) b)) âŸ©
    subst B (sym (â‰…â†’â‰¡ p)) (equivFun (act p) (invEq (act p) b))
      â‰¡âŸ¨ cong (subst B (sym (â‰…â†’â‰¡ p))) (sym (uaË¢ p (invEq (act p) b))) âŸ©
    subst B (sym (â‰…â†’â‰¡ p)) (subst B (â‰…â†’â‰¡ p) (invEq (act p) b))
      â‰¡âŸ¨ pathToIso (cong B (â‰…â†’â‰¡ p)) .Iso.leftInv (invEq (act p) b) âŸ©
    invEq (act p) b
    âˆ

Substâ†’DUA : {A : Type â„“A} {â„“â‰…A : Level} {ğ’®-A : UARel A â„“â‰…A} {B : A â†’ Type â„“B}
  â†’ SubstRel ğ’®-A B â†’ DUARel ğ’®-A B â„“B
DUARel._â‰…á´°âŸ¨_âŸ©_ (Substâ†’DUA ğ’®Ë¢-B) b p b' =
  equivFun (SubstRel.act ğ’®Ë¢-B p) b â‰¡ b'
DUARel.uaá´° (Substâ†’DUA {ğ’®-A = ğ’®-A} {B = B} ğ’®Ë¢-B) b p b' =
  equivFun (SubstRel.act ğ’®Ë¢-B p) b â‰¡ b'
    â‰ƒâŸ¨ invEquiv (compPathlEquiv (sym (SubstRel.uaË¢ ğ’®Ë¢-B p b))) âŸ©
  subst B (â‰…â†’â‰¡ p) b â‰¡ b'
    â‰ƒâŸ¨ invEquiv (PathPâ‰ƒPath (Î» i â†’ B (â‰…â†’â‰¡ p i)) b b') âŸ©
  PathP (Î» i â†’ B (â‰…â†’â‰¡ p i)) b b'
  â– 
  where
  open UARel ğ’®-A
