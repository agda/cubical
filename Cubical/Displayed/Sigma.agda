{-

  Functions building UARels and DUARels on Î£-types

-}
{-# OPTIONS --safe #-}
module Cubical.Displayed.Sigma where

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.Equiv

open import Cubical.Data.Sigma

open import Cubical.Displayed.Base
open import Cubical.Displayed.Subst
open import Cubical.Displayed.Morphism
open import Cubical.Displayed.Constant

private
  variable
    â„“ â„“A â„“A' â„“P â„“â‰…A â„“â‰…A' â„“B â„“B' â„“â‰…B â„“â‰…B' â„“C â„“â‰…C : Level

-- UARel on a Î£-type

âˆ«Ë¢ : {A : Type â„“A} {ğ’®-A : UARel A â„“â‰…A} {B : A â†’ Type â„“B} (ğ’®Ë¢-B : SubstRel ğ’®-A B)
  â†’ UARel (Î£ A B) (â„“-max â„“â‰…A â„“B)
âˆ«Ë¢ ğ’®Ë¢-B = âˆ« (Substâ†’DUA ğ’®Ë¢-B)

_Ã—ğ’®_ : {A : Type â„“A} (ğ’®-A : UARel A â„“â‰…A) {B : Type â„“B} (ğ’®-B : UARel B â„“â‰…B)
  â†’ UARel (A Ã— B) (â„“-max â„“â‰…A â„“â‰…B)
ğ’®-A Ã—ğ’® ğ’®-B = âˆ« (ğ’®á´°-const ğ’®-A ğ’®-B)

-- Projection UARel morphisms

ğ’®-fst : {A : Type â„“A} {ğ’®-A : UARel A â„“â‰…A} {B : A â†’ Type â„“B} {ğ’®á´°-B : DUARel ğ’®-A B â„“â‰…B}
  â†’ UARelHom (âˆ« ğ’®á´°-B) ğ’®-A
ğ’®-fst .UARelHom.fun = fst
ğ’®-fst .UARelHom.rel = fst
ğ’®-fst .UARelHom.ua p = refl

ğ’®-snd : {A : Type â„“A} {ğ’®-A : UARel A â„“â‰…A} {B : Type â„“B} {ğ’®-B : UARel B â„“â‰…B}
  â†’ UARelHom (ğ’®-A Ã—ğ’® ğ’®-B) ğ’®-B
ğ’®-snd .UARelHom.fun = snd
ğ’®-snd .UARelHom.rel = snd
ğ’®-snd .UARelHom.ua p = refl

-- Lift a DUARel to live over a Î£-type

ğ’®á´°-Lift : {A : Type â„“A} (ğ’®-A : UARel A â„“â‰…A) {B : A â†’ Type â„“B} (ğ’®á´°-B : DUARel ğ’®-A B â„“â‰…B)
  {C : A â†’ Type â„“C} (ğ’®á´°-C : DUARel ğ’®-A C â„“â‰…C)
  â†’ DUARel (âˆ« ğ’®á´°-C) (Î» (a , _) â†’ B a) â„“â‰…B
ğ’®á´°-Lift _  ğ’®á´°-B _ = ğ’®á´°-reindex ğ’®-fst ğ’®á´°-B

-- DUARel on a Î£-type

module _ {A : Type â„“A} {ğ’®-A : UARel A â„“â‰…A}
  {B : A â†’ Type â„“B} {â„“â‰…B : Level} (ğ’®á´°-B : DUARel ğ’®-A B â„“â‰…B)
  {C : Î£ A B â†’ Type â„“C} {â„“â‰…C : Level} (ğ’®á´°-C : DUARel (âˆ« ğ’®á´°-B) C â„“â‰…C)
  where

  open UARel ğ’®-A
  private
    module B = DUARel ğ’®á´°-B
    module C = DUARel ğ’®á´°-C

  ğ’®á´°-Î£ : DUARel ğ’®-A (Î» a â†’ Î£[ b âˆˆ B a ] C (a , b)) (â„“-max â„“â‰…B â„“â‰…C)
  DUARel._â‰…á´°âŸ¨_âŸ©_ ğ’®á´°-Î£ (b , c) p (b' , c') =
    Î£[ q âˆˆ b B.â‰…á´°âŸ¨ p âŸ© b' ]  (c C.â‰…á´°âŸ¨ p , q âŸ© c')
  DUARel.uaá´° ğ’®á´°-Î£ (b ,  c) p (b' , c') =
    compEquiv
      (Î£-cong-equiv (B.uaá´° b p b') (Î» q â†’ C.uaá´° c (p , q) c'))
      Î£Pathâ‰ƒPathÎ£

-- DUARel on a non-dependent Î£-type

module _ {A : Type â„“A} {ğ’®-A : UARel A â„“â‰…A}
  {B : A â†’ Type â„“B} {â„“â‰…B : Level} (ğ’®á´°-B : DUARel ğ’®-A B â„“â‰…B)
  {C : A â†’ Type â„“C} {â„“â‰…C : Level} (ğ’®á´°-C : DUARel ğ’®-A C â„“â‰…C)
  where

  _Ã—ğ’®á´°_ : DUARel ğ’®-A (Î» a â†’ B a Ã— C a) (â„“-max â„“â‰…B â„“â‰…C)
  _Ã—ğ’®á´°_ = ğ’®á´°-Î£ ğ’®á´°-B (ğ’®á´°-Lift ğ’®-A ğ’®á´°-C ğ’®á´°-B)

-- SubstRel on a Î£-type

module _ {A : Type â„“A} {ğ’®-A : UARel A â„“â‰…A}
  {B : A â†’ Type â„“B} (ğ’®Ë¢-B : SubstRel ğ’®-A B)
  {C : Î£ A B â†’ Type â„“C} (ğ’®Ë¢-C : SubstRel (âˆ«Ë¢ ğ’®Ë¢-B) C)
  where

  open UARel ğ’®-A
  open SubstRel
  private
    module B = SubstRel ğ’®Ë¢-B
    module C = SubstRel ğ’®Ë¢-C

  ğ’®Ë¢-Î£ : SubstRel ğ’®-A (Î» a â†’ Î£[ b âˆˆ B a ] C (a , b))
  ğ’®Ë¢-Î£ .act p = Î£-cong-equiv (B.act p) (Î» b â†’ C.act (p , refl))
  ğ’®Ë¢-Î£ .uaË¢ p _ =
    fromPathP
      (DUARel.uaá´° (ğ’®á´°-Î£ (Substâ†’DUA ğ’®Ë¢-B) (Substâ†’DUA ğ’®Ë¢-C))  _ p _ .fst (refl , refl))

-- SubstRel on a non-dependent product

module _ {A : Type â„“A} {ğ’®-A : UARel A â„“â‰…A}
  {B : A â†’ Type â„“B} (ğ’®Ë¢-B : SubstRel ğ’®-A B)
  {C : A â†’ Type â„“C} (ğ’®Ë¢-C : SubstRel ğ’®-A C)
  where

  open UARel ğ’®-A
  open SubstRel
  private
    module B = SubstRel ğ’®Ë¢-B
    module C = SubstRel ğ’®Ë¢-C

  _Ã—ğ’®Ë¢_ : SubstRel ğ’®-A (Î» a â†’ B a Ã— C a)
  _Ã—ğ’®Ë¢_ .act p = â‰ƒ-Ã— (B.act p) (C.act p)
  _Ã—ğ’®Ë¢_ .uaË¢ p (b , c) = Î£PathP (B.uaË¢ p b , C.uaË¢ p c)
