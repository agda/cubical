{-

  Definition of univalent and displayed univalent relations

-}
{-# OPTIONS --safe #-}
module Cubical.Displayed.Base where

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.Isomorphism
open import Cubical.Foundations.Equiv
open import Cubical.Foundations.Transport

open import Cubical.Data.Sigma

open import Cubical.Relation.Binary

private
  variable
    â„“ â„“A â„“A' â„“â‰…A â„“B â„“B' â„“â‰…B â„“C â„“â‰…C : Level

record UARel (A : Type â„“A) (â„“â‰…A : Level) : Type (â„“-max â„“A (â„“-suc â„“â‰…A)) where
  no-eta-equality
  constructor uarel
  field
    _â‰…_ : A â†’ A â†’ Type â„“â‰…A
    ua : (a a' : A) â†’ (a â‰… a') â‰ƒ (a â‰¡ a')

  uaIso : (a a' : A) â†’ Iso (a â‰… a') (a â‰¡ a')
  uaIso a a' = equivToIso (ua a a')

  â‰…â†’â‰¡ : {a a' : A} (p : a â‰… a') â†’ a â‰¡ a'
  â‰…â†’â‰¡ {a} {a'} = Iso.fun (uaIso a a')
  â‰¡â†’â‰… : {a a' : A} (p : a â‰¡ a') â†’ a â‰… a'
  â‰¡â†’â‰… {a} {a'} = Iso.inv (uaIso a a')

  Ï : (a : A) â†’ a â‰… a
  Ï a = â‰¡â†’â‰… refl

open BinaryRelation

-- another constructor for UARel using contractibility of relational singletons
make-ğ’® : {A : Type â„“A} {_â‰…_ : A â†’ A â†’ Type â„“â‰…A}
          (Ï : isRefl _â‰…_) (contrTotal : contrRelSingl _â‰…_) â†’ UARel A â„“â‰…A
UARel._â‰…_ (make-ğ’® {_â‰…_ = _â‰…_} _ _) = _â‰…_
UARel.ua (make-ğ’® {_â‰…_ = _â‰…_} Ï c) = contrRelSinglâ†’isUnivalent _â‰…_ Ï c

record DUARel {A : Type â„“A} (ğ’®-A : UARel A â„“â‰…A)
              (B : A â†’ Type â„“B) (â„“â‰…B : Level) : Type (â„“-max (â„“-max â„“A â„“B) (â„“-max â„“â‰…A (â„“-suc â„“â‰…B))) where
  no-eta-equality
  constructor duarel
  open UARel ğ’®-A

  field
    _â‰…á´°âŸ¨_âŸ©_ : {a a' : A} â†’ B a â†’ a â‰… a' â†’ B a' â†’ Type â„“â‰…B
    uaá´° : {a a' : A} (b : B a) (p : a â‰… a') (b' : B a') â†’ (b â‰…á´°âŸ¨ p âŸ© b') â‰ƒ PathP (Î» i â†’ B (â‰…â†’â‰¡ p i)) b b'

  fiberRel : (a : A) â†’ Rel (B a) (B a) â„“â‰…B
  fiberRel a = _â‰…á´°âŸ¨ Ï a âŸ©_

  uaá´°Ï : {a : A} (b b' : B a) â†’ b â‰…á´°âŸ¨ Ï a âŸ© b' â‰ƒ (b â‰¡ b')
  uaá´°Ï {a} b b' =
    compEquiv
      (uaá´° b (Ï _) b')
      (substEquiv (Î» q â†’ PathP (Î» i â†’ B (q i)) b b') (secEq (ua a a) refl))

  Ïá´° : {a : A} â†’ (b : B a) â†’ b â‰…á´°âŸ¨ Ï a âŸ© b
  Ïá´° {a} b = invEq (uaá´°Ï b b) refl


-- total UARel induced by a DUARel

module _ {A : Type â„“A} {ğ’®-A : UARel A â„“â‰…A}
  {B : A â†’ Type â„“B} {â„“â‰…B : Level}
  (ğ’®á´°-B : DUARel ğ’®-A B â„“â‰…B)
  where

  open UARel ğ’®-A
  open DUARel ğ’®á´°-B

  âˆ« : UARel (Î£ A B) (â„“-max â„“â‰…A â„“â‰…B)
  UARel._â‰…_ âˆ« (a , b) (a' , b') = Î£[ p âˆˆ a â‰… a' ] (b â‰…á´°âŸ¨ p âŸ© b')
  UARel.ua âˆ« (a , b) (a' , b') =
    compEquiv
      (Î£-cong-equiv (ua a a') (Î» p â†’ uaá´° b p b'))
      Î£Pathâ‰ƒPathÎ£

