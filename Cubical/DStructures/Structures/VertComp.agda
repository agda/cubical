{-
This module contains
- the type of vertical composition operations
  that can be defined on a reflexive graph


-}
{-# OPTIONS --cubical --no-import-sorts --safe #-}
module Cubical.DStructures.Structures.VertComp where

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.Equiv
open import Cubical.Foundations.HLevels
open import Cubical.Foundations.Isomorphism
open import Cubical.Functions.FunExtEquiv

open import Cubical.Homotopy.Base

open import Cubical.Data.Sigma

open import Cubical.Relation.Binary

open import Cubical.Algebra.Group
open import Cubical.Structures.LeftAction

open import Cubical.DStructures.Base
open import Cubical.DStructures.Meta.Properties
open import Cubical.DStructures.Structures.Constant
open import Cubical.DStructures.Meta.Combine
open import Cubical.DStructures.Structures.Type
open import Cubical.DStructures.Structures.Group
open import Cubical.DStructures.Structures.ReflGraph

open MorphismLemmas
open GroupLemmas

private
  variable
    â„“ â„“' : Level

{-
-- The type of vertical composition operations
-- that can be defined over a reflexive graph ğ’¢
--

-- we use the property isComposable instead of defining
-- a type of composable morphisms of Gâ‚, because
-- otherwise it would be difficult to formulate
-- properties involving an odd number of composable morphisms
-- in a uniform and clean way.
-}
record VertComp (ğ’¢ : ReflGraph â„“ â„“') : Type (â„“-max â„“ â„“') where
  no-eta-equality
  constructor vertcomp
  open ReflGraphNotation ğ’¢
  open ReflGraphLemmas ğ’¢

  -- the vertical composition operation with convenient syntax
  field
    vcomp : (g f : âŸ¨ Gâ‚ âŸ©) â†’ isComposable g f â†’ âŸ¨ Gâ‚ âŸ©
  syntax vcomp g f p = g âˆ˜âŸ¨ p âŸ© f

  field
    -- vcomp preserves source and target
    Ïƒ-âˆ˜ : (g f : âŸ¨ Gâ‚ âŸ©) (c : isComposable g f) â†’ s (g âˆ˜âŸ¨ c âŸ© f) â‰¡ s f
    Ï„-âˆ˜ : (g f : âŸ¨ Gâ‚ âŸ©) (c : isComposable g f) â†’ t (g âˆ˜âŸ¨ c âŸ© f) â‰¡ t g
    -- vcomp is a homomorphism, also known as interchange law
    isHom-âˆ˜ : (g f : âŸ¨ Gâ‚ âŸ©) (c : isComposable g f)
              (g' f' : âŸ¨ Gâ‚ âŸ©) (c' : isComposable g' f')
              (c'' : isComposable (g +â‚ g') (f +â‚ f'))
              â†’ (g +â‚ g') âˆ˜âŸ¨ c'' âŸ© (f +â‚ f') â‰¡ (g âˆ˜âŸ¨ c âŸ© f) +â‚ (g' âˆ˜âŸ¨ c' âŸ© f')
    -- vcomp is associative
    assoc-âˆ˜ : (h g f : âŸ¨ Gâ‚ âŸ©)
              (c-hg : isComposable h g)
              (c-gf  : isComposable g f)
              (c-h-gf : isComposable h (g âˆ˜âŸ¨ c-gf âŸ© f))
              (c-hg-f : isComposable (h âˆ˜âŸ¨ c-hg âŸ© g) f)
              â†’ h âˆ˜âŸ¨ c-h-gf âŸ© (g âˆ˜âŸ¨ c-gf âŸ© f) â‰¡ (h âˆ˜âŸ¨ c-hg âŸ© g) âˆ˜âŸ¨ c-hg-f âŸ© f
    -- composing with identity arrows does nothing
    lid-âˆ˜ : (f : âŸ¨ Gâ‚ âŸ©) (c : isComposable (ğ’¾ (t f)) f)
            â†’ ğ’¾ (t f) âˆ˜âŸ¨ c âŸ© f â‰¡ f
    rid-âˆ˜ : (g : âŸ¨ Gâ‚ âŸ©) (c : isComposable g (ğ’¾ (s g))) â†’ g âˆ˜âŸ¨ c âŸ© ğ’¾ (s g) â‰¡ g

    -- alternative lid/rid definition, but taking paramter c is more flexible
    -- lid-âˆ˜ : (f : âŸ¨ Gâ‚ âŸ©) â†’ ğ’¾ (t f) âˆ˜âŸ¨ ÏƒÎ¹-â‰¡-fun (t f) âŸ© f â‰¡ f
    -- assoc-âˆ˜ : (h g f : âŸ¨ Gâ‚ âŸ©) (c : isComposable h g) (c' : isComposable g f)
    --         â†’ h âˆ˜âŸ¨ c âˆ™ sym (Ï„-âˆ˜ g f c') âŸ© (g âˆ˜âŸ¨ c' âŸ© f) â‰¡ (h âˆ˜âŸ¨ c âŸ© g) âˆ˜âŸ¨ Ïƒ-âˆ˜ h g c âˆ™ c' âŸ© f
               -- â†’ h âˆ˜âŸ¨ c-hg âˆ™ sym (Ï„-âˆ˜ g f c-gf) âŸ© (g âˆ˜âŸ¨ c-gf âŸ© f)
               --  â‰¡ (h âˆ˜âŸ¨ c-hg âŸ© g) âˆ˜âŸ¨ Ïƒ-âˆ˜ h g c-hg âˆ™ c-gf âŸ© f


module _ {ğ’¢ : ReflGraph â„“ â„“'} where
  open ReflGraphNotation ğ’¢
  open ReflGraphLemmas ğ’¢

  -- lemmas about a given vertical composition
  module _ (ğ’ : VertComp ğ’¢) where

    open VertComp ğ’

    -- These are all propositions, so we use abstract.
    -- Most of these lemmas are nontrivial, because we need to keep a
    -- proof of composability at hand.
    abstract
      -- if (g, f), and (g', f') are composable,
      -- then so is (g + g', f + f')
      +-c : (g f : âŸ¨ Gâ‚ âŸ©) (c : isComposable g f)
           (g' f' : âŸ¨ Gâ‚ âŸ©) (c' : isComposable g' f')
           â†’ isComposable (g +â‚ g') (f +â‚ f')
      +-c g f c g' f' c' = Ïƒ .isHom g g'
                          âˆ™âˆ™ cong (_+â‚€ s g') c
                          âˆ™âˆ™ cong (t f +â‚€_) c'
                          âˆ™ sym (Ï„ .isHom f f')

      -- if (g, f) is composable, and g â‰¡ g',
      -- then (g', f) is composable
      âˆ˜-cong-l-c : {g f : âŸ¨ Gâ‚ âŸ©} (c : isComposable g f)
                  {g' : âŸ¨ Gâ‚ âŸ©} (p : g â‰¡ g')
                  â†’ isComposable g' f
      âˆ˜-cong-l-c c p = cong s (sym p) âˆ™ c

      -- if (g, f) is composable, and f â‰¡ f',
      -- then (g, f') is composable
      âˆ˜-cong-r-c : {g f : âŸ¨ Gâ‚ âŸ©} (c : isComposable g f)
                  {f' : âŸ¨ Gâ‚ âŸ©} (p : f â‰¡ f')
                  â†’ isComposable g f'
      âˆ˜-cong-r-c c p = c âˆ™ cong t p

      -- if (g, f) are composable, and (g, f) â‰¡ (g', f'),
      -- then (g', f') is composable
      -- by combining the two lemmas above
      âˆ˜-cong-c : {g f : âŸ¨ Gâ‚ âŸ©} (c : isComposable g f)
                {g' f' : âŸ¨ Gâ‚ âŸ©} (p : g â‰¡ g') (q : f â‰¡ f')
                  â†’ isComposable g' f'
      âˆ˜-cong-c c p q = âˆ˜-cong-l-c c p âˆ™ cong t q

      -- if (g, f) is composable, and g â‰¡ g',
      -- then g âˆ˜ f â‰¡ g' âˆ˜ f
      âˆ˜-cong-l : {g f : âŸ¨ Gâ‚ âŸ©} (c : isComposable g f)
                {g' : âŸ¨ Gâ‚ âŸ©} (p : g â‰¡ g')
                â†’ g âˆ˜âŸ¨ c âŸ© f â‰¡ g' âˆ˜âŸ¨ âˆ˜-cong-l-c c p âŸ© f
      âˆ˜-cong-l {g = g} {f = f} c {g'} p = congâ‚‚ (Î» h d â†’ h âˆ˜âŸ¨ d âŸ© f)
                                               p
                                               (toPathP (isPropIsComposable g'
                                                                            f
                                                                            (transp (Î» i â†’ isComposable (p i) f) i0 c)
                                                                            (âˆ˜-cong-l-c c p)))

      -- if (g, f) is composable, and f â‰¡ f',
      -- then g âˆ˜ f â‰¡ g âˆ˜ f'
      âˆ˜-cong-r : {g f : âŸ¨ Gâ‚ âŸ©} (c : isComposable g f)
                {f' : âŸ¨ Gâ‚ âŸ©} (p : f â‰¡ f')
                â†’ g âˆ˜âŸ¨ c âŸ© f â‰¡ g âˆ˜âŸ¨ âˆ˜-cong-r-c c p âŸ© f'
      âˆ˜-cong-r {g = g} c {f'} p = congâ‚‚ (Î» h d â†’ g âˆ˜âŸ¨ d âŸ© h)
                                  p
                                  (toPathP (isPropIsComposable g
                                                               f'
                                                               (transp (Î» i â†’ isComposable g (p i)) i0 c)
                                                               (âˆ˜-cong-r-c c p)))

      -- if (g, f) are composable, and (g, f) â‰¡ (g', f'),
      -- then g âˆ˜ f â‰¡ g' âˆ˜ f'
      âˆ˜-cong : {g f : âŸ¨ Gâ‚ âŸ©} (c : isComposable g f)
                {g' f' : âŸ¨ Gâ‚ âŸ©} (p : g â‰¡ g') (q : f â‰¡ f')
                â†’ g âˆ˜âŸ¨ c âŸ© f â‰¡ g' âˆ˜âŸ¨ âˆ˜-cong-c c p q âŸ© f'
      âˆ˜-cong c p q = âˆ˜-cong-l c p
                    âˆ™ âˆ˜-cong-r (âˆ˜-cong-l-c c p) q

      -- an alternate version of lid-âˆ˜
      -- where a composable g is assumed and Î¹ (Ïƒ g)
      -- instead of Î¹ (Ï„ f) is used
      âˆ˜-lid' : {g f : âŸ¨ Gâ‚ âŸ©} (c : isComposable g f)
              (c' : isComposable (ğ’¾s g) f)
              â†’ (ğ’¾s g) âˆ˜âŸ¨ c' âŸ© f â‰¡ f
      âˆ˜-lid' {g} {f} c c' = (ğ’¾s g) âˆ˜âŸ¨ c' âŸ© f
                               â‰¡âŸ¨ âˆ˜-cong-l c' (cong ğ’¾ c) âŸ©
                           ğ’¾t f âˆ˜âŸ¨ âˆ˜-cong-l-c c' (cong ğ’¾ c) âŸ© f
                               â‰¡âŸ¨ lid-âˆ˜ f (âˆ˜-cong-l-c c' (cong ğ’¾ c)) âŸ©
                           f âˆ

      -- Fundamental theorem:
      -- Any vertical composition is necessarily of the form
      -- g âˆ˜âŸ¨ _ âŸ© f  â‰¡ g - Î¹ (Ïƒ g) + f
      -- This implies contractibility of VertComp ğ’¢
      VertCompâ†’+â‚ : (g f : âŸ¨ Gâ‚ âŸ©) (c : isComposable g f)
                   â†’ g âˆ˜âŸ¨ c âŸ© f â‰¡ (g -â‚ ğ’¾s g) +â‚ f
      VertCompâ†’+â‚ g f c = g âˆ˜âŸ¨ c âŸ© f
                           â‰¡âŸ¨ âˆ˜-cong c
                                     (sym (rIdâ‚ g) âˆ™ cong (g +â‚_) (sym (lCancelâ‚ isg)))
                                     (sym (lIdâ‚ f) âˆ™ cong (_+â‚ f) (sym (rCancelâ‚ isg))) âŸ©
                         (g +â‚ (-isg +â‚ isg)) âˆ˜âŸ¨ câ‚ âŸ© ((isg -â‚ isg) +â‚ f)
                           â‰¡âŸ¨ âˆ˜-cong-l câ‚ (assocâ‚ g -isg isg) âŸ©
                         ((g -â‚ isg) +â‚ isg) âˆ˜âŸ¨ câ‚‚ âŸ© ((isg -â‚ isg) +â‚ f)
                           â‰¡âŸ¨ isHom-âˆ˜ (g -â‚ isg) (isg -â‚ isg) câ‚„
                                      isg f câ‚ƒ
                                      câ‚‚ âŸ©
                         ((g +â‚ -isg) âˆ˜âŸ¨ câ‚„ âŸ© (isg +â‚ -isg)) +â‚ (isg âˆ˜âŸ¨ câ‚ƒ âŸ© f)
                           â‰¡âŸ¨ cong (_+â‚ (isg âˆ˜âŸ¨ câ‚ƒ âŸ© f))
                                   (isHom-âˆ˜ g isg câ‚… -isg -isg câ‚† câ‚„) âŸ©
                         ((g âˆ˜âŸ¨ câ‚… âŸ© isg) +â‚ (-isg âˆ˜âŸ¨ câ‚† âŸ© -isg)) +â‚ (isg âˆ˜âŸ¨ câ‚ƒ âŸ© f)
                           â‰¡âŸ¨ cong (Î» z â†’ (z +â‚ (-isg âˆ˜âŸ¨ câ‚† âŸ© -isg)) +â‚ (isg âˆ˜âŸ¨ câ‚ƒ âŸ© f))
                                    (rid-âˆ˜ g (isComp-g-isg g)) âŸ©
                         (g +â‚ (-isg âˆ˜âŸ¨ câ‚† âŸ© -isg)) +â‚ (isg âˆ˜âŸ¨ câ‚ƒ âŸ© f)
                           â‰¡âŸ¨ cong ((g +â‚ (-isg âˆ˜âŸ¨ câ‚† âŸ© -isg)) +â‚_)
                                   (âˆ˜-lid' c câ‚ƒ) âŸ©
                         (g +â‚ (-isg âˆ˜âŸ¨ câ‚† âŸ© -isg)) +â‚ f
                           â‰¡âŸ¨ cong (Î» z â†’ (g +â‚ z) +â‚ f)
                                   (-isg âˆ˜âŸ¨ câ‚† âŸ© -isg
                                     â‰¡âŸ¨ âˆ˜-cong-r câ‚†
                                                 -- prove that is(-isg)â‰¡-isg
                                                 (sym (cong ğ’¾s (sym (mapInv Î¹ (s g)))
                                                      âˆ™âˆ™ cong ğ’¾ (ÏƒÎ¹-â‰¡-fun (-â‚€ s g))
                                                      âˆ™âˆ™ mapInv Î¹ (s g))) âŸ©
                                   -isg âˆ˜âŸ¨ câ‚ˆ âŸ© (ğ’¾s -isg)
                                     â‰¡âŸ¨ rid-âˆ˜ -isg câ‚ˆ âŸ©
                                   -isg âˆ) âŸ©
                         (g -â‚ isg) +â‚ f âˆ
                         where
                           -- abbreviations to reduce the amount of parentheses
                           isg = ğ’¾s g
                           -isg = -â‚ isg
                           itf = ğ’¾t f
                           -- composability proofs,
                           -- none of which are really interesting.
                           câ‚ : isComposable (g +â‚ (-isg +â‚ isg)) ((isg -â‚ isg) +â‚ f)
                           câ‚ = âˆ˜-cong-c c
                                         (sym (rIdâ‚ g) âˆ™ cong (g +â‚_) (sym (lCancelâ‚ isg)))
                                         (sym (lIdâ‚ f) âˆ™ cong (_+â‚ f) (sym (rCancelâ‚ isg)))
                           câ‚‚ : isComposable ((g -â‚ isg) +â‚ isg) ((isg -â‚ isg) +â‚ f)
                           câ‚‚ = âˆ˜-cong-l-c câ‚ (assocâ‚ g -isg isg)
                           câ‚ƒ : isComposable isg f
                           câ‚ƒ = ÏƒÎ¹-â‰¡-fun (s g) âˆ™ c
                           câ‚„ : isComposable (g -â‚ isg) (isg -â‚ isg)
                           câ‚„ = s (g -â‚ isg)
                                  â‰¡âŸ¨ Ïƒ-g--isg g âŸ©
                                0â‚€
                                  â‰¡âŸ¨ sym (cong t (rCancelâ‚ isg) âˆ™ mapId Ï„) âŸ©
                                t (isg -â‚ isg) âˆ
                           câ‚… : isComposable g isg
                           câ‚… = isComp-g-isg g
                           câ‚† : isComposable -isg -isg
                           câ‚† = s -isg
                                  â‰¡âŸ¨ mapInv Ïƒ isg âŸ©
                                -â‚€ (s isg)
                                  â‰¡âŸ¨ cong -â‚€_ (ÏƒÎ¹-â‰¡-fun (s g)) âŸ©
                                -â‚€ (s g)
                                  â‰¡âŸ¨ cong -â‚€_ (sym (Ï„Î¹-â‰¡-fun (s g))) âŸ©
                                -â‚€ (t isg)
                                  â‰¡âŸ¨ sym (mapInv Ï„ isg) âŸ©
                                t -isg âˆ
                           câ‚ˆ : isComposable -isg (ğ’¾s -isg)
                           câ‚ˆ = âˆ˜-cong-r-c câ‚†
                                           (sym (cong ğ’¾s (sym (mapInv Î¹ (s g)))
                                             âˆ™âˆ™ cong ğ’¾ (ÏƒÎ¹-â‰¡-fun (-â‚€ s g))
                                             âˆ™âˆ™ mapInv Î¹ (s g)))

      -- properties of the interchange law
      IC2 : (g g' f : âŸ¨ Gâ‚ âŸ©) (c-gf : isComposable g f)
           â†’  (g' +â‚ (-is g' -â‚ is g)) +â‚ f â‰¡ (-is g +â‚ f) +â‚ (g' -â‚ is g')
      IC2 g g' f c-gf =
       (g' +â‚ (-isg' +â‚ -isg)) +â‚ f
         â‰¡âŸ¨ cong ((g' +â‚ (-isg' +â‚ -isg)) +â‚_)
                 (sym (rCancel-rId Gâ‚ f f') âˆ™ assocâ‚ f f' -f') âŸ©
       (g' +â‚ (-isg' +â‚ -isg)) +â‚ ((f +â‚ f') -â‚ f')
         â‰¡âŸ¨ assocâ‚ (g' +â‚ (-isg' +â‚ -isg)) (f +â‚ f') (-â‚ f') âŸ©
       ((g' +â‚ (-isg' +â‚ -isg)) +â‚ (f +â‚ f')) -â‚ f'
         â‰¡âŸ¨ cong (_-â‚ f')
            (sym (lCancel-lId Gâ‚ g _)) âŸ©
       ((-g +â‚ g) +â‚ ((g' +â‚ (-isg' +â‚ -isg)) +â‚ (f +â‚ f'))) -â‚ f'
         â‰¡âŸ¨ cong (_-â‚ f')
                 (sym (assocâ‚ -g g _)) âŸ©
       (-g +â‚ (g +â‚ ((g' +â‚ (-isg' +â‚ -isg)) +â‚ (f +â‚ f')))) -â‚ f'
         â‰¡âŸ¨ cong (Î» z â†’ (-g +â‚ z) -â‚ f')
                 (assocâ‚ g _ (f +â‚ f')) âŸ©
       (-g +â‚ ((g +â‚ (g' +â‚ (-isg' +â‚ -isg))) +â‚ (f +â‚ f'))) -â‚ f'
         â‰¡âŸ¨ cong (Î» z â†’ (-g +â‚ (z +â‚ (f +â‚ f'))) -â‚ f')
                 (assocâ‚ g g' (-isg' -â‚ isg)) âŸ©
       (-g +â‚ (((g +â‚ g') +â‚ (-isg' +â‚ -isg)) +â‚ (f +â‚ f'))) -â‚ f'
         â‰¡âŸ¨ cong (Î» z â†’ (-g +â‚ z) -â‚ f')
                 (sym q) âŸ©
       (-g +â‚ ((g +â‚ g') âˆ˜âŸ¨ c-gf'+ âŸ© (f +â‚ f'))) -â‚ f'
         â‰¡âŸ¨ cong (Î» z â†’ (-g +â‚ z) -â‚ f')
                 (isHom-âˆ˜ g f c-gf
                          g' f' c-gf'
                          c-gf'+) âŸ©
       (-g +â‚ ((g âˆ˜âŸ¨ c-gf âŸ© f) +â‚ (g' âˆ˜âŸ¨ c-gf' âŸ© f'))) -â‚ f'
         â‰¡âŸ¨ cong (Î» z â†’ (-g +â‚ ((g âˆ˜âŸ¨ c-gf âŸ© f) +â‚ z)) -â‚ f')
                 (VertCompâ†’+â‚ g' f' c-gf') âŸ©
       (-g +â‚ ((g âˆ˜âŸ¨ c-gf âŸ© f) +â‚ ((g' -â‚ isg') +â‚ f'))) -â‚ f'
         â‰¡âŸ¨ cong (Î» z â†’ (-g +â‚ (z +â‚ ((g' -â‚ isg') +â‚ f'))) -â‚ f')
                 (VertCompâ†’+â‚ g f c-gf) âŸ©
       (-g +â‚ (((g -â‚ isg) +â‚ f) +â‚ ((g' -â‚ isg') +â‚ f'))) -â‚ f'
         â‰¡âŸ¨ cong (Î» z â†’ (-g +â‚ (z +â‚ ((g' -â‚ isg') +â‚ f'))) -â‚ f')
                 (sym (assocâ‚ g -isg f)) âŸ©
       (-g +â‚ ((g +â‚ (-isg +â‚ f)) +â‚ ((g' -â‚ isg') +â‚ f'))) -â‚ f'
         â‰¡âŸ¨ cong (Î» z â†’ (-g +â‚ z) -â‚ f')
                 (sym (assocâ‚ g (-isg +â‚ f) _)) âŸ©
       (-g +â‚ (g +â‚ ((-isg +â‚ f) +â‚ ((g' -â‚ isg') +â‚ f')))) -â‚ f'
         â‰¡âŸ¨ cong (_-â‚ f')
                 (assocâ‚ -g g _) âŸ©
       ((-g +â‚ g) +â‚ ((-isg +â‚ f) +â‚ ((g' -â‚ isg') +â‚ f'))) -â‚ f'
         â‰¡âŸ¨ cong (_-â‚ f')
            (lCancel-lId Gâ‚ g _) âŸ©
       ((-isg +â‚ f) +â‚ ((g' -â‚ isg') +â‚ f')) -â‚ f'
         â‰¡âŸ¨ sym (assocâ‚ (-isg +â‚ f) _ -f') âŸ©
       (-isg +â‚ f) +â‚ (((g' -â‚ isg') +â‚ f') -â‚ f')
         â‰¡âŸ¨ cong ((-isg +â‚ f) +â‚_)
                 (sym (assocâ‚ (g' -â‚ isg') f' -f')) âŸ©
       (-isg +â‚ f) +â‚ ((g' -â‚ isg') +â‚ (f' -â‚ f'))
         â‰¡âŸ¨ cong ((-isg +â‚ f) +â‚_ )
                 (rCancel-rId Gâ‚ (g' -â‚ isg') f') âŸ©
       (-isg +â‚ f) +â‚ (g' -â‚ isg') âˆ
       where
         -- abbreviations to reduce the number of parentheses
         -g = -â‚ g
         isg = ğ’¾s g
         isg' = ğ’¾s g'
         -isg = -â‚ isg
         -isg' = -â‚ isg'
         f' = isg'
         -f' = -â‚ f'
         -- composability proofs
         c-gf' = isComp-g-isg g'
         c-gf'+ = +-c g f c-gf g' f' c-gf'
         --
         q = (g +â‚ g') âˆ˜âŸ¨ c-gf'+ âŸ© (f +â‚ f')
               â‰¡âŸ¨ VertCompâ†’+â‚ (g +â‚ g') (f +â‚ f') c-gf'+ âŸ©
             ((g +â‚ g') -â‚ (ğ’¾s (g +â‚ g'))) +â‚ (f +â‚ f')
               â‰¡âŸ¨ cong (Î» z â†’ ((g +â‚ g') -â‚ z) +â‚ (f +â‚ f'))
                       (Î¹âˆ˜Ïƒ .isHom g g') âŸ©
             ((g +â‚ g') -â‚ (isg +â‚ isg')) +â‚ (f +â‚ f')
               â‰¡âŸ¨ cong (Î» z â†’ ((g +â‚ g') +â‚ z) +â‚ (f +â‚ f'))
                       (invDistr Gâ‚ isg isg') âŸ©
             ((g +â‚ g') +â‚ (-isg' +â‚ -isg)) +â‚ (f +â‚ f') âˆ

      IC3 : (g g' f : âŸ¨ Gâ‚ âŸ©) (c-gf : isComposable g f)
            â†’ (-â‚ f) +â‚ ((is g +â‚ is g') -â‚ g') â‰¡ (is g' -â‚ g') +â‚ ((-â‚ f) +â‚ is g)
      IC3 g g' f c-gf =
        -f +â‚ ((isg +â‚ isg') -â‚ g')
          â‰¡âŸ¨ cong (Î» z â†’ -f +â‚ ((isg +â‚ z) -â‚ g'))
                  (sym (invInvo Gâ‚ isg')) âŸ©
        -f +â‚ ((isg -â‚ -isg') -â‚ g')
          â‰¡âŸ¨ cong (Î» z â†’ -f +â‚ ((z -â‚ -isg') -â‚ g'))
                  (sym (invInvo Gâ‚ isg)) âŸ©
        -f +â‚ (((-â‚ -isg) -â‚ -isg') -â‚ g')
          â‰¡âŸ¨ cong (Î» z â†’ -f +â‚ (z -â‚ g'))
                  (sym (invDistr Gâ‚ -isg' -isg)) âŸ©
        -f +â‚ ((-â‚ (-isg' +â‚ -isg)) -â‚ g')
          â‰¡âŸ¨ cong (Î» z â†’ -f +â‚ z)
                  (sym (invDistr Gâ‚ g' (-isg' +â‚ -isg))) âŸ©
        -f -â‚ (g' +â‚ (-isg' +â‚ -isg))
          â‰¡âŸ¨ sym (invDistr Gâ‚ _ f) âŸ©
        -â‚ ((g' +â‚ (-isg' +â‚ -isg)) +â‚ f)
          â‰¡âŸ¨ cong -â‚_
                  (IC2 g g' f c-gf) âŸ©
        -â‚ ((-isg +â‚ f) +â‚ (g' -â‚ isg'))
          â‰¡âŸ¨ invDistr Gâ‚ (-isg +â‚ f) (g' -â‚ isg') âŸ©
        (-â‚ (g' -â‚ isg')) +â‚ (-â‚ (-isg +â‚ f))
          â‰¡âŸ¨ cong ((-â‚ (g' -â‚ isg')) +â‚_)
                  (invDistr Gâ‚ -isg f) âŸ©
        (-â‚ (g' -â‚ isg')) +â‚ (-f -â‚ -isg)
          â‰¡âŸ¨ cong (_+â‚ (-f -â‚ -isg))
                  (invDistr Gâ‚ g' -isg') âŸ©
        ((-â‚ -isg') -â‚ g') +â‚ (-f -â‚ -isg)
          â‰¡âŸ¨ cong (Î» z â†’ (z -â‚ g') +â‚ (-f -â‚ -isg))
                  (invInvo Gâ‚ isg') âŸ©
        (isg' -â‚ g') +â‚ (-f -â‚ -isg)
          â‰¡âŸ¨ cong (Î» z â†’ (isg' -â‚ g') +â‚ (-f +â‚ z))
                  (invInvo Gâ‚ isg) âŸ©
        (isg' -â‚ g') +â‚ (-f +â‚ isg) âˆ
        where
          -f = -â‚ f
          -g = -â‚ g
          isg = ğ’¾s g
          isg' = ğ’¾s g'
          -isg = -â‚ isg
          -isg' = -â‚ isg'


      IC4 : (g g' f : âŸ¨ Gâ‚ âŸ©) (c-gf : isComposable g f)
            â†’ f +â‚ (((-is g) -â‚ (is g')) +â‚ g') â‰¡ ((-is g') +â‚ g') +â‚ (f -â‚ (is g))
      IC4 g g' f c-gf =
        f +â‚ ((-isg -â‚ isg') +â‚ g')
          â‰¡âŸ¨ cong (Î» z â†’ f +â‚ ((-isg -â‚ isg') +â‚ z))
                  (sym (invInvo Gâ‚ g')) âŸ©
        (f +â‚ ((-isg -â‚ isg') -â‚ -g'))
          â‰¡âŸ¨ cong (Î» z â†’ f +â‚ ((-isg +â‚ z) -â‚ -g'))
                  (sym (mapInv Î¹âˆ˜Ïƒ g')) âŸ©
        f +â‚ ((-isg +â‚ (is- g')) -â‚ -g')
          â‰¡âŸ¨ cong (Î» z â†’ f +â‚ ((z +â‚ (is- g')) -â‚ -g'))
                  (sym (mapInv Î¹âˆ˜Ïƒ g)) âŸ©
        f +â‚ (((is- g) +â‚ (is- g')) -â‚ -g')
          â‰¡âŸ¨ cong (_+â‚ ((is- g +â‚ is- g') -â‚ -g'))
                  (sym (invInvo Gâ‚ f)) âŸ©
        (-â‚ -f) +â‚ (((is- g) +â‚ (is- g')) -â‚ -g')
          â‰¡âŸ¨ IC3 -g -g' -f c--gf âŸ©
        ((is- g') -â‚ -g') +â‚ ((-â‚ -f) +â‚ (is- g))
          â‰¡âŸ¨ cong (Î» z â†’ (z -â‚ -g') +â‚ ((-â‚ -f) +â‚ (is- g)))
                  (mapInv Î¹âˆ˜Ïƒ g') âŸ©
        (-isg' -â‚ -g') +â‚ ((-â‚ -f) +â‚ (is- g))
          â‰¡âŸ¨ cong (Î» z â†’ (-isg' +â‚ z) +â‚ ((-â‚ -f) +â‚ (is- g)))
                  (invInvo Gâ‚ g') âŸ©
        (-isg' +â‚ g') +â‚ ((-â‚ -f) +â‚ (is- g))
          â‰¡âŸ¨ cong (Î» z â†’ (-isg' +â‚ g') +â‚ (z +â‚ (is- g)))
                  (invInvo Gâ‚ f) âŸ©
        (-isg' +â‚ g') +â‚ (f +â‚ (is- g))
          â‰¡âŸ¨ cong (Î» z â†’ (-isg' +â‚ g') +â‚ (f +â‚ z))
                  (mapInv Î¹âˆ˜Ïƒ g) âŸ©
        (-isg' +â‚ g') +â‚ (f -â‚ isg) âˆ
        where
          -f = -â‚ f
          -g = -â‚ g
          -g' = -â‚ g'
          isg = ğ’¾s g
          isg' = ğ’¾s g'
          -isg = -â‚ isg
          -isg' = -â‚ isg'
          c--gf = s -g
                    â‰¡âŸ¨ mapInv Ïƒ g âŸ©
                  -â‚€ (s g)
                    â‰¡âŸ¨ cong -â‚€_ c-gf âŸ©
                  -â‚€ (t f)
                    â‰¡âŸ¨ sym (mapInv Ï„ f) âŸ©
                  t -f âˆ
      -- g = itf
      IC5 : (g' f : âŸ¨ Gâ‚ âŸ©)
            â†’ f +â‚ (((-it f) -â‚ (is g')) +â‚ g') â‰¡ ((-is g') +â‚ g') +â‚ (f -â‚ (it f))
      IC5 g' f =
        f +â‚ ((-itf -â‚ isg') +â‚ g')
          â‰¡âŸ¨ cong (Î» z â†’ f +â‚ (((-â‚ (ğ’¾ z)) -â‚ isg') +â‚ g'))
                  (sym c-gf) âŸ©
        f +â‚ ((-isg -â‚ isg') +â‚ g')
          â‰¡âŸ¨ IC4 g g' f c-gf âŸ©
        (-isg' +â‚ g') +â‚ (f -â‚ isg)
          â‰¡âŸ¨ cong (Î» z â†’ (-isg' +â‚ g') +â‚ (f -â‚ (ğ’¾ z)))
                  c-gf âŸ©
        (-isg' +â‚ g') +â‚ (f -â‚ itf) âˆ
        where
          -f = -â‚ f
          -itf = -it f
          itf = it f
          g = it f
          -g = -â‚ g
          -g' = -â‚ g'
          isg = ğ’¾s g
          isg' = ğ’¾s g'
          -isg = -â‚ isg
          -isg' = -â‚ isg'
          c-gf : isComposable g f
          c-gf = isComp-itf-f f

  open VertComp

  -- the record VertComp has no eta equality, so this can be used to
  -- construct paths between vertical compositions
  Î·-VertComp : (ğ’± : VertComp ğ’¢) â†’ vertcomp (vcomp ğ’±) (Ïƒ-âˆ˜ ğ’±) (Ï„-âˆ˜ ğ’±) (isHom-âˆ˜ ğ’±) (assoc-âˆ˜ ğ’±) (lid-âˆ˜ ğ’±) (rid-âˆ˜ ğ’±) â‰¡ ğ’±
  vcomp (Î·-VertComp ğ’± i) = vcomp ğ’±
  Ïƒ-âˆ˜ (Î·-VertComp ğ’± i) = Ïƒ-âˆ˜ ğ’±
  Ï„-âˆ˜ (Î·-VertComp ğ’± i) = Ï„-âˆ˜ ğ’±
  isHom-âˆ˜ (Î·-VertComp ğ’± i) = isHom-âˆ˜ ğ’±
  assoc-âˆ˜ (Î·-VertComp ğ’± i) = assoc-âˆ˜ ğ’±
  lid-âˆ˜(Î·-VertComp ğ’± i) = lid-âˆ˜ ğ’±
  rid-âˆ˜ (Î·-VertComp ğ’± i) = rid-âˆ˜ ğ’±


  -- this is just a helper for the module below
  module _ (ğ’ ğ’' : VertComp ğ’¢) where
    pâˆ˜ : vcomp ğ’ â‰¡ vcomp ğ’'
    pâˆ˜ = funExtâ‚ƒ (Î» g f c â†’ VertCompâ†’+â‚ ğ’ g f c âˆ™ sym (VertCompâ†’+â‚ ğ’' g f c))

    pÏƒ : PathP (Î» j â†’ (g f : âŸ¨ Gâ‚ âŸ©) (c : isComposable g f) â†’ s (pâˆ˜ j g f c) â‰¡ s f) (Ïƒ-âˆ˜ ğ’) (Ïƒ-âˆ˜ ğ’')
    pÏƒ = isPropâ†’PathP (Î» i â†’ isPropÎ 3 (Î» g f c â†’ setâ‚€ (s (pâˆ˜ i g f c)) (s f))) (Ïƒ-âˆ˜ ğ’) (Ïƒ-âˆ˜ ğ’')

    passoc : PathP (Î» i â†’ (h g f : âŸ¨ Gâ‚ âŸ©)
                          (c-hg : isComposable h g)
                          (c-gf : isComposable g f)
                          (c-h-gf : isComposable h (pâˆ˜ i g f c-gf))
                          (c-hg-f : isComposable (pâˆ˜ i h g c-hg) f) â†’
                          pâˆ˜ i h (pâˆ˜ i g f c-gf) c-h-gf â‰¡ pâˆ˜ i (pâˆ˜ i h g c-hg) f c-hg-f) (assoc-âˆ˜ ğ’) (assoc-âˆ˜ ğ’')
    passoc = isPropâ†’PathP (Î» j â†’ isPropÎ 4 (Î» h g f c-hg â†’ isPropÎ 3 (Î» c-gf c-h-gf c-hg-f â†’ setâ‚ (pâˆ˜ j h (pâˆ˜ j g f c-gf) c-h-gf) (pâˆ˜ j (pâˆ˜ j h g c-hg) f c-hg-f)))) (assoc-âˆ˜ ğ’) (assoc-âˆ˜ ğ’')
    -- (pâˆ˜ j h (pâˆ˜ j g f c-gf) c-h-gf â‰¡ pâˆ˜ j (pâˆ˜ j h g c-hg) f c-hg-f)

-- proof that there is at most one vertical composition on a reflexive graph
module _ (ğ’¢ : ReflGraph â„“ â„“') where
  open ReflGraphNotation ğ’¢
  open ReflGraphLemmas ğ’¢
  open VertComp
  isPropVertComp : isProp (VertComp ğ’¢)
  vcomp (isPropVertComp ğ’ ğ’' i) = funExtâ‚ƒ (Î» g f c â†’ VertCompâ†’+â‚ ğ’ g f c âˆ™ sym (VertCompâ†’+â‚ ğ’' g f c)) i
  -- Ïƒ-âˆ˜ (isPropVertComp ğ’ ğ’' i) = funExtâ‚ƒ P i
  Ïƒ-âˆ˜ (isPropVertComp ğ’ ğ’' i) = pÏƒ ğ’ ğ’' i
    where
      P : (g f : âŸ¨ Gâ‚ âŸ©) (c : isComposable g f)
          â†’ PathP (Î» j â†’ s (vcomp (isPropVertComp ğ’ ğ’' j) g f c) â‰¡ s f)
                  (Ïƒ-âˆ˜ ğ’ g f c)
                  (Ïƒ-âˆ˜ ğ’' g f c)
      P g f c = isPropâ†’PathP (Î» j â†’ setâ‚€ (s (vcomp (isPropVertComp ğ’ ğ’' j) g f c)) (s f)) (Ïƒ-âˆ˜ ğ’ g f c) (Ïƒ-âˆ˜ ğ’' g f c)
  Ï„-âˆ˜ (isPropVertComp ğ’ ğ’' i) = funExtâ‚ƒ P i
    where
      P : (g f : âŸ¨ Gâ‚ âŸ©) (c : isComposable g f) â†’ PathP (Î» j â†’ t (vcomp (isPropVertComp ğ’ ğ’' j) g f c) â‰¡ t g) (Ï„-âˆ˜ ğ’ g f c) (Ï„-âˆ˜ ğ’' g f c)
      P g f c = isPropâ†’PathP (Î» j â†’ setâ‚€ (t (vcomp (isPropVertComp ğ’ ğ’' j) g f c)) (t g)) (Ï„-âˆ˜ ğ’ g f c) (Ï„-âˆ˜ ğ’' g f c)
  isHom-âˆ˜ (isPropVertComp ğ’ ğ’' i) = funExtâ‚ƒ (Î» g f c â†’ funExtâ‚ƒ (Î» g' f' c' â†’ funExt (Î» c+ â†’ P g f c g' f' c' c+))) i
    where
      P : (g f : âŸ¨ Gâ‚ âŸ©) (c : isComposable g f)
          (g' f' : âŸ¨ Gâ‚ âŸ©) (c' : isComposable g' f')
          (c+ : isComposable (g +â‚ g') (f +â‚ f'))
          â†’ PathP (Î» j â†’ vcomp (isPropVertComp ğ’ ğ’' j) (g +â‚ g') (f +â‚ f') c+
                         â‰¡ (vcomp (isPropVertComp ğ’ ğ’' j) g f c) +â‚ (vcomp (isPropVertComp ğ’ ğ’' j) g' f' c'))
                  (isHom-âˆ˜ ğ’ g f c g' f' c' c+)
                  (isHom-âˆ˜ ğ’' g f c g' f' c' c+)
      P g f c g' f' c' c+ = isPropâ†’PathP (Î» j â†’ setâ‚ (vcomp (isPropVertComp ğ’ ğ’' j) (g +â‚ g') (f +â‚ f') c+)
                                                     ((vcomp (isPropVertComp ğ’ ğ’' j) g f c) +â‚ (vcomp (isPropVertComp ğ’ ğ’' j) g' f' c')))
                                         (isHom-âˆ˜ ğ’ g f c g' f' c' c+)
                                         (isHom-âˆ˜ ğ’' g f c g' f' c' c+)
  assoc-âˆ˜ (isPropVertComp ğ’ ğ’' i) = passoc ğ’ ğ’' i
  lid-âˆ˜ (isPropVertComp ğ’ ğ’' i) = funExtâ‚‚ P i
    where
      P : (f : âŸ¨ Gâ‚ âŸ©) (c : isComposable (ğ’¾ (t f)) f)
          â†’ PathP (Î» j â†’ vcomp (isPropVertComp ğ’ ğ’' j) (ğ’¾ (t f)) f c â‰¡ f) (lid-âˆ˜ ğ’ f c) (lid-âˆ˜ ğ’' f c)
      P f c = isPropâ†’PathP (Î» j â†’ setâ‚ (vcomp (isPropVertComp ğ’ ğ’' j) (ğ’¾ (t f)) f c) f) (lid-âˆ˜ ğ’ f c) (lid-âˆ˜ ğ’' f c)
  rid-âˆ˜ (isPropVertComp ğ’ ğ’' i) = funExtâ‚‚ P i
    where
      P : (g : âŸ¨ Gâ‚ âŸ©) (c : isComposable g (ğ’¾ (s g)))
          â†’ PathP (Î» j â†’ vcomp (isPropVertComp ğ’ ğ’' j) g (ğ’¾ (s g)) c â‰¡ g) (rid-âˆ˜ ğ’ g c) (rid-âˆ˜ ğ’' g c)
      P g c = isPropâ†’PathP (Î» j â†’ setâ‚ (vcomp (isPropVertComp ğ’ ğ’' j) g (ğ’¾ (s g)) c) g) (rid-âˆ˜ ğ’ g c) (rid-âˆ˜ ğ’' g c)
