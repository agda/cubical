{-# OPTIONS --safe #-}
{-
  Tosets are totally-ordered sets,
  i.e. strongly connected posets,
  a poset where every element can be compared
-}
module Cubical.Relation.Binary.Order.Toset.Base where

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.Equiv
open import Cubical.Foundations.HLevels
open import Cubical.Foundations.Isomorphism
open import Cubical.Foundations.Univalence
open import Cubical.Foundations.Transport
open import Cubical.Foundations.SIP

open import Cubical.HITs.PropositionalTruncation

open import Cubical.Data.Sigma

open import Cubical.Reflection.RecordEquiv
open import Cubical.Reflection.StrictEquiv

open import Cubical.Displayed.Base
open import Cubical.Displayed.Auto
open import Cubical.Displayed.Record
open import Cubical.Displayed.Universe

open import Cubical.Relation.Binary.Base

open Iso
open BinaryRelation


private
  variable
    ‚Ñì ‚Ñì' ‚Ñì'' ‚Ñì‚ÇÄ ‚Ñì‚ÇÄ' ‚Ñì‚ÇÅ ‚Ñì‚ÇÅ' : Level

record IsToset {A : Type ‚Ñì} (_‚â§_ : A ‚Üí A ‚Üí Type ‚Ñì') : Type (‚Ñì-max ‚Ñì ‚Ñì') where
  no-eta-equality
  constructor istoset

  field
    is-set : isSet A
    is-prop-valued : isPropValued _‚â§_
    is-refl : isRefl _‚â§_
    is-trans : isTrans _‚â§_
    is-antisym : isAntisym _‚â§_
    is-strongly-connected : isStronglyConnected _‚â§_

unquoteDecl IsTosetIsoŒ£ = declareRecordIsoŒ£ IsTosetIsoŒ£ (quote IsToset)


record TosetStr (‚Ñì' : Level) (A : Type ‚Ñì) : Type (‚Ñì-max ‚Ñì (‚Ñì-suc ‚Ñì')) where

  constructor tosetstr

  field
    _‚â§_     : A ‚Üí A ‚Üí Type ‚Ñì'
    isToset : IsToset _‚â§_

  infixl 7 _‚â§_

  open IsToset isToset public

Toset : ‚àÄ ‚Ñì ‚Ñì' ‚Üí Type (‚Ñì-max (‚Ñì-suc ‚Ñì) (‚Ñì-suc ‚Ñì'))
Toset ‚Ñì ‚Ñì' = TypeWithStr ‚Ñì (TosetStr ‚Ñì')

toset : (A : Type ‚Ñì) (_‚â§_ : A ‚Üí A ‚Üí Type ‚Ñì') (h : IsToset _‚â§_) ‚Üí Toset ‚Ñì ‚Ñì'
toset A _‚â§_ h = A , tosetstr _‚â§_ h

record IsTosetEquiv {A : Type ‚Ñì‚ÇÄ} {B : Type ‚Ñì‚ÇÅ}
  (M : TosetStr ‚Ñì‚ÇÄ' A) (e : A ‚âÉ B) (N : TosetStr ‚Ñì‚ÇÅ' B)
  : Type (‚Ñì-max (‚Ñì-max ‚Ñì‚ÇÄ ‚Ñì‚ÇÄ') ‚Ñì‚ÇÅ')
  where
  constructor
   istosetequiv
  -- Shorter qualified names
  private
    module M = TosetStr M
    module N = TosetStr N

  field
    pres‚â§ : (x y : A) ‚Üí x M.‚â§ y ‚âÉ equivFun e x N.‚â§ equivFun e y


TosetEquiv : (M : Toset ‚Ñì‚ÇÄ ‚Ñì‚ÇÄ') (M : Toset ‚Ñì‚ÇÅ ‚Ñì‚ÇÅ') ‚Üí Type (‚Ñì-max (‚Ñì-max ‚Ñì‚ÇÄ ‚Ñì‚ÇÄ') (‚Ñì-max ‚Ñì‚ÇÅ ‚Ñì‚ÇÅ'))
TosetEquiv M N = Œ£[ e ‚àà ‚ü® M ‚ü© ‚âÉ ‚ü® N ‚ü© ] IsTosetEquiv (M .snd) e (N .snd)

isPropIsToset : {A : Type ‚Ñì} (_‚â§_ : A ‚Üí A ‚Üí Type ‚Ñì') ‚Üí isProp (IsToset _‚â§_)
isPropIsToset _‚â§_ = isOfHLevelRetractFromIso 1 IsTosetIsoŒ£
  (isPropŒ£ isPropIsSet
    Œª isSetA ‚Üí isPropŒ£ (isPropŒ†2 (Œª _ _ ‚Üí isPropIsProp))
      Œª isPropValued‚â§ ‚Üí isProp√ó3
                         (isPropŒ† (Œª _ ‚Üí isPropValued‚â§ _ _))
                           (isPropŒ†5 Œª _ _ _ _ _ ‚Üí isPropValued‚â§ _ _)
                             (isPropŒ†4 Œª _ _ _ _ ‚Üí isSetA _ _)
                               (isPropŒ†2 Œª _ _ ‚Üí squash‚ÇÅ))

ùíÆ·¥∞-Toset : DUARel (ùíÆ-Univ ‚Ñì) (TosetStr ‚Ñì') (‚Ñì-max ‚Ñì ‚Ñì')
ùíÆ·¥∞-Toset =
  ùíÆ·¥∞-Record (ùíÆ-Univ _) IsTosetEquiv
    (fields:
      data[ _‚â§_ ‚à£ autoDUARel _ _ ‚à£ pres‚â§ ]
      prop[ isToset ‚à£ (Œª _ _ ‚Üí isPropIsToset _) ])
    where
    open TosetStr
    open IsToset
    open IsTosetEquiv

TosetPath : (M N : Toset ‚Ñì ‚Ñì') ‚Üí TosetEquiv M N ‚âÉ (M ‚â° N)
TosetPath = ‚à´ ùíÆ·¥∞-Toset .UARel.ua

-- an easier way of establishing an equivalence of tosets
module _ {P : Toset ‚Ñì‚ÇÄ ‚Ñì‚ÇÄ'} {S : Toset ‚Ñì‚ÇÅ ‚Ñì‚ÇÅ'} (e : ‚ü® P ‚ü© ‚âÉ ‚ü® S ‚ü©) where
  private
    module P = TosetStr (P .snd)
    module S = TosetStr (S .snd)

  module _ (isMon : ‚àÄ x y ‚Üí x P.‚â§ y ‚Üí equivFun e x S.‚â§ equivFun e y)
           (isMonInv : ‚àÄ x y ‚Üí x S.‚â§ y ‚Üí invEq e x P.‚â§ invEq e y) where
    open IsTosetEquiv
    open IsToset

    makeIsTosetEquiv : IsTosetEquiv (P .snd) e (S .snd)
    pres‚â§ makeIsTosetEquiv x y = propBiimpl‚ÜíEquiv (P.isToset .is-prop-valued _ _)
                                                  (S.isToset .is-prop-valued _ _)
                                                  (isMon _ _) (isMonInv' _ _)
      where
      isMonInv' : ‚àÄ x y ‚Üí equivFun e x S.‚â§ equivFun e y ‚Üí x P.‚â§ y
      isMonInv' x y ex‚â§ey = transport (Œª i ‚Üí retEq e x i P.‚â§ retEq e y i) (isMonInv _ _ ex‚â§ey)


module TosetReasoning (P' : Toset ‚Ñì ‚Ñì') where
 private P = fst P'
 open TosetStr (snd P')
 open IsToset

 _‚â§‚ü®_‚ü©_ : (x : P) {y z : P} ‚Üí x ‚â§ y ‚Üí y ‚â§ z ‚Üí x ‚â§ z
 x ‚â§‚ü® p ‚ü© q = isToset .is-trans x _ _ p q

 _‚óæ : (x : P) ‚Üí x ‚â§ x
 x ‚óæ = isToset .is-refl x

 infixr 0 _‚â§‚ü®_‚ü©_
 infix  1 _‚óæ
