{-# OPTIONS --safe #-}
{-
  Strict posets are posets where the relation is strict,
  i.e. irreflexive rather than reflexive
-}
module Cubical.Relation.Binary.Order.StrictPoset.Base where

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.Equiv
open import Cubical.Foundations.HLevels
open import Cubical.Foundations.Isomorphism
open import Cubical.Foundations.Univalence
open import Cubical.Foundations.Transport
open import Cubical.Foundations.SIP

open import Cubical.Data.Sigma

open import Cubical.Reflection.RecordEquiv
open import Cubical.Reflection.StrictEquiv

open import Cubical.Displayed.Base
open import Cubical.Displayed.Auto
open import Cubical.Displayed.Record
open import Cubical.Displayed.Universe

open import Cubical.Relation.Binary.Base
open import Cubical.Relation.Nullary.Properties

open Iso
open BinaryRelation


private
  variable
    ‚Ñì ‚Ñì' ‚Ñì'' ‚Ñì‚ÇÄ ‚Ñì‚ÇÄ' ‚Ñì‚ÇÅ ‚Ñì‚ÇÅ' : Level

record IsStrictPoset {A : Type ‚Ñì} (_<_ : A ‚Üí A ‚Üí Type ‚Ñì') : Type (‚Ñì-max ‚Ñì ‚Ñì') where
  no-eta-equality
  constructor isstrictposet

  field
    is-set : isSet A
    is-prop-valued : isPropValued _<_
    is-irrefl : isIrrefl _<_
    is-trans : isTrans _<_
    is-asym : isAsym _<_

unquoteDecl IsStrictPosetIsoŒ£ = declareRecordIsoŒ£ IsStrictPosetIsoŒ£ (quote IsStrictPoset)


record StrictPosetStr (‚Ñì' : Level) (A : Type ‚Ñì) : Type (‚Ñì-max ‚Ñì (‚Ñì-suc ‚Ñì')) where

  constructor strictposetstr

  field
    _<_     : A ‚Üí A ‚Üí Type ‚Ñì'
    isStrictPoset : IsStrictPoset _<_

  infixl 7 _<_

  open IsStrictPoset isStrictPoset public

StrictPoset : ‚àÄ ‚Ñì ‚Ñì' ‚Üí Type (‚Ñì-max (‚Ñì-suc ‚Ñì) (‚Ñì-suc ‚Ñì'))
StrictPoset ‚Ñì ‚Ñì' = TypeWithStr ‚Ñì (StrictPosetStr ‚Ñì')

strictposet : (A : Type ‚Ñì) (_<_ : A ‚Üí A ‚Üí Type ‚Ñì') (h : IsStrictPoset _<_) ‚Üí StrictPoset ‚Ñì ‚Ñì'
strictposet A _<_ h = A , strictposetstr _<_ h

record IsStrictPosetEquiv {A : Type ‚Ñì‚ÇÄ} {B : Type ‚Ñì‚ÇÅ}
  (M : StrictPosetStr ‚Ñì‚ÇÄ' A) (e : A ‚âÉ B) (N : StrictPosetStr ‚Ñì‚ÇÅ' B)
  : Type (‚Ñì-max (‚Ñì-max ‚Ñì‚ÇÄ ‚Ñì‚ÇÄ') ‚Ñì‚ÇÅ')
  where
  constructor
   isstrictposetequiv
  -- Shorter qualified names
  private
    module M = StrictPosetStr M
    module N = StrictPosetStr N

  field
    pres< : (x y : A) ‚Üí x M.< y ‚âÉ equivFun e x N.< equivFun e y


StrictPosetEquiv : (M : StrictPoset ‚Ñì‚ÇÄ ‚Ñì‚ÇÄ') (M : StrictPoset ‚Ñì‚ÇÅ ‚Ñì‚ÇÅ') ‚Üí Type (‚Ñì-max (‚Ñì-max ‚Ñì‚ÇÄ ‚Ñì‚ÇÄ') (‚Ñì-max ‚Ñì‚ÇÅ ‚Ñì‚ÇÅ'))
StrictPosetEquiv M N = Œ£[ e ‚àà ‚ü® M ‚ü© ‚âÉ ‚ü® N ‚ü© ] IsStrictPosetEquiv (M .snd) e (N .snd)

isPropIsStrictPoset : {A : Type ‚Ñì} (_<_ : A ‚Üí A ‚Üí Type ‚Ñì') ‚Üí isProp (IsStrictPoset _<_)
isPropIsStrictPoset _<_ = isOfHLevelRetractFromIso 1 IsStrictPosetIsoŒ£
  (isPropŒ£ isPropIsSet
    Œª isSetA ‚Üí isPropŒ£ (isPropŒ†2 (Œª _ _ ‚Üí isPropIsProp))
      Œª isPropValued< ‚Üí isProp√ó2 (isPropŒ† (Œª x ‚Üí isProp¬¨ (x < x)))
                                 (isPropŒ†5 (Œª _ _ _ _ _ ‚Üí isPropValued< _ _))
                                 (isPropŒ†3 Œª x y _ ‚Üí isProp¬¨ (y < x)))

ùíÆ·¥∞-StrictPoset : DUARel (ùíÆ-Univ ‚Ñì) (StrictPosetStr ‚Ñì') (‚Ñì-max ‚Ñì ‚Ñì')
ùíÆ·¥∞-StrictPoset =
  ùíÆ·¥∞-Record (ùíÆ-Univ _) IsStrictPosetEquiv
    (fields:
      data[ _<_ ‚à£ autoDUARel _ _ ‚à£ pres< ]
      prop[ isStrictPoset ‚à£ (Œª _ _ ‚Üí isPropIsStrictPoset _) ])
    where
    open StrictPosetStr
    open IsStrictPoset
    open IsStrictPosetEquiv

StrictPosetPath : (M N : StrictPoset ‚Ñì ‚Ñì') ‚Üí StrictPosetEquiv M N ‚âÉ (M ‚â° N)
StrictPosetPath = ‚à´ ùíÆ·¥∞-StrictPoset .UARel.ua

-- an easier way of establishing an equivalence of strict posets
module _ {P : StrictPoset ‚Ñì‚ÇÄ ‚Ñì‚ÇÄ'} {S : StrictPoset ‚Ñì‚ÇÅ ‚Ñì‚ÇÅ'} (e : ‚ü® P ‚ü© ‚âÉ ‚ü® S ‚ü©) where
  private
    module P = StrictPosetStr (P .snd)
    module S = StrictPosetStr (S .snd)

  module _ (isMon : ‚àÄ x y ‚Üí x P.< y ‚Üí equivFun e x S.< equivFun e y)
           (isMonInv : ‚àÄ x y ‚Üí x S.< y ‚Üí invEq e x P.< invEq e y) where
    open IsStrictPosetEquiv
    open IsStrictPoset

    makeIsStrictPosetEquiv : IsStrictPosetEquiv (P .snd) e (S .snd)
    pres< makeIsStrictPosetEquiv x y = propBiimpl‚ÜíEquiv (P.isStrictPoset .is-prop-valued _ _)
                                                  (S.isStrictPoset .is-prop-valued _ _)
                                                  (isMon _ _) (isMonInv' _ _)
      where
      isMonInv' : ‚àÄ x y ‚Üí equivFun e x S.< equivFun e y ‚Üí x P.< y
      isMonInv' x y ex<ey = transport (Œª i ‚Üí retEq e x i P.< retEq e y i) (isMonInv _ _ ex<ey)
