{-
  Strict orders are linear orders that aren't connected,
  or a quasiorder with comparison
-}
module Cubical.Relation.Binary.Order.StrictOrder.Base where

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.Equiv
open import Cubical.Foundations.HLevels
open import Cubical.Foundations.Isomorphism
open import Cubical.Foundations.Univalence
open import Cubical.Foundations.Transport
open import Cubical.Foundations.SIP

open import Cubical.Data.Sigma

open import Cubical.HITs.PropositionalTruncation

open import Cubical.Reflection.RecordEquiv
open import Cubical.Reflection.StrictEquiv

open import Cubical.Displayed.Base
open import Cubical.Displayed.Auto
open import Cubical.Displayed.Record
open import Cubical.Displayed.Universe

open import Cubical.Relation.Binary.Base
open import Cubical.Relation.Nullary.Properties

open Iso
open BinaryRelation


private
  variable
    ‚Ñì ‚Ñì' ‚Ñì'' ‚Ñì‚ÇÄ ‚Ñì‚ÇÄ' ‚Ñì‚ÇÅ ‚Ñì‚ÇÅ' : Level

record IsStrictOrder {A : Type ‚Ñì} (_<_ : A ‚Üí A ‚Üí Type ‚Ñì') : Type (‚Ñì-max ‚Ñì ‚Ñì') where
  no-eta-equality
  constructor isstrictorder

  field
    is-set : isSet A
    is-prop-valued : isPropValued _<_
    is-irrefl : isIrrefl _<_
    is-trans : isTrans _<_
    is-asym : isAsym _<_
    is-weakly-linear : isWeaklyLinear _<_

unquoteDecl IsStrictOrderIsoŒ£ = declareRecordIsoŒ£ IsStrictOrderIsoŒ£ (quote IsStrictOrder)


record StrictOrderStr (‚Ñì' : Level) (A : Type ‚Ñì) : Type (‚Ñì-max ‚Ñì (‚Ñì-suc ‚Ñì')) where

  constructor strictorderstr

  field
    _<_     : A ‚Üí A ‚Üí Type ‚Ñì'
    isStrictOrder : IsStrictOrder _<_

  infixl 7 _<_

  open IsStrictOrder isStrictOrder public

StrictOrder : ‚àÄ ‚Ñì ‚Ñì' ‚Üí Type (‚Ñì-max (‚Ñì-suc ‚Ñì) (‚Ñì-suc ‚Ñì'))
StrictOrder ‚Ñì ‚Ñì' = TypeWithStr ‚Ñì (StrictOrderStr ‚Ñì')

strictorder : (A : Type ‚Ñì) ‚Üí (_<_ : Rel A A ‚Ñì') ‚Üí IsStrictOrder _<_ ‚Üí StrictOrder ‚Ñì ‚Ñì'
strictorder A _<_ strict = A , (strictorderstr _<_ strict)

record IsStrictOrderEquiv {A : Type ‚Ñì‚ÇÄ} {B : Type ‚Ñì‚ÇÅ}
  (M : StrictOrderStr ‚Ñì‚ÇÄ' A) (e : A ‚âÉ B) (N : StrictOrderStr ‚Ñì‚ÇÅ' B)
  : Type (‚Ñì-max (‚Ñì-max ‚Ñì‚ÇÄ ‚Ñì‚ÇÄ') ‚Ñì‚ÇÅ')
  where
  constructor
   isstrictorderequiv
  -- Shorter qualified names
  private
    module M = StrictOrderStr M
    module N = StrictOrderStr N

  field
    pres< : (x y : A) ‚Üí x M.< y ‚âÉ equivFun e x N.< equivFun e y


StrictOrderEquiv : (M : StrictOrder ‚Ñì‚ÇÄ ‚Ñì‚ÇÄ') (M : StrictOrder ‚Ñì‚ÇÅ ‚Ñì‚ÇÅ') ‚Üí Type (‚Ñì-max (‚Ñì-max ‚Ñì‚ÇÄ ‚Ñì‚ÇÄ') (‚Ñì-max ‚Ñì‚ÇÅ ‚Ñì‚ÇÅ'))
StrictOrderEquiv M N = Œ£[ e ‚àà ‚ü® M ‚ü© ‚âÉ ‚ü® N ‚ü© ] IsStrictOrderEquiv (M .snd) e (N .snd)

isPropIsStrictOrder : {A : Type ‚Ñì} (_<_ : A ‚Üí A ‚Üí Type ‚Ñì') ‚Üí isProp (IsStrictOrder _<_)
isPropIsStrictOrder _<_ = isOfHLevelRetractFromIso 1 IsStrictOrderIsoŒ£
  (isPropŒ£ isPropIsSet
    Œª isSetA ‚Üí isPropŒ£ (isPropŒ†2 (Œª _ _ ‚Üí isPropIsProp))
      Œª isPropValued< ‚Üí isProp√ó3 (isPropŒ† (Œª x ‚Üí isProp¬¨ (x < x)))
                                 (isPropŒ†5 (Œª _ _ _ _ _ ‚Üí isPropValued< _ _))
                                 (isPropŒ†3 (Œª x y _ ‚Üí isProp¬¨ (y < x)))
                                 (isPropŒ†4 Œª _ _ _ _ ‚Üí squash‚ÇÅ))

ùíÆ·¥∞-StrictOrder : DUARel (ùíÆ-Univ ‚Ñì) (StrictOrderStr ‚Ñì') (‚Ñì-max ‚Ñì ‚Ñì')
ùíÆ·¥∞-StrictOrder =
  ùíÆ·¥∞-Record (ùíÆ-Univ _) IsStrictOrderEquiv
    (fields:
      data[ _<_ ‚à£ autoDUARel _ _ ‚à£ pres< ]
      prop[ isStrictOrder ‚à£ (Œª _ _ ‚Üí isPropIsStrictOrder _) ])
    where
    open StrictOrderStr
    open IsStrictOrder
    open IsStrictOrderEquiv

StrictOrderPath : (M N : StrictOrder ‚Ñì ‚Ñì') ‚Üí StrictOrderEquiv M N ‚âÉ (M ‚â° N)
StrictOrderPath = ‚à´ ùíÆ·¥∞-StrictOrder .UARel.ua

-- an easier way of establishing an equivalence of strict orders
module _ {P : StrictOrder ‚Ñì‚ÇÄ ‚Ñì‚ÇÄ'} {S : StrictOrder ‚Ñì‚ÇÅ ‚Ñì‚ÇÅ'} (e : ‚ü® P ‚ü© ‚âÉ ‚ü® S ‚ü©) where
  private
    module P = StrictOrderStr (P .snd)
    module S = StrictOrderStr (S .snd)

  module _ (isMon : ‚àÄ x y ‚Üí x P.< y ‚Üí equivFun e x S.< equivFun e y)
           (isMonInv : ‚àÄ x y ‚Üí x S.< y ‚Üí invEq e x P.< invEq e y) where
    open IsStrictOrderEquiv
    open IsStrictOrder

    makeIsStrictOrderEquiv : IsStrictOrderEquiv (P .snd) e (S .snd)
    pres< makeIsStrictOrderEquiv x y = propBiimpl‚ÜíEquiv (P.isStrictOrder .is-prop-valued _ _)
                                                  (S.isStrictOrder .is-prop-valued _ _)
                                                  (isMon _ _) (isMonInv' _ _)
      where
      isMonInv' : ‚àÄ x y ‚Üí equivFun e x S.< equivFun e y ‚Üí x P.< y
      isMonInv' x y ex<ey = transport (Œª i ‚Üí retEq e x i P.< retEq e y i) (isMonInv _ _ ex<ey)
