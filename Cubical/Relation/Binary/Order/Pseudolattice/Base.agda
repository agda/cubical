module Cubical.Relation.Binary.Order.Pseudolattice.Base where

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.Function
open import Cubical.Foundations.Equiv
open import Cubical.Foundations.HLevels
open import Cubical.Foundations.SIP

open import Cubical.Reflection.RecordEquiv
open import Cubical.Reflection.StrictEquiv

open import Cubical.Displayed.Base
open import Cubical.Displayed.Auto
open import Cubical.Displayed.Record
open import Cubical.Displayed.Universe

open import Cubical.Relation.Binary.Base
open import Cubical.Relation.Binary.Order.Poset renaming (
  isPseudolattice to pseudolattice ;
  isPropIsPseudolattice to is-prop-is-pseudolattice)

open BinaryRelation

private
  variable
    ‚Ñì ‚Ñì' ‚Ñì‚ÇÄ ‚Ñì‚ÇÄ' ‚Ñì‚ÇÅ ‚Ñì‚ÇÅ' : Level

record IsPseudolattice {L : Type ‚Ñì} (_‚â§_ : L ‚Üí L ‚Üí Type ‚Ñì') : Type (‚Ñì-max ‚Ñì ‚Ñì') where
  constructor ispseudolattice

  field
    isPoset : IsPoset _‚â§_
    isPseudolattice : pseudolattice (poset L _‚â§_ isPoset)

  open IsPoset isPoset public

  _‚àßl_ : L ‚Üí L ‚Üí L
  a ‚àßl b = (isPseudolattice .fst a b) .fst

  _‚à®l_ : L ‚Üí L ‚Üí L
  a ‚à®l b = (isPseudolattice .snd a b) .fst

  infixl 7 _‚àßl_
  infixl 6 _‚à®l_


unquoteDecl IsPseudolatticeIsoŒ£ = declareRecordIsoŒ£ IsPseudolatticeIsoŒ£ (quote IsPseudolattice)

record PseudolatticeStr (‚Ñì' : Level) (L : Type ‚Ñì) : Type (‚Ñì-suc (‚Ñì-max ‚Ñì ‚Ñì')) where
  constructor pseudolatticestr

  field
    _‚â§_ : L ‚Üí L ‚Üí Type ‚Ñì'
    is-pseudolattice : IsPseudolattice _‚â§_

  infix 5 _‚â§_

  open IsPseudolattice is-pseudolattice public


unquoteDecl PseudolatticeStrIsoŒ£ = declareRecordIsoŒ£ PseudolatticeStrIsoŒ£ (quote PseudolatticeStr)

Pseudolattice : ‚àÄ ‚Ñì ‚Ñì' ‚Üí Type (‚Ñì-suc (‚Ñì-max ‚Ñì ‚Ñì'))
Pseudolattice ‚Ñì ‚Ñì' = TypeWithStr ‚Ñì (PseudolatticeStr ‚Ñì')

makeIsPseudolattice : {L : Type ‚Ñì} {_‚â§_ : L ‚Üí L ‚Üí Type ‚Ñì'}
                      (is-setL : isSet L)
                      (is-prop-valued : isPropValued _‚â§_)
                      (is-refl : isRefl _‚â§_)
                      (is-trans : isTrans _‚â§_)
                      (is-antisym : isAntisym _‚â§_)
                      (is-meet-semipseudolattice : isMeetSemipseudolattice (poset L _‚â§_ (isposet is-setL is-prop-valued is-refl is-trans is-antisym)))
                      (is-join-semipseudolattice : isJoinSemipseudolattice (poset L _‚â§_ (isposet is-setL is-prop-valued is-refl is-trans is-antisym)))
                      ‚Üí IsPseudolattice _‚â§_
makeIsPseudolattice {_‚â§_ = _‚â§_} is-setL is-prop-valued is-refl is-trans is-antisym is-meet-semipseudolattice is-join-semipseudolattice = PS
  where
    PS : IsPseudolattice _‚â§_
    PS .IsPseudolattice.isPoset = isposet is-setL is-prop-valued is-refl is-trans is-antisym
    PS .IsPseudolattice.isPseudolattice = is-meet-semipseudolattice , is-join-semipseudolattice

module _
  (P : Poset ‚Ñì ‚Ñì') (_‚àß_ _‚à®_ : ‚ü® P ‚ü© ‚Üí ‚ü® P ‚ü© ‚Üí ‚ü® P ‚ü©) where
  open PosetStr (str P) renaming (_‚â§_ to infix 8 _‚â§_)
  module _
    (œÄ‚ÇÅ : ‚àÄ {a b}   ‚Üí a ‚àß b ‚â§ a)
    (œÄ‚ÇÇ : ‚àÄ {a b}   ‚Üí a ‚àß b ‚â§ b)
    (œï  : ‚àÄ {a b x} ‚Üí x ‚â§ a ‚Üí x ‚â§ b ‚Üí x ‚â§ a ‚àß b)
    (Œπ‚ÇÅ : ‚àÄ {a b}   ‚Üí a ‚â§ a ‚à® b)
    (Œπ‚ÇÇ : ‚àÄ {a b}   ‚Üí b ‚â§ a ‚à® b)
    (œà  : ‚àÄ {a b x} ‚Üí a ‚â§ x ‚Üí b ‚â§ x ‚Üí a ‚à® b ‚â§ x) where

    makePseudolatticeFromPoset : Pseudolattice ‚Ñì ‚Ñì'
    makePseudolatticeFromPoset .fst = ‚ü® P ‚ü©
    makePseudolatticeFromPoset .snd .PseudolatticeStr._‚â§_ = (str P) .PosetStr._‚â§_
    makePseudolatticeFromPoset .snd .PseudolatticeStr.is-pseudolattice = isPL where
      isPL : IsPseudolattice _‚â§_
      isPL .IsPseudolattice.isPoset = isPoset
      isPL .IsPseudolattice.isPseudolattice .fst a b .fst = a ‚àß b
      isPL .IsPseudolattice.isPseudolattice .fst a b .snd x = propBiimpl‚ÜíEquiv
        (is-prop-valued _ _)
        (isProp√ó (is-prop-valued _ _) (is-prop-valued _ _))
        (Œª x‚â§a‚àßb ‚Üí is-trans _ _ _ x‚â§a‚àßb œÄ‚ÇÅ , is-trans _ _ _ x‚â§a‚àßb œÄ‚ÇÇ)
        (uncurry œï)
      isPL .IsPseudolattice.isPseudolattice .snd a b .fst = a ‚à® b
      isPL .IsPseudolattice.isPseudolattice .snd a b .snd x = propBiimpl‚ÜíEquiv
        (is-prop-valued _ _)
        (isProp√ó (is-prop-valued _ _) (is-prop-valued _ _))
        (Œª a‚à®b‚â§x ‚Üí is-trans _ _ _ Œπ‚ÇÅ a‚à®b‚â§x , is-trans _ _ _ Œπ‚ÇÇ a‚à®b‚â§x)
        (uncurry œà)


record IsPseudolatticeEquiv {A : Type ‚Ñì‚ÇÄ} {B : Type ‚Ñì‚ÇÅ}
  (M : PseudolatticeStr ‚Ñì‚ÇÄ' A) (e : A ‚âÉ B) (N : PseudolatticeStr ‚Ñì‚ÇÅ' B)
  : Type (‚Ñì-max (‚Ñì-max ‚Ñì‚ÇÄ ‚Ñì‚ÇÄ') ‚Ñì‚ÇÅ')
  where
  constructor
   ispseudolatticeequiv
  -- Shorter qualified names
  private
    module M = PseudolatticeStr M
    module N = PseudolatticeStr N

  field
    pres‚â§ : (x y : A) ‚Üí x M.‚â§ y ‚âÉ equivFun e x N.‚â§ equivFun e y


PseudolatticeEquiv : (M : Pseudolattice ‚Ñì‚ÇÄ ‚Ñì‚ÇÄ') (N : Pseudolattice ‚Ñì‚ÇÅ ‚Ñì‚ÇÅ')
                     ‚Üí Type (‚Ñì-max (‚Ñì-max ‚Ñì‚ÇÄ ‚Ñì‚ÇÄ') (‚Ñì-max ‚Ñì‚ÇÅ ‚Ñì‚ÇÅ'))
PseudolatticeEquiv M N = Œ£[ e ‚àà ‚ü® M ‚ü© ‚âÉ ‚ü® N ‚ü© ] IsPseudolatticeEquiv (M .snd) e (N .snd)

isPropIsPseudolattice : {L : Type ‚Ñì} (_‚â§_ : L ‚Üí L ‚Üí Type ‚Ñì') ‚Üí isProp (IsPseudolattice _‚â§_)
isPropIsPseudolattice {L = L} _‚â§_ = isOfHLevelRetractFromIso 1
  IsPseudolatticeIsoŒ£ $ isPropŒ£
  (isPropIsPoset _‚â§_) Œª isPoset ‚Üí
  is-prop-is-pseudolattice (poset L _‚â§_ isPoset)

ùíÆ·¥∞-Pseudolattice : DUARel (ùíÆ-Univ ‚Ñì) (PseudolatticeStr ‚Ñì') (‚Ñì-max ‚Ñì ‚Ñì')
ùíÆ·¥∞-Pseudolattice =
  ùíÆ·¥∞-Record (ùíÆ-Univ _) IsPseudolatticeEquiv
    (fields:
      data[ _‚â§_ ‚à£ autoDUARel _ _ ‚à£ pres‚â§ ]
      prop[ is-pseudolattice ‚à£ (Œª _ _ ‚Üí isPropIsPseudolattice _) ])
    where
    open PseudolatticeStr
    open IsPseudolattice
    open IsPseudolatticeEquiv

PseudolatticePath : (M N : Pseudolattice ‚Ñì ‚Ñì') ‚Üí PseudolatticeEquiv M N ‚âÉ (M ‚â° N)
PseudolatticePath = ‚à´ ùíÆ·¥∞-Pseudolattice .UARel.ua

-- an easier way of establishing an equivalence of pseudolattices
module _ {P : Pseudolattice ‚Ñì‚ÇÄ ‚Ñì‚ÇÄ'} {S : Pseudolattice ‚Ñì‚ÇÅ ‚Ñì‚ÇÅ'} (e : ‚ü® P ‚ü© ‚âÉ ‚ü® S ‚ü©) where
  private
    module P = PseudolatticeStr (P .snd)
    module S = PseudolatticeStr (S .snd)

  module _ (isMon : ‚àÄ x y ‚Üí x P.‚â§ y ‚Üí equivFun e x S.‚â§ equivFun e y)
           (isMonInv : ‚àÄ x y ‚Üí x S.‚â§ y ‚Üí invEq e x P.‚â§ invEq e y) where
    open IsPseudolatticeEquiv
    open IsPseudolattice

    makeIsPseudolatticeEquiv : IsPseudolatticeEquiv (P .snd) e (S .snd)
    pres‚â§ makeIsPseudolatticeEquiv x y = propBiimpl‚ÜíEquiv
                                          (P.is-pseudolattice .is-prop-valued _ _)
                                          (S.is-pseudolattice .is-prop-valued _ _)
                                          (isMon _ _) (isMonInv' _ _)
      where
      isMonInv' : ‚àÄ x y ‚Üí equivFun e x S.‚â§ equivFun e y ‚Üí x P.‚â§ y
      isMonInv' x y ex‚â§ey = transport (Œª i ‚Üí retEq e x i P.‚â§ retEq e y i) (isMonInv _ _ ex‚â§ey)


module PseudolatticeReasoning (P' : Pseudolattice ‚Ñì ‚Ñì') where
 private P = fst P'
 open PseudolatticeStr (snd P')
 open IsPseudolattice

 _‚â§‚ü®_‚ü©_ : (x : P) {y z : P} ‚Üí x ‚â§ y ‚Üí y ‚â§ z ‚Üí x ‚â§ z
 x ‚â§‚ü® p ‚ü© q = is-pseudolattice .is-trans x _ _ p q

 _‚óæ : (x : P) ‚Üí x ‚â§ x
 x ‚óæ = is-pseudolattice .is-refl x

 infixr 0 _‚â§‚ü®_‚ü©_
 infix  1 _‚óæ
