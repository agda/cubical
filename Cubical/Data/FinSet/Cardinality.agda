{-

Properties and Formulae about Cardinality

This file contains:
- Relation between abstract properties and cardinality in special cases;
- Combinatorial formulae, namely, cardinality of A+B, AÃ—B, Î£AB, Î AB, etc;
- A general form of Pigeonhole Principle;
- Maximal value of numerical function on finite sets;
- Set truncation of FinSet is equivalent to â„•;
- FinProp is equivalent to Bool.

-}
{-# OPTIONS --safe #-}

module Cubical.Data.FinSet.Cardinality where

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.HLevels
open import Cubical.Foundations.Function
open import Cubical.Foundations.Isomorphism
open import Cubical.Foundations.Univalence
open import Cubical.Foundations.Equiv renaming (_âˆ™â‚‘_ to _â‹†_)
open import Cubical.Foundations.Equiv.Properties
open import Cubical.Foundations.Transport

open import Cubical.HITs.PropositionalTruncation as Prop
open import Cubical.HITs.SetTruncation as Set

open import Cubical.Data.Nat
open import Cubical.Data.Nat.Order
open import Cubical.Data.Unit
open import Cubical.Data.Empty as Empty
open import Cubical.Data.Bool hiding (_â‰Ÿ_)
open import Cubical.Data.Sum
open import Cubical.Data.Sigma

open import Cubical.Data.Fin using (Fin-inj)
open import Cubical.Data.Fin.LehmerCode as LehmerCode
open import Cubical.Data.SumFin
open import Cubical.Data.FinSet.Base
open import Cubical.Data.FinSet.Properties
open import Cubical.Data.FinSet.FiniteChoice
open import Cubical.Data.FinSet.Constructors
open import Cubical.Data.FinSet.Induction hiding (_+_)

open import Cubical.Relation.Nullary

open import Cubical.Functions.Fibration
open import Cubical.Functions.Embedding
open import Cubical.Functions.Surjection

private
  variable
    â„“ â„“' â„“'' : Level
    n : â„•
    X : FinSet â„“
    Y : FinSet â„“'

-- cardinality of finite sets

âˆ£â‰ƒcardâˆ£ : (X : FinSet â„“) â†’ âˆ¥ X .fst â‰ƒ Fin (card X) âˆ¥
âˆ£â‰ƒcardâˆ£ X = X .snd .snd

-- cardinality is invariant under equivalences

cardEquiv : (X : FinSet â„“)(Y : FinSet â„“') â†’ âˆ¥ X .fst â‰ƒ Y .fst âˆ¥ â†’ card X â‰¡ card Y
cardEquiv X Y e =
  Prop.rec (isSetâ„• _ _) (Î» p â†’ Fin-inj _ _ (ua p))
    (âˆ£ invEquiv (SumFinâ‰ƒFin _) âˆ£ â‹†Ì‚ âˆ£invEquivâˆ£ (âˆ£â‰ƒcardâˆ£ X) â‹†Ì‚ e â‹†Ì‚ âˆ£â‰ƒcardâˆ£ Y â‹†Ì‚ âˆ£ SumFinâ‰ƒFin _ âˆ£)

cardInj : card X â‰¡ card Y â†’ âˆ¥ X .fst â‰ƒ Y .fst âˆ¥
cardInj {X = X} {Y = Y} p =
  âˆ£â‰ƒcardâˆ£ X â‹†Ì‚ âˆ£ pathToEquiv (cong Fin p) âˆ£ â‹†Ì‚ âˆ£invEquivâˆ£ (âˆ£â‰ƒcardâˆ£ Y)

cardReflection : card X â‰¡ n â†’ âˆ¥ X .fst â‰ƒ Fin n âˆ¥
cardReflection {X = X} = cardInj {X = X} {Y = _ , isFinSetFin}

cardâ‰¡MereEquiv : (card X â‰¡ card Y) â‰¡ âˆ¥ X .fst â‰ƒ Y .fst âˆ¥
cardâ‰¡MereEquiv {X = X} {Y = Y} =
  hPropExt (isSetâ„• _ _) isPropPropTrunc (cardInj {X = X} {Y = Y}) (cardEquiv X Y)

-- special properties about specific cardinality

module _
  {X : FinSet â„“} where

  cardâ‰¡0â†’isEmpty : card X â‰¡ 0 â†’ Â¬ X .fst
  cardâ‰¡0â†’isEmpty p x =
    Prop.rec isPropâŠ¥ (Î» e â†’ subst Fin p (e .fst x)) (âˆ£â‰ƒcardâˆ£ X)

  card>0â†’isInhab : card X > 0 â†’ âˆ¥ X .fst âˆ¥
  card>0â†’isInhab p =
    Prop.map (Î» e â†’ invEq e (Fin>0â†’isInhab _ p)) (âˆ£â‰ƒcardâˆ£ X)

  card>1â†’hasNonEqualTerm : card X > 1 â†’ âˆ¥ Î£[ a âˆˆ X .fst ] Î£[ b âˆˆ X .fst ] Â¬ a â‰¡ b âˆ¥
  card>1â†’hasNonEqualTerm p =
    Prop.map
      (Î» e â†’
        e .fst (Fin>1â†’hasNonEqualTerm _ p .fst) ,
        e .fst (Fin>1â†’hasNonEqualTerm _ p .snd .fst) ,
        Fin>1â†’hasNonEqualTerm _ p .snd .snd âˆ˜ invEq (congEquiv e))
      (âˆ£invEquivâˆ£ (âˆ£â‰ƒcardâˆ£ X))

  cardâ‰¡1â†’isContr : card X â‰¡ 1 â†’ isContr (X .fst)
  cardâ‰¡1â†’isContr p =
    Prop.rec isPropIsContr
        (Î» e â†’ isOfHLevelRespectEquiv 0 (invEquiv (e â‹† substEquiv Fin p)) isContrSumFin1) (âˆ£â‰ƒcardâˆ£ X)

  cardâ‰¤1â†’isProp : card X â‰¤ 1 â†’ isProp (X .fst)
  cardâ‰¤1â†’isProp p =
    Prop.rec isPropIsProp (Î» e â†’ isOfHLevelRespectEquiv 1 (invEquiv e) (Finâ‰¤1â†’isProp (card X) p)) (âˆ£â‰ƒcardâˆ£ X)

  cardâ‰¡n : card X â‰¡ n â†’ âˆ¥ X â‰¡ ğ”½in n âˆ¥
  cardâ‰¡n {n = n} p =
    Prop.map
        (Î» e â†’
          (Î» i â†’
            ua e i ,
            isPropâ†’PathP {B = Î» j â†’ isFinSet (ua e j)}
              (Î» _ â†’ isPropIsFinSet) (X .snd) (ğ”½in n .snd) i ))
        (âˆ£â‰ƒcardâˆ£ X â‹†Ì‚ âˆ£ pathToEquiv (cong Fin p) â‹† invEquiv (ğ”½inâ‰ƒFin n) âˆ£)

  cardâ‰¡0 : card X â‰¡ 0 â†’ X â‰¡ ğŸ˜
  cardâ‰¡0 p =
    propTruncIdempotentâ‰ƒ
      (isOfHLevelRespectEquiv
        1 (FinSetâ‰¡ X ğŸ˜)
          (isOfHLevelâ‰¡ 1
            (cardâ‰¤1â†’isProp (subst (Î» a â†’ a â‰¤ 1) (sym p) (â‰¤-solver 0 1))) (isPropâŠ¥*))) .fst
      (cardâ‰¡n p)

  cardâ‰¡1 : card X â‰¡ 1 â†’ X â‰¡ ğŸ™
  cardâ‰¡1 p =
    propTruncIdempotentâ‰ƒ
      (isOfHLevelRespectEquiv
        1 (FinSetâ‰¡ X ğŸ™)
          (isOfHLevelâ‰¡ 1
            (cardâ‰¤1â†’isProp (subst (Î» a â†’ a â‰¤ 1) (sym p) (â‰¤-solver 1 1))) (isPropUnit*))) .fst
      (Prop.map (Î» q â†’ q âˆ™ ğ”½in1â‰¡ğŸ™) (cardâ‰¡n p))

module _
  (X : FinSet â„“) where

  isEmptyâ†’cardâ‰¡0 : Â¬ X .fst â†’ card X â‰¡ 0
  isEmptyâ†’cardâ‰¡0 p =
    Prop.rec (isSetâ„• _ _) (Î» e â†’ sym (isEmptyâ†’Finâ‰¡0 _ (p âˆ˜ invEq e))) (âˆ£â‰ƒcardâˆ£ X)

  isInhabâ†’card>0 : âˆ¥ X .fst âˆ¥ â†’ card X > 0
  isInhabâ†’card>0 = Prop.rec2 mâ‰¤n-isProp (Î» p x â†’ isInhabâ†’Fin>0 _ (p .fst x)) (âˆ£â‰ƒcardâˆ£ X)

  hasNonEqualTermâ†’card>1 : {a b : X. fst} â†’ Â¬ a â‰¡ b â†’ card X > 1
  hasNonEqualTermâ†’card>1 {a = a} {b = b} q =
    Prop.rec mâ‰¤n-isProp (Î» p â†’ hasNonEqualTermâ†’Fin>1 _ (p .fst a) (p .fst b) (q âˆ˜ invEq (congEquiv p))) (âˆ£â‰ƒcardâˆ£ X)

  isContrâ†’cardâ‰¡1 : isContr (X .fst) â†’ card X â‰¡ 1
  isContrâ†’cardâ‰¡1 p = cardEquiv X (_ , isFinSetUnit) âˆ£ isContrâ†’â‰ƒUnit p âˆ£

  isPropâ†’cardâ‰¤1 : isProp (X .fst) â†’ card X â‰¤ 1
  isPropâ†’cardâ‰¤1 p = isPropâ†’Finâ‰¤1 (card X) (Prop.rec isPropIsProp (Î» e â†’ isOfHLevelRespectEquiv 1 e p) (âˆ£â‰ƒcardâˆ£ X))

{- formulae about cardinality -}

-- results to be used in direct induction on FinSet

cardğŸ˜ : card (ğŸ˜ {â„“}) â‰¡ 0
cardğŸ˜ {â„“ = â„“} = isEmptyâ†’cardâ‰¡0 (ğŸ˜ {â„“}) (Empty.rec*)

cardğŸ™ : card (ğŸ™ {â„“}) â‰¡ 1
cardğŸ™ {â„“ = â„“} = isContrâ†’cardâ‰¡1 (ğŸ™ {â„“}) isContrUnit*

cardğ”½in : (n : â„•) â†’ card (ğ”½in {â„“} n) â‰¡ n
cardğ”½in {â„“ = â„“} n =  cardEquiv (ğ”½in {â„“} n) (_ , isFinSetFin) âˆ£ ğ”½inâ‰ƒFin n âˆ£

-- addition/product formula

module _
  (X : FinSet â„“ )
  (Y : FinSet â„“') where

  card+ : card (_ , isFinSetâŠ X Y) â‰¡ card X + card Y
  card+ = refl

  cardÃ— : card (_ , isFinSetÃ— X Y) â‰¡ card X Â· card Y
  cardÃ— = refl

-- total summation/product of numerical functions from finite sets

module _
  (X : FinSet â„“)
  (f : X .fst â†’ â„•) where

  sum : â„•
  sum = card (_ , isFinSetÎ£ X (Î» x â†’ Fin (f x) , isFinSetFin))

  prod : â„•
  prod = card (_ , isFinSetÎ  X (Î» x â†’ Fin (f x) , isFinSetFin))

module _
  (f : ğŸ˜ {â„“} .fst â†’ â„•) where

  sumğŸ˜ : sum ğŸ˜ f â‰¡ 0
  sumğŸ˜ =
    isEmptyâ†’cardâ‰¡0 (_ , isFinSetÎ£ ğŸ˜ (Î» x â†’ Fin (f x) , isFinSetFin))
                   ((invEquiv (Î£-cong-equiv-fst (invEquiv ğŸ˜â‰ƒEmpty)) â‹† Î£Empty _) .fst)

  prodğŸ˜ : prod ğŸ˜ f â‰¡ 1
  prodğŸ˜ =
    isContrâ†’cardâ‰¡1 (_ , isFinSetÎ  ğŸ˜ (Î» x â†’ Fin (f x) , isFinSetFin))
                   (isContrÎ âŠ¥*)

module _
  (f : ğŸ™ {â„“} .fst â†’ â„•) where

  sumğŸ™ : sum ğŸ™ f â‰¡ f tt*
  sumğŸ™ =
    cardEquiv (_ , isFinSetÎ£ ğŸ™ (Î» x â†’ Fin (f x) , isFinSetFin))
              (Fin (f tt*) , isFinSetFin) âˆ£ Î£-contractFst isContrUnit* âˆ£

  prodğŸ™ : prod ğŸ™ f â‰¡ f tt*
  prodğŸ™ =
    cardEquiv (_ , isFinSetÎ  ğŸ™ (Î» x â†’ Fin (f x) , isFinSetFin))
              (Fin (f tt*) , isFinSetFin) âˆ£ Î Unit* _ âˆ£

module _
  (X : FinSet â„“ )
  (Y : FinSet â„“')
  (f : X .fst âŠ Y .fst â†’ â„•) where

  sumâŠ : sum (_ , isFinSetâŠ X Y) f â‰¡ sum X (f âˆ˜ inl) + sum Y (f âˆ˜ inr)
  sumâŠ =
    cardEquiv (_ , isFinSetÎ£ (_ , isFinSetâŠ X Y) (Î» x â†’ Fin (f x) , isFinSetFin))
              (_ , isFinSetâŠ (_ , isFinSetÎ£ X (Î» x â†’ Fin (f (inl x)) , isFinSetFin))
                             (_ , isFinSetÎ£ Y (Î» y â†’ Fin (f (inr y)) , isFinSetFin))) âˆ£ Î£âŠâ‰ƒ âˆ£
    âˆ™ card+ (_ , isFinSetÎ£ X (Î» x â†’ Fin (f (inl x)) , isFinSetFin))
            (_ , isFinSetÎ£ Y (Î» y â†’ Fin (f (inr y)) , isFinSetFin))

  prodâŠ : prod (_ , isFinSetâŠ X Y) f â‰¡ prod X (f âˆ˜ inl) Â· prod Y (f âˆ˜ inr)
  prodâŠ =
    cardEquiv (_ , isFinSetÎ  (_ , isFinSetâŠ X Y) (Î» x â†’ Fin (f x) , isFinSetFin))
              (_ , isFinSetÃ— (_ , isFinSetÎ  X (Î» x â†’ Fin (f (inl x)) , isFinSetFin))
                             (_ , isFinSetÎ  Y (Î» y â†’ Fin (f (inr y)) , isFinSetFin))) âˆ£ Î âŠâ‰ƒ âˆ£
    âˆ™ cardÃ— (_ , isFinSetÎ  X (Î» x â†’ Fin (f (inl x)) , isFinSetFin))
            (_ , isFinSetÎ  Y (Î» y â†’ Fin (f (inr y)) , isFinSetFin))

-- technical lemma
module _
  (n : â„•)(f : ğ”½in {â„“} (1 + n) .fst â†’ â„•) where

  sumğ”½in1+n : sum (ğ”½in (1 + n)) f â‰¡ f (inl tt*) + sum (ğ”½in n) (f âˆ˜ inr)
  sumğ”½in1+n = sumâŠ ğŸ™ (ğ”½in n) f âˆ™ (Î» i â†’ sumğŸ™ (f âˆ˜ inl) i + sum (ğ”½in n) (f âˆ˜ inr))

  prodğ”½in1+n : prod (ğ”½in (1 + n)) f â‰¡ f (inl tt*) Â· prod (ğ”½in n) (f âˆ˜ inr)
  prodğ”½in1+n = prodâŠ ğŸ™ (ğ”½in n) f âˆ™ (Î» i â†’ prodğŸ™ (f âˆ˜ inl) i Â· prod (ğ”½in n) (f âˆ˜ inr))

sumConstğ”½in : (n : â„•)(f : ğ”½in {â„“} n .fst â†’ â„•)(c : â„•)(h : (x : ğ”½in n .fst) â†’ f x â‰¡ c) â†’ sum (ğ”½in n) f â‰¡ c Â· n
sumConstğ”½in 0 f c _ = sumğŸ˜ f âˆ™ 0â‰¡mÂ·0 c
sumConstğ”½in (suc n) f c h =
    sumğ”½in1+n n f
  âˆ™ (Î» i â†’ h (inl tt*) i + sumConstğ”½in n (f âˆ˜ inr) c (h âˆ˜ inr) i)
  âˆ™ sym (Â·-suc c n)

prodConstğ”½in : (n : â„•)(f : ğ”½in {â„“} n .fst â†’ â„•)(c : â„•)(h : (x : ğ”½in n .fst) â†’ f x â‰¡ c) â†’ prod (ğ”½in n) f â‰¡ c ^ n
prodConstğ”½in 0 f c _ = prodğŸ˜ f
prodConstğ”½in (suc n) f c h =
    prodğ”½in1+n n f
  âˆ™ (Î» i â†’ h (inl tt*) i Â· prodConstğ”½in n (f âˆ˜ inr) c (h âˆ˜ inr) i)

module _
  (X : FinSet â„“)
  (f : X .fst â†’ â„•)
  (c : â„•)(h : (x : X .fst) â†’ f x â‰¡ c) where

  sumConst : sum X f â‰¡ c Â· card X
  sumConst =
    elimProp
      (Î» X â†’ (f : X .fst â†’ â„•)(c : â„•)(h : (x : X .fst) â†’ f x â‰¡ c) â†’ sum X f â‰¡ c Â· (card X))
      (Î» X â†’ isPropÎ 3 (Î» _ _ _ â†’ isSetâ„• _ _))
      (Î» n f c h â†’ sumConstğ”½in n f c h âˆ™ (Î» i â†’ c Â· cardğ”½in {â„“ = â„“} n (~ i))) X f c h

  prodConst : prod X f â‰¡ c ^ card X
  prodConst =
    elimProp
      (Î» X â†’ (f : X .fst â†’ â„•)(c : â„•)(h : (x : X .fst) â†’ f x â‰¡ c) â†’ prod X f â‰¡ c ^ (card X))
      (Î» X â†’ isPropÎ 3 (Î» _ _ _ â†’ isSetâ„• _ _))
      (Î» n f c h â†’ prodConstğ”½in n f c h âˆ™ (Î» i â†’ c ^ cardğ”½in {â„“ = â„“} n (~ i))) X f c h

private
  â‰¡â‰¤ : {m n l k r s : â„•} â†’ m â‰¤ n â†’ l â‰¤ k â†’ r â‰¡ m + l â†’ s â‰¡ n + k â†’ r â‰¤ s
  â‰¡â‰¤ {m = m} {l = l} {k = k} p q u v = subst2 (_â‰¤_) (sym u) (sym v) (â‰¤-+-â‰¤ p q)

  â‰¡< : {m n l k r s : â„•} â†’ m < n â†’ l â‰¤ k â†’ r â‰¡ m + l â†’ s â‰¡ n + k â†’ r < s
  â‰¡< {m = m} {l = l} {k = k} p q u v = subst2 (_<_) (sym u) (sym v) (<-+-â‰¤ p q)

sumâ‰¤ğ”½in : (n : â„•)(f g : ğ”½in {â„“} n .fst â†’ â„•)(h : (x : ğ”½in n .fst) â†’ f x â‰¤ g x) â†’ sum (ğ”½in n) f â‰¤ sum (ğ”½in n) g
sumâ‰¤ğ”½in 0 f g _ = subst2 (_â‰¤_) (sym (sumğŸ˜ f)) (sym (sumğŸ˜ g)) â‰¤-refl
sumâ‰¤ğ”½in (suc n) f g h =
  â‰¡â‰¤ (h (inl tt*)) (sumâ‰¤ğ”½in n (f âˆ˜ inr) (g âˆ˜ inr) (h âˆ˜ inr)) (sumğ”½in1+n n f) (sumğ”½in1+n n g)

sum<ğ”½in : (n : â„•)(f g : ğ”½in {â„“} n .fst â†’ â„•)(t : âˆ¥ ğ”½in {â„“} n .fst âˆ¥)(h : (x : ğ”½in n .fst) â†’ f x < g x)
  â†’ sum (ğ”½in n) f < sum (ğ”½in n) g
sum<ğ”½in {â„“ = â„“} 0 _ _ t _ = Empty.rec (<â†’â‰¢ (isInhabâ†’card>0 (ğ”½in 0) t) (cardğŸ˜ {â„“ = â„“}))
sum<ğ”½in (suc n) f g t h =
  â‰¡< (h (inl tt*)) (sumâ‰¤ğ”½in n (f âˆ˜ inr) (g âˆ˜ inr) (<-weaken âˆ˜ h âˆ˜ inr)) (sumğ”½in1+n n f) (sumğ”½in1+n n g)

module _
  (X : FinSet â„“)
  (f g : X .fst â†’ â„•) where

    module _
      (h : (x : X .fst) â†’ f x â‰¡ g x) where

      sumâ‰¡ : sum X f â‰¡ sum X g
      sumâ‰¡ i = sum X (Î» x â†’ h x i)

      prodâ‰¡ : prod X f â‰¡ prod X g
      prodâ‰¡ i = prod X (Î» x â†’ h x i)

    module _
      (h : (x : X .fst) â†’ f x â‰¤ g x) where

      sumâ‰¤ : sum X f â‰¤ sum X g
      sumâ‰¤ =
        elimProp
          (Î» X â†’ (f g : X .fst â†’ â„•)(h : (x : X .fst) â†’ f x â‰¤ g x) â†’ sum X f â‰¤ sum X g)
          (Î» X â†’ isPropÎ 3 (Î» _ _ _ â†’ mâ‰¤n-isProp)) sumâ‰¤ğ”½in X f g h

    module _
      (t : âˆ¥ X .fst âˆ¥)
      (h : (x : X .fst) â†’ f x < g x) where

      sum< : sum X f < sum X g
      sum< =
        elimProp
          (Î» X â†’ (f g : X .fst â†’ â„•)(t : âˆ¥ X .fst âˆ¥)(h : (x : X .fst) â†’ f x < g x) â†’ sum X f < sum X g)
          (Î» X â†’ isPropÎ 4 (Î» _ _ _ _ â†’ mâ‰¤n-isProp)) sum<ğ”½in X f g t h

module _
  (X : FinSet â„“)
  (f : X .fst â†’ â„•) where

  module _
    (c : â„•)(h : (x : X .fst) â†’ f x â‰¤ c) where

    sumBounded : sum X f â‰¤ c Â· card X
    sumBounded = subst (Î» a â†’ sum X f â‰¤ a) (sumConst X (Î» _ â†’ c) c (Î» _ â†’ refl)) (sumâ‰¤ X f (Î» _ â†’ c) h)

  module _
    (c : â„•)(h : (x : X .fst) â†’ f x â‰¥ c) where

    sumBoundedBelow : sum X f â‰¥ c Â· card X
    sumBoundedBelow = subst (Î» a â†’ sum X f â‰¥ a) (sumConst X (Î» _ â†’ c) c (Î» _ â†’ refl)) (sumâ‰¤ X (Î» _ â†’ c) f h)

-- some combinatorial identities

module _
  (X : FinSet â„“ )
  (Y : X .fst â†’ FinSet â„“') where

  cardÎ£ : card (_ , isFinSetÎ£ X Y) â‰¡ sum X (Î» x â†’ card (Y x))
  cardÎ£ =
    cardEquiv (_ , isFinSetÎ£ X Y) (_ , isFinSetÎ£ X (Î» x â†’ Fin (card (Y x)) , isFinSetFin))
              (Prop.map Î£-cong-equiv-snd
                (choice X (Î» x â†’ Y x .fst â‰ƒ Fin (card (Y x))) (Î» x â†’ âˆ£â‰ƒcardâˆ£ (Y x))))

  cardÎ  : card (_ , isFinSetÎ  X Y) â‰¡ prod X (Î» x â†’ card (Y x))
  cardÎ  =
    cardEquiv (_ , isFinSetÎ  X Y) (_ , isFinSetÎ  X (Î» x â†’ Fin (card (Y x)) , isFinSetFin))
              (Prop.map equivÎ Cod
                (choice X (Î» x â†’ Y x .fst â‰ƒ Fin (card (Y x))) (Î» x â†’ âˆ£â‰ƒcardâˆ£ (Y x))))

module _
  (X : FinSet â„“ )
  (Y : FinSet â„“') where

  cardâ†’ : card (_ , isFinSetâ†’ X Y) â‰¡ card Y ^ card X
  cardâ†’ = cardÎ  X (Î» _ â†’ Y) âˆ™ prodConst X (Î» _ â†’ card Y) (card Y) (Î» _ â†’ refl)

module _
  (X : FinSet â„“ ) where

  cardAut : card (_ , isFinSetAut X) â‰¡ LehmerCode.factorial (card X)
  cardAut = refl

module _
  (X : FinSet â„“ )
  (Y : FinSet â„“')
  (f : X .fst â†’ Y .fst) where

  sumCardFiber : card X â‰¡ sum Y (Î» y â†’ card (_ , isFinSetFiber X Y f y))
  sumCardFiber =
      cardEquiv X (_ , isFinSetÎ£ Y (Î» y â†’ _ , isFinSetFiber X Y f y)) âˆ£ totalEquiv f âˆ£
    âˆ™ cardÎ£ Y (Î» y â†’ _ , isFinSetFiber X Y f y)

-- the pigeonhole priniple

-- a logical lemma
private
  Â¬Î Qâ†’Â¬Â¬Î£P : (X : Type â„“)
      (P : X â†’ Type â„“' )
      (Q : X â†’ Type â„“'')
      (r : (x : X) â†’ Â¬ (P x) â†’ Q x)
    â†’ Â¬ ((x : X) â†’ Q x) â†’ Â¬ Â¬ (Î£ X P)
  Â¬Î Qâ†’Â¬Â¬Î£P _ _ _ r g f = g (Î» x â†’ r x (Î» p â†’ f (x , p)))

module _
  (f : X .fst â†’ Y .fst)
  (n : â„•) where

  fiberCount : ((y : Y .fst) â†’ card (_ , isFinSetFiber X Y f y) â‰¤ n) â†’ card X â‰¤ n Â· card Y
  fiberCount h =
    subst (Î» a â†’ a â‰¤ _) (sym (sumCardFiber X Y f))
          (sumBounded Y (Î» y â†’ card (_ , isFinSetFiber X Y f y)) n h)

  module _
    (p : card X > n Â· card Y) where

    Â¬Â¬pigeonHole : Â¬ Â¬ (Î£[ y âˆˆ Y .fst ] card (_ , isFinSetFiber X Y f y) > n)
    Â¬Â¬pigeonHole =
      Â¬Î Qâ†’Â¬Â¬Î£P (Y .fst) (Î» y â†’ _ > n) (Î» y â†’ _ â‰¤ n)
               (Î» y â†’ <-asym') (Î» h â†’ <-asym p (fiberCount h))

    pigeonHole : âˆ¥ Î£[ y âˆˆ Y .fst ] card (_ , isFinSetFiber X Y f y) > n âˆ¥
    pigeonHole = PeirceLaw (isFinSetÎ£ Y (Î» _ â†’ _ , isDecPropâ†’isFinSet mâ‰¤n-isProp (â‰¤Dec _ _))) Â¬Â¬pigeonHole

-- a special case, proved in Cubical.Data.Fin.Properties

-- a technical lemma
private
  Î£âˆ¥Pâˆ¥â†’âˆ¥Î£Pâˆ¥ : (X : Type â„“)(P : X â†’ Type â„“')
    â†’ Î£ X (Î» x â†’ âˆ¥ P x âˆ¥) â†’ âˆ¥ Î£ X P âˆ¥
  Î£âˆ¥Pâˆ¥â†’âˆ¥Î£Pâˆ¥ _ _ (x , p) = Prop.map (Î» q â†’ x , q) p

module _
  (f : X .fst â†’ Y .fst)
  (p : card X > card Y) where

  fiberNonEqualTerm : Î£[ y âˆˆ Y .fst ] card (_ , isFinSetFiber X Y f y) > 1
    â†’ âˆ¥ Î£[ y âˆˆ Y .fst ] Î£[ a âˆˆ fiber f y ] Î£[ b âˆˆ fiber f y ] Â¬ a â‰¡ b âˆ¥
  fiberNonEqualTerm (y , p) = Î£âˆ¥Pâˆ¥â†’âˆ¥Î£Pâˆ¥ _ _ (y , card>1â†’hasNonEqualTerm {X = _ , isFinSetFiber X Y f y} p)

  nonInj : Î£[ y âˆˆ Y .fst ] Î£[ a âˆˆ fiber f y ] Î£[ b âˆˆ fiber f y ] Â¬ a â‰¡ b
    â†’ Î£[ x âˆˆ X .fst ] Î£[ x' âˆˆ X .fst ] (Â¬ x â‰¡ x') Ã— (f x â‰¡ f x')
  nonInj (y , (x , p) , (x' , q) , t) .fst = x
  nonInj (y , (x , p) , (x' , q) , t) .snd .fst = x'
  nonInj (y , (x , p) , (x' , q) , t) .snd .snd .fst u =
    t (Î» i â†’ u i , isSetâ†’SquareP (Î» i j â†’ isFinSetâ†’isSet (Y .snd)) p q (cong f u) refl i)
  nonInj (y , (x , p) , (x' , q) , t) .snd .snd .snd = p âˆ™ sym q

  pigeonHole' : âˆ¥ Î£[ x âˆˆ X .fst ] Î£[ x' âˆˆ X .fst ] (Â¬ x â‰¡ x') Ã— (f x â‰¡ f x') âˆ¥
  pigeonHole' =
    Prop.map nonInj
      (Prop.rec isPropPropTrunc fiberNonEqualTerm
        (pigeonHole {X = X} {Y = Y} f 1 (subst (Î» a â†’ _ > a) (sym (Â·-identityË¡ _)) p)))

-- cardinality and injection/surjection

module _
  (X : FinSet â„“ )
  (Y : FinSet â„“') where

  module _
    (f : X .fst â†’ Y .fst) where

    cardâ†ªInequality' : isEmbedding f â†’ card X â‰¤ card Y
    cardâ†ªInequality' p =
      subst2 (_â‰¤_)
        (sym (sumCardFiber X Y f))
        (Â·-identityË¡ _)
        (sumBounded Y (Î» y â†’ card (_ , isFinSetFiber X Y f y)) 1
          (Î» y â†’ isPropâ†’cardâ‰¤1 (_ , isFinSetFiber X Y f y)
            (isEmbeddingâ†’hasPropFibers p y)))

    cardâ† Inequality' : isSurjection f â†’ card X â‰¥ card Y
    cardâ† Inequality' p =
      subst2 (_â‰¥_)
        (sym (sumCardFiber X Y f))
        (Â·-identityË¡ _)
        (sumBoundedBelow Y (Î» y â†’ card (_ , isFinSetFiber X Y f y)) 1
          (Î» y â†’ isInhabâ†’card>0 (_ , isFinSetFiber X Y f y) (p y)))

  cardâ†ªInequality : âˆ¥ X .fst â†ªÂ Y .fst âˆ¥ â†’ card X â‰¤ card Y
  cardâ†ªInequality = Prop.rec mâ‰¤n-isProp (Î» (f , p) â†’ cardâ†ªInequality' f p)

  cardâ† Inequality : âˆ¥ X .fst â† Â Y .fst âˆ¥ â†’ card X â‰¥ card Y
  cardâ† Inequality = Prop.rec mâ‰¤n-isProp (Î» (f , p) â†’ cardâ† Inequality' f p)

-- maximal value of numerical functions

module _
  (X : Type â„“)
  (f : X â†’ â„•) where

  module _
    (x : X) where

    isMax : Type â„“
    isMax = (x' : X) â†’ f x' â‰¤ f x

    isPropIsMax : isProp isMax
    isPropIsMax = isPropÎ  (Î» _ â†’ mâ‰¤n-isProp)

  uniqMax : (x x' : X) â†’ isMax x â†’ isMax x' â†’ f x â‰¡ f x'
  uniqMax x x' p q = â‰¤-antisym (q x) (p x')

  Î£Max : Type â„“
  Î£Max = Î£[ x âˆˆ X ] isMax x

  âˆƒMax : Type â„“
  âˆƒMax = âˆ¥ Î£Max âˆ¥

  âˆƒMaxâ†’maxValue : âˆƒMax â†’ â„•
  âˆƒMaxâ†’maxValue =
    SetElim.recâ†’Set
      isSetâ„• (Î» (x , p) â†’ f x)
             (Î» (x , p) (x' , q) â†’ uniqMax x x' p q)

-- lemma about maximal value on sum type
module _
  (X : Type â„“ )
  (Y : Type â„“')
  (f : X âŠ Y â†’ â„•) where

  Î£MaxâŠ-case : ((x , p) : Î£Max X (f âˆ˜ inl))((y , q) : Î£Max Y (f âˆ˜ inr))
    â†’ Trichotomy (f (inl x)) (f (inr y)) â†’ Î£Max (X âŠ Y) f
  Î£MaxâŠ-case (x , p) (y , q) (lt r) .fst = inr y
  Î£MaxâŠ-case (x , p) (y , q) (lt r) .snd (inl x') = â‰¤-trans (p x') (<-weaken r)
  Î£MaxâŠ-case (x , p) (y , q) (lt r) .snd (inr y') = q y'
  Î£MaxâŠ-case (x , p) (y , q) (eq r) .fst = inr y
  Î£MaxâŠ-case (x , p) (y , q) (eq r) .snd (inl x') = â‰¤-trans (p x') (_ , r)
  Î£MaxâŠ-case (x , p) (y , q) (eq r) .snd (inr y') = q y'
  Î£MaxâŠ-case (x , p) (y , q) (gt r) .fst = inl x
  Î£MaxâŠ-case (x , p) (y , q) (gt r) .snd (inl x') = p x'
  Î£MaxâŠ-case (x , p) (y , q) (gt r) .snd (inr y') = â‰¤-trans (q y') (<-weaken r)

  âˆƒMaxâŠ : âˆƒMax X (f âˆ˜ inl) â†’ âˆƒMax Y (f âˆ˜ inr) â†’ âˆƒMax (X âŠ Y) f
  âˆƒMaxâŠ = Prop.map2 (Î» p q â†’ Î£MaxâŠ-case p q (_â‰Ÿ_ _ _))

Î£MaxğŸ™ : (f : ğŸ™ {â„“} .fst â†’ â„•) â†’ Î£Max _ f
Î£MaxğŸ™ f .fst = tt*
Î£MaxğŸ™ f .snd x = _ , cong f (sym (isContrUnit* .snd x))

âˆƒMaxğŸ™ : (f : ğŸ™ {â„“} .fst â†’ â„•) â†’ âˆƒMax _ f
âˆƒMaxğŸ™ f = âˆ£ Î£MaxğŸ™ f âˆ£

âˆƒMaxğ”½in : (n : â„•)(f : ğ”½in {â„“} n .fst â†’ â„•)(x : âˆ¥ ğ”½in {â„“} n .fst âˆ¥) â†’ âˆƒMax _ f
âˆƒMaxğ”½in {â„“ = â„“} 0 _ x = Empty.rec (<â†’â‰¢ (isInhabâ†’card>0 (ğ”½in 0) x) (cardğŸ˜ {â„“ = â„“}))
âˆƒMaxğ”½in 1 f _ =
  subst (Î» X â†’ (f : X .fst â†’ â„•) â†’ âˆƒMax _ f) (sym ğ”½in1â‰¡ğŸ™) âˆƒMaxğŸ™ f
âˆƒMaxğ”½in (suc (suc n)) f _ =
  âˆƒMaxâŠ (ğŸ™ .fst) (ğ”½in (suc n) .fst) f (âˆƒMaxğŸ™ (f âˆ˜ inl)) (âˆƒMaxğ”½in (suc n) (f âˆ˜ inr) âˆ£ * {n = n} âˆ£)

module _
  (X : FinSet â„“)
  (f : X .fst â†’ â„•)
  (x : âˆ¥ X .fst âˆ¥) where

  âˆƒMaxFinSet : âˆƒMax _ f
  âˆƒMaxFinSet =
    elimProp
      (Î» X â†’ (f : X .fst â†’ â„•)(x : âˆ¥ X .fst âˆ¥) â†’ âˆƒMax _ f)
      (Î» X â†’ isPropÎ 2 (Î» _ _ â†’ isPropPropTrunc)) âˆƒMaxğ”½in X f x

  maxValue : â„•
  maxValue = âˆƒMaxâ†’maxValue _ _ âˆƒMaxFinSet

{- some formal consequences of card -}

-- card induces equivalence from set truncation of FinSet to natural numbers

open Iso

Iso-âˆ¥FinSetâˆ¥â‚‚-â„• : Iso âˆ¥ FinSet â„“ âˆ¥â‚‚ â„•
Iso-âˆ¥FinSetâˆ¥â‚‚-â„• .fun = Set.rec isSetâ„• card
Iso-âˆ¥FinSetâˆ¥â‚‚-â„• .inv n = âˆ£ ğ”½in n âˆ£â‚‚
Iso-âˆ¥FinSetâˆ¥â‚‚-â„• .rightInv n = cardğ”½in n
Iso-âˆ¥FinSetâˆ¥â‚‚-â„• {â„“ = â„“} .leftInv =
  Set.elim {B = Î» X â†’ âˆ£ ğ”½in (Set.rec isSetâ„• card X) âˆ£â‚‚ â‰¡ X}
    (Î» X â†’ isSetPathImplicit)
    (elimProp (Î» X â†’ âˆ£ ğ”½in (card X) âˆ£â‚‚ â‰¡ âˆ£ X âˆ£â‚‚) (Î» X â†’ squashâ‚‚ _ _)
      (Î» n i â†’ âˆ£ ğ”½in (cardğ”½in {â„“ = â„“} n i) âˆ£â‚‚))

-- this is the definition of natural numbers you learned from school
âˆ¥FinSetâˆ¥â‚‚â‰ƒâ„• : âˆ¥ FinSet â„“ âˆ¥â‚‚ â‰ƒ â„•
âˆ¥FinSetâˆ¥â‚‚â‰ƒâ„• = isoToEquiv Iso-âˆ¥FinSetâˆ¥â‚‚-â„•

-- FinProp is equivalent to Bool

Boolâ†’FinProp : Bool â†’ FinProp â„“
Boolâ†’FinProp true = ğŸ™ , isPropUnit*
Boolâ†’FinProp false = ğŸ˜ , isPropâŠ¥*

injBoolâ†’FinProp : (x y : Bool) â†’ Boolâ†’FinProp {â„“ = â„“} x â‰¡ Boolâ†’FinProp y â†’ x â‰¡ y
injBoolâ†’FinProp true true _ = refl
injBoolâ†’FinProp false false _ = refl
injBoolâ†’FinProp true false p = Empty.rec (snotz (cong (card âˆ˜ fst) p))
injBoolâ†’FinProp false true p = Empty.rec (znots (cong (card âˆ˜ fst) p))

isEmbeddingBoolâ†’FinProp : isEmbedding (Boolâ†’FinProp {â„“ = â„“})
isEmbeddingBoolâ†’FinProp = injEmbedding isSetBool isSetFinProp (Î» {x} {y} â†’ injBoolâ†’FinProp x y)

card-case : (P : FinProp â„“) â†’ {n : â„•} â†’ card (P .fst) â‰¡ n â†’ Î£[ x âˆˆ Bool ] Boolâ†’FinProp x â‰¡ P
card-case P {n = 0} p = false , FinPropâ‰¡ (ğŸ˜ , isPropâŠ¥*) P .fst (cong fst (sym (cardâ‰¡0 {X = P .fst} p)))
card-case P {n = 1} p = true , FinPropâ‰¡ (ğŸ™ , isPropUnit*) P .fst (cong fst (sym (cardâ‰¡1 {X = P .fst} p)))
card-case P {n = suc (suc n)} p =
  Empty.rec (Â¬-<-zero (pred-â‰¤-pred (subst (Î» a â†’ a â‰¤ 1) p (isPropâ†’cardâ‰¤1 (P .fst) (P .snd)))))

isSurjectionBoolâ†’FinProp : isSurjection (Boolâ†’FinProp {â„“ = â„“})
isSurjectionBoolâ†’FinProp P = âˆ£ card-case P refl âˆ£

FinPropâ‰ƒBool : FinProp â„“ â‰ƒ Bool
FinPropâ‰ƒBool =
  invEquiv (Boolâ†’FinProp ,
    isEmbeddingÃ—isSurjectionâ†’isEquiv (isEmbeddingBoolâ†’FinProp  , isSurjectionBoolâ†’FinProp))

isFinSetFinProp : isFinSet (FinProp â„“)
isFinSetFinProp = EquivPresIsFinSet (invEquiv FinPropâ‰ƒBool) isFinSetBool

-- a more computationally efficient version of equivalence type

module _
  (X : FinSet â„“ )
  (Y : FinSet â„“') where

  isFinSetâ‰ƒEff' : Dec (card X â‰¡ card Y) â†’ isFinSet (X .fst â‰ƒ Y .fst)
  isFinSetâ‰ƒEff' (yes p) = factorial (card Y) ,
    Prop.elim2 (Î» _ _ â†’ isPropPropTrunc {A = _ â‰ƒ Fin _})
      (Î» p1 p2
        â†’ âˆ£ equivComp (p1 â‹† pathToEquiv (cong Fin p) â‹† SumFinâ‰ƒFin _) (p2 â‹† SumFinâ‰ƒFin _)
          â‹† lehmerEquiv â‹† lehmerFinEquiv
          â‹† invEquiv (SumFinâ‰ƒFin _) âˆ£)
      (âˆ£â‰ƒcardâˆ£ X) (âˆ£â‰ƒcardâˆ£ Y)
  isFinSetâ‰ƒEff' (no Â¬p) = 0 , âˆ£ uninhabEquiv (Â¬p âˆ˜ cardEquiv X Y âˆ˜ âˆ£_âˆ£) (idfun _) âˆ£

  isFinSetâ‰ƒEff : isFinSet (X .fst â‰ƒ Y .fst)
  isFinSetâ‰ƒEff = isFinSetâ‰ƒEff' (discreteâ„• _ _)

module _
  (X Y : FinSet â„“) where

  isFinSetTypeâ‰¡Eff : isFinSet (X .fst â‰¡ Y .fst)
  isFinSetTypeâ‰¡Eff = EquivPresIsFinSet (invEquiv univalence) (isFinSetâ‰ƒEff X Y)
