{-

Inductive eliminators to establish properties of all finite sets directly

-}
{-# OPTIONS --safe #-}

module Cubical.Data.FinSet.Induction where

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.HLevels
open import Cubical.Foundations.Equiv renaming (_âˆ™â‚‘_ to _â‹†_)
open import Cubical.Foundations.Isomorphism
open import Cubical.Foundations.Univalence

open import Cubical.HITs.PropositionalTruncation as Prop
open import Cubical.HITs.SetTruncation as Set

open import Cubical.Data.Nat
  renaming (_+_ to _+â„•_) hiding (elim)
open import Cubical.Data.Unit
open import Cubical.Data.Empty as Empty
open import Cubical.Data.Sum

open import Cubical.Data.Fin renaming (Fin to Finâ„•)
open import Cubical.Data.SumFin
open import Cubical.Data.FinSet.Base
open import Cubical.Data.FinSet.Properties
open import Cubical.Data.FinSet.Constructors

private
  variable
    â„“ â„“' : Level

-- definitions mimicking that of natural numbers

module _
  {â„“ : Level} where

  ğŸ˜ : FinSet â„“
  ğŸ˜ = âŠ¥* , 0 , âˆ£ uninhabEquiv Empty.rec* Empty.rec âˆ£

  ğŸ™ : FinSet â„“
  ğŸ™ = Unit* , isContrâ†’isFinSet (isContrUnit*)

  _+_ : FinSet â„“ â†’ FinSet â„“ â†’ FinSet â„“
  X + Y = X .fst âŠ Y .fst , isFinSetâŠ X Y

  -- ğ”½in can be seen as a universe polymorphic version of SumFin
  ğ”½in : â„• â†’ FinSet â„“
  ğ”½in 0 = ğŸ˜
  ğ”½in (suc n) = ğŸ™ + ğ”½in n

  -- useful properties

  ğŸ˜â‰ƒEmpty : ğŸ˜ .fst â‰ƒ âŠ¥
  ğŸ˜â‰ƒEmpty = uninhabEquiv rec* (Î» x â†’ x)

  ğŸ™â‰ƒUnit : ğŸ™ .fst â‰ƒ Unit
  ğŸ™â‰ƒUnit = isContrâ†’â‰ƒUnit (isContrUnit*)

  * : {n : â„•} â†’ ğ”½in (suc n) .fst
  * = inl tt*

  ğ”½inâ‰ƒFin : (n : â„•) â†’ ğ”½in n .fst â‰ƒ Fin n
  ğ”½inâ‰ƒFin 0 = ğŸ˜â‰ƒEmpty
  ğ”½inâ‰ƒFin (suc n) = âŠ-equiv ğŸ™â‰ƒUnit (ğ”½inâ‰ƒFin n)

  ğ”½inâ‰ƒFinâ„• : (n : â„•) â†’ ğ”½in n .fst â‰ƒ Finâ„• n
  ğ”½inâ‰ƒFinâ„• n = ğ”½inâ‰ƒFin n â‹† SumFinâ‰ƒFin n

  -- ğ”½in preserves addition

  ğŸ˜+Xâ‰¡X : {X : FinSet â„“} â†’ ğŸ˜ + X â‰¡ X
  ğŸ˜+Xâ‰¡X {X = X} i .fst = ua (âŠ-swap-â‰ƒ â‹† âŠ-equiv (idEquiv (X .fst)) ğŸ˜â‰ƒEmpty â‹† âŠ-âŠ¥-â‰ƒ) i
  ğŸ˜+Xâ‰¡X {X = X} i .snd =
    isPropâ†’PathP {B = Î» i â†’ isFinSet (ğŸ˜+Xâ‰¡X {X = X} i .fst)}
                 (Î» _ â†’ isPropIsFinSet) ((ğŸ˜ + X) .snd) (X .snd) i

  ğ”½in1â‰¡ğŸ™ : ğ”½in 1 â‰¡ ğŸ™
  ğ”½in1â‰¡ğŸ™ i .fst = ua (âŠ-equiv (idEquiv (ğŸ™ .fst)) ğŸ˜â‰ƒEmpty â‹† âŠ-âŠ¥-â‰ƒ) i
  ğ”½in1â‰¡ğŸ™ i .snd =
    isPropâ†’PathP {B = Î» i â†’ isFinSet (ğ”½in1â‰¡ğŸ™ i .fst)}
                 (Î» _ â†’ isPropIsFinSet) (ğ”½in 1 .snd) (ğŸ™ .snd) i

  ğ”½in+ : (m n : â„•) â†’ ğ”½in m + ğ”½in n â‰¡ ğ”½in (m +â„• n)
  ğ”½in+ 0 n = ğŸ˜+Xâ‰¡X
  ğ”½in+ (suc m) n i .fst = (ua (âŠ-assoc-â‰ƒ) âˆ™ (Î» i â†’ (ğŸ™ + ğ”½in+ m n i) .fst)) i
  ğ”½in+ (suc m) n i .snd =
    isPropâ†’PathP {B = Î» i â†’ isFinSet (ğ”½in+ (suc m) n i .fst)}
                 (Î» _ â†’ isPropIsFinSet) ((ğ”½in (suc m) + ğ”½in n) .snd) (ğ”½in (suc m +â„• n) .snd) i

-- every finite sets are merely equal to some ğ”½in

âˆ£â‰¡ğ”½inâˆ£ : (X : FinSet â„“) â†’ âˆ¥ Î£[ n âˆˆ â„• ] X â‰¡ ğ”½in n âˆ¥
âˆ£â‰¡ğ”½inâˆ£ X = Prop.map (Î» (n , p) â†’ n , path X (n , p)) (isFinSetâ†’isFinSet' (X .snd))
  where
    path : (X : FinSet â„“) â†’ ((n , _) : isFinOrd (X .fst)) â†’ X â‰¡ ğ”½in n
    path X (n , p) i .fst = ua (p â‹† invEquiv (ğ”½inâ‰ƒFin n)) i
    path X (n , p) i .snd =
      isPropâ†’PathP {B = Î» i â†’ isFinSet (path X (n , p) i .fst)}
                   (Î» _ â†’ isPropIsFinSet) (X .snd) (ğ”½in n .snd) i

-- the eliminators

module _
  (P : FinSet â„“ â†’ Type â„“')
  (h : (X : FinSet â„“) â†’ isProp (P X)) where

  module _
    (p : (n : â„•) â†’ P (ğ”½in n)) where

    elimProp : (X : FinSet â„“) â†’ P X
    elimProp X = Prop.rec (h X) (Î» (n , q) â†’ transport (Î» i â†’ P (q (~ i))) (p n)) (âˆ£â‰¡ğ”½inâˆ£ X)

  module _
    (p0 : P ğŸ˜)
    (p1 : {X : FinSet â„“} â†’ P X â†’ P (ğŸ™ + X)) where

    elimPropğ”½in : (n : â„•) â†’ P (ğ”½in n)
    elimPropğ”½in 0 = p0
    elimPropğ”½in (suc n) = p1 (elimPropğ”½in n)

    elimPropğŸ™+ : (X : FinSet â„“) â†’ P X
    elimPropğŸ™+ = elimProp elimPropğ”½in

  module _
    (p0 : P ğŸ˜)(p1 : P ğŸ™)
    (p+ : {X Y : FinSet â„“} â†’ P X â†’ P Y â†’ P (X + Y)) where

    elimProp+ : (X : FinSet â„“) â†’ P X
    elimProp+ = elimPropğŸ™+ p0 (Î» p â†’ p+ p1 p)
