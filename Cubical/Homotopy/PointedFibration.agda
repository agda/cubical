{-# OPTIONS --cubical --safe #-}

module Cubical.Homotopy.PointedFibration where

open import Cubical.Core.Everything

open import Cubical.Data.Nat

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.Pointed
open import Cubical.Foundations.Isomorphism
open import Cubical.Foundations.GroupoidLaws
open import Cubical.Foundations.Equiv
open import Cubical.Foundations.HLevels
open import Cubical.Homotopy.Base
open import Cubical.Homotopy.ConnectedOld
open import Cubical.Homotopy.Loopspace
open import Cubical.Data.Nat
open import Cubical.Data.Nat.Order
open LowerBoundedInduction
open import Cubical.Data.Sigma
open import Cubical.Data.Unit
open import Cubical.Foundations.HLevels'
open import Cubical.Foundations.Structure

private
  variable
    â„“ â„“' : Level

-- Different versions of Theorem 9. We abstract â„“' again here
-- to avoid some issues with implicit arguments.
module _ {â„“' : Level} (X : Pointed â„“) where
  -- block of private stuff to reduce redundancy in the proof of the theorme
  private
    module _ (n k : â„•) where
      -- A, together with its paramenters/context, is just the statement of the theorem.
      A : Type (â„“-max â„“ (â„“-suc â„“'))
      A = (isConnX : isConnected (k + 1) (typ X))
          â†’ (Y : typ X â†’ Î£[ Yx âˆˆ Pointed â„“' ] (isOfHLevel (n + k) (typ Yx)))
          â†’ isOfHLevel (n) (Î âˆ™ X (Î» x â†’ typ (fst (Y x))) (pt (fst (Y (pt X)))))

    module PointSec (n k : â„•)
                    (isConnX : isConnected (k + 1) (typ X))
                    (Y : typ X â†’ Î£[ Yx âˆˆ Pointed â„“' ] (isOfHLevel (n + k) (typ Yx))) where
      -- The type of pointed sections (x : X) â†’â‚šâ‚œ Y x
      secâˆ™ : Type (â„“-max â„“ â„“')
      secâˆ™ = Î âˆ™ X (Î» x â†’ typ (fst (Y x))) (pt (fst (Y (pt X))))

      -- Note that if isOfHLevel had a uniform interface for n â‰¥ 1 then this should be a part of the where
      -- clause in the theorem.
      module PointSecProps where
        -- Given s, the type of pointed sections (x : X) â†’â‚šâ‚œ Î©(Y x, s x)
        secâˆ™' : (s : secâˆ™) â†’ Type (â„“-max â„“ â„“')
        secâˆ™' s = Î âˆ™ X (Î» x â†’ s .fst x â‰¡ s .fst x) refl

        -- towards secâˆ™' s â‰ƒ (s â‰¡ s)
        secIso : (s : secâˆ™) â†’ Iso (secâˆ™' s) (s âˆ™âˆ¼ s)
        secIso (_ , sâ‚‚) = iso (Î» (H , p) â†’ H , p âˆ™ sym (rCancel sâ‚‚))
                        (Î» (H , p) â†’ H , p âˆ™ rCancel sâ‚‚)
                        (Î» (H , p) â†’ Î£PathP (refl ,
                                            sym (assoc p (rCancel sâ‚‚) (sym (rCancel sâ‚‚))) âˆ™âˆ™
                                            cong (p âˆ™_) (rCancel (rCancel sâ‚‚)) âˆ™âˆ™
                                            sym (rUnit p)))
                        (Î» (H , p) â†’ Î£PathP (refl ,
                                            sym (assoc p
                                                       (sym (rCancel sâ‚‚))
                                                       (rCancel sâ‚‚)) âˆ™âˆ™
                                            cong (p âˆ™_) (lCancel (rCancel sâ‚‚)) âˆ™âˆ™
                                            sym (rUnit p)))

        -- compose the equivalences
        secâ‰ƒ : (s : secâˆ™) â†’ secâˆ™' s â‰ƒ (s â‰¡ s)
        secâ‰ƒ = Î» (s : secâˆ™) â†’ compEquiv (isoToEquiv (secIso s)) (funExtâˆ™â‰ƒ s s)

  -- p.9 Theorem 3 of Higher Types in HoTT
  secâˆ™Trunc : {n k : â„•} â†’ A (n + 1) (k)
  secâˆ™Trunc {n = 0} {k} isConnX Y = isContrâ†’isProp (sâ‚€ , Î» s â†’ funExtâˆ™ (sâ‚€âˆ™âˆ¼s s))
    where
      secâˆ™ : Type (â„“-max â„“ â„“')
      secâˆ™ = Î âˆ™ X (Î» x â†’ typ (fst (Y x))) (pt (fst (Y (pt X))))

      module _ where
        -- trivial section
        sâ‚€ : secâˆ™
        sâ‚€ = (Î» a â†’ pt (fst (Y a))) , refl

        -- abbreviations
        sâ‚€â‚ = fst sâ‚€
        â‹† = pt X

        -- the k-connected map ğŸ™ â†’ X
        f : Unit â†’ typ X
        f tt = â‹†

        -- proof that f is k-connected
        fkconn : isConnectedFun k f
        fkconn = UnitConnectedFunElim isConnX f

        -- use the elimnation principle of the k-connected map f
        open elim f

        -- notation
        module _ (s : secâˆ™) where
          sâ‚ = fst s
          sâ‚‚ = snd s

          -- the regular homotopies between the trivial section and s coincide with the
          -- identity type sâ‚€â‚ â‹† â‰¡ sâ‚ â‹†
          -- the Unit type will be eliminated in the next step
          IsoHtpyğŸ™Idpt : Iso (sâ‚€â‚ âˆ¼ sâ‚) (Unit â†’ sâ‚€â‚ â‹† â‰¡ sâ‚ â‹†)
          IsoHtpyğŸ™Idpt = isIsoPrecompose k (Î» (x : typ X) â†’ (sâ‚€â‚ x â‰¡ sâ‚ x) , HLâ† (HLâ†’ (snd (Y x)) (sâ‚€â‚ x) (sâ‚ x))) fkconn
          -- IsoHtpyğŸ™Idpt = isIsoPrecompose (Î» (x : typ X) â†’ (sâ‚€â‚ x â‰¡ sâ‚ x) , HLâ† ((HLâ†’ (snd (Y x))) (sâ‚€â‚ x) (sâ‚ x))) fkconn

          IsoHtpyIdpt : Iso (sâ‚€â‚ âˆ¼ sâ‚) (sâ‚€â‚ â‹† â‰¡ sâ‚ â‹†)
          IsoHtpyIdpt = compIso IsoHtpyğŸ™Idpt (ğŸ™-universal (sâ‚€â‚ â‹† â‰¡ sâ‚ â‹†))

          -- judgementally,
          -- (sâ‚€ âˆ™âˆ¼ s) â‰¡ (Î£[ h âˆˆ (sâ‚€â‚ âˆ¼ sâ‚) ] (h â‹† â‰¡ (snd sâ‚€) âˆ™ sâ‚‚ â»Â¹))
          -- The right inverse of IsoHtpyIdpt gives such a pointed homotopy
          sâ‚€âˆ™âˆ¼s : sâ‚€ âˆ™âˆ¼ s
          fst sâ‚€âˆ™âˆ¼s = Iso.inv IsoHtpyIdpt (refl âˆ™ sâ‚‚ â»Â¹)
          snd sâ‚€âˆ™âˆ¼s =
            Iso.inv IsoHtpyIdpt (refl âˆ™ sâ‚‚ â»Â¹) â‹†
              â‰¡âŸ¨ refl âŸ©
            Iso.fun IsoHtpyIdpt (Iso.inv IsoHtpyIdpt (refl âˆ™ sâ‚‚ â»Â¹))
              â‰¡âŸ¨ Iso.rightInv IsoHtpyIdpt (refl âˆ™ sâ‚‚ â»Â¹) âŸ©
            refl âˆ™ sâ‚‚ â»Â¹ âˆ
         

  secâˆ™Trunc {n = 1} {k} isConnX Y = truncSelfIdâ†’truncId {n = 0} (Î» s â†’ EquivPresHLevel {n = 1} (secâ‰ƒ s) (secâˆ™Trunc {n = 0} {k} isConnX Î» x â†’ ((s .fst x â‰¡ s .fst x) , refl) , (snd (Y x) (s .fst x) (s .fst x))))
    where
      open PointSec 2 k isConnX Y
      open PointSecProps

  secâˆ™Trunc {n = suc (suc m)} {k} isConnX Y =
    -- suffices to show that loop spaces are truncated
    truncSelfIdâ†’truncId
      -- each self-identity type of a section s is equivalent to a type of sections
      Î» s â†’ EquivPresHLevel (secâ‰ƒ s)
        -- that the induction hypothesis can be applied to
        (secâˆ™Trunc {n = suc m} isConnX Î» x â†’ ((s .fst x â‰¡ s .fst x) , refl) , snd (Y x) (s .fst x) (s .fst x))
    where
      open PointSec (suc (suc m) + 1) k isConnX Y
      open PointSecProps


  -- alternate version of secâˆ™Trunc with bound on n instead of adding a bound
  secâˆ™Trunc' : {n k : â„•} (1â‰¤n : 1 â‰¤ n) â†’ A n k
  secâˆ™Trunc' {n = n} {k = k} 1â‰¤n
    = +Typeâ†’â‰¤Type 1 (Î» n â†’ A n k) (Î» r isConnX Y â†’ secâˆ™Trunc {n = r} {k = k} isConnX Y) n 1â‰¤n


module _ (X : Pointed â„“) (Y : Pointed â„“') where
  pointed-maps-truncated : {n k : â„•}
                           â†’ 1 â‰¤ n
                           â†’ isConnected (k + 1) (typ X)
                           â†’ isOfHLevel (n + k) (typ Y)
                           â†’ isOfHLevel (n) (X â†’âˆ™ Y)
  pointed-maps-truncated {n = n} 1â‰¤n connX truncY =
    secâˆ™Trunc' X 1â‰¤n connX Î» _ â†’ Y , truncY
