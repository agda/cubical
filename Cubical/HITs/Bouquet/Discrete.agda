
{-

This module, uses normalization and decidable equality from `Cubical.Algebra.Group.Free` to demonstrate that Bouquet over discrete type is hGroupoid by establishing a coding between loops in the bouquet and elements of the FreeGroup represented by normalised words.

-}

module Cubical.HITs.Bouquet.Discrete where

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.Function
open import Cubical.Foundations.Univalence
open import Cubical.Foundations.Isomorphism
open import Cubical.Foundations.Transport
open import Cubical.Foundations.HLevels

open import Cubical.Data.Bool
open import Cubical.Data.Empty as âŠ¥
open import Cubical.Data.Sigma
open import Cubical.Data.List renaming (rec to recList)
open import Cubical.Data.Sum

open import Cubical.Relation.Nullary


open import Cubical.HITs.Bouquet.Base
open import Cubical.Algebra.Group.Free


private
  variable
    â„“ : Level

module _ {A : Type â„“} (_â‰Ÿ_ : Discrete A) where

  open NormalForm A

  open Discrete _â‰Ÿ_


  CodeBouquet : Bouquet A â†’ Type â„“
  CodeBouquet base = Î£ _ IsNormalised
  CodeBouquet (loop a i) = ua (Î·Â·â‰ƒ (true , a)) i

  coâ†’ : âˆ€ x â†’ base â‰¡ x â†’ CodeBouquet x
  coâ†’ x p = subst CodeBouquet p ([] , lower)

  coâ†base-step : Bool Ã— A â†’ Path (Bouquet A) base base

  coâ†base-step (b , a) = if b then (idfun _) else sym $ loop a

  coâ†base : [ğŸšÃ— A ] â†’ Path (Bouquet A) base base
  coâ†base = recList refl (flip _âˆ™_ âˆ˜ coâ†base-step)

  coâ† : âˆ€ x â†’ CodeBouquet x â†’ base â‰¡ x
  coâ† base = coâ†base âˆ˜ fst
  coâ† (loop a i) x j = coâ†Sq a i j x
   where

   coâ†Sq : (a : A) â†’ SquareP (Î» i j â†’  ua (Î·Â·â‰ƒ (true , a)) i â†’ Bouquet A)
                        (funExt (coâ†base âˆ˜ fst))
                        (funExt (coâ†base âˆ˜ fst))
                        (Î» _ _ â†’ base)
                        (Î» i _ â†’ loop a i)
   coâ†Sq a = congP (Î» _ â†’ funExt) (uaâ†’ (uncurry
      (Î» xs y â†’ coâ†Sq' a xs y (HeadIsRedex? ((true , a) âˆ· xs)))))
    where
    coâ†Sq' : (a : A) â†’ (x : [ğŸšÃ— A ]) (y : IsNormalised x) â†’
       âˆ€ u â†’ PathP (Î» i â†’ base â‰¡ loop a i)
       (recList (Î» _ â†’ base) (flip _âˆ™_ âˆ˜ coâ†base-step) x)
       (recList (Î» _ â†’ base) (flip _âˆ™_ âˆ˜ coâ†base-step) (preÎ·Â· (true , a) x u ))
    coâ†Sq' a ((false , sndâ‚) âˆ· xs) y (yes p) =
      cong (Î» x' â†’ coâ†base ((false , x') âˆ· xs)) (cong snd (sym p))
        â— symP (compPath-filler (coâ†base xs) (sym (loop a)))
    coâ†Sq' a xs y (no Â¬p) = compPath-filler _ _
    coâ†Sq' a ((true , sndâ‚) âˆ· xs) y (yes p) = âŠ¥.rec (trueâ‰¢false (cong fst p))

  coSec : âˆ€ x â†’ section (coâ† x) (coâ†’ x)
  coSec _ = J (Î» x b â†’ coâ† x (coâ†’ x b) â‰¡ b) refl

  coRet : (x : [ğŸšÃ— A ]) (y : IsNormalised x) â†’
            fst (subst CodeBouquet (coâ† base (x , y)) ([] , lower))
                  â‰¡ x
  coRet [] y = refl
  coRet (x@(b , a) âˆ· xs) y =
    cong fst (substComposite CodeBouquet (coâ† base (xs , y âˆ˜ inr))
      (coâ†base-step x) _)
      âˆ™âˆ™
      cong (fst âˆ˜ subst CodeBouquet (coâ†base-step x))
         (Î£â‰¡Prop isPropIsNormalised (coRet xs (y âˆ˜ inr))) âˆ™âˆ™
      lem b xs (y âˆ˜ inr) âˆ™ Î·Â·âˆ· x xs (y âˆ˜ inl)

   where
   lem : âˆ€ b xs y â†’ fst
      (subst CodeBouquet (coâ†base-step (b , a)) (xs , y))
      â‰¡ Î·Â· (b , a) xs
   lem false xs y = cong fst (~uaÎ² (Î·Â·â‰ƒ (true , a)) (xs , y ))
   lem true xs y = cong fst (uaÎ² (Î·Â·â‰ƒ (true , a)) (xs , y ))


  codeDecode : Iso (Path (Bouquet A) base base)
                   (Î£ _ IsNormalised)
  Iso.fun codeDecode p = subst CodeBouquet p ([] , lower)
  Iso.inv codeDecode = coâ† base
  Iso.rightInv codeDecode = Î£â‰¡Prop isPropIsNormalised âˆ˜ uncurry coRet
  Iso.leftInv codeDecode = coSec base

  isSetÎ©Bouquet : isSet (Path (Bouquet A) base base)
  isSetÎ©Bouquet = isOfHLevelRetractFromIso 2 codeDecode
                     (isSetÎ£ isSet[ğŸšÃ—] (isPropâ†’isSet âˆ˜ isPropIsNormalised))

  isGroupoidBouquet : isGroupoid (Bouquet A)
  isGroupoidBouquet base base = isSetÎ©Bouquet
  isGroupoidBouquet base (loop x i) = isPropâ†’PathP
   (Î» i â†’ isPropIsSet {A = Path (Bouquet A) base (loop x i)}) isSetÎ©Bouquet isSetÎ©Bouquet i
  isGroupoidBouquet (loop x i) base = isPropâ†’PathP
   (Î» i â†’ isPropIsSet {A = Path (Bouquet A) (loop x i) base}) isSetÎ©Bouquet isSetÎ©Bouquet i
  isGroupoidBouquet (loop x i) (loop xâ‚ iâ‚) = isSetâ†’SquareP
      (Î» i iâ‚ â†’ isPropâ†’isSet (isPropIsSet {A = Path (Bouquet A) (loop x i) (loop xâ‚ iâ‚)}))
      (isPropâ†’PathP
         (Î» i â†’ isPropIsSet {A = Path (Bouquet A) base (loop xâ‚ i)}) isSetÎ©Bouquet isSetÎ©Bouquet)
      (isPropâ†’PathP
         (Î» i â†’ isPropIsSet {A = Path (Bouquet A) base (loop xâ‚ i)}) isSetÎ©Bouquet isSetÎ©Bouquet)
      (isPropâ†’PathP
         (Î» i â†’ isPropIsSet {A = Path (Bouquet A) (loop x i) base}) isSetÎ©Bouquet isSetÎ©Bouquet)
      (isPropâ†’PathP
         (Î» i â†’ isPropIsSet {A = Path (Bouquet A) (loop x i) base}) isSetÎ©Bouquet isSetÎ©Bouquet)
     i iâ‚
