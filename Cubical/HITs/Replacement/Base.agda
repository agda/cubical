{-

Type-theoretic replacement: a construction taking a map F : A â†’ B where
  - A : Type â„“A
  - B : Type â„“B,
  - the identity types of B essentially have universe level â„“â‰…B,
and producing an image of F with universe level (â„“-max â„“A â„“â‰…B).

See Axiom 18.1.8 in

Egbert Rijke
Introduction to Homotopy Theory
https://arxiv.org/abs/2212.11082

for a definition of type-theoretic replacement.

This module constructs the replacement using higher induction-recursion. It is
possible to construct the replacement with much less powerful HITs, for which
see

Egbert Rijke
The join construction
https://arxiv.org/abs/1701.07538

but higher IR allows for a particularly simple construction.

-}
{-# OPTIONS --safe #-}
module Cubical.HITs.Replacement.Base where

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.HLevels
open import Cubical.Functions.Embedding
open import Cubical.Functions.Image
open import Cubical.HITs.PropositionalTruncation as Prop
open import Cubical.Foundations.Isomorphism
open import Cubical.Foundations.Equiv
open import Cubical.Functions.Surjection
open import Cubical.Displayed.Base

module _ {â„“A â„“B â„“â‰…B} {A : Type â„“A} {B : Type â„“B} (ð’®-B : UARel B â„“â‰…B) (f : A â†’ B)  where

  module B = UARel ð’®-B

  data Replacement : Type (â„“-max â„“A â„“â‰…B)
  unrep : Replacement â†’ B

  data Replacement where
    rep : A â†’ Replacement
    quo : âˆ€ r r' â†’ unrep r B.â‰… unrep r' â†’ r â‰¡ r'
    quoid : âˆ€ r â†’ quo r r (B.Ï (unrep r)) â‰¡ refl

  unrep (rep a) = f a
  unrep (quo r r' eqv i) = B.â‰…â†’â‰¡ eqv i
  unrep (quoid r j i) = B.uaIso (unrep r) (unrep r) .Iso.rightInv refl j i

  {-
    To eliminate into a proposition, we need only provide the point constructor
    case.
  -}

  elimProp : âˆ€ {â„“} {P : Replacement â†’ Type â„“}
    â†’ ((r : Replacement) â†’ isProp (P r))
    â†’ ((x : A) â†’ P (rep x))
    â†’ (r : Replacement) â†’ P r
  elimProp prop g (rep x) = g x
  elimProp prop g (quo r r' eqv i) =
    isPropâ†’PathP (Î» i â†’ prop (quo r r' eqv i))
      (elimProp prop g r)
      (elimProp prop g r')
      i
  elimProp prop g (quoid r i j) =
    isSetâ†’SquareP (Î» i j â†’ isPropâ†’isSet (prop (quoid r i j)))
      (isPropâ†’PathP (Î» i â†’ prop (quo r r _ i)) _ _)
      (Î» _ â†’ elimProp prop g r)
      (Î» _ â†’ elimProp prop g r)
      (Î» _ â†’ elimProp prop g r)
      i j

  {-
    Our image factorization is F â‰¡ unrep âˆ˜ rep.
    Note that this equation holds judgmentally.
  -}

  -- Surjection half of the image factorization

  isSurjectiveRep : isSurjection rep
  isSurjectiveRep = elimProp (Î» _ â†’ squashâ‚) (Î» x â†’ âˆ£ x , refl âˆ£â‚)

  -- Embedding half of the image factorization

  isEmbeddingUnrep : isEmbedding unrep
  isEmbeddingUnrep r r' =
    isoToIsEquiv (iso _ (inv r r') (elInv r r') (invEl r r'))
    where
    inv : âˆ€ r r' â†’ unrep r â‰¡ unrep r' â†’ r â‰¡ r'
    inv r r' Q = quo r r' (B.â‰¡â†’â‰… Q)

    elInv : âˆ€ r r' Q â†’  cong unrep (inv r r' Q) â‰¡ Q
    elInv r r' Q = B.uaIso (unrep r) (unrep r') .Iso.rightInv Q

    invEl : âˆ€ r r' p â†’ inv r r' (cong unrep p) â‰¡ p
    invEl r = J> quoid r

  -- Equivalence to the image with level (â„“-max â„“A â„“B) that always exists

  replacementâ‰ƒImage : Replacement â‰ƒ Image f
  replacementâ‰ƒImage =
    imagesEquiv
      (rep , isSurjectiveRep)
      (unrep , isEmbeddingUnrep)
      (restrictToImage f , isSurjectionImageRestriction f)
      (imageInclusion f)
      refl
