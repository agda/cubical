{- Conatural number properties (Tesla Ice Zhang et al., Feb. 2019)

This file defines operations and properties on conatural numbers:

- Infinity (âˆ).

- Proof that âˆ + 1 is equivalent to âˆ.

- Proof that conatural is an hSet.

- Bisimulation on conatural

- Proof that bisimulation is equivalent to equivalence (Coinductive Proof
  Principle).

- Proof that this bisimulation is prop valued

The standard library also defines bisimulation on conaturals:

https://github.com/agda/agda-stdlib/blob/master/src/Codata/Conat/Bisimilarity.agda
-}

{-# OPTIONS --guardedness #-}
module Cubical.Codata.Conat.Properties where

open import Cubical.Data.Unit
open import Cubical.Data.Sum
open import Cubical.Data.Sigma
open import Cubical.Data.Empty as âŠ¥
open import Cubical.Data.Bool
  renaming (Boolâ†’Type to âŸ¨_âŸ©)

import Cubical.Data.Nat as Nat
import Cubical.Data.Nat.Order.Recursive as Nat

open import Cubical.Functions.Embedding

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.Equiv
open import Cubical.Foundations.Function
open import Cubical.Foundations.HLevels
open import Cubical.Foundations.Path
open import Cubical.Foundations.Transport
open import Cubical.Foundations.Isomorphism
open import Cubical.Foundations.Univalence

open import Cubical.Relation.Nullary
open import Cubical.Codata.Conat.Base

import Cubical.Axiom.Omniscience as Omni

Unwrap-prev : Conatâ€² â†’ Typeâ‚€
Unwrap-prev  zero   = Unit
Unwrap-prev (suc _) = Conat

unwrap-prev : (n : Conatâ€²) -> Unwrap-prev n
unwrap-prev  zero   = _
unwrap-prev (suc x) = x

private -- tests
  ğŸ˜ = conat zero
  ğŸ™  = succ ğŸ˜
  ğŸš  = succ ğŸ™

  succğŸ™â‰¡ğŸš : succ ğŸ™ â‰¡ ğŸš
  succğŸ™â‰¡ğŸš i = ğŸš

  predğŸšâ‰¡ğŸ™ : unwrap-prev (force ğŸš) â‰¡ ğŸ™
  predğŸšâ‰¡ğŸ™ i = ğŸ™

âˆ : Conat
force âˆ = suc âˆ

âˆ+1â‰¡âˆ : succ âˆ â‰¡ âˆ
force (âˆ+1â‰¡âˆ _) = suc âˆ

âˆ+2â‰¡âˆ : succ (succ âˆ) â‰¡ âˆ
âˆ+2â‰¡âˆ = (cong succ âˆ+1â‰¡âˆ) âˆ™ âˆ+1â‰¡âˆ

_+_ : Conat â†’ Conat â†’ Conat
_+â€²_ : Conatâ€² â†’ Conat â†’ Conatâ€²

force (x + y) = force x +â€² y
zero +â€² y = force y
suc x +â€² y = suc (x + y)

n+âˆâ‰¡âˆ : âˆ€ n â†’ n + âˆ â‰¡ âˆ
n+â€²âˆâ‰¡âˆâ€² : âˆ€ n â†’ n +â€² âˆ â‰¡ suc âˆ

force (n+âˆâ‰¡âˆ n i) = n+â€²âˆâ‰¡âˆâ€² (force n) i
n+â€²âˆâ‰¡âˆâ€² zero = refl
n+â€²âˆâ‰¡âˆâ€² (suc n) = Î» i â†’ suc (n+âˆâ‰¡âˆ n i)

âˆ+âˆâ‰¡âˆ : âˆ + âˆ â‰¡ âˆ
force (âˆ+âˆâ‰¡âˆ i) = suc (âˆ+âˆâ‰¡âˆ i)

+-zeroË¡ : âˆ€ n â†’ ğŸ˜ + n â‰¡ n
force (+-zeroË¡ n _) = force n

+-zeroÊ³ : âˆ€ n â†’ n + ğŸ˜ â‰¡ n
+â€²-zeroÊ³ : âˆ€ n â†’ n +â€² ğŸ˜ â‰¡ n

force (+-zeroÊ³ n i) = +â€²-zeroÊ³ (force n) i
+â€²-zeroÊ³ zero _ = zero
+â€²-zeroÊ³ (suc n) i = suc (+-zeroÊ³ n i)

+-assoc : âˆ€ m n p â†’ (m + n) + p â‰¡ m + (n + p)
+â€²-assoc : âˆ€ m n p â†’ (m +â€² n) +â€² p â‰¡ m +â€² (n + p)

force (+-assoc m n p i) = +â€²-assoc (force m) n p i
+â€²-assoc zero _ _ = refl
+â€²-assoc (suc m) n p i = suc (+-assoc m n p i)


conat-absurd : âˆ€ {y : Conat} {â„“} {Whatever : Type â„“} â†’ zero â‰¡ suc y â†’ Whatever
conat-absurd eq = âŠ¥.rec (transport (cong diag eq) tt)
  where
  diag : Conatâ€² â†’ Typeâ‚€
  diag zero = Unit
  diag (suc _) = âŠ¥

embed : Nat.â„• â†’ Conat
embed Nat.zero .force = zero
embed (Nat.suc n) .force = suc (embed n)

embed-inj : âˆ€ m n â†’ embed m â‰¡ embed n â†’ m â‰¡ n
embed-inj m n p with âŠPath.encode _ _ (cong force p)
embed-inj Nat.zero Nat.zero _ | _ = refl
embed-inj (Nat.suc m) (Nat.suc n) _ | lift q
  = cong Nat.suc (embed-inj m n q)

embedâ‰¢âˆ : âˆ€ n â†’ embed n â‰¡ âˆ â†’ âŠ¥
embedâ‰¢âˆ Nat.zero = lower âˆ˜ âŠPath.encode _ _ âˆ˜ cong force
embedâ‰¢âˆ (Nat.suc n) = embedâ‰¢âˆ n âˆ˜ lower âˆ˜ âŠPath.encode _ _ âˆ˜ cong force

cover : Nat.â„• â†’ Conat
cover Nat.zero = âˆ
cover (Nat.suc n) = embed n

cover-inj : âˆ€ m n â†’ cover m â‰¡ cover n â†’ m â‰¡ n
cover-inj Nat.zero Nat.zero _ = refl
cover-inj (Nat.suc m) (Nat.suc n) p = cong Nat.suc (embed-inj m n p)
cover-inj Nat.zero (Nat.suc n) = âŠ¥.rec âˆ˜ embedâ‰¢âˆ n âˆ˜ sym
cover-inj (Nat.suc m) Nat.zero = âŠ¥.rec âˆ˜ embedâ‰¢âˆ m

module IsSet where
  â‰¡-stable  : {x y : Conat} â†’ Stable (x â‰¡ y)
  â‰¡â€²-stable : {x y : Conatâ€²} â†’ Stable (x â‰¡ y)

  force (â‰¡-stable Â¬Â¬p i) = â‰¡â€²-stable (Î» Â¬p â†’ Â¬Â¬p (Î» p â†’ Â¬p (cong force p))) i
  â‰¡â€²-stable {zero}  {zero}  Â¬Â¬pâ€² = refl
  â‰¡â€²-stable {suc x} {suc y} Â¬Â¬pâ€² =
     congS suc (â‰¡-stable Î» Â¬p â†’ Â¬Â¬pâ€² Î» p â†’ Â¬p (cong predâ€²â€² p))
  â‰¡â€²-stable {zero}  {suc y} Â¬Â¬pâ€² = âŠ¥.rec (Â¬Â¬pâ€² conat-absurd)
  â‰¡â€²-stable {suc x} {zero}  Â¬Â¬pâ€² = âŠ¥.rec (Â¬Â¬pâ€² Î» p â†’ conat-absurd (sym p))

  isSetConat : isSet Conat
  isSetConat _ _ = Separatedâ†’isSet (Î» _ _ â†’ â‰¡-stable) _ _

  isSetConatâ€² : isSet Conatâ€²
  isSetConatâ€² m n pâ€² qâ€² = cong (cong force) (isSetConat (conat m) (conat n) p q)
    where p = Î» where i .force â†’ pâ€² i
          q = Î» where i .force â†’ qâ€² i

module Bisimulation where
  open IsSet using (isSetConat)

  record _â‰ˆ_ (x y : Conat) : Typeâ‚€
  data _â‰ˆâ€²_ (x y : Conatâ€²) : Typeâ‚€
  _â‰ˆâ€²â€²_ : Conatâ€² â†’ Conatâ€² â†’ Typeâ‚€
  zero  â‰ˆâ€²â€² zero  = Unit
  suc x â‰ˆâ€²â€² suc y = x â‰ˆ y
  -- So impossible proofs are preserved
  x â‰ˆâ€²â€² y = âŠ¥

  record _â‰ˆ_ x y where
    coinductive
    field prove : force x â‰ˆâ€² force y

  data _â‰ˆâ€²_  x y where
    con : x â‰ˆâ€²â€² y â†’ x â‰ˆâ€² y

  open _â‰ˆ_ public

  bisim : âˆ€ {x y} â†’ x â‰ˆ y â†’ x â‰¡ y
  bisimâ€² : âˆ€ {x y} â†’ x â‰ˆâ€² y â†’ x â‰¡ y

  bisimâ€² {zero} {zero} (con tt) = refl
  bisimâ€² {zero} {suc x} (con ())
  bisimâ€² {suc x} {zero} (con ())
  bisimâ€² {suc x} {suc y} (con eq) i = suc (bisim eq i)
  force (bisim eq i) = bisimâ€² (prove eq) i

  misib : âˆ€ {x y} â†’ x â‰¡ y â†’ x â‰ˆ y
  misibâ€² : âˆ€ {x y} â†’ x â‰¡ y â†’ x â‰ˆâ€² y

  misibâ€² {zero} {zero} _ = con tt
  misibâ€² {zero} {suc x} = conat-absurd
  misibâ€² {suc x} {zero} p = conat-absurd (sym p)
  -- misibâ€² {suc x} {suc y} p = con Î» where .prove â†’ misibâ€² (cong predâ€² p)
  misibâ€² {suc x} {suc y} p = con (misib (cong predâ€²â€² p))
  prove (misib xâ‰¡y) = misibâ€² (cong force xâ‰¡y)

  isoâ€³ : âˆ€ {x y} â†’ (p : x â‰ˆ y) â†’ misib (bisim p) â‰¡ p
  isoâ€² : âˆ€ {x y} â†’ (p : x â‰ˆâ€² y) â†’ misibâ€² (bisimâ€² p) â‰¡ p

  isoâ€² {zero} {zero} (con p) = refl
  isoâ€² {zero} {suc x} (con ())
  isoâ€² {suc x} {zero} (con ())
  isoâ€² {suc x} {suc y} (con p) = cong con (isoâ€³ p)
  prove (isoâ€³ p i) = isoâ€² (prove p) i

  osi : âˆ€ {x y} â†’ (p : x â‰¡ y) â†’ bisim (misib p) â‰¡ p
  osi p = isSetConat _ _ _ p

  pathâ‰ƒbisim : âˆ€ {x y} â†’ (x â‰¡ y) â‰ƒ (x â‰ˆ y)
  pathâ‰ƒbisim = isoToEquiv (iso misib bisim isoâ€³ osi)

  pathâ‰¡bisim : âˆ€ {x y} â†’ (x â‰¡ y) â‰¡ (x â‰ˆ y)
  pathâ‰¡bisim = ua pathâ‰ƒbisim

  isPropâ‰ˆ : âˆ€ {x y} â†’ isProp (x â‰ˆ y)
  isPropâ‰ˆ = subst isProp pathâ‰¡bisim (isSetConat _ _)

module WLPO where
  -- search a decidable predicate on â„• for the first true
  search : (Nat.â„• â†’ Bool) â†’ Conat
  searchâ€² : (Nat.â„• â†’ Bool) â†’ Bool â†’ Conatâ€²

  search f .force = searchâ€² f (f 0)

  searchâ€² _  true = zero
  searchâ€² f false = suc (search (f âˆ˜ Nat.suc))

  -- the constantly false predicate searches to âˆ
  search-false : search (const false) â‰¡ âˆ
  search-false i .force = suc (search-false i)

  wrap : Conatâ€² â†’ Conat
  wrap zero = âˆ
  wrap (suc m) = m

  search-lemma : âˆ€ Î± n â†’ search Î± â‰¡ âˆ â†’ Î± n â‰¡ false
  search-lemma Î± Nat.zero p with Î± 0 | cong force p
  ... | false | q = refl
  ... | true | q = âŠ¥.rec (âŠPath.encode zero (suc âˆ) q .lower)
  search-lemma Î± (Nat.suc n) p with Î± 0 | cong force p
  ... | false | q = search-lemma (Î± âˆ˜ Nat.suc) n (cong wrap q)
  ... |  true | q = âŠ¥.rec (âŠPath.encode zero (suc âˆ) q .lower)

  search-n : âˆ€ Î± n â†’ search Î± â‰¡ embed n â†’ Î± n â‰¡ true
  search-n Î± Nat.zero p with Î± 0 | âŠPath.encode _ _ (cong force p)
  ... |  true | _ = refl
  search-n Î± (Nat.suc n) p with Î± 0 | âŠPath.encode _ _ (cong force p)
  ... | false | q = search-n (Î± âˆ˜ Nat.suc) n (q .lower)


  module _ (f : Conat â†’ Nat.â„•) (emb : isEmbedding f) where
    discreteConat : Discrete Conat
    discreteConat
      = Embedding-into-Discreteâ†’Discrete (f , emb) Nat.discreteâ„•

    wlpo' : Omni.WLPO' Nat.â„•
    wlpo' Î± with discreteConat (search Î±) âˆ
    ... | yes p = yes Î» i n â†’ search-lemma Î± n p i
    ... | no Â¬p = no (Â¬p âˆ˜ _âˆ™ search-false âˆ˜ cong search)

module LPO where
  open WLPO using (search; search-lemma; search-n)

  module Un (uncover : Conat â†’ Nat.â„•) (sect : section cover uncover) where
    search-0 : âˆ€ Î± â†’ uncover (search Î±) â‰¡ 0 â†’ âˆ€ n â†’ Î± n â‰¡ false
    search-0 Î± p n
      = search-lemma Î± n (sym (sect (search Î±)) âˆ™ cong cover p)

    search-n' : âˆ€ Î± n â†’ uncover (search Î±) â‰¡ Nat.suc n â†’ Î± n â‰¡ true
    search-n' Î± n p = search-n Î± n (sym (sect (search Î±)) âˆ™ cong cover p)

    -- So, surjectivity of `cover` implies LPO, since `cover` has
    -- already been shown injective, and surjectivity would make it an
    -- equivalence (as â„• and Conat are sets).
    lpo' : Omni.LPOâˆ Nat.â„•
    lpo' Î± = disc (uncover (search Î±)) refl where
      disc : âˆ€ n â†’ uncover (search Î±) â‰¡ n â†’ _
      disc Nat.zero p = inl Î» n â†’ subst âŸ¨_âŸ© (search-0 Î± p n)
      disc (Nat.suc n) p = inr (n , substâ» âŸ¨_âŸ© (search-n' Î± n p) _)
