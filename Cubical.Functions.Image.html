<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Cubical.Functions.Image</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Symbol">{-#</a> <a id="5" class="Keyword">OPTIONS</a> <a id="13" class="Pragma">--safe</a> <a id="20" class="Symbol">#-}</a>
<a id="24" class="Keyword">module</a> <a id="31" href="Cubical.Functions.Image.html" class="Module">Cubical.Functions.Image</a> <a id="55" class="Keyword">where</a>

<a id="62" class="Keyword">open</a> <a id="67" class="Keyword">import</a> <a id="74" href="Cubical.Foundations.Prelude.html" class="Module">Cubical.Foundations.Prelude</a>
<a id="102" class="Keyword">open</a> <a id="107" class="Keyword">import</a> <a id="114" href="Cubical.Foundations.Function.html" class="Module">Cubical.Foundations.Function</a>
<a id="143" class="Keyword">open</a> <a id="148" class="Keyword">import</a> <a id="155" href="Cubical.Foundations.Isomorphism.html" class="Module">Cubical.Foundations.Isomorphism</a>
<a id="187" class="Keyword">open</a> <a id="192" class="Keyword">import</a> <a id="199" href="Cubical.Foundations.HLevels.html" class="Module">Cubical.Foundations.HLevels</a>
<a id="227" class="Keyword">open</a> <a id="232" class="Keyword">import</a> <a id="239" href="Cubical.Foundations.Equiv.html" class="Module">Cubical.Foundations.Equiv</a>
<a id="265" class="Keyword">open</a> <a id="270" class="Keyword">import</a> <a id="277" href="Cubical.Foundations.Equiv.Fiberwise.html" class="Module">Cubical.Foundations.Equiv.Fiberwise</a>
<a id="313" class="Keyword">open</a> <a id="318" class="Keyword">import</a> <a id="325" href="Cubical.Foundations.Powerset.html" class="Module">Cubical.Foundations.Powerset</a>

<a id="355" class="Keyword">open</a> <a id="360" class="Keyword">import</a> <a id="367" href="Cubical.Functions.Embedding.html" class="Module">Cubical.Functions.Embedding</a>
<a id="395" class="Keyword">open</a> <a id="400" class="Keyword">import</a> <a id="407" href="Cubical.Functions.Surjection.html" class="Module">Cubical.Functions.Surjection</a>

<a id="437" class="Keyword">open</a> <a id="442" class="Keyword">import</a> <a id="449" href="Cubical.HITs.PropositionalTruncation.html" class="Module">Cubical.HITs.PropositionalTruncation</a> <a id="486" class="Symbol">as</a> <a id="489" class="Module">PT</a>
<a id="492" class="Keyword">open</a> <a id="497" class="Keyword">import</a> <a id="504" href="Cubical.Data.Sigma.html" class="Module">Cubical.Data.Sigma</a>

<a id="524" class="Keyword">private</a> <a id="532" class="Keyword">variable</a>
  <a id="543" href="Cubical.Functions.Image.html#543" class="Generalizable">ℓ</a> <a id="545" href="Cubical.Functions.Image.html#545" class="Generalizable">ℓ&#39;</a> <a id="548" class="Symbol">:</a> <a id="550" href="Agda.Primitive.html#591" class="Postulate">Level</a>
  <a id="558" href="Cubical.Functions.Image.html#558" class="Generalizable">A</a> <a id="560" href="Cubical.Functions.Image.html#560" class="Generalizable">B</a> <a id="562" class="Symbol">:</a> <a id="564" href="Agda.Primitive.html#320" class="Primitive">Type</a> <a id="569" href="Cubical.Functions.Image.html#543" class="Generalizable">ℓ</a>

<a id="572" class="Keyword">module</a> <a id="579" href="Cubical.Functions.Image.html#579" class="Module">_</a> <a id="581" class="Symbol">(</a><a id="582" href="Cubical.Functions.Image.html#582" class="Bound">f</a> <a id="584" class="Symbol">:</a> <a id="586" href="Cubical.Functions.Image.html#558" class="Generalizable">A</a> <a id="588" class="Symbol">→</a> <a id="590" href="Cubical.Functions.Image.html#560" class="Generalizable">B</a><a id="591" class="Symbol">)</a> <a id="593" class="Keyword">where</a>
  <a id="601" href="Cubical.Functions.Image.html#601" class="Function">isInImage</a> <a id="611" class="Symbol">:</a> <a id="613" href="Cubical.Functions.Image.html#590" class="Bound">B</a> <a id="615" class="Symbol">→</a> <a id="617" href="Agda.Primitive.html#320" class="Primitive">Type</a> <a id="622" class="Symbol">_</a>
  <a id="626" href="Cubical.Functions.Image.html#601" class="Function">isInImage</a> <a id="636" href="Cubical.Functions.Image.html#636" class="Bound">y</a> <a id="638" class="Symbol">=</a> <a id="640" href="Cubical.Data.Sigma.Base.html#682" class="Function">∃[</a> <a id="643" href="Cubical.Functions.Image.html#643" class="Bound">x</a> <a id="645" href="Cubical.Data.Sigma.Base.html#682" class="Function">∈</a> <a id="647" href="Cubical.Functions.Image.html#586" class="Bound">A</a> <a id="649" href="Cubical.Data.Sigma.Base.html#682" class="Function">]</a> <a id="651" href="Cubical.Functions.Image.html#582" class="Bound">f</a> <a id="653" href="Cubical.Functions.Image.html#643" class="Bound">x</a> <a id="655" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="657" href="Cubical.Functions.Image.html#636" class="Bound">y</a>

  <a id="662" href="Cubical.Functions.Image.html#662" class="Function">isPropIsInImage</a> <a id="678" class="Symbol">:</a> <a id="680" class="Symbol">(</a><a id="681" href="Cubical.Functions.Image.html#681" class="Bound">x</a> <a id="683" class="Symbol">:</a> <a id="685" href="Cubical.Functions.Image.html#590" class="Bound">B</a><a id="686" class="Symbol">)</a> <a id="688" class="Symbol">→</a> <a id="690" href="Cubical.Foundations.Prelude.html#14217" class="Function">isProp</a> <a id="697" class="Symbol">(</a><a id="698" href="Cubical.Functions.Image.html#601" class="Function">isInImage</a> <a id="708" href="Cubical.Functions.Image.html#681" class="Bound">x</a><a id="709" class="Symbol">)</a>
  <a id="713" href="Cubical.Functions.Image.html#662" class="Function">isPropIsInImage</a> <a id="729" href="Cubical.Functions.Image.html#729" class="Bound">x</a> <a id="731" class="Symbol">=</a> <a id="733" href="Cubical.HITs.PropositionalTruncation.Properties.html#5584" class="Function">isPropPropTrunc</a>

  <a id="752" href="Cubical.Functions.Image.html#752" class="Function">Image</a> <a id="758" class="Symbol">:</a> <a id="760" href="Agda.Primitive.html#320" class="Primitive">Type</a> <a id="765" class="Symbol">_</a>
  <a id="769" href="Cubical.Functions.Image.html#752" class="Function">Image</a> <a id="775" class="Symbol">=</a> <a id="777" href="Cubical.Core.Primitives.html#6268" class="Function">Σ[</a> <a id="780" href="Cubical.Functions.Image.html#780" class="Bound">y</a> <a id="782" href="Cubical.Core.Primitives.html#6268" class="Function">∈</a> <a id="784" href="Cubical.Functions.Image.html#590" class="Bound">B</a> <a id="786" href="Cubical.Core.Primitives.html#6268" class="Function">]</a> <a id="788" href="Cubical.Functions.Image.html#601" class="Function">isInImage</a> <a id="798" href="Cubical.Functions.Image.html#780" class="Bound">y</a>

  <a id="803" href="Cubical.Functions.Image.html#803" class="Function">imageInclusion</a> <a id="818" class="Symbol">:</a> <a id="820" href="Cubical.Functions.Image.html#752" class="Function">Image</a> <a id="826" href="Cubical.Functions.Embedding.html#2165" class="Function Operator">↪</a> <a id="828" href="Cubical.Functions.Image.html#590" class="Bound">B</a>
  <a id="832" href="Cubical.Functions.Image.html#803" class="Function">imageInclusion</a> <a id="847" class="Symbol">=</a> <a id="849" href="Agda.Builtin.Sigma.html#234" class="Field">fst</a> <a id="853" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a>
    <a id="859" href="Cubical.Functions.Embedding.html#3729" class="Function">hasPropFibers→isEmbedding</a> <a id="885" class="Symbol">{</a><a id="886" class="Argument">f</a> <a id="888" class="Symbol">=</a> <a id="890" href="Agda.Builtin.Sigma.html#234" class="Field">fst</a><a id="893" class="Symbol">}</a>
      <a id="901" class="Symbol">λ</a> <a id="903" href="Cubical.Functions.Image.html#903" class="Bound">y</a> <a id="905" class="Symbol">→</a> <a id="907" href="Cubical.Foundations.HLevels.html#8859" class="Function">isOfHLevelRetractFromIso</a> <a id="932" class="Number">1</a> <a id="934" class="Symbol">(</a><a id="935" href="Cubical.Functions.Image.html#976" class="Function">ϕ</a> <a id="937" href="Cubical.Functions.Image.html#903" class="Bound">y</a><a id="938" class="Symbol">)</a> <a id="940" href="Cubical.HITs.PropositionalTruncation.Properties.html#5584" class="Function">isPropPropTrunc</a>
      <a id="962" class="Keyword">where</a>
        <a id="976" href="Cubical.Functions.Image.html#976" class="Function">ϕ</a> <a id="978" class="Symbol">:</a> <a id="980" class="Symbol">(</a><a id="981" href="Cubical.Functions.Image.html#981" class="Bound">y</a> <a id="983" class="Symbol">:</a> <a id="985" href="Cubical.Functions.Image.html#590" class="Bound">B</a><a id="986" class="Symbol">)</a> <a id="988" class="Symbol">→</a> <a id="990" href="Cubical.Foundations.Isomorphism.html#773" class="Record">Iso</a> <a id="994" class="Symbol">_</a> <a id="996" class="Symbol">_</a>
        <a id="1006" href="Cubical.Functions.Image.html#976" class="Function">ϕ</a> <a id="1008" href="Cubical.Functions.Image.html#1008" class="Bound">y</a> <a id="1010" class="Symbol">=</a> <a id="1012" href="Cubical.Foundations.Isomorphism.html#3382" class="Function">invIso</a> <a id="1019" class="Symbol">(</a><a id="1020" href="Cubical.Data.Sigma.Properties.html#16471" class="Function">fiberProjIso</a> <a id="1033" href="Cubical.Functions.Image.html#590" class="Bound">B</a> <a id="1035" href="Cubical.Functions.Image.html#601" class="Function">isInImage</a> <a id="1045" href="Cubical.Functions.Image.html#1008" class="Bound">y</a><a id="1046" class="Symbol">)</a>

  <a id="1051" href="Cubical.Functions.Image.html#1051" class="Function">restrictToImage</a> <a id="1067" class="Symbol">:</a> <a id="1069" href="Cubical.Functions.Image.html#586" class="Bound">A</a> <a id="1071" class="Symbol">→</a> <a id="1073" href="Cubical.Functions.Image.html#752" class="Function">Image</a>
  <a id="1081" href="Cubical.Functions.Image.html#1051" class="Function">restrictToImage</a> <a id="1097" href="Cubical.Functions.Image.html#1097" class="Bound">x</a> <a id="1099" class="Symbol">=</a> <a id="1101" class="Symbol">(</a><a id="1102" href="Cubical.Functions.Image.html#582" class="Bound">f</a> <a id="1104" href="Cubical.Functions.Image.html#1097" class="Bound">x</a><a id="1105" class="Symbol">)</a> <a id="1107" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="1109" href="Cubical.HITs.PropositionalTruncation.Base.html#288" class="InductiveConstructor Operator">∣</a> <a id="1111" href="Cubical.Functions.Image.html#1097" class="Bound">x</a> <a id="1113" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="1115" href="Cubical.Foundations.Prelude.html#915" class="Function">refl</a> <a id="1120" href="Cubical.HITs.PropositionalTruncation.Base.html#288" class="InductiveConstructor Operator">∣₁</a>

  <a id="1126" href="Cubical.Functions.Image.html#1126" class="Function">isSurjectionImageRestriction</a> <a id="1155" class="Symbol">:</a> <a id="1157" href="Cubical.Functions.Surjection.html#533" class="Function">isSurjection</a> <a id="1170" href="Cubical.Functions.Image.html#1051" class="Function">restrictToImage</a>
  <a id="1188" href="Cubical.Functions.Image.html#1126" class="Function">isSurjectionImageRestriction</a> <a id="1217" class="Symbol">(</a><a id="1218" href="Cubical.Functions.Image.html#1218" class="Bound">y</a> <a id="1220" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="1222" href="Cubical.Functions.Image.html#1222" class="Bound">y∈im</a><a id="1226" class="Symbol">)</a> <a id="1228" class="Symbol">=</a>
    <a id="1234" href="Cubical.HITs.PropositionalTruncation.Properties.html#894" class="Function">PT.rec</a> <a id="1241" href="Cubical.HITs.PropositionalTruncation.Properties.html#5584" class="Function">isPropPropTrunc</a>
           <a id="1268" class="Symbol">(λ</a> <a id="1271" class="Symbol">(</a><a id="1272" href="Cubical.Functions.Image.html#1272" class="Bound">x</a> <a id="1274" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="1276" href="Cubical.Functions.Image.html#1276" class="Bound">fx≡y</a><a id="1280" class="Symbol">)</a>
             <a id="1295" class="Symbol">→</a> <a id="1297" href="Cubical.HITs.PropositionalTruncation.Base.html#288" class="InductiveConstructor Operator">∣</a> <a id="1299" href="Cubical.Functions.Image.html#1272" class="Bound">x</a> <a id="1301" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="1303" href="Cubical.Data.Sigma.Properties.html#13039" class="Function">Σ≡Prop</a> <a id="1310" href="Cubical.Functions.Image.html#662" class="Function">isPropIsInImage</a> <a id="1326" href="Cubical.Functions.Image.html#1276" class="Bound">fx≡y</a> <a id="1331" href="Cubical.HITs.PropositionalTruncation.Base.html#288" class="InductiveConstructor Operator">∣₁</a><a id="1333" class="Symbol">)</a>
           <a id="1346" href="Cubical.Functions.Image.html#1222" class="Bound">y∈im</a>

  <a id="1354" href="Cubical.Functions.Image.html#1354" class="Function">imageFactorization</a> <a id="1373" class="Symbol">:</a> <a id="1375" href="Agda.Builtin.Sigma.html#234" class="Field">fst</a> <a id="1379" href="Cubical.Functions.Image.html#803" class="Function">imageInclusion</a> <a id="1394" href="Cubical.Foundations.Function.html#653" class="Function Operator">∘</a> <a id="1396" href="Cubical.Functions.Image.html#1051" class="Function">restrictToImage</a> <a id="1412" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="1414" href="Cubical.Functions.Image.html#582" class="Bound">f</a>
  <a id="1418" href="Cubical.Functions.Image.html#1354" class="Function">imageFactorization</a> <a id="1437" class="Symbol">=</a> <a id="1439" href="Cubical.Foundations.Prelude.html#915" class="Function">refl</a>

<a id="1445" class="Comment">{-
  Images of subsets.
  The universe level of domain and codomain is equal, so we can use
  the notion of powerset ℙ - so subsets of a type A : Type ℓ, are
  maps A → hProp ℓ.
-}</a>
<a id="1626" class="Keyword">module</a> <a id="1633" href="Cubical.Functions.Image.html#1633" class="Module">_</a> <a id="1635" class="Symbol">{</a><a id="1636" href="Cubical.Functions.Image.html#1636" class="Bound">A</a> <a id="1638" href="Cubical.Functions.Image.html#1638" class="Bound">B</a> <a id="1640" class="Symbol">:</a> <a id="1642" href="Agda.Primitive.html#320" class="Primitive">Type</a> <a id="1647" href="Cubical.Functions.Image.html#543" class="Generalizable">ℓ</a><a id="1648" class="Symbol">}</a> <a id="1650" class="Symbol">(</a><a id="1651" href="Cubical.Functions.Image.html#1651" class="Bound">f</a> <a id="1653" class="Symbol">:</a> <a id="1655" href="Cubical.Functions.Image.html#1636" class="Bound">A</a> <a id="1657" class="Symbol">→</a> <a id="1659" href="Cubical.Functions.Image.html#1638" class="Bound">B</a><a id="1660" class="Symbol">)</a> <a id="1662" class="Keyword">where</a>
  <a id="1670" href="Cubical.Functions.Image.html#1670" class="Function">isInSubsetImage</a> <a id="1686" class="Symbol">:</a> <a id="1688" href="Cubical.Foundations.Powerset.html#657" class="Function">ℙ</a> <a id="1690" href="Cubical.Functions.Image.html#1636" class="Bound">A</a> <a id="1692" class="Symbol">→</a> <a id="1694" href="Cubical.Functions.Image.html#1638" class="Bound">B</a> <a id="1696" class="Symbol">→</a> <a id="1698" href="Agda.Primitive.html#320" class="Primitive">Type</a> <a id="1703" class="Symbol">_</a>
  <a id="1707" href="Cubical.Functions.Image.html#1670" class="Function">isInSubsetImage</a> <a id="1723" href="Cubical.Functions.Image.html#1723" class="Bound">U</a> <a id="1725" href="Cubical.Functions.Image.html#1725" class="Bound">y</a> <a id="1727" class="Symbol">=</a> <a id="1729" href="Cubical.Data.Sigma.Base.html#682" class="Function">∃[</a> <a id="1732" href="Cubical.Functions.Image.html#1732" class="Bound">x</a> <a id="1734" href="Cubical.Data.Sigma.Base.html#682" class="Function">∈</a> <a id="1736" href="Cubical.Functions.Image.html#1636" class="Bound">A</a> <a id="1738" href="Cubical.Data.Sigma.Base.html#682" class="Function">]</a> <a id="1740" class="Symbol">(</a><a id="1741" href="Cubical.Functions.Image.html#1732" class="Bound">x</a> <a id="1743" href="Cubical.Foundations.Powerset.html#772" class="Function Operator">∈</a> <a id="1745" href="Cubical.Functions.Image.html#1723" class="Bound">U</a><a id="1746" class="Symbol">)</a> <a id="1748" href="Cubical.Data.Sigma.Base.html#461" class="Function Operator">×</a> <a id="1750" class="Symbol">(</a><a id="1751" href="Cubical.Functions.Image.html#1651" class="Bound">f</a> <a id="1753" href="Cubical.Functions.Image.html#1732" class="Bound">x</a> <a id="1755" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="1757" href="Cubical.Functions.Image.html#1725" class="Bound">y</a><a id="1758" class="Symbol">)</a>

  <a id="1763" href="Cubical.Functions.Image.html#1763" class="Function">SubsetImage</a> <a id="1775" class="Symbol">:</a> <a id="1777" href="Cubical.Foundations.Powerset.html#657" class="Function">ℙ</a> <a id="1779" href="Cubical.Functions.Image.html#1636" class="Bound">A</a> <a id="1781" class="Symbol">→</a> <a id="1783" href="Cubical.Foundations.Powerset.html#657" class="Function">ℙ</a> <a id="1785" href="Cubical.Functions.Image.html#1638" class="Bound">B</a>
  <a id="1789" href="Cubical.Functions.Image.html#1763" class="Function">SubsetImage</a> <a id="1801" href="Cubical.Functions.Image.html#1801" class="Bound">U</a> <a id="1803" href="Cubical.Functions.Image.html#1803" class="Bound">y</a> <a id="1805" class="Symbol">=</a> <a id="1807" href="Cubical.Functions.Image.html#1670" class="Function">isInSubsetImage</a> <a id="1823" href="Cubical.Functions.Image.html#1801" class="Bound">U</a> <a id="1825" href="Cubical.Functions.Image.html#1803" class="Bound">y</a> <a id="1827" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="1829" href="Cubical.HITs.PropositionalTruncation.Properties.html#5584" class="Function">isPropPropTrunc</a>

<a id="1846" class="Comment">{-
  The following is also true for a general modality in place of ∥_∥₁,
  i.e. the modal-connected factor of a modal map is an equivalence.
-}</a>
<a id="1990" class="Keyword">module</a> <a id="1997" href="Cubical.Functions.Image.html#1997" class="Module">_</a> <a id="1999" class="Symbol">(</a><a id="2000" href="Cubical.Functions.Image.html#2000" class="Bound">f</a> <a id="2002" class="Symbol">:</a> <a id="2004" href="Cubical.Functions.Image.html#558" class="Generalizable">A</a> <a id="2006" href="Cubical.Functions.Embedding.html#2165" class="Function Operator">↪</a> <a id="2008" href="Cubical.Functions.Image.html#560" class="Generalizable">B</a><a id="2009" class="Symbol">)</a> <a id="2011" class="Keyword">where</a>
  <a id="2019" class="Keyword">private</a>
    <a id="2031" href="Cubical.Functions.Image.html#2031" class="Function">f&#39;</a> <a id="2034" class="Symbol">=</a> <a id="2036" href="Agda.Builtin.Sigma.html#234" class="Field">fst</a> <a id="2040" href="Cubical.Functions.Image.html#2000" class="Bound">f</a>
    <a id="2046" href="Cubical.Functions.Image.html#2046" class="Function">r</a> <a id="2048" class="Symbol">=</a> <a id="2050" href="Cubical.Functions.Image.html#1051" class="Function">restrictToImage</a> <a id="2066" href="Cubical.Functions.Image.html#2031" class="Function">f&#39;</a>
    <a id="2073" href="Cubical.Functions.Image.html#2073" class="Function">propFibers</a> <a id="2084" class="Symbol">=</a> <a id="2086" href="Cubical.Functions.Embedding.html#2921" class="Function">isEmbedding→hasPropFibers</a> <a id="2112" class="Symbol">(</a><a id="2113" href="Agda.Builtin.Sigma.html#246" class="Field">snd</a> <a id="2117" href="Cubical.Functions.Image.html#2000" class="Bound">f</a><a id="2118" class="Symbol">)</a>

    <a id="2125" href="Cubical.Functions.Image.html#2125" class="Function">restrictionHasSameFibers</a> <a id="2150" class="Symbol">:</a> <a id="2152" class="Symbol">(</a><a id="2153" href="Cubical.Functions.Image.html#2153" class="Symbol">(</a><a id="2154" href="Cubical.Functions.Image.html#2154" class="Bound">y</a> <a id="2156" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="2158" href="Cubical.Functions.Image.html#2158" class="Bound">y∈im</a><a id="2162" href="Cubical.Functions.Image.html#2153" class="Symbol">)</a> <a id="2164" class="Symbol">:</a> <a id="2166" href="Cubical.Functions.Image.html#752" class="Function">Image</a> <a id="2172" href="Cubical.Functions.Image.html#2031" class="Function">f&#39;</a><a id="2174" class="Symbol">)</a> <a id="2176" class="Symbol">→</a>  <a id="2179" href="Cubical.Foundations.Equiv.Base.html#253" class="Function">fiber</a> <a id="2185" href="Cubical.Functions.Image.html#2046" class="Function">r</a> <a id="2187" class="Symbol">(</a><a id="2188" href="Cubical.Functions.Image.html#2154" class="Bound">y</a> <a id="2190" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="2192" href="Cubical.Functions.Image.html#2158" class="Bound">y∈im</a><a id="2196" class="Symbol">)</a> <a id="2198" href="Agda.Builtin.Cubical.Glue.html#1005" class="Function Operator">≃</a> <a id="2200" href="Cubical.Foundations.Equiv.Base.html#253" class="Function">fiber</a> <a id="2206" href="Cubical.Functions.Image.html#2031" class="Function">f&#39;</a> <a id="2209" href="Cubical.Functions.Image.html#2154" class="Bound">y</a>
    <a id="2215" href="Cubical.Functions.Image.html#2125" class="Function">restrictionHasSameFibers</a> <a id="2240" class="Symbol">(</a><a id="2241" href="Cubical.Functions.Image.html#2241" class="Bound">y</a> <a id="2243" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="2245" href="Cubical.Functions.Image.html#2245" class="Bound">y∈im</a><a id="2249" class="Symbol">)</a> <a id="2251" class="Symbol">=</a>
            <a id="2265" class="Symbol">_</a> <a id="2267" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a>
           <a id="2280" href="Cubical.Foundations.Equiv.Fiberwise.html#1782" class="Function">totalEquiv</a> <a id="2291" class="Symbol">(λ</a> <a id="2294" href="Cubical.Functions.Image.html#2294" class="Bound">x</a> <a id="2296" class="Symbol">→</a> <a id="2298" href="Cubical.Functions.Image.html#2046" class="Function">r</a> <a id="2300" href="Cubical.Functions.Image.html#2294" class="Bound">x</a> <a id="2302" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="2304" class="Symbol">(</a><a id="2305" href="Cubical.Functions.Image.html#2241" class="Bound">y</a> <a id="2307" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="2309" href="Cubical.Functions.Image.html#2245" class="Bound">y∈im</a><a id="2313" class="Symbol">))</a> <a id="2316" class="Symbol">(λ</a> <a id="2319" href="Cubical.Functions.Image.html#2319" class="Bound">x</a> <a id="2321" class="Symbol">→</a> <a id="2323" href="Agda.Builtin.Sigma.html#234" class="Field">fst</a> <a id="2327" class="Symbol">(</a><a id="2328" href="Cubical.Functions.Image.html#2046" class="Function">r</a> <a id="2330" href="Cubical.Functions.Image.html#2319" class="Bound">x</a><a id="2331" class="Symbol">)</a> <a id="2333" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="2335" href="Cubical.Functions.Image.html#2241" class="Bound">y</a><a id="2336" class="Symbol">)</a>
                      <a id="2360" class="Symbol">(λ</a> <a id="2363" href="Cubical.Functions.Image.html#2363" class="Bound">x</a> <a id="2365" class="Symbol">→</a> <a id="2367" href="Cubical.Foundations.Prelude.html#1430" class="Function">cong</a> <a id="2372" href="Agda.Builtin.Sigma.html#234" class="Field">fst</a> <a id="2376" class="Symbol">)</a>
                      <a id="2400" class="Symbol">λ</a> <a id="2402" href="Cubical.Functions.Image.html#2402" class="Bound">x</a> <a id="2404" class="Symbol">→</a> <a id="2406" href="Agda.Builtin.Sigma.html#246" class="Field">snd</a> <a id="2410" class="Symbol">(</a><a id="2411" href="Cubical.Functions.Image.html#803" class="Function">imageInclusion</a> <a id="2426" href="Cubical.Functions.Image.html#2031" class="Function">f&#39;</a><a id="2428" class="Symbol">)</a> <a id="2430" class="Symbol">(</a><a id="2431" href="Cubical.Functions.Image.html#2046" class="Function">r</a> <a id="2433" href="Cubical.Functions.Image.html#2402" class="Bound">x</a><a id="2434" class="Symbol">)</a> <a id="2436" class="Symbol">(</a><a id="2437" href="Cubical.Functions.Image.html#2241" class="Bound">y</a> <a id="2439" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="2441" href="Cubical.Functions.Image.html#2245" class="Bound">y∈im</a><a id="2445" class="Symbol">)</a>

  <a id="2450" href="Cubical.Functions.Image.html#2450" class="Function">isEquivEmbeddingOntoImage</a> <a id="2476" class="Symbol">:</a> <a id="2478" href="Agda.Builtin.Cubical.Glue.html#822" class="Record">isEquiv</a> <a id="2486" class="Symbol">(</a><a id="2487" href="Cubical.Functions.Image.html#1051" class="Function">restrictToImage</a> <a id="2503" class="Symbol">(</a><a id="2504" href="Agda.Builtin.Sigma.html#234" class="Field">fst</a> <a id="2508" href="Cubical.Functions.Image.html#2000" class="Bound">f</a><a id="2509" class="Symbol">))</a>
  <a id="2514" href="Cubical.Functions.Image.html#2450" class="Function">isEquivEmbeddingOntoImage</a> <a id="2540" class="Symbol">=</a>
    <a id="2546" href="Cubical.Functions.Surjection.html#1162" class="Function">isEmbedding×isSurjection→isEquiv</a>
      <a id="2585" class="Symbol">(</a><a id="2586" href="Cubical.Functions.Embedding.html#3729" class="Function">hasPropFibers→isEmbedding</a>
        <a id="2620" class="Symbol">(λ</a> <a id="2623" href="Cubical.Functions.Image.html#2623" class="Bound">y</a> <a id="2625" class="Symbol">→</a> <a id="2627" href="Cubical.Foundations.HLevels.html#8859" class="Function">isOfHLevelRetractFromIso</a> <a id="2652" class="Number">1</a>
                 <a id="2671" class="Symbol">(</a><a id="2672" href="Cubical.Foundations.Equiv.html#3099" class="Function">equivToIso</a> <a id="2683" class="Symbol">(</a><a id="2684" href="Cubical.Functions.Image.html#2125" class="Function">restrictionHasSameFibers</a> <a id="2709" href="Cubical.Functions.Image.html#2623" class="Bound">y</a><a id="2710" class="Symbol">))</a>
                 <a id="2730" class="Symbol">(</a><a id="2731" href="Cubical.Functions.Embedding.html#2921" class="Function">isEmbedding→hasPropFibers</a> <a id="2757" class="Symbol">(</a><a id="2758" href="Agda.Builtin.Sigma.html#246" class="Field">snd</a> <a id="2762" href="Cubical.Functions.Image.html#2000" class="Bound">f</a><a id="2763" class="Symbol">)</a> <a id="2765" class="Symbol">(</a><a id="2766" href="Agda.Builtin.Sigma.html#234" class="Field">fst</a> <a id="2770" href="Cubical.Functions.Image.html#2623" class="Bound">y</a><a id="2771" class="Symbol">))</a> <a id="2774" class="Symbol">)</a> <a id="2776" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a>
      <a id="2784" class="Symbol">(</a><a id="2785" href="Cubical.Functions.Image.html#1126" class="Function">isSurjectionImageRestriction</a> <a id="2814" class="Symbol">(</a><a id="2815" href="Agda.Builtin.Sigma.html#234" class="Field">fst</a> <a id="2819" href="Cubical.Functions.Image.html#2000" class="Bound">f</a><a id="2820" class="Symbol">)))</a>

<a id="2825" class="Comment">{-
  This is the extension to an &#39;iff&#39;, which is also a general modal fact.
-}</a>
<a id="isEmbeddingFromIsEquivToImage"></a><a id="2904" href="Cubical.Functions.Image.html#2904" class="Function">isEmbeddingFromIsEquivToImage</a> <a id="2934" class="Symbol">:</a> <a id="2936" class="Symbol">(</a><a id="2937" href="Cubical.Functions.Image.html#2937" class="Bound">f</a> <a id="2939" class="Symbol">:</a> <a id="2941" href="Cubical.Functions.Image.html#558" class="Generalizable">A</a> <a id="2943" class="Symbol">→</a> <a id="2945" href="Cubical.Functions.Image.html#560" class="Generalizable">B</a><a id="2946" class="Symbol">)</a> <a id="2948" class="Symbol">→</a> <a id="2950" href="Agda.Builtin.Cubical.Glue.html#822" class="Record">isEquiv</a> <a id="2958" class="Symbol">(</a><a id="2959" href="Cubical.Functions.Image.html#1051" class="Function">restrictToImage</a> <a id="2975" href="Cubical.Functions.Image.html#2937" class="Bound">f</a><a id="2976" class="Symbol">)</a> <a id="2978" class="Symbol">→</a> <a id="2980" href="Cubical.Functions.Embedding.html#1279" class="Function">isEmbedding</a> <a id="2992" href="Cubical.Functions.Image.html#2937" class="Bound">f</a>
<a id="2994" href="Cubical.Functions.Image.html#2904" class="Function">isEmbeddingFromIsEquivToImage</a> <a id="3024" href="Cubical.Functions.Image.html#3024" class="Bound">f</a> <a id="3026" href="Cubical.Functions.Image.html#3026" class="Bound">isEquiv-r</a> <a id="3036" class="Symbol">=</a> <a id="3038" href="Cubical.Functions.Embedding.html#8714" class="Function">isEmbedding-∘</a> <a id="3052" class="Symbol">(</a><a id="3053" href="Agda.Builtin.Sigma.html#246" class="Field">snd</a> <a id="3057" class="Symbol">(</a><a id="3058" href="Cubical.Functions.Image.html#803" class="Function">imageInclusion</a> <a id="3073" href="Cubical.Functions.Image.html#3024" class="Bound">f</a><a id="3074" class="Symbol">))</a> <a id="3077" class="Symbol">(</a><a id="3078" href="Cubical.Functions.Embedding.html#5511" class="Function">isEquiv→isEmbedding</a> <a id="3098" href="Cubical.Functions.Image.html#3026" class="Bound">isEquiv-r</a><a id="3107" class="Symbol">)</a>
</pre></body></html>