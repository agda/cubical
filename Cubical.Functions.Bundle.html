<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Cubical.Functions.Bundle</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Symbol">{-#</a> <a id="5" class="Keyword">OPTIONS</a> <a id="13" class="Pragma">--safe</a> <a id="20" class="Symbol">#-}</a>
<a id="24" class="Keyword">module</a> <a id="31" href="Cubical.Functions.Bundle.html" class="Module">Cubical.Functions.Bundle</a> <a id="56" class="Keyword">where</a>

<a id="63" class="Keyword">open</a> <a id="68" class="Keyword">import</a> <a id="75" href="Cubical.Foundations.Prelude.html" class="Module">Cubical.Foundations.Prelude</a>
<a id="103" class="Keyword">open</a> <a id="108" class="Keyword">import</a> <a id="115" href="Cubical.Foundations.Equiv.html" class="Module">Cubical.Foundations.Equiv</a>
<a id="141" class="Keyword">open</a> <a id="146" class="Keyword">import</a> <a id="153" href="Cubical.Foundations.Univalence.html" class="Module">Cubical.Foundations.Univalence</a>
<a id="184" class="Keyword">open</a> <a id="189" class="Keyword">import</a> <a id="196" href="Cubical.Functions.Fibration.html" class="Module">Cubical.Functions.Fibration</a>

<a id="225" class="Keyword">open</a> <a id="230" class="Keyword">import</a> <a id="237" href="Cubical.Foundations.Structure.html" class="Module">Cubical.Foundations.Structure</a>
<a id="267" class="Keyword">open</a> <a id="272" class="Keyword">import</a> <a id="279" href="Cubical.Structures.TypeEqvTo.html" class="Module">Cubical.Structures.TypeEqvTo</a>

<a id="309" class="Keyword">open</a> <a id="314" class="Keyword">import</a> <a id="321" href="Cubical.Data.Sigma.Properties.html" class="Module">Cubical.Data.Sigma.Properties</a>
<a id="351" class="Keyword">open</a> <a id="356" class="Keyword">import</a> <a id="363" href="Cubical.HITs.PropositionalTruncation.html" class="Module">Cubical.HITs.PropositionalTruncation</a>

<a id="401" class="Keyword">module</a> <a id="408" href="Cubical.Functions.Bundle.html#408" class="Module">_</a> <a id="410" class="Symbol">{</a><a id="411" href="Cubical.Functions.Bundle.html#411" class="Bound">ℓb</a> <a id="414" href="Cubical.Functions.Bundle.html#414" class="Bound">ℓf</a><a id="416" class="Symbol">}</a> <a id="418" class="Symbol">{</a><a id="419" href="Cubical.Functions.Bundle.html#419" class="Bound">B</a> <a id="421" class="Symbol">:</a> <a id="423" href="Agda.Primitive.html#388" class="Primitive">Type</a> <a id="428" href="Cubical.Functions.Bundle.html#411" class="Bound">ℓb</a><a id="430" class="Symbol">}</a> <a id="432" class="Symbol">{</a><a id="433" href="Cubical.Functions.Bundle.html#433" class="Bound">F</a> <a id="435" class="Symbol">:</a> <a id="437" href="Agda.Primitive.html#388" class="Primitive">Type</a> <a id="442" href="Cubical.Functions.Bundle.html#414" class="Bound">ℓf</a><a id="444" class="Symbol">}</a> <a id="446" class="Symbol">{</a><a id="447" href="Cubical.Functions.Bundle.html#447" class="Bound">ℓ</a><a id="448" class="Symbol">}</a> <a id="450" class="Keyword">where</a>

<a id="457" class="Comment">{-
    A fiber bundle with base space B and fibers F is a map `p⁻¹ : B → TypeEqvTo F`
     taking points in the base space to their respective fibers.

    e.g. a double cover is a map `B → TypeEqvTo Bool`
-}</a>

  <a id="669" href="Cubical.Functions.Bundle.html#669" class="Function">Total</a> <a id="675" class="Symbol">:</a> <a id="677" class="Symbol">(</a><a id="678" href="Cubical.Functions.Bundle.html#678" class="Bound">p⁻¹</a> <a id="682" class="Symbol">:</a> <a id="684" href="Cubical.Functions.Bundle.html#419" class="Bound">B</a> <a id="686" class="Symbol">→</a> <a id="688" href="Cubical.Structures.TypeEqvTo.html#422" class="Function">TypeEqvTo</a> <a id="698" href="Cubical.Functions.Bundle.html#447" class="Bound">ℓ</a> <a id="700" href="Cubical.Functions.Bundle.html#433" class="Bound">F</a><a id="701" class="Symbol">)</a> <a id="703" class="Symbol">→</a> <a id="705" href="Agda.Primitive.html#388" class="Primitive">Type</a> <a id="710" class="Symbol">(</a><a id="711" href="Agda.Primitive.html#961" class="Primitive">ℓ-max</a> <a id="717" href="Cubical.Functions.Bundle.html#411" class="Bound">ℓb</a> <a id="720" href="Cubical.Functions.Bundle.html#447" class="Bound">ℓ</a><a id="721" class="Symbol">)</a>
  <a id="725" href="Cubical.Functions.Bundle.html#669" class="Function">Total</a> <a id="731" href="Cubical.Functions.Bundle.html#731" class="Bound">p⁻¹</a> <a id="735" class="Symbol">=</a> <a id="737" href="Cubical.Core.Primitives.html#6268" class="Function">Σ[</a> <a id="740" href="Cubical.Functions.Bundle.html#740" class="Bound">x</a> <a id="742" href="Cubical.Core.Primitives.html#6268" class="Function">∈</a> <a id="744" href="Cubical.Functions.Bundle.html#419" class="Bound">B</a> <a id="746" href="Cubical.Core.Primitives.html#6268" class="Function">]</a> <a id="748" href="Cubical.Functions.Bundle.html#731" class="Bound">p⁻¹</a> <a id="752" href="Cubical.Functions.Bundle.html#740" class="Bound">x</a> <a id="754" class="Symbol">.</a><a id="755" href="Agda.Builtin.Sigma.html#251" class="Field">fst</a>

  <a id="762" href="Cubical.Functions.Bundle.html#762" class="Function">pr</a> <a id="765" class="Symbol">:</a> <a id="767" class="Symbol">(</a><a id="768" href="Cubical.Functions.Bundle.html#768" class="Bound">p⁻¹</a> <a id="772" class="Symbol">:</a> <a id="774" href="Cubical.Functions.Bundle.html#419" class="Bound">B</a> <a id="776" class="Symbol">→</a> <a id="778" href="Cubical.Structures.TypeEqvTo.html#422" class="Function">TypeEqvTo</a> <a id="788" href="Cubical.Functions.Bundle.html#447" class="Bound">ℓ</a> <a id="790" href="Cubical.Functions.Bundle.html#433" class="Bound">F</a><a id="791" class="Symbol">)</a> <a id="793" class="Symbol">→</a> <a id="795" href="Cubical.Functions.Bundle.html#669" class="Function">Total</a> <a id="801" href="Cubical.Functions.Bundle.html#768" class="Bound">p⁻¹</a> <a id="805" class="Symbol">→</a> <a id="807" href="Cubical.Functions.Bundle.html#419" class="Bound">B</a>
  <a id="811" href="Cubical.Functions.Bundle.html#762" class="Function">pr</a> <a id="814" href="Cubical.Functions.Bundle.html#814" class="Bound">p⁻¹</a> <a id="818" class="Symbol">=</a> <a id="820" href="Agda.Builtin.Sigma.html#251" class="Field">fst</a>

  <a id="827" href="Cubical.Functions.Bundle.html#827" class="Function">inc</a> <a id="831" class="Symbol">:</a> <a id="833" class="Symbol">(</a><a id="834" href="Cubical.Functions.Bundle.html#834" class="Bound">p⁻¹</a> <a id="838" class="Symbol">:</a> <a id="840" href="Cubical.Functions.Bundle.html#419" class="Bound">B</a> <a id="842" class="Symbol">→</a> <a id="844" href="Cubical.Structures.TypeEqvTo.html#422" class="Function">TypeEqvTo</a> <a id="854" href="Cubical.Functions.Bundle.html#447" class="Bound">ℓ</a> <a id="856" href="Cubical.Functions.Bundle.html#433" class="Bound">F</a><a id="857" class="Symbol">)</a> <a id="859" class="Symbol">(</a><a id="860" href="Cubical.Functions.Bundle.html#860" class="Bound">x</a> <a id="862" class="Symbol">:</a> <a id="864" href="Cubical.Functions.Bundle.html#419" class="Bound">B</a><a id="865" class="Symbol">)</a> <a id="867" class="Symbol">→</a> <a id="869" href="Cubical.Functions.Bundle.html#834" class="Bound">p⁻¹</a> <a id="873" href="Cubical.Functions.Bundle.html#860" class="Bound">x</a> <a id="875" class="Symbol">.</a><a id="876" href="Agda.Builtin.Sigma.html#251" class="Field">fst</a> <a id="880" class="Symbol">→</a> <a id="882" href="Cubical.Functions.Bundle.html#669" class="Function">Total</a> <a id="888" href="Cubical.Functions.Bundle.html#834" class="Bound">p⁻¹</a>
  <a id="894" href="Cubical.Functions.Bundle.html#827" class="Function">inc</a> <a id="898" href="Cubical.Functions.Bundle.html#898" class="Bound">p⁻¹</a> <a id="902" href="Cubical.Functions.Bundle.html#902" class="Bound">x</a> <a id="904" class="Symbol">=</a> <a id="906" class="Symbol">(</a><a id="907" href="Cubical.Functions.Bundle.html#902" class="Bound">x</a> <a id="909" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,_</a><a id="911" class="Symbol">)</a>

  <a id="916" href="Cubical.Functions.Bundle.html#916" class="Function">fibPrEquiv</a> <a id="927" class="Symbol">:</a> <a id="929" class="Symbol">(</a><a id="930" href="Cubical.Functions.Bundle.html#930" class="Bound">p⁻¹</a> <a id="934" class="Symbol">:</a> <a id="936" href="Cubical.Functions.Bundle.html#419" class="Bound">B</a> <a id="938" class="Symbol">→</a> <a id="940" href="Cubical.Structures.TypeEqvTo.html#422" class="Function">TypeEqvTo</a> <a id="950" href="Cubical.Functions.Bundle.html#447" class="Bound">ℓ</a> <a id="952" href="Cubical.Functions.Bundle.html#433" class="Bound">F</a><a id="953" class="Symbol">)</a> <a id="955" class="Symbol">(</a><a id="956" href="Cubical.Functions.Bundle.html#956" class="Bound">x</a> <a id="958" class="Symbol">:</a> <a id="960" href="Cubical.Functions.Bundle.html#419" class="Bound">B</a><a id="961" class="Symbol">)</a> <a id="963" class="Symbol">→</a> <a id="965" href="Cubical.Foundations.Equiv.Base.html#253" class="Function">fiber</a> <a id="971" class="Symbol">(</a><a id="972" href="Cubical.Functions.Bundle.html#762" class="Function">pr</a> <a id="975" href="Cubical.Functions.Bundle.html#930" class="Bound">p⁻¹</a><a id="978" class="Symbol">)</a> <a id="980" href="Cubical.Functions.Bundle.html#956" class="Bound">x</a> <a id="982" href="Agda.Builtin.Cubical.Equiv.html#1012" class="Function Operator">≃</a> <a id="984" href="Cubical.Functions.Bundle.html#930" class="Bound">p⁻¹</a> <a id="988" href="Cubical.Functions.Bundle.html#956" class="Bound">x</a> <a id="990" class="Symbol">.</a><a id="991" href="Agda.Builtin.Sigma.html#251" class="Field">fst</a>
  <a id="997" href="Cubical.Functions.Bundle.html#916" class="Function">fibPrEquiv</a> <a id="1008" href="Cubical.Functions.Bundle.html#1008" class="Bound">p⁻¹</a> <a id="1012" href="Cubical.Functions.Bundle.html#1012" class="Bound">x</a> <a id="1014" class="Symbol">=</a> <a id="1016" href="Cubical.Functions.Fibration.html#1369" class="Function">fiberEquiv</a> <a id="1027" class="Symbol">(λ</a> <a id="1030" href="Cubical.Functions.Bundle.html#1030" class="Bound">x</a> <a id="1032" class="Symbol">→</a> <a id="1034" href="Cubical.Foundations.Structure.html#515" class="Function">typ</a> <a id="1038" class="Symbol">(</a><a id="1039" href="Cubical.Functions.Bundle.html#1008" class="Bound">p⁻¹</a> <a id="1043" href="Cubical.Functions.Bundle.html#1030" class="Bound">x</a><a id="1044" class="Symbol">))</a> <a id="1047" href="Cubical.Functions.Bundle.html#1012" class="Bound">x</a>

<a id="1050" class="Keyword">module</a> <a id="1057" href="Cubical.Functions.Bundle.html#1057" class="Module">_</a> <a id="1059" class="Symbol">{</a><a id="1060" href="Cubical.Functions.Bundle.html#1060" class="Bound">ℓb</a> <a id="1063" href="Cubical.Functions.Bundle.html#1063" class="Bound">ℓf</a><a id="1065" class="Symbol">}</a> <a id="1067" class="Symbol">(</a><a id="1068" href="Cubical.Functions.Bundle.html#1068" class="Bound">B</a> <a id="1070" class="Symbol">:</a> <a id="1072" href="Agda.Primitive.html#388" class="Primitive">Type</a> <a id="1077" href="Cubical.Functions.Bundle.html#1060" class="Bound">ℓb</a><a id="1079" class="Symbol">)</a> <a id="1081" class="Symbol">(</a><a id="1082" href="Cubical.Functions.Bundle.html#1082" class="Bound">ℓ</a> <a id="1084" class="Symbol">:</a> <a id="1086" href="Agda.Primitive.html#742" class="Postulate">Level</a><a id="1091" class="Symbol">)</a> <a id="1093" class="Symbol">(</a><a id="1094" href="Cubical.Functions.Bundle.html#1094" class="Bound">F</a> <a id="1096" class="Symbol">:</a> <a id="1098" href="Agda.Primitive.html#388" class="Primitive">Type</a> <a id="1103" href="Cubical.Functions.Bundle.html#1063" class="Bound">ℓf</a><a id="1105" class="Symbol">)</a> <a id="1107" class="Keyword">where</a>
  <a id="1115" class="Keyword">private</a>
    <a id="1127" href="Cubical.Functions.Bundle.html#1127" class="Function">ℓ&#39;</a> <a id="1130" class="Symbol">=</a> <a id="1132" href="Agda.Primitive.html#961" class="Primitive">ℓ-max</a> <a id="1138" href="Cubical.Functions.Bundle.html#1060" class="Bound">ℓb</a> <a id="1141" href="Cubical.Functions.Bundle.html#1082" class="Bound">ℓ</a>

<a id="1144" class="Comment">{-
    Equivalently, a fiber bundle with base space B and fibers F is a type E and
     a map `p : E → B` with fibers merely equivalent to F.
-}</a>

  <a id="1292" href="Cubical.Functions.Bundle.html#1292" class="Function">bundleEquiv</a> <a id="1304" class="Symbol">:</a> <a id="1306" class="Symbol">(</a><a id="1307" href="Cubical.Functions.Bundle.html#1068" class="Bound">B</a> <a id="1309" class="Symbol">→</a> <a id="1311" href="Cubical.Structures.TypeEqvTo.html#422" class="Function">TypeEqvTo</a> <a id="1321" href="Cubical.Functions.Bundle.html#1127" class="Function">ℓ&#39;</a> <a id="1324" href="Cubical.Functions.Bundle.html#1094" class="Bound">F</a><a id="1325" class="Symbol">)</a> <a id="1327" href="Agda.Builtin.Cubical.Equiv.html#1012" class="Function Operator">≃</a> <a id="1329" class="Symbol">(</a><a id="1330" href="Cubical.Core.Primitives.html#6268" class="Function">Σ[</a> <a id="1333" href="Cubical.Functions.Bundle.html#1333" class="Bound">E</a> <a id="1335" href="Cubical.Core.Primitives.html#6268" class="Function">∈</a> <a id="1337" href="Agda.Primitive.html#388" class="Primitive">Type</a> <a id="1342" href="Cubical.Functions.Bundle.html#1127" class="Function">ℓ&#39;</a> <a id="1345" href="Cubical.Core.Primitives.html#6268" class="Function">]</a> <a id="1347" href="Cubical.Core.Primitives.html#6268" class="Function">Σ[</a> <a id="1350" href="Cubical.Functions.Bundle.html#1350" class="Bound">p</a> <a id="1352" href="Cubical.Core.Primitives.html#6268" class="Function">∈</a> <a id="1354" class="Symbol">(</a><a id="1355" href="Cubical.Functions.Bundle.html#1333" class="Bound">E</a> <a id="1357" class="Symbol">→</a> <a id="1359" href="Cubical.Functions.Bundle.html#1068" class="Bound">B</a><a id="1360" class="Symbol">)</a> <a id="1362" href="Cubical.Core.Primitives.html#6268" class="Function">]</a> <a id="1364" class="Symbol">∀</a> <a id="1366" href="Cubical.Functions.Bundle.html#1366" class="Bound">x</a> <a id="1368" class="Symbol">→</a> <a id="1370" href="Cubical.HITs.PropositionalTruncation.Base.html#249" class="Datatype Operator">∥</a> <a id="1372" href="Cubical.Foundations.Equiv.Base.html#253" class="Function">fiber</a> <a id="1378" href="Cubical.Functions.Bundle.html#1350" class="Bound">p</a> <a id="1380" href="Cubical.Functions.Bundle.html#1366" class="Bound">x</a> <a id="1382" href="Agda.Builtin.Cubical.Equiv.html#1012" class="Function Operator">≃</a> <a id="1384" href="Cubical.Functions.Bundle.html#1094" class="Bound">F</a> <a id="1386" href="Cubical.HITs.PropositionalTruncation.Base.html#249" class="Datatype Operator">∥₁</a><a id="1388" class="Symbol">)</a>
  <a id="1392" href="Cubical.Functions.Bundle.html#1292" class="Function">bundleEquiv</a> <a id="1404" class="Symbol">=</a> <a id="1406" href="Cubical.Foundations.Equiv.html#3542" class="Function">compEquiv</a> <a id="1416" class="Symbol">(</a><a id="1417" href="Cubical.Foundations.Equiv.html#3542" class="Function">compEquiv</a> <a id="1427" href="Cubical.Data.Sigma.Properties.html#6233" class="Function">Σ-Π-≃</a> <a id="1433" class="Symbol">(</a><a id="1434" href="Cubical.Foundations.Univalence.html#7487" class="Function">pathToEquiv</a> <a id="1446" href="Cubical.Functions.Bundle.html#1503" class="Function">p</a><a id="1447" class="Symbol">))</a> <a id="1450" href="Cubical.Data.Sigma.Properties.html#5926" class="Function">Σ-assoc-≃</a>
    <a id="1464" class="Keyword">where</a> <a id="1470" href="Cubical.Functions.Bundle.html#1470" class="Function">e</a> <a id="1472" class="Symbol">=</a> <a id="1474" href="Cubical.Functions.Fibration.html#1974" class="Function">fibrationEquiv</a> <a id="1489" href="Cubical.Functions.Bundle.html#1068" class="Bound">B</a> <a id="1491" href="Cubical.Functions.Bundle.html#1082" class="Bound">ℓ</a>
          <a id="1503" href="Cubical.Functions.Bundle.html#1503" class="Function">p</a> <a id="1505" class="Symbol">:</a>   <a id="1509" class="Symbol">(</a><a id="1510" href="Cubical.Core.Primitives.html#6268" class="Function">Σ[</a> <a id="1513" href="Cubical.Functions.Bundle.html#1513" class="Bound">p⁻¹</a> <a id="1517" href="Cubical.Core.Primitives.html#6268" class="Function">∈</a> <a id="1519" class="Symbol">(</a><a id="1520" href="Cubical.Functions.Bundle.html#1068" class="Bound">B</a> <a id="1522" class="Symbol">→</a> <a id="1524" href="Agda.Primitive.html#388" class="Primitive">Type</a> <a id="1529" href="Cubical.Functions.Bundle.html#1127" class="Function">ℓ&#39;</a><a id="1531" class="Symbol">)</a> <a id="1533" href="Cubical.Core.Primitives.html#6268" class="Function">]</a>            <a id="1546" class="Symbol">∀</a> <a id="1548" href="Cubical.Functions.Bundle.html#1548" class="Bound">x</a> <a id="1550" class="Symbol">→</a> <a id="1552" href="Cubical.HITs.PropositionalTruncation.Base.html#249" class="Datatype Operator">∥</a> <a id="1554" href="Cubical.Functions.Bundle.html#1513" class="Bound">p⁻¹</a> <a id="1558" href="Cubical.Functions.Bundle.html#1548" class="Bound">x</a> <a id="1560" href="Agda.Builtin.Cubical.Equiv.html#1012" class="Function Operator">≃</a> <a id="1562" href="Cubical.Functions.Bundle.html#1094" class="Bound">F</a> <a id="1564" href="Cubical.HITs.PropositionalTruncation.Base.html#249" class="Datatype Operator">∥₁</a><a id="1566" class="Symbol">)</a>
              <a id="1582" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="1584" class="Symbol">(</a><a id="1585" href="Cubical.Core.Primitives.html#6268" class="Function">Σ[</a> <a id="1588" href="Cubical.Functions.Bundle.html#1588" class="Bound">p</a> <a id="1590" href="Cubical.Core.Primitives.html#6268" class="Function">∈</a> <a id="1592" class="Symbol">(</a><a id="1593" href="Cubical.Core.Primitives.html#6268" class="Function">Σ[</a> <a id="1596" href="Cubical.Functions.Bundle.html#1596" class="Bound">E</a> <a id="1598" href="Cubical.Core.Primitives.html#6268" class="Function">∈</a> <a id="1600" href="Agda.Primitive.html#388" class="Primitive">Type</a> <a id="1605" href="Cubical.Functions.Bundle.html#1127" class="Function">ℓ&#39;</a> <a id="1608" href="Cubical.Core.Primitives.html#6268" class="Function">]</a> <a id="1610" class="Symbol">(</a><a id="1611" href="Cubical.Functions.Bundle.html#1596" class="Bound">E</a> <a id="1613" class="Symbol">→</a> <a id="1615" href="Cubical.Functions.Bundle.html#1068" class="Bound">B</a><a id="1616" class="Symbol">))</a> <a id="1619" href="Cubical.Core.Primitives.html#6268" class="Function">]</a> <a id="1621" class="Symbol">∀</a> <a id="1623" href="Cubical.Functions.Bundle.html#1623" class="Bound">x</a> <a id="1625" class="Symbol">→</a> <a id="1627" href="Cubical.HITs.PropositionalTruncation.Base.html#249" class="Datatype Operator">∥</a> <a id="1629" href="Cubical.Foundations.Equiv.Base.html#253" class="Function">fiber</a> <a id="1635" class="Symbol">(</a><a id="1636" href="Agda.Builtin.Sigma.html#263" class="Field">snd</a> <a id="1640" href="Cubical.Functions.Bundle.html#1588" class="Bound">p</a><a id="1641" class="Symbol">)</a> <a id="1643" href="Cubical.Functions.Bundle.html#1623" class="Bound">x</a> <a id="1645" href="Agda.Builtin.Cubical.Equiv.html#1012" class="Function Operator">≃</a> <a id="1647" href="Cubical.Functions.Bundle.html#1094" class="Bound">F</a> <a id="1649" href="Cubical.HITs.PropositionalTruncation.Base.html#249" class="Datatype Operator">∥₁</a> <a id="1652" class="Symbol">)</a>
          <a id="1664" href="Cubical.Functions.Bundle.html#1503" class="Function">p</a> <a id="1666" href="Cubical.Functions.Bundle.html#1666" class="Bound">i</a> <a id="1668" class="Symbol">=</a> <a id="1670" href="Cubical.Core.Primitives.html#6268" class="Function">Σ[</a> <a id="1673" href="Cubical.Functions.Bundle.html#1673" class="Bound">q</a> <a id="1675" href="Cubical.Core.Primitives.html#6268" class="Function">∈</a> <a id="1677" href="Cubical.Foundations.Univalence.html#928" class="Function">ua</a> <a id="1680" href="Cubical.Functions.Bundle.html#1470" class="Function">e</a> <a id="1682" class="Symbol">(</a><a id="1683" href="Cubical.Core.Primitives.html#501" class="Primitive Operator">~</a> <a id="1685" href="Cubical.Functions.Bundle.html#1666" class="Bound">i</a><a id="1686" class="Symbol">)</a> <a id="1688" href="Cubical.Core.Primitives.html#6268" class="Function">]</a> <a id="1690" class="Symbol">∀</a> <a id="1692" href="Cubical.Functions.Bundle.html#1692" class="Bound">x</a> <a id="1694" class="Symbol">→</a> <a id="1696" href="Cubical.HITs.PropositionalTruncation.Base.html#249" class="Datatype Operator">∥</a> <a id="1698" href="Cubical.Foundations.Univalence.html#1450" class="Function">ua-unglue</a> <a id="1708" href="Cubical.Functions.Bundle.html#1470" class="Function">e</a> <a id="1710" class="Symbol">(</a><a id="1711" href="Cubical.Core.Primitives.html#501" class="Primitive Operator">~</a> <a id="1713" href="Cubical.Functions.Bundle.html#1666" class="Bound">i</a><a id="1714" class="Symbol">)</a> <a id="1716" href="Cubical.Functions.Bundle.html#1673" class="Bound">q</a> <a id="1718" href="Cubical.Functions.Bundle.html#1692" class="Bound">x</a> <a id="1720" href="Agda.Builtin.Cubical.Equiv.html#1012" class="Function Operator">≃</a> <a id="1722" href="Cubical.Functions.Bundle.html#1094" class="Bound">F</a> <a id="1724" href="Cubical.HITs.PropositionalTruncation.Base.html#249" class="Datatype Operator">∥₁</a>
</pre></body></html>