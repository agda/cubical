<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Cubical.Functions.Bundle</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Keyword">module</a> <a id="8" href="Cubical.Functions.Bundle.html" class="Module">Cubical.Functions.Bundle</a> <a id="33" class="Keyword">where</a>

<a id="40" class="Keyword">open</a> <a id="45" class="Keyword">import</a> <a id="52" href="Cubical.Foundations.Prelude.html" class="Module">Cubical.Foundations.Prelude</a>
<a id="80" class="Keyword">open</a> <a id="85" class="Keyword">import</a> <a id="92" href="Cubical.Foundations.Equiv.html" class="Module">Cubical.Foundations.Equiv</a>
<a id="118" class="Keyword">open</a> <a id="123" class="Keyword">import</a> <a id="130" href="Cubical.Foundations.Univalence.html" class="Module">Cubical.Foundations.Univalence</a>
<a id="161" class="Keyword">open</a> <a id="166" class="Keyword">import</a> <a id="173" href="Cubical.Functions.Fibration.html" class="Module">Cubical.Functions.Fibration</a>

<a id="202" class="Keyword">open</a> <a id="207" class="Keyword">import</a> <a id="214" href="Cubical.Foundations.Structure.html" class="Module">Cubical.Foundations.Structure</a>
<a id="244" class="Keyword">open</a> <a id="249" class="Keyword">import</a> <a id="256" href="Cubical.Structures.TypeEqvTo.html" class="Module">Cubical.Structures.TypeEqvTo</a>

<a id="286" class="Keyword">open</a> <a id="291" class="Keyword">import</a> <a id="298" href="Cubical.Data.Sigma.Properties.html" class="Module">Cubical.Data.Sigma.Properties</a>
<a id="328" class="Keyword">open</a> <a id="333" class="Keyword">import</a> <a id="340" href="Cubical.HITs.PropositionalTruncation.html" class="Module">Cubical.HITs.PropositionalTruncation</a>

<a id="378" class="Keyword">module</a> <a id="385" href="Cubical.Functions.Bundle.html#385" class="Module">_</a> <a id="387" class="Symbol">{</a><a id="388" href="Cubical.Functions.Bundle.html#388" class="Bound">ℓb</a> <a id="391" href="Cubical.Functions.Bundle.html#391" class="Bound">ℓf</a><a id="393" class="Symbol">}</a> <a id="395" class="Symbol">{</a><a id="396" href="Cubical.Functions.Bundle.html#396" class="Bound">B</a> <a id="398" class="Symbol">:</a> <a id="400" href="Agda.Primitive.html#388" class="Primitive">Type</a> <a id="405" href="Cubical.Functions.Bundle.html#388" class="Bound">ℓb</a><a id="407" class="Symbol">}</a> <a id="409" class="Symbol">{</a><a id="410" href="Cubical.Functions.Bundle.html#410" class="Bound">F</a> <a id="412" class="Symbol">:</a> <a id="414" href="Agda.Primitive.html#388" class="Primitive">Type</a> <a id="419" href="Cubical.Functions.Bundle.html#391" class="Bound">ℓf</a><a id="421" class="Symbol">}</a> <a id="423" class="Symbol">{</a><a id="424" href="Cubical.Functions.Bundle.html#424" class="Bound">ℓ</a><a id="425" class="Symbol">}</a> <a id="427" class="Keyword">where</a>

<a id="434" class="Comment">{-
    A fiber bundle with base space B and fibers F is a map `p⁻¹ : B → TypeEqvTo F`
     taking points in the base space to their respective fibers.

    e.g. a double cover is a map `B → TypeEqvTo Bool`
-}</a>

  <a id="646" href="Cubical.Functions.Bundle.html#646" class="Function">Total</a> <a id="652" class="Symbol">:</a> <a id="654" class="Symbol">(</a><a id="655" href="Cubical.Functions.Bundle.html#655" class="Bound">p⁻¹</a> <a id="659" class="Symbol">:</a> <a id="661" href="Cubical.Functions.Bundle.html#396" class="Bound">B</a> <a id="663" class="Symbol">→</a> <a id="665" href="Cubical.Structures.TypeEqvTo.html#399" class="Function">TypeEqvTo</a> <a id="675" href="Cubical.Functions.Bundle.html#424" class="Bound">ℓ</a> <a id="677" href="Cubical.Functions.Bundle.html#410" class="Bound">F</a><a id="678" class="Symbol">)</a> <a id="680" class="Symbol">→</a> <a id="682" href="Agda.Primitive.html#388" class="Primitive">Type</a> <a id="687" class="Symbol">(</a><a id="688" href="Agda.Primitive.html#961" class="Primitive">ℓ-max</a> <a id="694" href="Cubical.Functions.Bundle.html#388" class="Bound">ℓb</a> <a id="697" href="Cubical.Functions.Bundle.html#424" class="Bound">ℓ</a><a id="698" class="Symbol">)</a>
  <a id="702" href="Cubical.Functions.Bundle.html#646" class="Function">Total</a> <a id="708" href="Cubical.Functions.Bundle.html#708" class="Bound">p⁻¹</a> <a id="712" class="Symbol">=</a> <a id="714" href="Cubical.Core.Primitives.html#6245" class="Function">Σ[</a> <a id="717" href="Cubical.Functions.Bundle.html#717" class="Bound">x</a> <a id="719" href="Cubical.Core.Primitives.html#6245" class="Function">∈</a> <a id="721" href="Cubical.Functions.Bundle.html#396" class="Bound">B</a> <a id="723" href="Cubical.Core.Primitives.html#6245" class="Function">]</a> <a id="725" href="Cubical.Functions.Bundle.html#708" class="Bound">p⁻¹</a> <a id="729" href="Cubical.Functions.Bundle.html#717" class="Bound">x</a> <a id="731" class="Symbol">.</a><a id="732" href="Agda.Builtin.Sigma.html#251" class="Field">fst</a>

  <a id="739" href="Cubical.Functions.Bundle.html#739" class="Function">pr</a> <a id="742" class="Symbol">:</a> <a id="744" class="Symbol">(</a><a id="745" href="Cubical.Functions.Bundle.html#745" class="Bound">p⁻¹</a> <a id="749" class="Symbol">:</a> <a id="751" href="Cubical.Functions.Bundle.html#396" class="Bound">B</a> <a id="753" class="Symbol">→</a> <a id="755" href="Cubical.Structures.TypeEqvTo.html#399" class="Function">TypeEqvTo</a> <a id="765" href="Cubical.Functions.Bundle.html#424" class="Bound">ℓ</a> <a id="767" href="Cubical.Functions.Bundle.html#410" class="Bound">F</a><a id="768" class="Symbol">)</a> <a id="770" class="Symbol">→</a> <a id="772" href="Cubical.Functions.Bundle.html#646" class="Function">Total</a> <a id="778" href="Cubical.Functions.Bundle.html#745" class="Bound">p⁻¹</a> <a id="782" class="Symbol">→</a> <a id="784" href="Cubical.Functions.Bundle.html#396" class="Bound">B</a>
  <a id="788" href="Cubical.Functions.Bundle.html#739" class="Function">pr</a> <a id="791" href="Cubical.Functions.Bundle.html#791" class="Bound">p⁻¹</a> <a id="795" class="Symbol">=</a> <a id="797" href="Agda.Builtin.Sigma.html#251" class="Field">fst</a>

  <a id="804" href="Cubical.Functions.Bundle.html#804" class="Function">inc</a> <a id="808" class="Symbol">:</a> <a id="810" class="Symbol">(</a><a id="811" href="Cubical.Functions.Bundle.html#811" class="Bound">p⁻¹</a> <a id="815" class="Symbol">:</a> <a id="817" href="Cubical.Functions.Bundle.html#396" class="Bound">B</a> <a id="819" class="Symbol">→</a> <a id="821" href="Cubical.Structures.TypeEqvTo.html#399" class="Function">TypeEqvTo</a> <a id="831" href="Cubical.Functions.Bundle.html#424" class="Bound">ℓ</a> <a id="833" href="Cubical.Functions.Bundle.html#410" class="Bound">F</a><a id="834" class="Symbol">)</a> <a id="836" class="Symbol">(</a><a id="837" href="Cubical.Functions.Bundle.html#837" class="Bound">x</a> <a id="839" class="Symbol">:</a> <a id="841" href="Cubical.Functions.Bundle.html#396" class="Bound">B</a><a id="842" class="Symbol">)</a> <a id="844" class="Symbol">→</a> <a id="846" href="Cubical.Functions.Bundle.html#811" class="Bound">p⁻¹</a> <a id="850" href="Cubical.Functions.Bundle.html#837" class="Bound">x</a> <a id="852" class="Symbol">.</a><a id="853" href="Agda.Builtin.Sigma.html#251" class="Field">fst</a> <a id="857" class="Symbol">→</a> <a id="859" href="Cubical.Functions.Bundle.html#646" class="Function">Total</a> <a id="865" href="Cubical.Functions.Bundle.html#811" class="Bound">p⁻¹</a>
  <a id="871" href="Cubical.Functions.Bundle.html#804" class="Function">inc</a> <a id="875" href="Cubical.Functions.Bundle.html#875" class="Bound">p⁻¹</a> <a id="879" href="Cubical.Functions.Bundle.html#879" class="Bound">x</a> <a id="881" class="Symbol">=</a> <a id="883" class="Symbol">(</a><a id="884" href="Cubical.Functions.Bundle.html#879" class="Bound">x</a> <a id="886" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,_</a><a id="888" class="Symbol">)</a>

  <a id="893" href="Cubical.Functions.Bundle.html#893" class="Function">fibPrEquiv</a> <a id="904" class="Symbol">:</a> <a id="906" class="Symbol">(</a><a id="907" href="Cubical.Functions.Bundle.html#907" class="Bound">p⁻¹</a> <a id="911" class="Symbol">:</a> <a id="913" href="Cubical.Functions.Bundle.html#396" class="Bound">B</a> <a id="915" class="Symbol">→</a> <a id="917" href="Cubical.Structures.TypeEqvTo.html#399" class="Function">TypeEqvTo</a> <a id="927" href="Cubical.Functions.Bundle.html#424" class="Bound">ℓ</a> <a id="929" href="Cubical.Functions.Bundle.html#410" class="Bound">F</a><a id="930" class="Symbol">)</a> <a id="932" class="Symbol">(</a><a id="933" href="Cubical.Functions.Bundle.html#933" class="Bound">x</a> <a id="935" class="Symbol">:</a> <a id="937" href="Cubical.Functions.Bundle.html#396" class="Bound">B</a><a id="938" class="Symbol">)</a> <a id="940" class="Symbol">→</a> <a id="942" href="Cubical.Foundations.Equiv.Base.html#230" class="Function">fiber</a> <a id="948" class="Symbol">(</a><a id="949" href="Cubical.Functions.Bundle.html#739" class="Function">pr</a> <a id="952" href="Cubical.Functions.Bundle.html#907" class="Bound">p⁻¹</a><a id="955" class="Symbol">)</a> <a id="957" href="Cubical.Functions.Bundle.html#933" class="Bound">x</a> <a id="959" href="Agda.Builtin.Cubical.Equiv.html#1012" class="Function Operator">≃</a> <a id="961" href="Cubical.Functions.Bundle.html#907" class="Bound">p⁻¹</a> <a id="965" href="Cubical.Functions.Bundle.html#933" class="Bound">x</a> <a id="967" class="Symbol">.</a><a id="968" href="Agda.Builtin.Sigma.html#251" class="Field">fst</a>
  <a id="974" href="Cubical.Functions.Bundle.html#893" class="Function">fibPrEquiv</a> <a id="985" href="Cubical.Functions.Bundle.html#985" class="Bound">p⁻¹</a> <a id="989" href="Cubical.Functions.Bundle.html#989" class="Bound">x</a> <a id="991" class="Symbol">=</a> <a id="993" href="Cubical.Functions.Fibration.html#1346" class="Function">fiberEquiv</a> <a id="1004" class="Symbol">(λ</a> <a id="1007" href="Cubical.Functions.Bundle.html#1007" class="Bound">x</a> <a id="1009" class="Symbol">→</a> <a id="1011" href="Cubical.Foundations.Structure.html#1009" class="Function">typ</a> <a id="1015" class="Symbol">(</a><a id="1016" href="Cubical.Functions.Bundle.html#985" class="Bound">p⁻¹</a> <a id="1020" href="Cubical.Functions.Bundle.html#1007" class="Bound">x</a><a id="1021" class="Symbol">))</a> <a id="1024" href="Cubical.Functions.Bundle.html#989" class="Bound">x</a>

<a id="1027" class="Keyword">module</a> <a id="1034" href="Cubical.Functions.Bundle.html#1034" class="Module">_</a> <a id="1036" class="Symbol">{</a><a id="1037" href="Cubical.Functions.Bundle.html#1037" class="Bound">ℓb</a> <a id="1040" href="Cubical.Functions.Bundle.html#1040" class="Bound">ℓf</a><a id="1042" class="Symbol">}</a> <a id="1044" class="Symbol">(</a><a id="1045" href="Cubical.Functions.Bundle.html#1045" class="Bound">B</a> <a id="1047" class="Symbol">:</a> <a id="1049" href="Agda.Primitive.html#388" class="Primitive">Type</a> <a id="1054" href="Cubical.Functions.Bundle.html#1037" class="Bound">ℓb</a><a id="1056" class="Symbol">)</a> <a id="1058" class="Symbol">(</a><a id="1059" href="Cubical.Functions.Bundle.html#1059" class="Bound">ℓ</a> <a id="1061" class="Symbol">:</a> <a id="1063" href="Agda.Primitive.html#742" class="Postulate">Level</a><a id="1068" class="Symbol">)</a> <a id="1070" class="Symbol">(</a><a id="1071" href="Cubical.Functions.Bundle.html#1071" class="Bound">F</a> <a id="1073" class="Symbol">:</a> <a id="1075" href="Agda.Primitive.html#388" class="Primitive">Type</a> <a id="1080" href="Cubical.Functions.Bundle.html#1040" class="Bound">ℓf</a><a id="1082" class="Symbol">)</a> <a id="1084" class="Keyword">where</a>
  <a id="1092" class="Keyword">private</a>
    <a id="1104" href="Cubical.Functions.Bundle.html#1104" class="Function">ℓ&#39;</a> <a id="1107" class="Symbol">=</a> <a id="1109" href="Agda.Primitive.html#961" class="Primitive">ℓ-max</a> <a id="1115" href="Cubical.Functions.Bundle.html#1037" class="Bound">ℓb</a> <a id="1118" href="Cubical.Functions.Bundle.html#1059" class="Bound">ℓ</a>

<a id="1121" class="Comment">{-
    Equivalently, a fiber bundle with base space B and fibers F is a type E and
     a map `p : E → B` with fibers merely equivalent to F.
-}</a>

  <a id="1269" href="Cubical.Functions.Bundle.html#1269" class="Function">bundleEquiv</a> <a id="1281" class="Symbol">:</a> <a id="1283" class="Symbol">(</a><a id="1284" href="Cubical.Functions.Bundle.html#1045" class="Bound">B</a> <a id="1286" class="Symbol">→</a> <a id="1288" href="Cubical.Structures.TypeEqvTo.html#399" class="Function">TypeEqvTo</a> <a id="1298" href="Cubical.Functions.Bundle.html#1104" class="Function">ℓ&#39;</a> <a id="1301" href="Cubical.Functions.Bundle.html#1071" class="Bound">F</a><a id="1302" class="Symbol">)</a> <a id="1304" href="Agda.Builtin.Cubical.Equiv.html#1012" class="Function Operator">≃</a> <a id="1306" class="Symbol">(</a><a id="1307" href="Cubical.Core.Primitives.html#6245" class="Function">Σ[</a> <a id="1310" href="Cubical.Functions.Bundle.html#1310" class="Bound">E</a> <a id="1312" href="Cubical.Core.Primitives.html#6245" class="Function">∈</a> <a id="1314" href="Agda.Primitive.html#388" class="Primitive">Type</a> <a id="1319" href="Cubical.Functions.Bundle.html#1104" class="Function">ℓ&#39;</a> <a id="1322" href="Cubical.Core.Primitives.html#6245" class="Function">]</a> <a id="1324" href="Cubical.Core.Primitives.html#6245" class="Function">Σ[</a> <a id="1327" href="Cubical.Functions.Bundle.html#1327" class="Bound">p</a> <a id="1329" href="Cubical.Core.Primitives.html#6245" class="Function">∈</a> <a id="1331" class="Symbol">(</a><a id="1332" href="Cubical.Functions.Bundle.html#1310" class="Bound">E</a> <a id="1334" class="Symbol">→</a> <a id="1336" href="Cubical.Functions.Bundle.html#1045" class="Bound">B</a><a id="1337" class="Symbol">)</a> <a id="1339" href="Cubical.Core.Primitives.html#6245" class="Function">]</a> <a id="1341" class="Symbol">∀</a> <a id="1343" href="Cubical.Functions.Bundle.html#1343" class="Bound">x</a> <a id="1345" class="Symbol">→</a> <a id="1347" href="Cubical.HITs.PropositionalTruncation.Base.html#226" class="Datatype Operator">∥</a> <a id="1349" href="Cubical.Foundations.Equiv.Base.html#230" class="Function">fiber</a> <a id="1355" href="Cubical.Functions.Bundle.html#1327" class="Bound">p</a> <a id="1357" href="Cubical.Functions.Bundle.html#1343" class="Bound">x</a> <a id="1359" href="Agda.Builtin.Cubical.Equiv.html#1012" class="Function Operator">≃</a> <a id="1361" href="Cubical.Functions.Bundle.html#1071" class="Bound">F</a> <a id="1363" href="Cubical.HITs.PropositionalTruncation.Base.html#226" class="Datatype Operator">∥₁</a><a id="1365" class="Symbol">)</a>
  <a id="1369" href="Cubical.Functions.Bundle.html#1269" class="Function">bundleEquiv</a> <a id="1381" class="Symbol">=</a> <a id="1383" href="Cubical.Foundations.Equiv.html#3727" class="Function">compEquiv</a> <a id="1393" class="Symbol">(</a><a id="1394" href="Cubical.Foundations.Equiv.html#3727" class="Function">compEquiv</a> <a id="1404" href="Cubical.Data.Sigma.Properties.html#6293" class="Function">Σ-Π-≃</a> <a id="1410" class="Symbol">(</a><a id="1411" href="Cubical.Foundations.Univalence.html#8058" class="Function">pathToEquiv</a> <a id="1423" href="Cubical.Functions.Bundle.html#1480" class="Function">p</a><a id="1424" class="Symbol">))</a> <a id="1427" href="Cubical.Data.Sigma.Properties.html#5986" class="Function">Σ-assoc-≃</a>
    <a id="1441" class="Keyword">where</a> <a id="1447" href="Cubical.Functions.Bundle.html#1447" class="Function">e</a> <a id="1449" class="Symbol">=</a> <a id="1451" href="Cubical.Functions.Fibration.html#1951" class="Function">fibrationEquiv</a> <a id="1466" href="Cubical.Functions.Bundle.html#1045" class="Bound">B</a> <a id="1468" href="Cubical.Functions.Bundle.html#1059" class="Bound">ℓ</a>
          <a id="1480" href="Cubical.Functions.Bundle.html#1480" class="Function">p</a> <a id="1482" class="Symbol">:</a>   <a id="1486" class="Symbol">(</a><a id="1487" href="Cubical.Core.Primitives.html#6245" class="Function">Σ[</a> <a id="1490" href="Cubical.Functions.Bundle.html#1490" class="Bound">p⁻¹</a> <a id="1494" href="Cubical.Core.Primitives.html#6245" class="Function">∈</a> <a id="1496" class="Symbol">(</a><a id="1497" href="Cubical.Functions.Bundle.html#1045" class="Bound">B</a> <a id="1499" class="Symbol">→</a> <a id="1501" href="Agda.Primitive.html#388" class="Primitive">Type</a> <a id="1506" href="Cubical.Functions.Bundle.html#1104" class="Function">ℓ&#39;</a><a id="1508" class="Symbol">)</a> <a id="1510" href="Cubical.Core.Primitives.html#6245" class="Function">]</a>            <a id="1523" class="Symbol">∀</a> <a id="1525" href="Cubical.Functions.Bundle.html#1525" class="Bound">x</a> <a id="1527" class="Symbol">→</a> <a id="1529" href="Cubical.HITs.PropositionalTruncation.Base.html#226" class="Datatype Operator">∥</a> <a id="1531" href="Cubical.Functions.Bundle.html#1490" class="Bound">p⁻¹</a> <a id="1535" href="Cubical.Functions.Bundle.html#1525" class="Bound">x</a> <a id="1537" href="Agda.Builtin.Cubical.Equiv.html#1012" class="Function Operator">≃</a> <a id="1539" href="Cubical.Functions.Bundle.html#1071" class="Bound">F</a> <a id="1541" href="Cubical.HITs.PropositionalTruncation.Base.html#226" class="Datatype Operator">∥₁</a><a id="1543" class="Symbol">)</a>
              <a id="1559" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="1561" class="Symbol">(</a><a id="1562" href="Cubical.Core.Primitives.html#6245" class="Function">Σ[</a> <a id="1565" href="Cubical.Functions.Bundle.html#1565" class="Bound">p</a> <a id="1567" href="Cubical.Core.Primitives.html#6245" class="Function">∈</a> <a id="1569" class="Symbol">(</a><a id="1570" href="Cubical.Core.Primitives.html#6245" class="Function">Σ[</a> <a id="1573" href="Cubical.Functions.Bundle.html#1573" class="Bound">E</a> <a id="1575" href="Cubical.Core.Primitives.html#6245" class="Function">∈</a> <a id="1577" href="Agda.Primitive.html#388" class="Primitive">Type</a> <a id="1582" href="Cubical.Functions.Bundle.html#1104" class="Function">ℓ&#39;</a> <a id="1585" href="Cubical.Core.Primitives.html#6245" class="Function">]</a> <a id="1587" class="Symbol">(</a><a id="1588" href="Cubical.Functions.Bundle.html#1573" class="Bound">E</a> <a id="1590" class="Symbol">→</a> <a id="1592" href="Cubical.Functions.Bundle.html#1045" class="Bound">B</a><a id="1593" class="Symbol">))</a> <a id="1596" href="Cubical.Core.Primitives.html#6245" class="Function">]</a> <a id="1598" class="Symbol">∀</a> <a id="1600" href="Cubical.Functions.Bundle.html#1600" class="Bound">x</a> <a id="1602" class="Symbol">→</a> <a id="1604" href="Cubical.HITs.PropositionalTruncation.Base.html#226" class="Datatype Operator">∥</a> <a id="1606" href="Cubical.Foundations.Equiv.Base.html#230" class="Function">fiber</a> <a id="1612" class="Symbol">(</a><a id="1613" href="Agda.Builtin.Sigma.html#263" class="Field">snd</a> <a id="1617" href="Cubical.Functions.Bundle.html#1565" class="Bound">p</a><a id="1618" class="Symbol">)</a> <a id="1620" href="Cubical.Functions.Bundle.html#1600" class="Bound">x</a> <a id="1622" href="Agda.Builtin.Cubical.Equiv.html#1012" class="Function Operator">≃</a> <a id="1624" href="Cubical.Functions.Bundle.html#1071" class="Bound">F</a> <a id="1626" href="Cubical.HITs.PropositionalTruncation.Base.html#226" class="Datatype Operator">∥₁</a> <a id="1629" class="Symbol">)</a>
          <a id="1641" href="Cubical.Functions.Bundle.html#1480" class="Function">p</a> <a id="1643" href="Cubical.Functions.Bundle.html#1643" class="Bound">i</a> <a id="1645" class="Symbol">=</a> <a id="1647" href="Cubical.Core.Primitives.html#6245" class="Function">Σ[</a> <a id="1650" href="Cubical.Functions.Bundle.html#1650" class="Bound">q</a> <a id="1652" href="Cubical.Core.Primitives.html#6245" class="Function">∈</a> <a id="1654" href="Cubical.Foundations.Univalence.html#905" class="Function">ua</a> <a id="1657" href="Cubical.Functions.Bundle.html#1447" class="Function">e</a> <a id="1659" class="Symbol">(</a><a id="1660" href="Cubical.Core.Primitives.html#478" class="Primitive Operator">~</a> <a id="1662" href="Cubical.Functions.Bundle.html#1643" class="Bound">i</a><a id="1663" class="Symbol">)</a> <a id="1665" href="Cubical.Core.Primitives.html#6245" class="Function">]</a> <a id="1667" class="Symbol">∀</a> <a id="1669" href="Cubical.Functions.Bundle.html#1669" class="Bound">x</a> <a id="1671" class="Symbol">→</a> <a id="1673" href="Cubical.HITs.PropositionalTruncation.Base.html#226" class="Datatype Operator">∥</a> <a id="1675" href="Cubical.Foundations.Univalence.html#1427" class="Function">ua-unglue</a> <a id="1685" href="Cubical.Functions.Bundle.html#1447" class="Function">e</a> <a id="1687" class="Symbol">(</a><a id="1688" href="Cubical.Core.Primitives.html#478" class="Primitive Operator">~</a> <a id="1690" href="Cubical.Functions.Bundle.html#1643" class="Bound">i</a><a id="1691" class="Symbol">)</a> <a id="1693" href="Cubical.Functions.Bundle.html#1650" class="Bound">q</a> <a id="1695" href="Cubical.Functions.Bundle.html#1669" class="Bound">x</a> <a id="1697" href="Agda.Builtin.Cubical.Equiv.html#1012" class="Function Operator">≃</a> <a id="1699" href="Cubical.Functions.Bundle.html#1071" class="Bound">F</a> <a id="1701" href="Cubical.HITs.PropositionalTruncation.Base.html#226" class="Datatype Operator">∥₁</a>
</pre></body></html>