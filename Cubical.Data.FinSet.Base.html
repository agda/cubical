<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Cubical.Data.FinSet.Base</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">{-

Definition of finite sets

There are may different formulations of finite sets in constructive mathematics,
and we will use Bishop finiteness as is usually called in the literature.

-}</a>
<a id="191" class="Symbol">{-#</a> <a id="195" class="Keyword">OPTIONS</a> <a id="203" class="Pragma">--safe</a> <a id="210" class="Symbol">#-}</a>

<a id="215" class="Keyword">module</a> <a id="222" href="Cubical.Data.FinSet.Base.html" class="Module">Cubical.Data.FinSet.Base</a> <a id="247" class="Keyword">where</a>

<a id="254" class="Keyword">open</a> <a id="259" class="Keyword">import</a> <a id="266" href="Cubical.Foundations.Prelude.html" class="Module">Cubical.Foundations.Prelude</a>
<a id="294" class="Keyword">open</a> <a id="299" class="Keyword">import</a> <a id="306" href="Cubical.Foundations.HLevels.html" class="Module">Cubical.Foundations.HLevels</a>
<a id="334" class="Keyword">open</a> <a id="339" class="Keyword">import</a> <a id="346" href="Cubical.Foundations.Structure.html" class="Module">Cubical.Foundations.Structure</a>
<a id="376" class="Keyword">open</a> <a id="381" class="Keyword">import</a> <a id="388" href="Cubical.Foundations.Equiv.html" class="Module">Cubical.Foundations.Equiv</a> <a id="414" class="Keyword">renaming</a> <a id="423" class="Symbol">(</a><a id="424" href="Cubical.Foundations.Equiv.html#4681" class="Function Operator">_∙ₑ_</a> <a id="429" class="Symbol">to</a> <a id="432" class="Function Operator">_⋆_</a><a id="435" class="Symbol">)</a>
<a id="437" class="Keyword">open</a> <a id="442" class="Keyword">import</a> <a id="449" href="Cubical.Foundations.Univalence.html" class="Module">Cubical.Foundations.Univalence</a>

<a id="481" class="Keyword">open</a> <a id="486" class="Keyword">import</a> <a id="493" href="Cubical.HITs.PropositionalTruncation.html" class="Module">Cubical.HITs.PropositionalTruncation</a> <a id="530" class="Symbol">as</a> <a id="533" class="Module">Prop</a>

<a id="539" class="Keyword">open</a> <a id="544" class="Keyword">import</a> <a id="551" href="Cubical.Data.Nat.html" class="Module">Cubical.Data.Nat</a>
<a id="568" class="Keyword">open</a> <a id="573" class="Keyword">import</a> <a id="580" href="Cubical.Data.Fin.html" class="Module">Cubical.Data.Fin</a> <a id="597" class="Keyword">renaming</a> <a id="606" class="Symbol">(</a><a id="607" href="Cubical.Data.Fin.Base.html#826" class="Function">Fin</a> <a id="611" class="Symbol">to</a> <a id="614" class="Function">Finℕ</a><a id="618" class="Symbol">)</a> <a id="620" class="Keyword">hiding</a> <a id="627" class="Symbol">(</a><a id="628" href="Cubical.Data.Fin.Properties.html#1526" class="Function">isSetFin</a><a id="636" class="Symbol">)</a>
<a id="638" class="Keyword">open</a> <a id="643" class="Keyword">import</a> <a id="650" href="Cubical.Data.SumFin.html" class="Module">Cubical.Data.SumFin</a>
<a id="670" class="Keyword">open</a> <a id="675" class="Keyword">import</a> <a id="682" href="Cubical.Data.Sigma.html" class="Module">Cubical.Data.Sigma</a>

<a id="702" class="Keyword">private</a>
  <a id="712" class="Keyword">variable</a>
    <a id="725" href="Cubical.Data.FinSet.Base.html#725" class="Generalizable">ℓ</a> <a id="727" href="Cubical.Data.FinSet.Base.html#727" class="Generalizable">ℓ&#39;</a> <a id="730" href="Cubical.Data.FinSet.Base.html#730" class="Generalizable">ℓ&#39;&#39;</a> <a id="734" class="Symbol">:</a> <a id="736" href="Agda.Primitive.html#597" class="Postulate">Level</a>
    <a id="746" href="Cubical.Data.FinSet.Base.html#746" class="Generalizable">A</a> <a id="748" class="Symbol">:</a> <a id="750" href="Agda.Primitive.html#326" class="Primitive">Type</a> <a id="755" href="Cubical.Data.FinSet.Base.html#725" class="Generalizable">ℓ</a>

<a id="758" class="Comment">-- definition of (Bishop) finite sets</a>

<a id="797" class="Comment">-- this definition makes cardinality computation more efficient</a>
<a id="isFinSet"></a><a id="861" href="Cubical.Data.FinSet.Base.html#861" class="Function">isFinSet</a> <a id="870" class="Symbol">:</a> <a id="872" href="Agda.Primitive.html#326" class="Primitive">Type</a> <a id="877" href="Cubical.Data.FinSet.Base.html#725" class="Generalizable">ℓ</a> <a id="879" class="Symbol">→</a> <a id="881" href="Agda.Primitive.html#326" class="Primitive">Type</a> <a id="886" href="Cubical.Data.FinSet.Base.html#725" class="Generalizable">ℓ</a>
<a id="888" href="Cubical.Data.FinSet.Base.html#861" class="Function">isFinSet</a> <a id="897" href="Cubical.Data.FinSet.Base.html#897" class="Bound">A</a> <a id="899" class="Symbol">=</a> <a id="901" href="Cubical.Core.Primitives.html#6306" class="Function">Σ[</a> <a id="904" href="Cubical.Data.FinSet.Base.html#904" class="Bound">n</a> <a id="906" href="Cubical.Core.Primitives.html#6306" class="Function">∈</a> <a id="908" href="Agda.Builtin.Nat.html#192" class="Datatype">ℕ</a> <a id="910" href="Cubical.Core.Primitives.html#6306" class="Function">]</a> <a id="912" href="Cubical.HITs.PropositionalTruncation.Base.html#249" class="Datatype Operator">∥</a> <a id="914" href="Cubical.Data.FinSet.Base.html#897" class="Bound">A</a> <a id="916" href="Agda.Builtin.Cubical.Glue.html#1051" class="Function Operator">≃</a> <a id="918" href="Cubical.Data.SumFin.Base.html#404" class="Function">Fin</a> <a id="922" href="Cubical.Data.FinSet.Base.html#904" class="Bound">n</a> <a id="924" href="Cubical.HITs.PropositionalTruncation.Base.html#249" class="Datatype Operator">∥₁</a>

<a id="isFinOrd"></a><a id="928" href="Cubical.Data.FinSet.Base.html#928" class="Function">isFinOrd</a> <a id="937" class="Symbol">:</a> <a id="939" href="Agda.Primitive.html#326" class="Primitive">Type</a> <a id="944" href="Cubical.Data.FinSet.Base.html#725" class="Generalizable">ℓ</a> <a id="946" class="Symbol">→</a> <a id="948" href="Agda.Primitive.html#326" class="Primitive">Type</a> <a id="953" href="Cubical.Data.FinSet.Base.html#725" class="Generalizable">ℓ</a>
<a id="955" href="Cubical.Data.FinSet.Base.html#928" class="Function">isFinOrd</a> <a id="964" href="Cubical.Data.FinSet.Base.html#964" class="Bound">A</a> <a id="966" class="Symbol">=</a> <a id="968" href="Cubical.Core.Primitives.html#6306" class="Function">Σ[</a> <a id="971" href="Cubical.Data.FinSet.Base.html#971" class="Bound">n</a> <a id="973" href="Cubical.Core.Primitives.html#6306" class="Function">∈</a> <a id="975" href="Agda.Builtin.Nat.html#192" class="Datatype">ℕ</a> <a id="977" href="Cubical.Core.Primitives.html#6306" class="Function">]</a> <a id="979" href="Cubical.Data.FinSet.Base.html#964" class="Bound">A</a> <a id="981" href="Agda.Builtin.Cubical.Glue.html#1051" class="Function Operator">≃</a> <a id="983" href="Cubical.Data.SumFin.Base.html#404" class="Function">Fin</a> <a id="987" href="Cubical.Data.FinSet.Base.html#971" class="Bound">n</a>

<a id="isFinOrd→isFinSet"></a><a id="990" href="Cubical.Data.FinSet.Base.html#990" class="Function">isFinOrd→isFinSet</a> <a id="1008" class="Symbol">:</a> <a id="1010" href="Cubical.Data.FinSet.Base.html#928" class="Function">isFinOrd</a> <a id="1019" href="Cubical.Data.FinSet.Base.html#746" class="Generalizable">A</a> <a id="1021" class="Symbol">→</a> <a id="1023" href="Cubical.Data.FinSet.Base.html#861" class="Function">isFinSet</a> <a id="1032" href="Cubical.Data.FinSet.Base.html#746" class="Generalizable">A</a>
<a id="1034" href="Cubical.Data.FinSet.Base.html#990" class="Function">isFinOrd→isFinSet</a> <a id="1052" class="Symbol">(_</a> <a id="1055" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="1057" href="Cubical.Data.FinSet.Base.html#1057" class="Bound">p</a><a id="1058" class="Symbol">)</a> <a id="1060" class="Symbol">=</a> <a id="1062" class="Symbol">_</a> <a id="1064" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="1066" href="Cubical.HITs.PropositionalTruncation.Base.html#288" class="InductiveConstructor Operator">∣</a> <a id="1068" href="Cubical.Data.FinSet.Base.html#1057" class="Bound">p</a> <a id="1070" href="Cubical.HITs.PropositionalTruncation.Base.html#288" class="InductiveConstructor Operator">∣₁</a>

<a id="1074" class="Comment">-- finite sets are sets</a>

<a id="isFinSet→isSet"></a><a id="1099" href="Cubical.Data.FinSet.Base.html#1099" class="Function">isFinSet→isSet</a> <a id="1114" class="Symbol">:</a> <a id="1116" href="Cubical.Data.FinSet.Base.html#861" class="Function">isFinSet</a> <a id="1125" href="Cubical.Data.FinSet.Base.html#746" class="Generalizable">A</a> <a id="1127" class="Symbol">→</a> <a id="1129" href="Cubical.Foundations.Prelude.html#14065" class="Function">isSet</a> <a id="1135" href="Cubical.Data.FinSet.Base.html#746" class="Generalizable">A</a>
<a id="1137" href="Cubical.Data.FinSet.Base.html#1099" class="Function">isFinSet→isSet</a> <a id="1152" href="Cubical.Data.FinSet.Base.html#1152" class="Bound">p</a> <a id="1154" class="Symbol">=</a> <a id="1156" href="Cubical.HITs.PropositionalTruncation.Properties.html#894" class="Function">rec</a> <a id="1160" href="Cubical.Foundations.HLevels.html#4441" class="Function">isPropIsSet</a> <a id="1172" class="Symbol">(λ</a> <a id="1175" href="Cubical.Data.FinSet.Base.html#1175" class="Bound">e</a> <a id="1177" class="Symbol">→</a> <a id="1179" href="Cubical.Foundations.HLevels.html#9072" class="Function">isOfHLevelRespectEquiv</a> <a id="1202" class="Number">2</a> <a id="1204" class="Symbol">(</a><a id="1205" href="Cubical.Foundations.Equiv.html#3368" class="Function">invEquiv</a> <a id="1214" href="Cubical.Data.FinSet.Base.html#1175" class="Bound">e</a><a id="1215" class="Symbol">)</a> <a id="1217" href="Cubical.Data.SumFin.Base.html#1662" class="Function">isSetFin</a><a id="1225" class="Symbol">)</a> <a id="1227" class="Symbol">(</a><a id="1228" href="Cubical.Data.FinSet.Base.html#1152" class="Bound">p</a> <a id="1230" class="Symbol">.</a><a id="1231" href="Agda.Builtin.Sigma.html#264" class="Field">snd</a><a id="1234" class="Symbol">)</a>

<a id="1237" class="Comment">-- isFinSet is proposition</a>

<a id="isPropIsFinSet"></a><a id="1265" href="Cubical.Data.FinSet.Base.html#1265" class="Function">isPropIsFinSet</a> <a id="1280" class="Symbol">:</a> <a id="1282" href="Cubical.Foundations.Prelude.html#14010" class="Function">isProp</a> <a id="1289" class="Symbol">(</a><a id="1290" href="Cubical.Data.FinSet.Base.html#861" class="Function">isFinSet</a> <a id="1299" href="Cubical.Data.FinSet.Base.html#746" class="Generalizable">A</a><a id="1300" class="Symbol">)</a>
<a id="1302" href="Cubical.Data.FinSet.Base.html#1265" class="Function">isPropIsFinSet</a> <a id="1317" href="Cubical.Data.FinSet.Base.html#1317" class="Bound">p</a> <a id="1319" href="Cubical.Data.FinSet.Base.html#1319" class="Bound">q</a> <a id="1321" class="Symbol">=</a> <a id="1323" href="Cubical.Data.Sigma.Properties.html#12886" class="Function">Σ≡PropEquiv</a> <a id="1335" class="Symbol">(λ</a> <a id="1338" href="Cubical.Data.FinSet.Base.html#1338" class="Bound">_</a> <a id="1340" class="Symbol">→</a> <a id="1342" href="Cubical.HITs.PropositionalTruncation.Properties.html#5584" class="Function">isPropPropTrunc</a><a id="1357" class="Symbol">)</a> <a id="1359" class="Symbol">.</a><a id="1360" href="Agda.Builtin.Sigma.html#252" class="Field">fst</a> <a id="1364" class="Symbol">(</a>
  <a id="1368" href="Cubical.HITs.PropositionalTruncation.Properties.html#3599" class="Function">Prop.elim2</a>
    <a id="1383" class="Symbol">(λ</a> <a id="1386" href="Cubical.Data.FinSet.Base.html#1386" class="Bound">_</a> <a id="1388" href="Cubical.Data.FinSet.Base.html#1388" class="Bound">_</a> <a id="1390" class="Symbol">→</a> <a id="1392" href="Cubical.Data.Nat.Properties.html#3705" class="Function">isSetℕ</a> <a id="1399" class="Symbol">_</a> <a id="1401" class="Symbol">_)</a>
    <a id="1408" class="Symbol">(λ</a> <a id="1411" href="Cubical.Data.FinSet.Base.html#1411" class="Bound">p</a> <a id="1413" href="Cubical.Data.FinSet.Base.html#1413" class="Bound">q</a> <a id="1415" class="Symbol">→</a> <a id="1417" href="Cubical.Data.Fin.Properties.html#14154" class="Function">Fin-inj</a> <a id="1425" class="Symbol">_</a> <a id="1427" class="Symbol">_</a> <a id="1429" class="Symbol">(</a><a id="1430" href="Cubical.Foundations.Univalence.html#928" class="Function">ua</a> <a id="1433" class="Symbol">(</a><a id="1434" href="Cubical.Foundations.Equiv.html#3368" class="Function">invEquiv</a> <a id="1443" class="Symbol">(</a><a id="1444" href="Cubical.Data.SumFin.Properties.html#1909" class="Function">SumFin≃Fin</a> <a id="1455" class="Symbol">_)</a> <a id="1458" href="Cubical.Data.FinSet.Base.html#432" class="Function Operator">⋆</a> <a id="1460" class="Symbol">(</a><a id="1461" href="Cubical.Foundations.Equiv.html#3368" class="Function">invEquiv</a> <a id="1470" href="Cubical.Data.FinSet.Base.html#1411" class="Bound">p</a><a id="1471" class="Symbol">)</a> <a id="1473" href="Cubical.Data.FinSet.Base.html#432" class="Function Operator">⋆</a> <a id="1475" href="Cubical.Data.FinSet.Base.html#1413" class="Bound">q</a> <a id="1477" href="Cubical.Data.FinSet.Base.html#432" class="Function Operator">⋆</a> <a id="1479" href="Cubical.Data.SumFin.Properties.html#1909" class="Function">SumFin≃Fin</a> <a id="1490" class="Symbol">_)))</a>
    <a id="1499" class="Symbol">(</a><a id="1500" href="Cubical.Data.FinSet.Base.html#1317" class="Bound">p</a> <a id="1502" class="Symbol">.</a><a id="1503" href="Agda.Builtin.Sigma.html#264" class="Field">snd</a><a id="1506" class="Symbol">)</a> <a id="1508" class="Symbol">(</a><a id="1509" href="Cubical.Data.FinSet.Base.html#1319" class="Bound">q</a> <a id="1511" class="Symbol">.</a><a id="1512" href="Agda.Builtin.Sigma.html#264" class="Field">snd</a><a id="1515" class="Symbol">))</a>

<a id="1519" class="Comment">-- isFinOrd is Set</a>
<a id="1538" class="Comment">-- ordering can be seen as extra structures over finite sets</a>

<a id="isSetIsFinOrd"></a><a id="1600" href="Cubical.Data.FinSet.Base.html#1600" class="Function">isSetIsFinOrd</a> <a id="1614" class="Symbol">:</a> <a id="1616" href="Cubical.Foundations.Prelude.html#14065" class="Function">isSet</a> <a id="1622" class="Symbol">(</a><a id="1623" href="Cubical.Data.FinSet.Base.html#928" class="Function">isFinOrd</a> <a id="1632" href="Cubical.Data.FinSet.Base.html#746" class="Generalizable">A</a><a id="1633" class="Symbol">)</a>
<a id="1635" href="Cubical.Data.FinSet.Base.html#1600" class="Function">isSetIsFinOrd</a> <a id="1649" class="Symbol">=</a> <a id="1651" href="Cubical.Foundations.HLevels.html#12504" class="Function">isOfHLevelΣ</a> <a id="1663" class="Number">2</a> <a id="1665" href="Cubical.Data.Nat.Properties.html#3705" class="Function">isSetℕ</a> <a id="1672" class="Symbol">(λ</a> <a id="1675" href="Cubical.Data.FinSet.Base.html#1675" class="Bound">_</a> <a id="1677" class="Symbol">→</a> <a id="1679" href="Cubical.Foundations.HLevels.html#20886" class="Function">isOfHLevel⁺≃ᵣ</a> <a id="1693" class="Number">1</a> <a id="1695" href="Cubical.Data.SumFin.Base.html#1662" class="Function">isSetFin</a><a id="1703" class="Symbol">)</a>

<a id="1706" class="Comment">-- alternative definition of isFinSet</a>

<a id="isFinSet&#39;"></a><a id="1745" href="Cubical.Data.FinSet.Base.html#1745" class="Function">isFinSet&#39;</a> <a id="1755" class="Symbol">:</a> <a id="1757" href="Agda.Primitive.html#326" class="Primitive">Type</a> <a id="1762" href="Cubical.Data.FinSet.Base.html#725" class="Generalizable">ℓ</a> <a id="1764" class="Symbol">→</a> <a id="1766" href="Agda.Primitive.html#326" class="Primitive">Type</a> <a id="1771" href="Cubical.Data.FinSet.Base.html#725" class="Generalizable">ℓ</a>
<a id="1773" href="Cubical.Data.FinSet.Base.html#1745" class="Function">isFinSet&#39;</a> <a id="1783" href="Cubical.Data.FinSet.Base.html#1783" class="Bound">A</a> <a id="1785" class="Symbol">=</a> <a id="1787" href="Cubical.HITs.PropositionalTruncation.Base.html#249" class="Datatype Operator">∥</a> <a id="1789" href="Cubical.Core.Primitives.html#6306" class="Function">Σ[</a> <a id="1792" href="Cubical.Data.FinSet.Base.html#1792" class="Bound">n</a> <a id="1794" href="Cubical.Core.Primitives.html#6306" class="Function">∈</a> <a id="1796" href="Agda.Builtin.Nat.html#192" class="Datatype">ℕ</a> <a id="1798" href="Cubical.Core.Primitives.html#6306" class="Function">]</a> <a id="1800" href="Cubical.Data.FinSet.Base.html#1783" class="Bound">A</a> <a id="1802" href="Agda.Builtin.Cubical.Glue.html#1051" class="Function Operator">≃</a> <a id="1804" href="Cubical.Data.SumFin.Base.html#404" class="Function">Fin</a> <a id="1808" href="Cubical.Data.FinSet.Base.html#1792" class="Bound">n</a> <a id="1810" href="Cubical.HITs.PropositionalTruncation.Base.html#249" class="Datatype Operator">∥₁</a>

<a id="isFinSet→isFinSet&#39;"></a><a id="1814" href="Cubical.Data.FinSet.Base.html#1814" class="Function">isFinSet→isFinSet&#39;</a> <a id="1833" class="Symbol">:</a> <a id="1835" href="Cubical.Data.FinSet.Base.html#861" class="Function">isFinSet</a> <a id="1844" href="Cubical.Data.FinSet.Base.html#746" class="Generalizable">A</a> <a id="1846" class="Symbol">→</a> <a id="1848" href="Cubical.Data.FinSet.Base.html#1745" class="Function">isFinSet&#39;</a> <a id="1858" href="Cubical.Data.FinSet.Base.html#746" class="Generalizable">A</a>
<a id="1860" href="Cubical.Data.FinSet.Base.html#1814" class="Function">isFinSet→isFinSet&#39;</a> <a id="1879" class="Symbol">(_</a> <a id="1882" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="1884" href="Cubical.Data.FinSet.Base.html#1884" class="Bound">p</a><a id="1885" class="Symbol">)</a> <a id="1887" class="Symbol">=</a> <a id="1889" href="Cubical.HITs.PropositionalTruncation.Properties.html#894" class="Function">Prop.rec</a> <a id="1898" href="Cubical.HITs.PropositionalTruncation.Properties.html#5584" class="Function">isPropPropTrunc</a> <a id="1914" class="Symbol">(λ</a> <a id="1917" href="Cubical.Data.FinSet.Base.html#1917" class="Bound">p</a> <a id="1919" class="Symbol">→</a> <a id="1921" href="Cubical.HITs.PropositionalTruncation.Base.html#288" class="InductiveConstructor Operator">∣</a> <a id="1923" class="Symbol">_</a> <a id="1925" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="1927" href="Cubical.Data.FinSet.Base.html#1917" class="Bound">p</a> <a id="1929" href="Cubical.HITs.PropositionalTruncation.Base.html#288" class="InductiveConstructor Operator">∣₁</a><a id="1931" class="Symbol">)</a> <a id="1933" href="Cubical.Data.FinSet.Base.html#1884" class="Bound">p</a>

<a id="isFinSet&#39;→isFinSet"></a><a id="1936" href="Cubical.Data.FinSet.Base.html#1936" class="Function">isFinSet&#39;→isFinSet</a> <a id="1955" class="Symbol">:</a> <a id="1957" href="Cubical.Data.FinSet.Base.html#1745" class="Function">isFinSet&#39;</a> <a id="1967" href="Cubical.Data.FinSet.Base.html#746" class="Generalizable">A</a> <a id="1969" class="Symbol">→</a> <a id="1971" href="Cubical.Data.FinSet.Base.html#861" class="Function">isFinSet</a> <a id="1980" href="Cubical.Data.FinSet.Base.html#746" class="Generalizable">A</a>
<a id="1982" href="Cubical.Data.FinSet.Base.html#1936" class="Function">isFinSet&#39;→isFinSet</a> <a id="2001" class="Symbol">=</a> <a id="2003" href="Cubical.HITs.PropositionalTruncation.Properties.html#894" class="Function">Prop.rec</a> <a id="2012" href="Cubical.Data.FinSet.Base.html#1265" class="Function">isPropIsFinSet</a> <a id="2027" class="Symbol">(λ</a> <a id="2030" class="Symbol">(</a><a id="2031" href="Cubical.Data.FinSet.Base.html#2031" class="Bound">n</a> <a id="2033" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="2035" href="Cubical.Data.FinSet.Base.html#2035" class="Bound">p</a><a id="2036" class="Symbol">)</a> <a id="2038" class="Symbol">→</a> <a id="2040" class="Symbol">_</a> <a id="2042" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="2044" href="Cubical.HITs.PropositionalTruncation.Base.html#288" class="InductiveConstructor Operator">∣</a> <a id="2046" href="Cubical.Data.FinSet.Base.html#2035" class="Bound">p</a> <a id="2048" href="Cubical.HITs.PropositionalTruncation.Base.html#288" class="InductiveConstructor Operator">∣₁</a> <a id="2051" class="Symbol">)</a>

<a id="isFinSet≡isFinSet&#39;"></a><a id="2054" href="Cubical.Data.FinSet.Base.html#2054" class="Function">isFinSet≡isFinSet&#39;</a> <a id="2073" class="Symbol">:</a> <a id="2075" href="Cubical.Data.FinSet.Base.html#861" class="Function">isFinSet</a> <a id="2084" href="Cubical.Data.FinSet.Base.html#746" class="Generalizable">A</a> <a id="2086" href="Agda.Builtin.Cubical.Path.html#388" class="Function Operator">≡</a> <a id="2088" href="Cubical.Data.FinSet.Base.html#1745" class="Function">isFinSet&#39;</a> <a id="2098" href="Cubical.Data.FinSet.Base.html#746" class="Generalizable">A</a>
<a id="2100" href="Cubical.Data.FinSet.Base.html#2054" class="Function">isFinSet≡isFinSet&#39;</a> <a id="2119" class="Symbol">=</a> <a id="2121" href="Cubical.Foundations.Univalence.html#1243" class="Function">hPropExt</a> <a id="2130" href="Cubical.Data.FinSet.Base.html#1265" class="Function">isPropIsFinSet</a> <a id="2145" href="Cubical.HITs.PropositionalTruncation.Properties.html#5584" class="Function">isPropPropTrunc</a> <a id="2161" href="Cubical.Data.FinSet.Base.html#1814" class="Function">isFinSet→isFinSet&#39;</a> <a id="2180" href="Cubical.Data.FinSet.Base.html#1936" class="Function">isFinSet&#39;→isFinSet</a>

<a id="2200" class="Comment">-- the type of finite sets/propositions</a>

<a id="FinSet"></a><a id="2241" href="Cubical.Data.FinSet.Base.html#2241" class="Function">FinSet</a> <a id="2248" class="Symbol">:</a> <a id="2250" class="Symbol">(</a><a id="2251" href="Cubical.Data.FinSet.Base.html#2251" class="Bound">ℓ</a> <a id="2253" class="Symbol">:</a> <a id="2255" href="Agda.Primitive.html#597" class="Postulate">Level</a><a id="2260" class="Symbol">)</a> <a id="2262" class="Symbol">→</a> <a id="2264" href="Agda.Primitive.html#326" class="Primitive">Type</a> <a id="2269" class="Symbol">(</a><a id="2270" href="Agda.Primitive.html#780" class="Primitive">ℓ-suc</a> <a id="2276" href="Cubical.Data.FinSet.Base.html#2251" class="Bound">ℓ</a><a id="2277" class="Symbol">)</a>
<a id="2279" href="Cubical.Data.FinSet.Base.html#2241" class="Function">FinSet</a> <a id="2286" href="Cubical.Data.FinSet.Base.html#2286" class="Bound">ℓ</a> <a id="2288" class="Symbol">=</a> <a id="2290" href="Cubical.Foundations.Structure.html#398" class="Function">TypeWithStr</a> <a id="2302" class="Symbol">_</a> <a id="2304" href="Cubical.Data.FinSet.Base.html#861" class="Function">isFinSet</a>

<a id="FinProp"></a><a id="2314" href="Cubical.Data.FinSet.Base.html#2314" class="Function">FinProp</a> <a id="2322" class="Symbol">:</a> <a id="2324" class="Symbol">(</a><a id="2325" href="Cubical.Data.FinSet.Base.html#2325" class="Bound">ℓ</a> <a id="2327" class="Symbol">:</a> <a id="2329" href="Agda.Primitive.html#597" class="Postulate">Level</a><a id="2334" class="Symbol">)</a> <a id="2336" class="Symbol">→</a> <a id="2338" href="Agda.Primitive.html#326" class="Primitive">Type</a> <a id="2343" class="Symbol">(</a><a id="2344" href="Agda.Primitive.html#780" class="Primitive">ℓ-suc</a> <a id="2350" href="Cubical.Data.FinSet.Base.html#2325" class="Bound">ℓ</a><a id="2351" class="Symbol">)</a>
<a id="2353" href="Cubical.Data.FinSet.Base.html#2314" class="Function">FinProp</a> <a id="2361" href="Cubical.Data.FinSet.Base.html#2361" class="Bound">ℓ</a> <a id="2363" class="Symbol">=</a> <a id="2365" href="Cubical.Core.Primitives.html#6306" class="Function">Σ[</a> <a id="2368" href="Cubical.Data.FinSet.Base.html#2368" class="Bound">P</a> <a id="2370" href="Cubical.Core.Primitives.html#6306" class="Function">∈</a> <a id="2372" href="Cubical.Data.FinSet.Base.html#2241" class="Function">FinSet</a> <a id="2379" href="Cubical.Data.FinSet.Base.html#2361" class="Bound">ℓ</a> <a id="2381" href="Cubical.Core.Primitives.html#6306" class="Function">]</a> <a id="2383" href="Cubical.Foundations.Prelude.html#14010" class="Function">isProp</a> <a id="2390" class="Symbol">(</a><a id="2391" href="Cubical.Data.FinSet.Base.html#2368" class="Bound">P</a> <a id="2393" class="Symbol">.</a><a id="2394" href="Agda.Builtin.Sigma.html#252" class="Field">fst</a><a id="2397" class="Symbol">)</a>

<a id="2400" class="Comment">-- cardinality of finite sets</a>

<a id="card"></a><a id="2431" href="Cubical.Data.FinSet.Base.html#2431" class="Function">card</a> <a id="2436" class="Symbol">:</a> <a id="2438" href="Cubical.Data.FinSet.Base.html#2241" class="Function">FinSet</a> <a id="2445" href="Cubical.Data.FinSet.Base.html#725" class="Generalizable">ℓ</a> <a id="2447" class="Symbol">→</a> <a id="2449" href="Agda.Builtin.Nat.html#192" class="Datatype">ℕ</a>
<a id="2451" href="Cubical.Data.FinSet.Base.html#2431" class="Function">card</a> <a id="2456" href="Cubical.Data.FinSet.Base.html#2456" class="Bound">X</a> <a id="2458" class="Symbol">=</a> <a id="2460" href="Cubical.Data.FinSet.Base.html#2456" class="Bound">X</a> <a id="2462" class="Symbol">.</a><a id="2463" href="Agda.Builtin.Sigma.html#264" class="Field">snd</a> <a id="2467" class="Symbol">.</a><a id="2468" href="Agda.Builtin.Sigma.html#252" class="Field">fst</a>

<a id="2473" class="Comment">-- equality between finite sets/propositions</a>

<a id="FinSet≡"></a><a id="2519" href="Cubical.Data.FinSet.Base.html#2519" class="Function">FinSet≡</a> <a id="2527" class="Symbol">:</a> <a id="2529" class="Symbol">(</a><a id="2530" href="Cubical.Data.FinSet.Base.html#2530" class="Bound">X</a> <a id="2532" href="Cubical.Data.FinSet.Base.html#2532" class="Bound">Y</a> <a id="2534" class="Symbol">:</a> <a id="2536" href="Cubical.Data.FinSet.Base.html#2241" class="Function">FinSet</a> <a id="2543" href="Cubical.Data.FinSet.Base.html#725" class="Generalizable">ℓ</a><a id="2544" class="Symbol">)</a> <a id="2546" class="Symbol">→</a> <a id="2548" class="Symbol">(</a><a id="2549" href="Cubical.Data.FinSet.Base.html#2530" class="Bound">X</a> <a id="2551" class="Symbol">.</a><a id="2552" href="Agda.Builtin.Sigma.html#252" class="Field">fst</a> <a id="2556" href="Agda.Builtin.Cubical.Path.html#388" class="Function Operator">≡</a> <a id="2558" href="Cubical.Data.FinSet.Base.html#2532" class="Bound">Y</a> <a id="2560" class="Symbol">.</a><a id="2561" href="Agda.Builtin.Sigma.html#252" class="Field">fst</a><a id="2564" class="Symbol">)</a> <a id="2566" href="Agda.Builtin.Cubical.Glue.html#1051" class="Function Operator">≃</a> <a id="2568" class="Symbol">(</a><a id="2569" href="Cubical.Data.FinSet.Base.html#2530" class="Bound">X</a> <a id="2571" href="Agda.Builtin.Cubical.Path.html#388" class="Function Operator">≡</a> <a id="2573" href="Cubical.Data.FinSet.Base.html#2532" class="Bound">Y</a><a id="2574" class="Symbol">)</a>
<a id="2576" href="Cubical.Data.FinSet.Base.html#2519" class="Function">FinSet≡</a> <a id="2584" class="Symbol">_</a> <a id="2586" class="Symbol">_</a> <a id="2588" class="Symbol">=</a> <a id="2590" href="Cubical.Data.Sigma.Properties.html#12886" class="Function">Σ≡PropEquiv</a> <a id="2602" class="Symbol">(λ</a> <a id="2605" href="Cubical.Data.FinSet.Base.html#2605" class="Bound">_</a> <a id="2607" class="Symbol">→</a> <a id="2609" href="Cubical.Data.FinSet.Base.html#1265" class="Function">isPropIsFinSet</a><a id="2623" class="Symbol">)</a>

<a id="FinProp≡"></a><a id="2626" href="Cubical.Data.FinSet.Base.html#2626" class="Function">FinProp≡</a> <a id="2635" class="Symbol">:</a> <a id="2637" class="Symbol">(</a><a id="2638" href="Cubical.Data.FinSet.Base.html#2638" class="Bound">X</a> <a id="2640" href="Cubical.Data.FinSet.Base.html#2640" class="Bound">Y</a> <a id="2642" class="Symbol">:</a> <a id="2644" href="Cubical.Data.FinSet.Base.html#2314" class="Function">FinProp</a> <a id="2652" href="Cubical.Data.FinSet.Base.html#725" class="Generalizable">ℓ</a><a id="2653" class="Symbol">)</a> <a id="2655" class="Symbol">→</a> <a id="2657" class="Symbol">(</a><a id="2658" href="Cubical.Data.FinSet.Base.html#2638" class="Bound">X</a> <a id="2660" class="Symbol">.</a><a id="2661" href="Agda.Builtin.Sigma.html#252" class="Field">fst</a> <a id="2665" class="Symbol">.</a><a id="2666" href="Agda.Builtin.Sigma.html#252" class="Field">fst</a> <a id="2670" href="Agda.Builtin.Cubical.Path.html#388" class="Function Operator">≡</a> <a id="2672" href="Cubical.Data.FinSet.Base.html#2640" class="Bound">Y</a> <a id="2674" class="Symbol">.</a><a id="2675" href="Agda.Builtin.Sigma.html#252" class="Field">fst</a> <a id="2679" class="Symbol">.</a><a id="2680" href="Agda.Builtin.Sigma.html#252" class="Field">fst</a><a id="2683" class="Symbol">)</a> <a id="2685" href="Agda.Builtin.Cubical.Glue.html#1051" class="Function Operator">≃</a> <a id="2687" class="Symbol">(</a><a id="2688" href="Cubical.Data.FinSet.Base.html#2638" class="Bound">X</a> <a id="2690" href="Agda.Builtin.Cubical.Path.html#388" class="Function Operator">≡</a> <a id="2692" href="Cubical.Data.FinSet.Base.html#2640" class="Bound">Y</a><a id="2693" class="Symbol">)</a>
<a id="2695" href="Cubical.Data.FinSet.Base.html#2626" class="Function">FinProp≡</a> <a id="2704" href="Cubical.Data.FinSet.Base.html#2704" class="Bound">X</a> <a id="2706" href="Cubical.Data.FinSet.Base.html#2706" class="Bound">Y</a> <a id="2708" class="Symbol">=</a> <a id="2710" href="Cubical.Foundations.Equiv.html#3542" class="Function">compEquiv</a> <a id="2720" class="Symbol">(</a><a id="2721" href="Cubical.Data.FinSet.Base.html#2519" class="Function">FinSet≡</a> <a id="2729" class="Symbol">(</a><a id="2730" href="Cubical.Data.FinSet.Base.html#2704" class="Bound">X</a> <a id="2732" class="Symbol">.</a><a id="2733" href="Agda.Builtin.Sigma.html#252" class="Field">fst</a><a id="2736" class="Symbol">)</a> <a id="2738" class="Symbol">(</a><a id="2739" href="Cubical.Data.FinSet.Base.html#2706" class="Bound">Y</a> <a id="2741" class="Symbol">.</a><a id="2742" href="Agda.Builtin.Sigma.html#252" class="Field">fst</a><a id="2745" class="Symbol">))</a> <a id="2748" class="Symbol">(</a><a id="2749" href="Cubical.Data.Sigma.Properties.html#12886" class="Function">Σ≡PropEquiv</a> <a id="2761" class="Symbol">(λ</a> <a id="2764" href="Cubical.Data.FinSet.Base.html#2764" class="Bound">_</a> <a id="2766" class="Symbol">→</a> <a id="2768" href="Cubical.Foundations.Prelude.html#18759" class="Function">isPropIsProp</a><a id="2780" class="Symbol">))</a>

<a id="2784" class="Comment">-- hlevels of FinSet and FinProp</a>

<a id="isGroupoidFinSet"></a><a id="2818" href="Cubical.Data.FinSet.Base.html#2818" class="Function">isGroupoidFinSet</a> <a id="2835" class="Symbol">:</a> <a id="2837" href="Cubical.Foundations.Prelude.html#14127" class="Function">isGroupoid</a> <a id="2848" class="Symbol">(</a><a id="2849" href="Cubical.Data.FinSet.Base.html#2241" class="Function">FinSet</a> <a id="2856" href="Cubical.Data.FinSet.Base.html#725" class="Generalizable">ℓ</a><a id="2857" class="Symbol">)</a>
<a id="2859" href="Cubical.Data.FinSet.Base.html#2818" class="Function">isGroupoidFinSet</a> <a id="2876" href="Cubical.Data.FinSet.Base.html#2876" class="Bound">X</a> <a id="2878" href="Cubical.Data.FinSet.Base.html#2878" class="Bound">Y</a> <a id="2880" class="Symbol">=</a>
  <a id="2884" href="Cubical.Foundations.HLevels.html#9072" class="Function">isOfHLevelRespectEquiv</a> <a id="2907" class="Number">2</a> <a id="2909" class="Symbol">(</a><a id="2910" href="Cubical.Data.FinSet.Base.html#2519" class="Function">FinSet≡</a> <a id="2918" href="Cubical.Data.FinSet.Base.html#2876" class="Bound">X</a> <a id="2920" href="Cubical.Data.FinSet.Base.html#2878" class="Bound">Y</a><a id="2921" class="Symbol">)</a>
    <a id="2927" class="Symbol">(</a><a id="2928" href="Cubical.Foundations.HLevels.html#20409" class="Function">isOfHLevel≡</a> <a id="2940" class="Number">2</a> <a id="2942" class="Symbol">(</a><a id="2943" href="Cubical.Data.FinSet.Base.html#1099" class="Function">isFinSet→isSet</a> <a id="2958" class="Symbol">(</a><a id="2959" href="Cubical.Data.FinSet.Base.html#2876" class="Bound">X</a> <a id="2961" class="Symbol">.</a><a id="2962" href="Agda.Builtin.Sigma.html#264" class="Field">snd</a><a id="2965" class="Symbol">))</a> <a id="2968" class="Symbol">(</a><a id="2969" href="Cubical.Data.FinSet.Base.html#1099" class="Function">isFinSet→isSet</a> <a id="2984" class="Symbol">(</a><a id="2985" href="Cubical.Data.FinSet.Base.html#2878" class="Bound">Y</a> <a id="2987" class="Symbol">.</a><a id="2988" href="Agda.Builtin.Sigma.html#264" class="Field">snd</a><a id="2991" class="Symbol">)))</a>

<a id="isSetFinProp"></a><a id="2996" href="Cubical.Data.FinSet.Base.html#2996" class="Function">isSetFinProp</a> <a id="3009" class="Symbol">:</a> <a id="3011" href="Cubical.Foundations.Prelude.html#14065" class="Function">isSet</a> <a id="3017" class="Symbol">(</a><a id="3018" href="Cubical.Data.FinSet.Base.html#2314" class="Function">FinProp</a> <a id="3026" href="Cubical.Data.FinSet.Base.html#725" class="Generalizable">ℓ</a><a id="3027" class="Symbol">)</a>
<a id="3029" href="Cubical.Data.FinSet.Base.html#2996" class="Function">isSetFinProp</a> <a id="3042" href="Cubical.Data.FinSet.Base.html#3042" class="Bound">X</a> <a id="3044" href="Cubical.Data.FinSet.Base.html#3044" class="Bound">Y</a> <a id="3046" class="Symbol">=</a>
  <a id="3050" href="Cubical.Foundations.HLevels.html#9072" class="Function">isOfHLevelRespectEquiv</a> <a id="3073" class="Number">1</a> <a id="3075" class="Symbol">(</a><a id="3076" href="Cubical.Data.FinSet.Base.html#2626" class="Function">FinProp≡</a> <a id="3085" href="Cubical.Data.FinSet.Base.html#3042" class="Bound">X</a> <a id="3087" href="Cubical.Data.FinSet.Base.html#3044" class="Bound">Y</a><a id="3088" class="Symbol">)</a> <a id="3090" class="Symbol">(</a><a id="3091" href="Cubical.Foundations.HLevels.html#20409" class="Function">isOfHLevel≡</a> <a id="3103" class="Number">1</a> <a id="3105" class="Symbol">(</a><a id="3106" href="Cubical.Data.FinSet.Base.html#3042" class="Bound">X</a> <a id="3108" class="Symbol">.</a><a id="3109" href="Agda.Builtin.Sigma.html#264" class="Field">snd</a><a id="3112" class="Symbol">)</a> <a id="3114" class="Symbol">(</a><a id="3115" href="Cubical.Data.FinSet.Base.html#3044" class="Bound">Y</a> <a id="3117" class="Symbol">.</a><a id="3118" href="Agda.Builtin.Sigma.html#264" class="Field">snd</a><a id="3121" class="Symbol">))</a>
</pre></body></html>