<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Cubical.HITs.James.Base</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">{-

The James Construction,
  also known as James Reduced Product

A very fundamental and useful construction in classical homotopy theory.
It can be seen as the free A‚àû-monoid constructed out of a given type,
namely the &quot;correct higher version&quot; of free monoid that is meaningful for all types,
instead of only h-Sets.

Referrence:
  Guillaume Brunerie,
  &quot;The James construction and œÄ‚ÇÑ(ùïä¬≥) in homotopy type theory&quot;
  (https://arxiv.org/abs/1710.10307)

-}</a>
<a id="458" class="Symbol">{-#</a> <a id="462" class="Keyword">OPTIONS</a> <a id="470" class="Pragma">--safe</a> <a id="477" class="Symbol">#-}</a>
<a id="481" class="Keyword">module</a> <a id="488" href="Cubical.HITs.James.Base.html" class="Module">Cubical.HITs.James.Base</a> <a id="512" class="Keyword">where</a>

<a id="519" class="Keyword">open</a> <a id="524" class="Keyword">import</a> <a id="531" href="Cubical.Foundations.Prelude.html" class="Module">Cubical.Foundations.Prelude</a>
<a id="559" class="Keyword">open</a> <a id="564" class="Keyword">import</a> <a id="571" href="Cubical.Foundations.Pointed.html" class="Module">Cubical.Foundations.Pointed</a>

<a id="600" class="Keyword">private</a>
  <a id="610" class="Keyword">variable</a>
    <a id="623" href="Cubical.HITs.James.Base.html#623" class="Generalizable">‚Ñì</a> <a id="625" href="Cubical.HITs.James.Base.html#625" class="Generalizable">‚Ñì&#39;</a> <a id="628" class="Symbol">:</a> <a id="630" href="Agda.Primitive.html#591" class="Postulate">Level</a>

<a id="637" class="Keyword">module</a> <a id="644" href="Cubical.HITs.James.Base.html#644" class="Module">_</a>
  <a id="648" class="Symbol">(</a><a id="649" href="Cubical.HITs.James.Base.html#649" class="Bound">(</a><a id="650" href="Cubical.HITs.James.Base.html#650" class="Bound">X</a> <a id="652" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="654" href="Cubical.HITs.James.Base.html#654" class="Bound">x‚ÇÄ</a><a id="656" href="Cubical.HITs.James.Base.html#649" class="Bound">)</a> <a id="658" class="Symbol">:</a> <a id="660" href="Cubical.Foundations.Pointed.Base.html#464" class="Function">Pointed</a> <a id="668" href="Cubical.HITs.James.Base.html#623" class="Generalizable">‚Ñì</a><a id="669" class="Symbol">)</a> <a id="671" class="Keyword">where</a>

  <a id="680" class="Keyword">infixr</a> <a id="687" class="Number">5</a> <a id="689" href="Cubical.HITs.James.Base.html#772" class="InductiveConstructor Operator">_‚à∑_</a>

  <a id="696" class="Comment">-- The James Construction</a>

  <a id="725" class="Keyword">data</a> <a id="730" href="Cubical.HITs.James.Base.html#730" class="Datatype">James</a> <a id="736" class="Symbol">:</a> <a id="738" href="Agda.Primitive.html#320" class="Primitive">Type</a> <a id="743" href="Cubical.HITs.James.Base.html#668" class="Bound">‚Ñì</a> <a id="745" class="Keyword">where</a>
    <a id="755" href="Cubical.HITs.James.Base.html#755" class="InductiveConstructor">[]</a>   <a id="760" class="Symbol">:</a> <a id="762" href="Cubical.HITs.James.Base.html#730" class="Datatype">James</a>
    <a id="772" href="Cubical.HITs.James.Base.html#772" class="InductiveConstructor Operator">_‚à∑_</a>  <a id="777" class="Symbol">:</a> <a id="779" href="Cubical.HITs.James.Base.html#650" class="Bound">X</a> <a id="781" class="Symbol">‚Üí</a> <a id="783" href="Cubical.HITs.James.Base.html#730" class="Datatype">James</a> <a id="789" class="Symbol">‚Üí</a> <a id="791" href="Cubical.HITs.James.Base.html#730" class="Datatype">James</a>
    <a id="801" href="Cubical.HITs.James.Base.html#801" class="InductiveConstructor">unit</a> <a id="806" class="Symbol">:</a> <a id="808" class="Symbol">(</a><a id="809" href="Cubical.HITs.James.Base.html#809" class="Bound">xs</a> <a id="812" class="Symbol">:</a> <a id="814" href="Cubical.HITs.James.Base.html#730" class="Datatype">James</a><a id="819" class="Symbol">)</a> <a id="821" class="Symbol">‚Üí</a> <a id="823" href="Cubical.HITs.James.Base.html#809" class="Bound">xs</a> <a id="826" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">‚â°</a> <a id="828" href="Cubical.HITs.James.Base.html#654" class="Bound">x‚ÇÄ</a> <a id="831" href="Cubical.HITs.James.Base.html#772" class="InductiveConstructor Operator">‚à∑</a> <a id="833" href="Cubical.HITs.James.Base.html#809" class="Bound">xs</a>

  <a id="839" href="Cubical.HITs.James.Base.html#839" class="Function">James‚àô</a> <a id="846" class="Symbol">:</a> <a id="848" href="Cubical.Foundations.Pointed.Base.html#464" class="Function">Pointed</a> <a id="856" href="Cubical.HITs.James.Base.html#668" class="Bound">‚Ñì</a>
  <a id="860" href="Cubical.HITs.James.Base.html#839" class="Function">James‚àô</a> <a id="867" class="Symbol">=</a> <a id="869" href="Cubical.HITs.James.Base.html#730" class="Datatype">James</a> <a id="875" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="877" href="Cubical.HITs.James.Base.html#755" class="InductiveConstructor">[]</a>


<a id="882" class="Comment">-- Basic operations on James construction, imitating those in Cubical.Data.List.Base</a>

<a id="968" class="Keyword">module</a> <a id="975" href="Cubical.HITs.James.Base.html#975" class="Module">_</a>
  <a id="979" class="Symbol">{</a><a id="980" href="Cubical.HITs.James.Base.html#980" class="Bound">X‚àô</a><a id="982" class="Symbol">@(</a><a id="984" href="Cubical.HITs.James.Base.html#984" class="Bound">X</a> <a id="986" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="988" href="Cubical.HITs.James.Base.html#988" class="Bound">x‚ÇÄ</a><a id="990" class="Symbol">)</a> <a id="992" class="Symbol">:</a> <a id="994" href="Cubical.Foundations.Pointed.Base.html#464" class="Function">Pointed</a> <a id="1002" href="Cubical.HITs.James.Base.html#623" class="Generalizable">‚Ñì</a><a id="1003" class="Symbol">}</a> <a id="1005" class="Keyword">where</a>

  <a id="1014" class="Keyword">infixr</a> <a id="1021" class="Number">5</a> <a id="1023" href="Cubical.HITs.James.Base.html#1086" class="Function Operator">_++_</a>
  <a id="1030" class="Keyword">infixl</a> <a id="1037" class="Number">5</a> <a id="1039" href="Cubical.HITs.James.Base.html#1462" class="Function Operator">_‚à∑ ≥_</a>

  <a id="1047" href="Cubical.HITs.James.Base.html#1047" class="Function Operator">[_]</a> <a id="1051" class="Symbol">:</a> <a id="1053" href="Cubical.HITs.James.Base.html#984" class="Bound">X</a> <a id="1055" class="Symbol">‚Üí</a> <a id="1057" href="Cubical.HITs.James.Base.html#730" class="Datatype">James</a> <a id="1063" href="Cubical.HITs.James.Base.html#980" class="Bound">X‚àô</a>
  <a id="1068" href="Cubical.HITs.James.Base.html#1047" class="Function Operator">[</a> <a id="1070" href="Cubical.HITs.James.Base.html#1070" class="Bound">x</a> <a id="1072" href="Cubical.HITs.James.Base.html#1047" class="Function Operator">]</a> <a id="1074" class="Symbol">=</a> <a id="1076" href="Cubical.HITs.James.Base.html#1070" class="Bound">x</a> <a id="1078" href="Cubical.HITs.James.Base.html#772" class="InductiveConstructor Operator">‚à∑</a> <a id="1080" href="Cubical.HITs.James.Base.html#755" class="InductiveConstructor">[]</a>

  <a id="1086" href="Cubical.HITs.James.Base.html#1086" class="Function Operator">_++_</a> <a id="1091" class="Symbol">:</a> <a id="1093" href="Cubical.HITs.James.Base.html#730" class="Datatype">James</a> <a id="1099" href="Cubical.HITs.James.Base.html#980" class="Bound">X‚àô</a> <a id="1102" class="Symbol">‚Üí</a> <a id="1104" href="Cubical.HITs.James.Base.html#730" class="Datatype">James</a> <a id="1110" href="Cubical.HITs.James.Base.html#980" class="Bound">X‚àô</a> <a id="1113" class="Symbol">‚Üí</a> <a id="1115" href="Cubical.HITs.James.Base.html#730" class="Datatype">James</a> <a id="1121" href="Cubical.HITs.James.Base.html#980" class="Bound">X‚àô</a>
  <a id="1126" href="Cubical.HITs.James.Base.html#755" class="InductiveConstructor">[]</a> <a id="1129" href="Cubical.HITs.James.Base.html#1086" class="Function Operator">++</a> <a id="1132" href="Cubical.HITs.James.Base.html#1132" class="Bound">ys</a> <a id="1135" class="Symbol">=</a> <a id="1137" href="Cubical.HITs.James.Base.html#1132" class="Bound">ys</a>
  <a id="1142" class="Symbol">(</a><a id="1143" href="Cubical.HITs.James.Base.html#1143" class="Bound">x</a> <a id="1145" href="Cubical.HITs.James.Base.html#772" class="InductiveConstructor Operator">‚à∑</a> <a id="1147" href="Cubical.HITs.James.Base.html#1147" class="Bound">xs</a><a id="1149" class="Symbol">)</a> <a id="1151" href="Cubical.HITs.James.Base.html#1086" class="Function Operator">++</a> <a id="1154" href="Cubical.HITs.James.Base.html#1154" class="Bound">ys</a> <a id="1157" class="Symbol">=</a> <a id="1159" href="Cubical.HITs.James.Base.html#1143" class="Bound">x</a> <a id="1161" href="Cubical.HITs.James.Base.html#772" class="InductiveConstructor Operator">‚à∑</a> <a id="1163" href="Cubical.HITs.James.Base.html#1147" class="Bound">xs</a> <a id="1166" href="Cubical.HITs.James.Base.html#1086" class="Function Operator">++</a> <a id="1169" href="Cubical.HITs.James.Base.html#1154" class="Bound">ys</a>
  <a id="1174" class="Symbol">(</a><a id="1175" href="Cubical.HITs.James.Base.html#801" class="InductiveConstructor">unit</a> <a id="1180" href="Cubical.HITs.James.Base.html#1180" class="Bound">xs</a> <a id="1183" href="Cubical.HITs.James.Base.html#1183" class="Bound">i</a><a id="1184" class="Symbol">)</a> <a id="1186" href="Cubical.HITs.James.Base.html#1086" class="Function Operator">++</a> <a id="1189" href="Cubical.HITs.James.Base.html#1189" class="Bound">ys</a> <a id="1192" class="Symbol">=</a> <a id="1194" href="Cubical.HITs.James.Base.html#801" class="InductiveConstructor">unit</a> <a id="1199" class="Symbol">(</a><a id="1200" href="Cubical.HITs.James.Base.html#1180" class="Bound">xs</a> <a id="1203" href="Cubical.HITs.James.Base.html#1086" class="Function Operator">++</a> <a id="1206" href="Cubical.HITs.James.Base.html#1189" class="Bound">ys</a><a id="1208" class="Symbol">)</a> <a id="1210" href="Cubical.HITs.James.Base.html#1183" class="Bound">i</a>

  <a id="1215" href="Cubical.HITs.James.Base.html#1215" class="Function">++‚ÇÄ</a> <a id="1219" class="Symbol">:</a> <a id="1221" class="Symbol">(</a><a id="1222" href="Cubical.HITs.James.Base.html#1222" class="Bound">xs</a> <a id="1225" class="Symbol">:</a> <a id="1227" href="Cubical.HITs.James.Base.html#730" class="Datatype">James</a> <a id="1233" href="Cubical.HITs.James.Base.html#980" class="Bound">X‚àô</a><a id="1235" class="Symbol">)</a> <a id="1237" class="Symbol">‚Üí</a> <a id="1239" href="Cubical.HITs.James.Base.html#1222" class="Bound">xs</a> <a id="1242" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">‚â°</a> <a id="1244" href="Cubical.HITs.James.Base.html#1222" class="Bound">xs</a> <a id="1247" href="Cubical.HITs.James.Base.html#1086" class="Function Operator">++</a> <a id="1250" href="Cubical.HITs.James.Base.html#1047" class="Function Operator">[</a> <a id="1252" href="Cubical.HITs.James.Base.html#988" class="Bound">x‚ÇÄ</a> <a id="1255" href="Cubical.HITs.James.Base.html#1047" class="Function Operator">]</a>
  <a id="1259" href="Cubical.HITs.James.Base.html#1215" class="Function">++‚ÇÄ</a> <a id="1263" href="Cubical.HITs.James.Base.html#755" class="InductiveConstructor">[]</a> <a id="1266" class="Symbol">=</a> <a id="1268" href="Cubical.HITs.James.Base.html#801" class="InductiveConstructor">unit</a> <a id="1273" href="Cubical.HITs.James.Base.html#755" class="InductiveConstructor">[]</a>
  <a id="1278" href="Cubical.HITs.James.Base.html#1215" class="Function">++‚ÇÄ</a> <a id="1282" class="Symbol">(</a><a id="1283" href="Cubical.HITs.James.Base.html#1283" class="Bound">x</a> <a id="1285" href="Cubical.HITs.James.Base.html#772" class="InductiveConstructor Operator">‚à∑</a> <a id="1287" href="Cubical.HITs.James.Base.html#1287" class="Bound">xs</a><a id="1289" class="Symbol">)</a> <a id="1291" href="Cubical.HITs.James.Base.html#1291" class="Bound">i</a> <a id="1293" class="Symbol">=</a> <a id="1295" href="Cubical.HITs.James.Base.html#1283" class="Bound">x</a> <a id="1297" href="Cubical.HITs.James.Base.html#772" class="InductiveConstructor Operator">‚à∑</a> <a id="1299" href="Cubical.HITs.James.Base.html#1215" class="Function">++‚ÇÄ</a> <a id="1303" href="Cubical.HITs.James.Base.html#1287" class="Bound">xs</a> <a id="1306" href="Cubical.HITs.James.Base.html#1291" class="Bound">i</a>
  <a id="1310" href="Cubical.HITs.James.Base.html#1215" class="Function">++‚ÇÄ</a> <a id="1314" class="Symbol">(</a><a id="1315" href="Cubical.HITs.James.Base.html#801" class="InductiveConstructor">unit</a> <a id="1320" href="Cubical.HITs.James.Base.html#1320" class="Bound">xs</a> <a id="1323" href="Cubical.HITs.James.Base.html#1323" class="Bound">i</a><a id="1324" class="Symbol">)</a> <a id="1326" href="Cubical.HITs.James.Base.html#1326" class="Bound">j</a> <a id="1328" class="Symbol">=</a> <a id="1330" href="Cubical.HITs.James.Base.html#801" class="InductiveConstructor">unit</a> <a id="1335" class="Symbol">(</a><a id="1336" href="Cubical.HITs.James.Base.html#1215" class="Function">++‚ÇÄ</a> <a id="1340" href="Cubical.HITs.James.Base.html#1320" class="Bound">xs</a> <a id="1343" href="Cubical.HITs.James.Base.html#1326" class="Bound">j</a><a id="1344" class="Symbol">)</a> <a id="1346" href="Cubical.HITs.James.Base.html#1323" class="Bound">i</a>

  <a id="1351" href="Cubical.HITs.James.Base.html#1351" class="Function">rev</a> <a id="1355" class="Symbol">:</a> <a id="1357" href="Cubical.HITs.James.Base.html#730" class="Datatype">James</a> <a id="1363" href="Cubical.HITs.James.Base.html#980" class="Bound">X‚àô</a> <a id="1366" class="Symbol">‚Üí</a> <a id="1368" href="Cubical.HITs.James.Base.html#730" class="Datatype">James</a> <a id="1374" href="Cubical.HITs.James.Base.html#980" class="Bound">X‚àô</a>
  <a id="1379" href="Cubical.HITs.James.Base.html#1351" class="Function">rev</a> <a id="1383" href="Cubical.HITs.James.Base.html#755" class="InductiveConstructor">[]</a> <a id="1386" class="Symbol">=</a> <a id="1388" href="Cubical.HITs.James.Base.html#755" class="InductiveConstructor">[]</a>
  <a id="1393" href="Cubical.HITs.James.Base.html#1351" class="Function">rev</a> <a id="1397" class="Symbol">(</a><a id="1398" href="Cubical.HITs.James.Base.html#1398" class="Bound">x</a> <a id="1400" href="Cubical.HITs.James.Base.html#772" class="InductiveConstructor Operator">‚à∑</a> <a id="1402" href="Cubical.HITs.James.Base.html#1402" class="Bound">xs</a><a id="1404" class="Symbol">)</a> <a id="1406" class="Symbol">=</a> <a id="1408" href="Cubical.HITs.James.Base.html#1351" class="Function">rev</a> <a id="1412" href="Cubical.HITs.James.Base.html#1402" class="Bound">xs</a> <a id="1415" href="Cubical.HITs.James.Base.html#1086" class="Function Operator">++</a> <a id="1418" href="Cubical.HITs.James.Base.html#1047" class="Function Operator">[</a> <a id="1420" href="Cubical.HITs.James.Base.html#1398" class="Bound">x</a> <a id="1422" href="Cubical.HITs.James.Base.html#1047" class="Function Operator">]</a>
  <a id="1426" href="Cubical.HITs.James.Base.html#1351" class="Function">rev</a> <a id="1430" class="Symbol">(</a><a id="1431" href="Cubical.HITs.James.Base.html#801" class="InductiveConstructor">unit</a> <a id="1436" href="Cubical.HITs.James.Base.html#1436" class="Bound">xs</a> <a id="1439" href="Cubical.HITs.James.Base.html#1439" class="Bound">i</a><a id="1440" class="Symbol">)</a> <a id="1442" class="Symbol">=</a> <a id="1444" href="Cubical.HITs.James.Base.html#1215" class="Function">++‚ÇÄ</a> <a id="1448" class="Symbol">(</a><a id="1449" href="Cubical.HITs.James.Base.html#1351" class="Function">rev</a> <a id="1453" href="Cubical.HITs.James.Base.html#1436" class="Bound">xs</a><a id="1455" class="Symbol">)</a> <a id="1457" href="Cubical.HITs.James.Base.html#1439" class="Bound">i</a>

  <a id="1462" href="Cubical.HITs.James.Base.html#1462" class="Function Operator">_‚à∑ ≥_</a> <a id="1467" class="Symbol">:</a> <a id="1469" href="Cubical.HITs.James.Base.html#730" class="Datatype">James</a> <a id="1475" href="Cubical.HITs.James.Base.html#980" class="Bound">X‚àô</a> <a id="1478" class="Symbol">‚Üí</a> <a id="1480" href="Cubical.HITs.James.Base.html#984" class="Bound">X</a> <a id="1482" class="Symbol">‚Üí</a> <a id="1484" href="Cubical.HITs.James.Base.html#730" class="Datatype">James</a> <a id="1490" href="Cubical.HITs.James.Base.html#980" class="Bound">X‚àô</a>
  <a id="1495" href="Cubical.HITs.James.Base.html#1495" class="Bound">xs</a> <a id="1498" href="Cubical.HITs.James.Base.html#1462" class="Function Operator">‚à∑ ≥</a> <a id="1501" href="Cubical.HITs.James.Base.html#1501" class="Bound">x</a> <a id="1503" class="Symbol">=</a> <a id="1505" href="Cubical.HITs.James.Base.html#1495" class="Bound">xs</a> <a id="1508" href="Cubical.HITs.James.Base.html#1086" class="Function Operator">++</a> <a id="1511" href="Cubical.HITs.James.Base.html#1501" class="Bound">x</a> <a id="1513" href="Cubical.HITs.James.Base.html#772" class="InductiveConstructor Operator">‚à∑</a> <a id="1515" href="Cubical.HITs.James.Base.html#755" class="InductiveConstructor">[]</a>

<a id="map"></a><a id="1519" href="Cubical.HITs.James.Base.html#1519" class="Function">map</a> <a id="1523" class="Symbol">:</a> <a id="1525" class="Symbol">{</a><a id="1526" href="Cubical.HITs.James.Base.html#1526" class="Bound">X</a> <a id="1528" class="Symbol">:</a> <a id="1530" href="Cubical.Foundations.Pointed.Base.html#464" class="Function">Pointed</a> <a id="1538" href="Cubical.HITs.James.Base.html#623" class="Generalizable">‚Ñì</a><a id="1539" class="Symbol">}{</a><a id="1541" href="Cubical.HITs.James.Base.html#1541" class="Bound">Y</a> <a id="1543" class="Symbol">:</a> <a id="1545" href="Cubical.Foundations.Pointed.Base.html#464" class="Function">Pointed</a> <a id="1553" href="Cubical.HITs.James.Base.html#625" class="Generalizable">‚Ñì&#39;</a><a id="1555" class="Symbol">}</a> <a id="1557" class="Symbol">‚Üí</a> <a id="1559" class="Symbol">(</a><a id="1560" href="Cubical.HITs.James.Base.html#1526" class="Bound">X</a> <a id="1562" href="Cubical.Foundations.Pointed.Base.html#638" class="Function Operator">‚Üí‚àô</a> <a id="1565" href="Cubical.HITs.James.Base.html#1541" class="Bound">Y</a><a id="1566" class="Symbol">)</a> <a id="1568" class="Symbol">‚Üí</a> <a id="1570" href="Cubical.HITs.James.Base.html#730" class="Datatype">James</a> <a id="1576" href="Cubical.HITs.James.Base.html#1526" class="Bound">X</a> <a id="1578" class="Symbol">‚Üí</a> <a id="1580" href="Cubical.HITs.James.Base.html#730" class="Datatype">James</a> <a id="1586" href="Cubical.HITs.James.Base.html#1541" class="Bound">Y</a>
<a id="1588" href="Cubical.HITs.James.Base.html#1519" class="Function">map</a> <a id="1592" href="Cubical.HITs.James.Base.html#1592" class="Bound">f</a> <a id="1594" href="Cubical.HITs.James.Base.html#755" class="InductiveConstructor">[]</a> <a id="1597" class="Symbol">=</a> <a id="1599" href="Cubical.HITs.James.Base.html#755" class="InductiveConstructor">[]</a>
<a id="1602" href="Cubical.HITs.James.Base.html#1519" class="Function">map</a> <a id="1606" href="Cubical.HITs.James.Base.html#1606" class="Bound">f</a> <a id="1608" class="Symbol">(</a><a id="1609" href="Cubical.HITs.James.Base.html#1609" class="Bound">x</a> <a id="1611" href="Cubical.HITs.James.Base.html#772" class="InductiveConstructor Operator">‚à∑</a> <a id="1613" href="Cubical.HITs.James.Base.html#1613" class="Bound">xs</a><a id="1615" class="Symbol">)</a> <a id="1617" class="Symbol">=</a> <a id="1619" href="Cubical.HITs.James.Base.html#1606" class="Bound">f</a> <a id="1621" class="Symbol">.</a><a id="1622" href="Agda.Builtin.Sigma.html#234" class="Field">fst</a> <a id="1626" href="Cubical.HITs.James.Base.html#1609" class="Bound">x</a> <a id="1628" href="Cubical.HITs.James.Base.html#772" class="InductiveConstructor Operator">‚à∑</a> <a id="1630" href="Cubical.HITs.James.Base.html#1519" class="Function">map</a> <a id="1634" href="Cubical.HITs.James.Base.html#1606" class="Bound">f</a> <a id="1636" href="Cubical.HITs.James.Base.html#1613" class="Bound">xs</a>
<a id="1639" href="Cubical.HITs.James.Base.html#1519" class="Function">map</a> <a id="1643" href="Cubical.HITs.James.Base.html#1643" class="Bound">f</a> <a id="1645" class="Symbol">(</a><a id="1646" href="Cubical.HITs.James.Base.html#801" class="InductiveConstructor">unit</a> <a id="1651" href="Cubical.HITs.James.Base.html#1651" class="Bound">xs</a> <a id="1654" href="Cubical.HITs.James.Base.html#1654" class="Bound">i</a><a id="1655" class="Symbol">)</a> <a id="1657" class="Symbol">=</a> <a id="1659" class="Symbol">(</a><a id="1660" href="Cubical.HITs.James.Base.html#801" class="InductiveConstructor">unit</a> <a id="1665" class="Symbol">(</a><a id="1666" href="Cubical.HITs.James.Base.html#1519" class="Function">map</a> <a id="1670" href="Cubical.HITs.James.Base.html#1643" class="Bound">f</a> <a id="1672" href="Cubical.HITs.James.Base.html#1651" class="Bound">xs</a><a id="1674" class="Symbol">)</a> <a id="1676" href="Cubical.Foundations.Prelude.html#4447" class="Function Operator">‚àô</a> <a id="1678" class="Symbol">(Œª</a> <a id="1681" href="Cubical.HITs.James.Base.html#1681" class="Bound">i</a> <a id="1683" class="Symbol">‚Üí</a> <a id="1685" href="Cubical.HITs.James.Base.html#1643" class="Bound">f</a> <a id="1687" class="Symbol">.</a><a id="1688" href="Agda.Builtin.Sigma.html#246" class="Field">snd</a> <a id="1692" class="Symbol">(</a><a id="1693" href="Cubical.Core.Primitives.html#501" class="Primitive Operator">~</a> <a id="1695" href="Cubical.HITs.James.Base.html#1681" class="Bound">i</a><a id="1696" class="Symbol">)</a> <a id="1698" href="Cubical.HITs.James.Base.html#772" class="InductiveConstructor Operator">‚à∑</a> <a id="1700" href="Cubical.HITs.James.Base.html#1519" class="Function">map</a> <a id="1704" href="Cubical.HITs.James.Base.html#1643" class="Bound">f</a> <a id="1706" href="Cubical.HITs.James.Base.html#1651" class="Bound">xs</a><a id="1708" class="Symbol">))</a> <a id="1711" href="Cubical.HITs.James.Base.html#1654" class="Bound">i</a>

<a id="map‚àô"></a><a id="1714" href="Cubical.HITs.James.Base.html#1714" class="Function">map‚àô</a> <a id="1719" class="Symbol">:</a> <a id="1721" class="Symbol">{</a><a id="1722" href="Cubical.HITs.James.Base.html#1722" class="Bound">X</a> <a id="1724" class="Symbol">:</a> <a id="1726" href="Cubical.Foundations.Pointed.Base.html#464" class="Function">Pointed</a> <a id="1734" href="Cubical.HITs.James.Base.html#623" class="Generalizable">‚Ñì</a><a id="1735" class="Symbol">}{</a><a id="1737" href="Cubical.HITs.James.Base.html#1737" class="Bound">Y</a> <a id="1739" class="Symbol">:</a> <a id="1741" href="Cubical.Foundations.Pointed.Base.html#464" class="Function">Pointed</a> <a id="1749" href="Cubical.HITs.James.Base.html#625" class="Generalizable">‚Ñì&#39;</a><a id="1751" class="Symbol">}</a> <a id="1753" class="Symbol">‚Üí</a> <a id="1755" class="Symbol">(</a><a id="1756" href="Cubical.HITs.James.Base.html#1722" class="Bound">X</a> <a id="1758" href="Cubical.Foundations.Pointed.Base.html#638" class="Function Operator">‚Üí‚àô</a> <a id="1761" href="Cubical.HITs.James.Base.html#1737" class="Bound">Y</a><a id="1762" class="Symbol">)</a> <a id="1764" class="Symbol">‚Üí</a> <a id="1766" href="Cubical.HITs.James.Base.html#839" class="Function">James‚àô</a> <a id="1773" href="Cubical.HITs.James.Base.html#1722" class="Bound">X</a> <a id="1775" href="Cubical.Foundations.Pointed.Base.html#638" class="Function Operator">‚Üí‚àô</a> <a id="1778" href="Cubical.HITs.James.Base.html#839" class="Function">James‚àô</a> <a id="1785" href="Cubical.HITs.James.Base.html#1737" class="Bound">Y</a>
<a id="1787" href="Cubical.HITs.James.Base.html#1714" class="Function">map‚àô</a> <a id="1792" href="Cubical.HITs.James.Base.html#1792" class="Bound">f</a> <a id="1794" class="Symbol">.</a><a id="1795" href="Agda.Builtin.Sigma.html#234" class="Field">fst</a> <a id="1799" class="Symbol">=</a> <a id="1801" href="Cubical.HITs.James.Base.html#1519" class="Function">map</a> <a id="1805" href="Cubical.HITs.James.Base.html#1792" class="Bound">f</a>
<a id="1807" href="Cubical.HITs.James.Base.html#1714" class="Function">map‚àô</a> <a id="1812" href="Cubical.HITs.James.Base.html#1812" class="Bound">f</a> <a id="1814" class="Symbol">.</a><a id="1815" href="Agda.Builtin.Sigma.html#246" class="Field">snd</a> <a id="1819" class="Symbol">=</a> <a id="1821" href="Cubical.Foundations.Prelude.html#915" class="Function">refl</a>
</pre></body></html>