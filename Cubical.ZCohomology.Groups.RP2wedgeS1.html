<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Cubical.ZCohomology.Groups.RP2wedgeS1</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Symbol">{-#</a> <a id="5" class="Keyword">OPTIONS</a> <a id="13" class="Pragma">--safe</a> <a id="20" class="Pragma">--lossy-unification</a> <a id="40" class="Symbol">#-}</a>
<a id="44" class="Keyword">module</a> <a id="51" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html" class="Module">Cubical.ZCohomology.Groups.RP2wedgeS1</a> <a id="89" class="Keyword">where</a>

<a id="96" class="Keyword">open</a> <a id="101" class="Keyword">import</a> <a id="108" href="Cubical.Foundations.Prelude.html" class="Module">Cubical.Foundations.Prelude</a>
<a id="136" class="Keyword">open</a> <a id="141" class="Keyword">import</a> <a id="148" href="Cubical.Foundations.Isomorphism.html" class="Module">Cubical.Foundations.Isomorphism</a>
<a id="180" class="Keyword">open</a> <a id="185" class="Keyword">import</a> <a id="192" href="Cubical.Foundations.Function.html" class="Module">Cubical.Foundations.Function</a>
<a id="221" class="Keyword">open</a> <a id="226" class="Keyword">import</a> <a id="233" href="Cubical.Foundations.Pointed.html" class="Module">Cubical.Foundations.Pointed</a>
<a id="261" class="Keyword">open</a> <a id="266" class="Keyword">import</a> <a id="273" href="Cubical.Foundations.HLevels.html" class="Module">Cubical.Foundations.HLevels</a>

<a id="302" class="Keyword">open</a> <a id="307" class="Keyword">import</a> <a id="314" href="Cubical.Data.Empty.html" class="Module">Cubical.Data.Empty</a> <a id="333" class="Symbol">as</a> <a id="336" class="Module">⊥</a>
<a id="338" class="Keyword">open</a> <a id="343" class="Keyword">import</a> <a id="350" href="Cubical.Data.Bool.html" class="Module">Cubical.Data.Bool</a>
<a id="368" class="Keyword">open</a> <a id="373" class="Keyword">import</a> <a id="380" href="Cubical.Data.Nat.html" class="Module">Cubical.Data.Nat</a>
<a id="397" class="Keyword">open</a> <a id="402" class="Keyword">import</a> <a id="409" href="Cubical.Data.Int.html" class="Module">Cubical.Data.Int</a> <a id="426" class="Keyword">renaming</a> <a id="435" class="Symbol">(</a><a id="436" href="Cubical.Data.Int.Base.html#1181" class="Function Operator">_+_</a> <a id="440" class="Symbol">to</a> <a id="443" class="Function Operator">_ℤ+_</a><a id="447" class="Symbol">)</a>
<a id="449" class="Keyword">open</a> <a id="454" class="Keyword">import</a> <a id="461" href="Cubical.Data.Sigma.html" class="Module">Cubical.Data.Sigma</a>

<a id="481" class="Keyword">open</a> <a id="486" class="Keyword">import</a> <a id="493" href="Cubical.Algebra.Group.html" class="Module">Cubical.Algebra.Group</a>
<a id="515" class="Keyword">open</a> <a id="520" class="Keyword">import</a> <a id="527" href="Cubical.Algebra.Group.DirProd.html" class="Module">Cubical.Algebra.Group.DirProd</a>
<a id="557" class="Keyword">open</a> <a id="562" class="Keyword">import</a> <a id="569" href="Cubical.Algebra.Group.Morphisms.html" class="Module">Cubical.Algebra.Group.Morphisms</a>
<a id="601" class="Keyword">open</a> <a id="606" class="Keyword">import</a> <a id="613" href="Cubical.Algebra.Group.MorphismProperties.html" class="Module">Cubical.Algebra.Group.MorphismProperties</a>
<a id="654" class="Keyword">open</a> <a id="659" class="Keyword">import</a> <a id="666" href="Cubical.Algebra.Group.Instances.Bool.html" class="Module">Cubical.Algebra.Group.Instances.Bool</a>
<a id="703" class="Keyword">open</a> <a id="708" class="Keyword">import</a> <a id="715" href="Cubical.Algebra.Group.Instances.Int.html" class="Module">Cubical.Algebra.Group.Instances.Int</a>
<a id="751" class="Keyword">open</a> <a id="756" class="Keyword">import</a> <a id="763" href="Cubical.Algebra.Group.Instances.Unit.html" class="Module">Cubical.Algebra.Group.Instances.Unit</a>

<a id="801" class="Keyword">open</a> <a id="806" class="Keyword">import</a> <a id="813" href="Cubical.HITs.SetTruncation.html" class="Module">Cubical.HITs.SetTruncation</a>
<a id="840" class="Keyword">open</a> <a id="845" class="Keyword">import</a> <a id="852" href="Cubical.HITs.Truncation.html" class="Module">Cubical.HITs.Truncation</a>
<a id="876" class="Keyword">open</a> <a id="881" class="Keyword">import</a> <a id="888" href="Cubical.HITs.Sn.html" class="Module">Cubical.HITs.Sn</a>
<a id="904" class="Keyword">open</a> <a id="909" class="Keyword">import</a> <a id="916" href="Cubical.HITs.S1.html" class="Module">Cubical.HITs.S1</a>
<a id="932" class="Keyword">open</a> <a id="937" class="Keyword">import</a> <a id="944" href="Cubical.HITs.RPn.Base.html" class="Module">Cubical.HITs.RPn.Base</a>
<a id="966" class="Keyword">open</a> <a id="971" class="Keyword">import</a> <a id="978" href="Cubical.HITs.Susp.html" class="Module">Cubical.HITs.Susp</a>
<a id="996" class="Keyword">open</a> <a id="1001" class="Keyword">import</a> <a id="1008" href="Cubical.HITs.Wedge.html" class="Module">Cubical.HITs.Wedge</a>
<a id="1027" class="Keyword">open</a> <a id="1032" class="Keyword">import</a> <a id="1039" href="Cubical.HITs.Pushout.html" class="Module">Cubical.HITs.Pushout</a>

<a id="1061" class="Keyword">open</a> <a id="1066" class="Keyword">import</a> <a id="1073" href="Cubical.ZCohomology.Base.html" class="Module">Cubical.ZCohomology.Base</a>
<a id="1098" class="Keyword">open</a> <a id="1103" class="Keyword">import</a> <a id="1110" href="Cubical.ZCohomology.Properties.html" class="Module">Cubical.ZCohomology.Properties</a>
<a id="1141" class="Keyword">open</a> <a id="1146" class="Keyword">import</a> <a id="1153" href="Cubical.ZCohomology.GroupStructure.html" class="Module">Cubical.ZCohomology.GroupStructure</a>
<a id="1188" class="Keyword">open</a> <a id="1193" class="Keyword">import</a> <a id="1200" href="Cubical.ZCohomology.Groups.Unit.html" class="Module">Cubical.ZCohomology.Groups.Unit</a>
<a id="1232" class="Keyword">open</a> <a id="1237" class="Keyword">import</a> <a id="1244" href="Cubical.ZCohomology.Groups.Sn.html" class="Module">Cubical.ZCohomology.Groups.Sn</a>
<a id="1274" class="Keyword">open</a> <a id="1279" class="Keyword">import</a> <a id="1286" href="Cubical.ZCohomology.Groups.RP2.html" class="Module">Cubical.ZCohomology.Groups.RP2</a>
<a id="1317" class="Keyword">open</a> <a id="1322" class="Keyword">import</a> <a id="1329" href="Cubical.ZCohomology.Groups.Wedge.html" class="Module">Cubical.ZCohomology.Groups.Wedge</a>
<a id="1362" class="Keyword">open</a> <a id="1367" class="Keyword">import</a> <a id="1374" href="Cubical.ZCohomology.Groups.Connected.html" class="Module">Cubical.ZCohomology.Groups.Connected</a>
<a id="1411" class="Keyword">open</a> <a id="1416" class="Keyword">import</a> <a id="1423" href="Cubical.ZCohomology.RingStructure.CupProduct.html" class="Module">Cubical.ZCohomology.RingStructure.CupProduct</a>




<a id="RP²⋁S¹"></a><a id="1472" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#1472" class="Function">RP²⋁S¹</a> <a id="1479" class="Symbol">:</a> <a id="1481" href="Agda.Primitive.html#320" class="Primitive">Type₀</a>
<a id="1487" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#1472" class="Function">RP²⋁S¹</a> <a id="1494" class="Symbol">=</a> <a id="1496" class="Symbol">(</a><a id="1497" href="Cubical.HITs.RPn.Base.html#1301" class="Datatype">RP²</a> <a id="1501" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="1503" href="Cubical.HITs.RPn.Base.html#1321" class="InductiveConstructor">point</a><a id="1508" class="Symbol">)</a> <a id="1510" href="Cubical.HITs.Wedge.Base.html#287" class="Function Operator">⋁</a> <a id="1512" class="Symbol">(</a><a id="1513" href="Cubical.HITs.Sn.Base.html#573" class="Function">S₊∙</a> <a id="1517" class="Number">1</a><a id="1518" class="Symbol">)</a>

<a id="1521" class="Comment">------------- H⁰(RP²⋁S¹) ------------</a>
<a id="H⁰-RP²⋁S¹≅ℤ"></a><a id="1559" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#1559" class="Function">H⁰-RP²⋁S¹≅ℤ</a> <a id="1571" class="Symbol">:</a> <a id="1573" href="Cubical.Algebra.Group.Morphisms.html#1296" class="Function">GroupIso</a> <a id="1582" class="Symbol">(</a><a id="1583" href="Cubical.ZCohomology.GroupStructure.html#28295" class="Function">coHomGr</a> <a id="1591" class="Number">0</a> <a id="1593" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#1472" class="Function">RP²⋁S¹</a><a id="1599" class="Symbol">)</a> <a id="1601" href="Cubical.Algebra.Group.Instances.Int.html#451" class="Function">ℤGroup</a>
<a id="1608" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#1559" class="Function">H⁰-RP²⋁S¹≅ℤ</a> <a id="1620" class="Symbol">=</a> <a id="1622" href="Cubical.ZCohomology.Groups.Connected.html#1468" class="Function">H⁰-connected</a> <a id="1635" class="Symbol">(</a><a id="1636" href="Cubical.HITs.Pushout.Base.html#579" class="InductiveConstructor">inl</a> <a id="1640" href="Cubical.HITs.RPn.Base.html#1321" class="InductiveConstructor">point</a><a id="1645" class="Symbol">)</a> <a id="1647" class="Symbol">(</a><a id="1648" href="Cubical.ZCohomology.Groups.Wedge.html#11436" class="Function">wedgeConnected</a> <a id="1663" class="Symbol">_</a> <a id="1665" class="Symbol">_</a> <a id="1667" href="Cubical.ZCohomology.Groups.RP2.html#2235" class="Function">connectedRP¹</a> <a id="1680" class="Symbol">(</a><a id="1681" href="Cubical.ZCohomology.Groups.Sn.html#1687" class="Function">Sn-connected</a> <a id="1694" class="Number">0</a><a id="1695" class="Symbol">))</a>

<a id="1699" class="Comment">------------- H¹(RP²⋁S¹) ------------</a>
<a id="H¹-RP²⋁S¹≅ℤ"></a><a id="1737" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#1737" class="Function">H¹-RP²⋁S¹≅ℤ</a> <a id="1749" class="Symbol">:</a> <a id="1751" href="Cubical.Algebra.Group.Morphisms.html#1296" class="Function">GroupIso</a> <a id="1760" class="Symbol">(</a><a id="1761" href="Cubical.ZCohomology.GroupStructure.html#28295" class="Function">coHomGr</a> <a id="1769" class="Number">1</a> <a id="1771" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#1472" class="Function">RP²⋁S¹</a><a id="1777" class="Symbol">)</a> <a id="1779" href="Cubical.Algebra.Group.Instances.Int.html#451" class="Function">ℤGroup</a>
<a id="1786" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#1737" class="Function">H¹-RP²⋁S¹≅ℤ</a> <a id="1798" class="Symbol">=</a> <a id="1800" class="Symbol">(</a><a id="1801" href="Cubical.ZCohomology.Groups.Wedge.html#3670" class="Function">Hⁿ-⋁</a> <a id="1806" class="Symbol">_</a> <a id="1808" class="Symbol">_</a> <a id="1810" class="Number">0</a><a id="1811" class="Symbol">)</a>
              <a id="1827" href="Cubical.ZCohomology.Groups.Sn.html#1538" class="Function Operator">□</a> <a id="1829" class="Symbol">((</a><a id="1831" href="Cubical.Algebra.Group.MorphismProperties.html#9494" class="Function">GroupIsoDirProd</a> <a id="1847" href="Cubical.ZCohomology.Groups.RP2.html#3982" class="Function">H¹-RP²≅0</a> <a id="1856" class="Symbol">(</a><a id="1857" href="Cubical.ZCohomology.Groups.Sn.html#13642" class="Function">Hⁿ-Sᵐ</a> <a id="1863" class="Number">1</a> <a id="1865" class="Number">1</a><a id="1866" class="Symbol">))</a>
              <a id="1883" href="Cubical.ZCohomology.Groups.Sn.html#1538" class="Function Operator">□</a> <a id="1885" href="Cubical.Algebra.Group.Instances.Unit.html#1210" class="Function">lUnitGroupIso</a><a id="1898" class="Symbol">)</a>

<a id="1901" class="Comment">-- ------------- H²(RP²⋁S¹) ------------</a>
<a id="H²-RP²⋁S¹≅Bool"></a><a id="1942" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#1942" class="Function">H²-RP²⋁S¹≅Bool</a> <a id="1957" class="Symbol">:</a> <a id="1959" href="Cubical.Algebra.Group.Morphisms.html#1296" class="Function">GroupIso</a> <a id="1968" class="Symbol">(</a><a id="1969" href="Cubical.ZCohomology.GroupStructure.html#28295" class="Function">coHomGr</a> <a id="1977" class="Number">2</a> <a id="1979" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#1472" class="Function">RP²⋁S¹</a><a id="1985" class="Symbol">)</a> <a id="1987" href="Cubical.Algebra.Group.Instances.Bool.html#626" class="Function">BoolGroup</a>
<a id="1997" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#1942" class="Function">H²-RP²⋁S¹≅Bool</a> <a id="2012" class="Symbol">=</a> <a id="2014" class="Symbol">(</a><a id="2015" href="Cubical.ZCohomology.Groups.Wedge.html#3670" class="Function">Hⁿ-⋁</a> <a id="2020" class="Symbol">_</a> <a id="2022" class="Symbol">_</a> <a id="2024" class="Number">1</a><a id="2025" class="Symbol">)</a>
                 <a id="2044" href="Cubical.ZCohomology.Groups.Sn.html#1538" class="Function Operator">□</a> <a id="2046" class="Symbol">((</a><a id="2048" href="Cubical.Algebra.Group.MorphismProperties.html#9494" class="Function">GroupIsoDirProd</a> <a id="2064" href="Cubical.ZCohomology.Groups.RP2.html#5228" class="Function">H²-RP²≅Bool</a> <a id="2076" class="Symbol">(</a><a id="2077" href="Cubical.ZCohomology.Groups.Sn.html#13642" class="Function">Hⁿ-Sᵐ</a> <a id="2083" class="Number">2</a> <a id="2085" class="Number">1</a><a id="2086" class="Symbol">))</a>
                 <a id="2106" href="Cubical.ZCohomology.Groups.Sn.html#1538" class="Function Operator">□</a> <a id="2108" href="Cubical.Algebra.Group.Instances.Unit.html#1465" class="Function">rUnitGroupIso</a><a id="2121" class="Symbol">)</a>

<a id="2124" class="Comment">------------- Hⁿ(RP²⋁S¹) for n ≥ 3 ------------</a>
<a id="Hⁿ-RP²⋁S¹≅0"></a><a id="2172" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#2172" class="Function">Hⁿ-RP²⋁S¹≅0</a> <a id="2184" class="Symbol">:</a> <a id="2186" class="Symbol">(</a><a id="2187" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#2187" class="Bound">n</a> <a id="2189" class="Symbol">:</a> <a id="2191" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a><a id="2192" class="Symbol">)</a> <a id="2194" class="Symbol">→</a> <a id="2196" href="Cubical.Algebra.Group.Morphisms.html#1296" class="Function">GroupIso</a> <a id="2205" class="Symbol">(</a><a id="2206" href="Cubical.ZCohomology.GroupStructure.html#28295" class="Function">coHomGr</a> <a id="2214" class="Symbol">(</a><a id="2215" class="Number">3</a> <a id="2217" href="Agda.Builtin.Nat.html#319" class="Primitive Operator">+</a> <a id="2219" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#2187" class="Bound">n</a><a id="2220" class="Symbol">)</a> <a id="2222" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#1472" class="Function">RP²⋁S¹</a><a id="2228" class="Symbol">)</a> <a id="2230" href="Cubical.Algebra.Group.Instances.Unit.html#599" class="Function">UnitGroup₀</a>
<a id="2241" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#2172" class="Function">Hⁿ-RP²⋁S¹≅0</a> <a id="2253" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#2253" class="Bound">n</a> <a id="2255" class="Symbol">=</a> <a id="2257" class="Symbol">(</a><a id="2258" href="Cubical.ZCohomology.Groups.Wedge.html#3670" class="Function">Hⁿ-⋁</a> <a id="2263" class="Symbol">_</a> <a id="2265" class="Symbol">_</a> <a id="2267" class="Symbol">(</a><a id="2268" class="Number">2</a> <a id="2270" href="Agda.Builtin.Nat.html#319" class="Primitive Operator">+</a> <a id="2272" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#2253" class="Bound">n</a><a id="2273" class="Symbol">))</a>
               <a id="2291" href="Cubical.ZCohomology.Groups.Sn.html#1538" class="Function Operator">□</a> <a id="2293" class="Symbol">((</a><a id="2295" href="Cubical.Algebra.Group.MorphismProperties.html#9494" class="Function">GroupIsoDirProd</a> <a id="2311" class="Symbol">(</a><a id="2312" href="Cubical.ZCohomology.Groups.RP2.html#6571" class="Function">Hⁿ-RP²≅0</a> <a id="2321" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#2253" class="Bound">n</a><a id="2322" class="Symbol">)</a> <a id="2324" class="Symbol">(</a><a id="2325" href="Cubical.ZCohomology.Groups.Sn.html#13642" class="Function">Hⁿ-Sᵐ</a> <a id="2331" class="Symbol">(</a><a id="2332" class="Number">3</a> <a id="2334" href="Agda.Builtin.Nat.html#319" class="Primitive Operator">+</a> <a id="2336" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#2253" class="Bound">n</a><a id="2337" class="Symbol">)</a> <a id="2339" class="Number">1</a><a id="2340" class="Symbol">))</a>
               <a id="2358" href="Cubical.ZCohomology.Groups.Sn.html#1538" class="Function Operator">□</a> <a id="2360" href="Cubical.Algebra.Group.Instances.Unit.html#1210" class="Function">lUnitGroupIso</a><a id="2373" class="Symbol">)</a>

<a id="2376" class="Comment">------------- Cup product is null for H² ------------</a>

<a id="2431" class="Keyword">open</a> <a id="2436" href="Cubical.Foundations.Isomorphism.html#773" class="Module">Iso</a>
<a id="2440" class="Keyword">open</a> <a id="2445" href="Cubical.Algebra.Group.Morphisms.html#736" class="Module">IsGroupHom</a>

<a id="α"></a><a id="2457" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#2457" class="Function">α</a> <a id="2459" class="Symbol">:</a> <a id="2461" href="Cubical.ZCohomology.Base.html#710" class="Function">coHom</a> <a id="2467" class="Number">1</a> <a id="2469" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#1472" class="Function">RP²⋁S¹</a>
<a id="2476" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#2457" class="Function">α</a> <a id="2478" class="Symbol">=</a> <a id="2480" href="Cubical.HITs.SetTruncation.Base.html#299" class="InductiveConstructor Operator">∣</a> <a id="2482" class="Symbol">(λ</a> <a id="2485" class="Symbol">{</a> <a id="2487" class="Symbol">(</a><a id="2488" href="Cubical.HITs.Pushout.Base.html#579" class="InductiveConstructor">inl</a> <a id="2492" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#2492" class="Bound">x</a><a id="2493" class="Symbol">)</a> <a id="2495" class="Symbol">→</a> <a id="2497" href="Cubical.HITs.Truncation.Base.html#696" class="InductiveConstructor Operator">∣</a> <a id="2499" href="Cubical.HITs.S1.Base.html#636" class="InductiveConstructor">base</a> <a id="2504" href="Cubical.HITs.Truncation.Base.html#696" class="InductiveConstructor Operator">∣</a>
         <a id="2515" class="Symbol">;</a> <a id="2517" class="Symbol">(</a><a id="2518" href="Cubical.HITs.Pushout.Base.html#603" class="InductiveConstructor">inr</a> <a id="2522" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#2522" class="Bound">x</a><a id="2523" class="Symbol">)</a> <a id="2525" class="Symbol">→</a> <a id="2527" href="Cubical.HITs.Truncation.Base.html#696" class="InductiveConstructor Operator">∣</a> <a id="2529" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#2522" class="Bound">x</a> <a id="2531" href="Cubical.HITs.Truncation.Base.html#696" class="InductiveConstructor Operator">∣</a>
         <a id="2542" class="Symbol">;</a> <a id="2544" class="Symbol">(</a><a id="2545" href="Cubical.HITs.Pushout.Base.html#627" class="InductiveConstructor">push</a> <a id="2550" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#2550" class="Bound">a</a> <a id="2552" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#2552" class="Bound">i</a><a id="2553" class="Symbol">)</a> <a id="2555" class="Symbol">→</a> <a id="2557" href="Cubical.HITs.Truncation.Base.html#696" class="InductiveConstructor Operator">∣</a> <a id="2559" href="Cubical.HITs.S1.Base.html#636" class="InductiveConstructor">base</a> <a id="2564" href="Cubical.HITs.Truncation.Base.html#696" class="InductiveConstructor Operator">∣</a> <a id="2566" class="Symbol">})</a> <a id="2569" href="Cubical.HITs.SetTruncation.Base.html#299" class="InductiveConstructor Operator">∣₂</a>

<a id="α↦1"></a><a id="2573" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#2573" class="Function">α↦1</a> <a id="2577" class="Symbol">:</a> <a id="2579" href="Cubical.Foundations.Isomorphism.html#885" class="Field">Iso.fun</a> <a id="2587" class="Symbol">(</a><a id="2588" href="Agda.Builtin.Sigma.html#234" class="Field">fst</a> <a id="2592" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#1737" class="Function">H¹-RP²⋁S¹≅ℤ</a><a id="2603" class="Symbol">)</a> <a id="2605" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#2457" class="Function">α</a> <a id="2607" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="2609" class="Number">1</a>
<a id="2611" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#2573" class="Function">α↦1</a> <a id="2615" class="Symbol">=</a> <a id="2617" href="Cubical.Foundations.Prelude.html#915" class="Function">refl</a>

<a id="1↦α"></a><a id="2623" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#2623" class="Function">1↦α</a> <a id="2627" class="Symbol">:</a> <a id="2629" href="Cubical.Foundations.Isomorphism.html#901" class="Field">Iso.inv</a> <a id="2637" class="Symbol">(</a><a id="2638" href="Agda.Builtin.Sigma.html#234" class="Field">fst</a> <a id="2642" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#1737" class="Function">H¹-RP²⋁S¹≅ℤ</a><a id="2653" class="Symbol">)</a> <a id="2655" class="Number">1</a> <a id="2657" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="2659" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#2457" class="Function">α</a>
<a id="2661" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#2623" class="Function">1↦α</a> <a id="2665" class="Symbol">=</a> <a id="2667" href="Cubical.Foundations.Prelude.html#1430" class="Function">cong</a> <a id="2672" class="Symbol">(</a><a id="2673" href="Cubical.Foundations.Isomorphism.html#901" class="Field">Iso.inv</a> <a id="2681" class="Symbol">(</a><a id="2682" href="Agda.Builtin.Sigma.html#234" class="Field">fst</a> <a id="2686" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#1737" class="Function">H¹-RP²⋁S¹≅ℤ</a><a id="2697" class="Symbol">))</a> <a id="2700" class="Symbol">(</a><a id="2701" href="Cubical.Foundations.Prelude.html#968" class="Function">sym</a> <a id="2705" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#2573" class="Function">α↦1</a><a id="2708" class="Symbol">)</a>
       <a id="2717" href="Cubical.Foundations.Prelude.html#4447" class="Function Operator">∙</a> <a id="2719" href="Cubical.Foundations.Isomorphism.html#948" class="Field">leftInv</a> <a id="2727" class="Symbol">(</a><a id="2728" href="Agda.Builtin.Sigma.html#234" class="Field">fst</a> <a id="2732" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#1737" class="Function">H¹-RP²⋁S¹≅ℤ</a><a id="2743" class="Symbol">)</a> <a id="2745" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#2457" class="Function">α</a>

<a id="2748" class="Comment">-- computes !</a>
<a id="lem-α²≡0"></a><a id="2762" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#2762" class="Function">lem-α²≡0</a> <a id="2771" class="Symbol">:</a> <a id="2773" href="Cubical.Foundations.Isomorphism.html#885" class="Field">Iso.fun</a> <a id="2781" class="Symbol">(</a><a id="2782" href="Agda.Builtin.Sigma.html#234" class="Field">fst</a> <a id="2786" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#1942" class="Function">H²-RP²⋁S¹≅Bool</a><a id="2800" class="Symbol">)</a> <a id="2802" class="Symbol">(</a><a id="2803" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#2457" class="Function">α</a> <a id="2805" href="Cubical.ZCohomology.RingStructure.CupProduct.html#2850" class="Function Operator">⌣</a> <a id="2807" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#2457" class="Function">α</a><a id="2808" class="Symbol">)</a> <a id="2810" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="2812" href="Agda.Builtin.Bool.html#181" class="InductiveConstructor">true</a>
<a id="2817" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#2762" class="Function">lem-α²≡0</a> <a id="2826" class="Symbol">=</a> <a id="2828" href="Cubical.Foundations.Prelude.html#915" class="Function">refl</a>

<a id="α²≡0"></a><a id="2834" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#2834" class="Function">α²≡0</a> <a id="2839" class="Symbol">:</a> <a id="2841" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#2457" class="Function">α</a> <a id="2843" href="Cubical.ZCohomology.RingStructure.CupProduct.html#2850" class="Function Operator">⌣</a> <a id="2845" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#2457" class="Function">α</a> <a id="2847" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="2849" href="Cubical.ZCohomology.GroupStructure.html#19733" class="Function">0ₕ</a> <a id="2852" class="Number">2</a>
<a id="2854" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#2834" class="Function">α²≡0</a> <a id="2859" class="Symbol">=</a> <a id="2861" href="Cubical.Foundations.Prelude.html#968" class="Function">sym</a> <a id="2865" class="Symbol">(</a><a id="2866" href="Cubical.Foundations.Isomorphism.html#948" class="Field">leftInv</a> <a id="2874" class="Symbol">(</a><a id="2875" href="Agda.Builtin.Sigma.html#234" class="Field">fst</a> <a id="2879" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#1942" class="Function">H²-RP²⋁S¹≅Bool</a><a id="2893" class="Symbol">)</a> <a id="2895" class="Symbol">(</a><a id="2896" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#2457" class="Function">α</a> <a id="2898" href="Cubical.ZCohomology.RingStructure.CupProduct.html#2850" class="Function Operator">⌣</a> <a id="2900" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#2457" class="Function">α</a><a id="2901" class="Symbol">))</a>
       <a id="2911" href="Cubical.Foundations.Prelude.html#4447" class="Function Operator">∙</a> <a id="2913" href="Cubical.Foundations.Prelude.html#1430" class="Function">cong</a> <a id="2918" class="Symbol">(</a><a id="2919" href="Cubical.Foundations.Isomorphism.html#901" class="Field">Iso.inv</a> <a id="2927" class="Symbol">(</a><a id="2928" href="Agda.Builtin.Sigma.html#234" class="Field">fst</a> <a id="2932" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#1942" class="Function">H²-RP²⋁S¹≅Bool</a><a id="2946" class="Symbol">))</a> <a id="2949" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#2762" class="Function">lem-α²≡0</a>
       <a id="2965" href="Cubical.Foundations.Prelude.html#4447" class="Function Operator">∙</a> <a id="2967" href="Cubical.Algebra.Group.Morphisms.html#1007" class="Field">pres1</a> <a id="2973" class="Symbol">(</a><a id="2974" href="Agda.Builtin.Sigma.html#246" class="Field">snd</a> <a id="2978" class="Symbol">(</a><a id="2979" href="Cubical.Algebra.Group.MorphismProperties.html#9334" class="Function">invGroupIso</a> <a id="2991" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#1942" class="Function">H²-RP²⋁S¹≅Bool</a><a id="3005" class="Symbol">))</a>

<a id="trivial-cup"></a><a id="3009" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#3009" class="Function">trivial-cup</a> <a id="3021" class="Symbol">:</a> <a id="3023" href="Cubical.Foundations.Isomorphism.html#901" class="Field">Iso.inv</a> <a id="3031" class="Symbol">(</a><a id="3032" href="Agda.Builtin.Sigma.html#234" class="Field">fst</a> <a id="3036" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#1737" class="Function">H¹-RP²⋁S¹≅ℤ</a><a id="3047" class="Symbol">)</a> <a id="3049" class="Number">1</a> <a id="3051" href="Cubical.ZCohomology.RingStructure.CupProduct.html#2850" class="Function Operator">⌣</a> <a id="3053" href="Cubical.Foundations.Isomorphism.html#901" class="Field">Iso.inv</a> <a id="3061" class="Symbol">(</a><a id="3062" href="Agda.Builtin.Sigma.html#234" class="Field">fst</a> <a id="3066" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#1737" class="Function">H¹-RP²⋁S¹≅ℤ</a><a id="3077" class="Symbol">)</a> <a id="3079" class="Number">1</a> <a id="3081" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="3083" href="Cubical.ZCohomology.GroupStructure.html#19733" class="Function">0ₕ</a> <a id="3086" class="Number">2</a>
<a id="3088" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#3009" class="Function">trivial-cup</a> <a id="3100" class="Symbol">=</a> <a id="3102" href="Cubical.Foundations.Prelude.html#2025" class="Function">cong₂</a> <a id="3108" href="Cubical.ZCohomology.RingStructure.CupProduct.html#2850" class="Function Operator">_⌣_</a> <a id="3112" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#2623" class="Function">1↦α</a> <a id="3116" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#2623" class="Function">1↦α</a> <a id="3120" href="Cubical.Foundations.Prelude.html#4447" class="Function Operator">∙</a> <a id="3122" href="Cubical.ZCohomology.Groups.RP2wedgeS1.html#2834" class="Function">α²≡0</a>
</pre></body></html>