<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Cubical.Experiments.IsoInt.Base</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">{-

The naive, but incorrect, way to define the integers as a HIT.

This file mainly contains a proof that IsoInt ≢ Int, and ends with a
 demonstration of how the same proof strategy fails for BiInvℤ.

-}</a>
<a id="206" class="Symbol">{-#</a> <a id="210" class="Keyword">OPTIONS</a> <a id="218" class="Pragma">--safe</a> <a id="225" class="Symbol">#-}</a>
<a id="229" class="Keyword">module</a> <a id="236" href="Cubical.Experiments.IsoInt.Base.html" class="Module">Cubical.Experiments.IsoInt.Base</a> <a id="268" class="Keyword">where</a>

<a id="275" class="Keyword">open</a> <a id="280" class="Keyword">import</a> <a id="287" href="Cubical.Foundations.Prelude.html" class="Module">Cubical.Foundations.Prelude</a>
<a id="315" class="Keyword">open</a> <a id="320" class="Keyword">import</a> <a id="327" href="Cubical.Foundations.Function.html" class="Module">Cubical.Foundations.Function</a>
<a id="356" class="Keyword">open</a> <a id="361" class="Keyword">import</a> <a id="368" href="Cubical.Foundations.Isomorphism.html" class="Module">Cubical.Foundations.Isomorphism</a>

<a id="401" class="Keyword">open</a> <a id="406" class="Keyword">import</a> <a id="413" href="Cubical.Data.Int.html" class="Module">Cubical.Data.Int</a>
<a id="430" class="Keyword">open</a> <a id="435" class="Keyword">import</a> <a id="442" href="Cubical.Data.Nat.html" class="Module">Cubical.Data.Nat</a>
<a id="459" class="Keyword">open</a> <a id="464" class="Keyword">import</a> <a id="471" href="Cubical.Data.Empty.html" class="Module">Cubical.Data.Empty</a>

<a id="491" class="Keyword">open</a> <a id="496" class="Keyword">import</a> <a id="503" href="Cubical.Relation.Nullary.html" class="Module">Cubical.Relation.Nullary</a>


<a id="530" class="Keyword">data</a> <a id="IsoInt"></a><a id="535" href="Cubical.Experiments.IsoInt.Base.html#535" class="Datatype">IsoInt</a> <a id="542" class="Symbol">:</a> <a id="544" href="Agda.Primitive.html#326" class="Primitive">Type₀</a> <a id="550" class="Keyword">where</a>
  <a id="IsoInt.zero"></a><a id="558" href="Cubical.Experiments.IsoInt.Base.html#558" class="InductiveConstructor">zero</a> <a id="563" class="Symbol">:</a> <a id="565" href="Cubical.Experiments.IsoInt.Base.html#535" class="Datatype">IsoInt</a>
  <a id="IsoInt.suc"></a><a id="574" href="Cubical.Experiments.IsoInt.Base.html#574" class="InductiveConstructor">suc</a> <a id="578" class="Symbol">:</a> <a id="580" href="Cubical.Experiments.IsoInt.Base.html#535" class="Datatype">IsoInt</a> <a id="587" class="Symbol">-&gt;</a> <a id="590" href="Cubical.Experiments.IsoInt.Base.html#535" class="Datatype">IsoInt</a>

  <a id="600" class="Comment">-- suc is an isomorphism:</a>
  <a id="IsoInt.pred"></a><a id="628" href="Cubical.Experiments.IsoInt.Base.html#628" class="InductiveConstructor">pred</a> <a id="633" class="Symbol">:</a> <a id="635" href="Cubical.Experiments.IsoInt.Base.html#535" class="Datatype">IsoInt</a> <a id="642" class="Symbol">-&gt;</a> <a id="645" href="Cubical.Experiments.IsoInt.Base.html#535" class="Datatype">IsoInt</a>
  <a id="IsoInt.suc-pred"></a><a id="654" href="Cubical.Experiments.IsoInt.Base.html#654" class="InductiveConstructor">suc-pred</a> <a id="663" class="Symbol">:</a> <a id="665" class="Symbol">∀</a> <a id="667" href="Cubical.Experiments.IsoInt.Base.html#667" class="Bound">z</a> <a id="669" class="Symbol">-&gt;</a> <a id="672" href="Cubical.Experiments.IsoInt.Base.html#574" class="InductiveConstructor">suc</a> <a id="676" class="Symbol">(</a><a id="677" href="Cubical.Experiments.IsoInt.Base.html#628" class="InductiveConstructor">pred</a> <a id="682" href="Cubical.Experiments.IsoInt.Base.html#667" class="Bound">z</a><a id="683" class="Symbol">)</a> <a id="685" href="Agda.Builtin.Cubical.Path.html#388" class="Function Operator">≡</a> <a id="687" href="Cubical.Experiments.IsoInt.Base.html#667" class="Bound">z</a>
  <a id="IsoInt.pred-suc"></a><a id="691" href="Cubical.Experiments.IsoInt.Base.html#691" class="InductiveConstructor">pred-suc</a> <a id="700" class="Symbol">:</a> <a id="702" class="Symbol">∀</a> <a id="704" href="Cubical.Experiments.IsoInt.Base.html#704" class="Bound">z</a> <a id="706" class="Symbol">-&gt;</a> <a id="709" href="Cubical.Experiments.IsoInt.Base.html#628" class="InductiveConstructor">pred</a> <a id="714" class="Symbol">(</a><a id="715" href="Cubical.Experiments.IsoInt.Base.html#574" class="InductiveConstructor">suc</a> <a id="719" href="Cubical.Experiments.IsoInt.Base.html#704" class="Bound">z</a><a id="720" class="Symbol">)</a> <a id="722" href="Agda.Builtin.Cubical.Path.html#388" class="Function Operator">≡</a> <a id="724" href="Cubical.Experiments.IsoInt.Base.html#704" class="Bound">z</a>


<a id="suc-iso"></a><a id="728" href="Cubical.Experiments.IsoInt.Base.html#728" class="Function">suc-iso</a> <a id="736" class="Symbol">:</a> <a id="738" href="Cubical.Foundations.Isomorphism.html#773" class="Record">Iso</a> <a id="742" href="Cubical.Experiments.IsoInt.Base.html#535" class="Datatype">IsoInt</a> <a id="749" href="Cubical.Experiments.IsoInt.Base.html#535" class="Datatype">IsoInt</a>
<a id="756" href="Cubical.Experiments.IsoInt.Base.html#728" class="Function">suc-iso</a> <a id="764" class="Symbol">=</a> <a id="766" class="Keyword">record</a> <a id="773" class="Symbol">{</a> <a id="775" href="Cubical.Foundations.Isomorphism.html#885" class="Field">fun</a> <a id="779" class="Symbol">=</a> <a id="781" href="Cubical.Experiments.IsoInt.Base.html#574" class="InductiveConstructor">suc</a> <a id="785" class="Symbol">;</a> <a id="787" href="Cubical.Foundations.Isomorphism.html#901" class="Field">inv</a> <a id="791" class="Symbol">=</a> <a id="793" href="Cubical.Experiments.IsoInt.Base.html#628" class="InductiveConstructor">pred</a> <a id="798" class="Symbol">;</a> <a id="800" href="Cubical.Foundations.Isomorphism.html#917" class="Field">rightInv</a> <a id="809" class="Symbol">=</a> <a id="811" href="Cubical.Experiments.IsoInt.Base.html#654" class="InductiveConstructor">suc-pred</a> <a id="820" class="Symbol">;</a> <a id="822" href="Cubical.Foundations.Isomorphism.html#948" class="Field">leftInv</a> <a id="830" class="Symbol">=</a> <a id="832" href="Cubical.Experiments.IsoInt.Base.html#691" class="InductiveConstructor">pred-suc</a> <a id="841" class="Symbol">}</a>


<a id="845" class="Comment">-- this submodule is adapted from Section 5 of</a>
<a id="892" class="Comment">--  http://www.cs.ru.nl/~herman/PUBS/HIT-programming.pdf</a>

<a id="950" class="Keyword">module</a> <a id="NonTrivial"></a><a id="957" href="Cubical.Experiments.IsoInt.Base.html#957" class="Module">NonTrivial</a> <a id="968" class="Keyword">where</a>

  <a id="977" class="Comment">-- these two paths are distinct!</a>
  <a id="NonTrivial.p₁"></a><a id="1012" href="Cubical.Experiments.IsoInt.Base.html#1012" class="Function">p₁</a> <a id="NonTrivial.p₂"></a><a id="1015" href="Cubical.Experiments.IsoInt.Base.html#1015" class="Function">p₂</a> <a id="1018" class="Symbol">:</a> <a id="1020" href="Cubical.Core.Primitives.html#1866" class="Function">Path</a> <a id="1025" href="Cubical.Experiments.IsoInt.Base.html#535" class="Datatype">IsoInt</a> <a id="1032" class="Symbol">(</a><a id="1033" href="Cubical.Experiments.IsoInt.Base.html#574" class="InductiveConstructor">suc</a> <a id="1037" class="Symbol">(</a><a id="1038" href="Cubical.Experiments.IsoInt.Base.html#628" class="InductiveConstructor">pred</a> <a id="1043" class="Symbol">(</a><a id="1044" href="Cubical.Experiments.IsoInt.Base.html#574" class="InductiveConstructor">suc</a> <a id="1048" href="Cubical.Experiments.IsoInt.Base.html#558" class="InductiveConstructor">zero</a><a id="1052" class="Symbol">)))</a> <a id="1056" class="Symbol">(</a><a id="1057" href="Cubical.Experiments.IsoInt.Base.html#574" class="InductiveConstructor">suc</a> <a id="1061" href="Cubical.Experiments.IsoInt.Base.html#558" class="InductiveConstructor">zero</a><a id="1065" class="Symbol">)</a>
  <a id="1069" href="Cubical.Experiments.IsoInt.Base.html#1012" class="Function">p₁</a> <a id="1072" href="Cubical.Experiments.IsoInt.Base.html#1072" class="Bound">i</a> <a id="1074" class="Symbol">=</a> <a id="1076" href="Cubical.Experiments.IsoInt.Base.html#654" class="InductiveConstructor">suc-pred</a> <a id="1085" class="Symbol">(</a><a id="1086" href="Cubical.Experiments.IsoInt.Base.html#574" class="InductiveConstructor">suc</a> <a id="1090" href="Cubical.Experiments.IsoInt.Base.html#558" class="InductiveConstructor">zero</a><a id="1094" class="Symbol">)</a> <a id="1096" href="Cubical.Experiments.IsoInt.Base.html#1072" class="Bound">i</a>
  <a id="1100" href="Cubical.Experiments.IsoInt.Base.html#1015" class="Function">p₂</a> <a id="1103" href="Cubical.Experiments.IsoInt.Base.html#1103" class="Bound">i</a> <a id="1105" class="Symbol">=</a> <a id="1107" href="Cubical.Experiments.IsoInt.Base.html#574" class="InductiveConstructor">suc</a> <a id="1111" class="Symbol">(</a><a id="1112" href="Cubical.Experiments.IsoInt.Base.html#691" class="InductiveConstructor">pred-suc</a> <a id="1121" href="Cubical.Experiments.IsoInt.Base.html#558" class="InductiveConstructor">zero</a> <a id="1126" href="Cubical.Experiments.IsoInt.Base.html#1103" class="Bound">i</a><a id="1127" class="Symbol">)</a>

  <a id="1132" class="Comment">-- to prove this we map into S¹, sending p₁ to refl and p₂ to loop</a>

  <a id="1202" class="Keyword">open</a> <a id="1207" class="Keyword">import</a> <a id="1214" href="Cubical.HITs.S1.html" class="Module">Cubical.HITs.S1</a>

  <a id="NonTrivial.toS¹"></a><a id="1233" href="Cubical.Experiments.IsoInt.Base.html#1233" class="Function">toS¹</a> <a id="1238" class="Symbol">:</a> <a id="1240" href="Cubical.Experiments.IsoInt.Base.html#535" class="Datatype">IsoInt</a> <a id="1247" class="Symbol">→</a> <a id="1249" href="Cubical.HITs.S1.Base.html#617" class="Datatype">S¹</a>
  <a id="1254" href="Cubical.Experiments.IsoInt.Base.html#1233" class="Function">toS¹</a> <a id="1259" href="Cubical.Experiments.IsoInt.Base.html#558" class="InductiveConstructor">zero</a>           <a id="1274" class="Symbol">=</a> <a id="1276" href="Cubical.HITs.S1.Base.html#636" class="InductiveConstructor">base</a>
  <a id="1283" href="Cubical.Experiments.IsoInt.Base.html#1233" class="Function">toS¹</a> <a id="1288" class="Symbol">(</a><a id="1289" href="Cubical.Experiments.IsoInt.Base.html#574" class="InductiveConstructor">suc</a> <a id="1293" href="Cubical.Experiments.IsoInt.Base.html#1293" class="Bound">x</a><a id="1294" class="Symbol">)</a>        <a id="1303" class="Symbol">=</a> <a id="1305" href="Cubical.Experiments.IsoInt.Base.html#1233" class="Function">toS¹</a> <a id="1310" href="Cubical.Experiments.IsoInt.Base.html#1293" class="Bound">x</a>
  <a id="1314" href="Cubical.Experiments.IsoInt.Base.html#1233" class="Function">toS¹</a> <a id="1319" class="Symbol">(</a><a id="1320" href="Cubical.Experiments.IsoInt.Base.html#628" class="InductiveConstructor">pred</a> <a id="1325" href="Cubical.Experiments.IsoInt.Base.html#1325" class="Bound">x</a><a id="1326" class="Symbol">)</a>       <a id="1334" class="Symbol">=</a> <a id="1336" href="Cubical.Experiments.IsoInt.Base.html#1233" class="Function">toS¹</a> <a id="1341" href="Cubical.Experiments.IsoInt.Base.html#1325" class="Bound">x</a>
  <a id="1345" href="Cubical.Experiments.IsoInt.Base.html#1233" class="Function">toS¹</a> <a id="1350" class="Symbol">(</a><a id="1351" href="Cubical.Experiments.IsoInt.Base.html#654" class="InductiveConstructor">suc-pred</a> <a id="1360" href="Cubical.Experiments.IsoInt.Base.html#1360" class="Bound">x</a> <a id="1362" href="Cubical.Experiments.IsoInt.Base.html#1362" class="Bound">i</a><a id="1363" class="Symbol">)</a> <a id="1365" class="Symbol">=</a> <a id="1367" href="Cubical.Foundations.Prelude.html#915" class="Function">refl</a> <a id="1372" class="Symbol">{</a><a id="1373" class="Argument">x</a> <a id="1375" class="Symbol">=</a> <a id="1377" href="Cubical.Experiments.IsoInt.Base.html#1233" class="Function">toS¹</a> <a id="1382" href="Cubical.Experiments.IsoInt.Base.html#1360" class="Bound">x</a><a id="1383" class="Symbol">}</a> <a id="1385" href="Cubical.Experiments.IsoInt.Base.html#1362" class="Bound">i</a>
  <a id="1389" href="Cubical.Experiments.IsoInt.Base.html#1233" class="Function">toS¹</a> <a id="1394" class="Symbol">(</a><a id="1395" href="Cubical.Experiments.IsoInt.Base.html#691" class="InductiveConstructor">pred-suc</a> <a id="1404" href="Cubical.Experiments.IsoInt.Base.html#1404" class="Bound">x</a> <a id="1406" href="Cubical.Experiments.IsoInt.Base.html#1406" class="Bound">i</a><a id="1407" class="Symbol">)</a> <a id="1409" class="Symbol">=</a> <a id="1411" href="Cubical.HITs.S1.Base.html#14398" class="Function">rotLoop</a> <a id="1419" class="Symbol">(</a><a id="1420" href="Cubical.Experiments.IsoInt.Base.html#1233" class="Function">toS¹</a> <a id="1425" href="Cubical.Experiments.IsoInt.Base.html#1404" class="Bound">x</a><a id="1426" class="Symbol">)</a> <a id="1428" href="Cubical.Experiments.IsoInt.Base.html#1406" class="Bound">i</a>

  <a id="NonTrivial.p₁≡refl"></a><a id="1433" href="Cubical.Experiments.IsoInt.Base.html#1433" class="Function">p₁≡refl</a> <a id="1441" class="Symbol">:</a> <a id="1443" href="Cubical.Foundations.Prelude.html#1430" class="Function">cong</a> <a id="1448" href="Cubical.Experiments.IsoInt.Base.html#1233" class="Function">toS¹</a> <a id="1453" href="Cubical.Experiments.IsoInt.Base.html#1012" class="Function">p₁</a> <a id="1456" href="Agda.Builtin.Cubical.Path.html#388" class="Function Operator">≡</a> <a id="1458" href="Cubical.Foundations.Prelude.html#915" class="Function">refl</a>
  <a id="1465" href="Cubical.Experiments.IsoInt.Base.html#1433" class="Function">p₁≡refl</a> <a id="1473" class="Symbol">=</a> <a id="1475" href="Cubical.Foundations.Prelude.html#915" class="Function">refl</a>

  <a id="NonTrivial.p₂≡loop"></a><a id="1483" href="Cubical.Experiments.IsoInt.Base.html#1483" class="Function">p₂≡loop</a> <a id="1491" class="Symbol">:</a> <a id="1493" href="Cubical.Foundations.Prelude.html#1430" class="Function">cong</a> <a id="1498" href="Cubical.Experiments.IsoInt.Base.html#1233" class="Function">toS¹</a> <a id="1503" href="Cubical.Experiments.IsoInt.Base.html#1015" class="Function">p₂</a> <a id="1506" href="Agda.Builtin.Cubical.Path.html#388" class="Function Operator">≡</a> <a id="1508" href="Cubical.HITs.S1.Base.html#648" class="InductiveConstructor">loop</a>
  <a id="1515" href="Cubical.Experiments.IsoInt.Base.html#1483" class="Function">p₂≡loop</a> <a id="1523" class="Symbol">=</a> <a id="1525" href="Cubical.Foundations.Prelude.html#915" class="Function">refl</a>

  <a id="1533" class="Comment">-- this is enough to show that p₁ and p₂ cannot be equal</a>
  <a id="NonTrivial.p₁≢p₂"></a><a id="1592" href="Cubical.Experiments.IsoInt.Base.html#1592" class="Function">p₁≢p₂</a> <a id="1598" class="Symbol">:</a> <a id="1600" href="Cubical.Relation.Nullary.Base.html#355" class="Function Operator">¬</a> <a id="1602" class="Symbol">(</a><a id="1603" href="Cubical.Experiments.IsoInt.Base.html#1012" class="Function">p₁</a> <a id="1606" href="Agda.Builtin.Cubical.Path.html#388" class="Function Operator">≡</a> <a id="1608" href="Cubical.Experiments.IsoInt.Base.html#1015" class="Function">p₂</a><a id="1610" class="Symbol">)</a>
  <a id="1614" href="Cubical.Experiments.IsoInt.Base.html#1592" class="Function">p₁≢p₂</a> <a id="1620" href="Cubical.Experiments.IsoInt.Base.html#1620" class="Bound">eq</a> <a id="1623" class="Symbol">=</a> <a id="1625" href="Cubical.Data.Nat.Properties.html#1027" class="Function">znots</a> <a id="1631" href="Cubical.Experiments.IsoInt.Base.html#1703" class="Function">0≡1</a>
    <a id="1639" class="Keyword">where</a> <a id="1645" class="Comment">-- using winding numbers, p₁ ≡ p₂ implies 0 ≡ 1</a>
          <a id="1703" href="Cubical.Experiments.IsoInt.Base.html#1703" class="Function">0≡1</a> <a id="1707" class="Symbol">:</a> <a id="1709" class="Number">0</a> <a id="1711" href="Agda.Builtin.Cubical.Path.html#388" class="Function Operator">≡</a> <a id="1713" class="Number">1</a>
          <a id="1725" href="Cubical.Experiments.IsoInt.Base.html#1703" class="Function">0≡1</a> <a id="1729" class="Symbol">=</a> <a id="1731" href="Cubical.Data.Int.Properties.html#873" class="Function">injPos</a> <a id="1738" class="Symbol">(</a><a id="1739" href="Cubical.Foundations.Prelude.html#1430" class="Function">cong</a> <a id="1744" class="Symbol">(</a><a id="1745" href="Cubical.HITs.S1.Base.html#1162" class="Function">winding</a> <a id="1753" href="Cubical.Foundations.Function.html#653" class="Function Operator">∘</a> <a id="1755" href="Cubical.Foundations.Prelude.html#1430" class="Function">cong</a> <a id="1760" href="Cubical.Experiments.IsoInt.Base.html#1233" class="Function">toS¹</a><a id="1764" class="Symbol">)</a> <a id="1766" href="Cubical.Experiments.IsoInt.Base.html#1620" class="Bound">eq</a><a id="1768" class="Symbol">)</a>


<a id="¬isSet-IsoInt"></a><a id="1772" href="Cubical.Experiments.IsoInt.Base.html#1772" class="Function">¬isSet-IsoInt</a> <a id="1786" class="Symbol">:</a> <a id="1788" href="Cubical.Relation.Nullary.Base.html#355" class="Function Operator">¬</a> <a id="1790" class="Symbol">(</a><a id="1791" href="Cubical.Foundations.Prelude.html#14065" class="Function">isSet</a> <a id="1797" href="Cubical.Experiments.IsoInt.Base.html#535" class="Datatype">IsoInt</a><a id="1803" class="Symbol">)</a>
<a id="1805" href="Cubical.Experiments.IsoInt.Base.html#1772" class="Function">¬isSet-IsoInt</a> <a id="1819" href="Cubical.Experiments.IsoInt.Base.html#1819" class="Bound">pf</a> <a id="1822" class="Symbol">=</a> <a id="1824" href="Cubical.Experiments.IsoInt.Base.html#1592" class="Function">NonTrivial.p₁≢p₂</a> <a id="1841" class="Symbol">(</a><a id="1842" href="Cubical.Experiments.IsoInt.Base.html#1819" class="Bound">pf</a> <a id="1845" class="Symbol">_</a> <a id="1847" class="Symbol">_</a> <a id="1849" href="Cubical.Experiments.IsoInt.Base.html#1012" class="Function">NonTrivial.p₁</a> <a id="1863" href="Cubical.Experiments.IsoInt.Base.html#1015" class="Function">NonTrivial.p₂</a><a id="1876" class="Symbol">)</a>

<a id="¬Int≡IsoInt"></a><a id="1879" href="Cubical.Experiments.IsoInt.Base.html#1879" class="Function">¬Int≡IsoInt</a> <a id="1891" class="Symbol">:</a> <a id="1893" href="Cubical.Relation.Nullary.Base.html#355" class="Function Operator">¬</a> <a id="1895" class="Symbol">(</a><a id="1896" href="Cubical.Data.Int.Base.html#393" class="Datatype">ℤ</a> <a id="1898" href="Agda.Builtin.Cubical.Path.html#388" class="Function Operator">≡</a> <a id="1900" href="Cubical.Experiments.IsoInt.Base.html#535" class="Datatype">IsoInt</a><a id="1906" class="Symbol">)</a>
<a id="1908" href="Cubical.Experiments.IsoInt.Base.html#1879" class="Function">¬Int≡IsoInt</a> <a id="1920" href="Cubical.Experiments.IsoInt.Base.html#1920" class="Bound">p</a> <a id="1922" class="Symbol">=</a> <a id="1924" href="Cubical.Experiments.IsoInt.Base.html#1772" class="Function">¬isSet-IsoInt</a> <a id="1938" class="Symbol">(</a><a id="1939" href="Cubical.Foundations.Prelude.html#9128" class="Function">subst</a> <a id="1945" href="Cubical.Foundations.Prelude.html#14065" class="Function">isSet</a> <a id="1951" href="Cubical.Experiments.IsoInt.Base.html#1920" class="Bound">p</a> <a id="1953" href="Cubical.Data.Int.Properties.html#1828" class="Function">isSetℤ</a><a id="1959" class="Symbol">)</a>



<a id="1964" class="Keyword">private</a>

  <a id="1975" class="Comment">-- Note: this same proof strategy fails for BiInvℤ!</a>

  <a id="2030" class="Keyword">open</a> <a id="2035" class="Keyword">import</a> <a id="2042" href="Cubical.Data.Int.MoreInts.BiInvInt.html" class="Module">Cubical.Data.Int.MoreInts.BiInvInt</a> <a id="2077" class="Keyword">hiding</a> <a id="2084" class="Symbol">(</a><a id="2085" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#1047" class="InductiveConstructor">zero</a><a id="2089" class="Symbol">;</a> <a id="2091" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#1063" class="InductiveConstructor">suc</a><a id="2094" class="Symbol">;</a> <a id="2096" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#2556" class="Function">pred</a><a id="2100" class="Symbol">;</a> <a id="2102" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#2594" class="Function">suc-pred</a><a id="2110" class="Symbol">;</a> <a id="2112" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#2615" class="Function">pred-suc</a><a id="2120" class="Symbol">)</a>
  <a id="2124" class="Keyword">import</a> <a id="2131" href="Cubical.Data.Int.MoreInts.BiInvInt.html" class="Module">Cubical.Data.Int.MoreInts.BiInvInt</a> <a id="2166" class="Symbol">as</a> <a id="2169" class="Module">BiI</a>

  <a id="p₁"></a><a id="2176" href="Cubical.Experiments.IsoInt.Base.html#2176" class="Function">p₁</a> <a id="p₂"></a><a id="2179" href="Cubical.Experiments.IsoInt.Base.html#2179" class="Function">p₂</a> <a id="2182" class="Symbol">:</a> <a id="2184" href="Cubical.Core.Primitives.html#1866" class="Function">Path</a> <a id="2189" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#1024" class="Datatype">BiInvℤ</a> <a id="2196" class="Symbol">(</a><a id="2197" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#1063" class="InductiveConstructor">BiI.suc</a> <a id="2205" class="Symbol">(</a><a id="2206" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#2556" class="Function">BiI.pred</a> <a id="2215" class="Symbol">(</a><a id="2216" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#1063" class="InductiveConstructor">BiI.suc</a> <a id="2224" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#1047" class="InductiveConstructor">BiI.zero</a><a id="2232" class="Symbol">)))</a> <a id="2236" class="Symbol">(</a><a id="2237" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#1063" class="InductiveConstructor">BiI.suc</a> <a id="2245" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#1047" class="InductiveConstructor">BiI.zero</a><a id="2253" class="Symbol">)</a>
  <a id="2257" href="Cubical.Experiments.IsoInt.Base.html#2176" class="Function">p₁</a> <a id="2260" href="Cubical.Experiments.IsoInt.Base.html#2260" class="Bound">i</a> <a id="2262" class="Symbol">=</a> <a id="2264" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#2594" class="Function">BiI.suc-pred</a> <a id="2277" class="Symbol">(</a><a id="2278" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#1063" class="InductiveConstructor">BiI.suc</a> <a id="2286" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#1047" class="InductiveConstructor">BiI.zero</a><a id="2294" class="Symbol">)</a> <a id="2296" href="Cubical.Experiments.IsoInt.Base.html#2260" class="Bound">i</a>
  <a id="2300" href="Cubical.Experiments.IsoInt.Base.html#2179" class="Function">p₂</a> <a id="2303" href="Cubical.Experiments.IsoInt.Base.html#2303" class="Bound">i</a> <a id="2305" class="Symbol">=</a> <a id="2307" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#1063" class="InductiveConstructor">BiI.suc</a> <a id="2315" class="Symbol">(</a><a id="2316" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#2615" class="Function">BiI.pred-suc</a> <a id="2329" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#1047" class="InductiveConstructor">BiI.zero</a> <a id="2338" href="Cubical.Experiments.IsoInt.Base.html#2303" class="Bound">i</a><a id="2339" class="Symbol">)</a>

  <a id="2344" class="Keyword">open</a> <a id="2349" class="Keyword">import</a> <a id="2356" href="Cubical.HITs.S1.html" class="Module">Cubical.HITs.S1</a>

  <a id="toS¹"></a><a id="2375" href="Cubical.Experiments.IsoInt.Base.html#2375" class="Function">toS¹</a> <a id="2380" class="Symbol">:</a> <a id="2382" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#1024" class="Datatype">BiInvℤ</a> <a id="2389" class="Symbol">→</a> <a id="2391" href="Cubical.HITs.S1.Base.html#617" class="Datatype">S¹</a>
  <a id="2396" href="Cubical.Experiments.IsoInt.Base.html#2375" class="Function">toS¹</a> <a id="2401" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#1047" class="InductiveConstructor">BiI.zero</a>            <a id="2421" class="Symbol">=</a> <a id="2423" href="Cubical.HITs.S1.Base.html#636" class="InductiveConstructor">base</a>
  <a id="2430" href="Cubical.Experiments.IsoInt.Base.html#2375" class="Function">toS¹</a> <a id="2435" class="Symbol">(</a><a id="2436" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#1063" class="InductiveConstructor">BiI.suc</a> <a id="2444" href="Cubical.Experiments.IsoInt.Base.html#2444" class="Bound">x</a><a id="2445" class="Symbol">)</a>         <a id="2455" class="Symbol">=</a> <a id="2457" href="Cubical.Experiments.IsoInt.Base.html#2375" class="Function">toS¹</a> <a id="2462" href="Cubical.Experiments.IsoInt.Base.html#2444" class="Bound">x</a>
  <a id="2466" href="Cubical.Experiments.IsoInt.Base.html#2375" class="Function">toS¹</a> <a id="2471" class="Symbol">(</a><a id="2472" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#1130" class="InductiveConstructor">BiI.predr</a> <a id="2482" href="Cubical.Experiments.IsoInt.Base.html#2482" class="Bound">x</a><a id="2483" class="Symbol">)</a>       <a id="2491" class="Symbol">=</a> <a id="2493" href="Cubical.Experiments.IsoInt.Base.html#2375" class="Function">toS¹</a> <a id="2498" href="Cubical.Experiments.IsoInt.Base.html#2482" class="Bound">x</a>
  <a id="2502" href="Cubical.Experiments.IsoInt.Base.html#2375" class="Function">toS¹</a> <a id="2507" class="Symbol">(</a><a id="2508" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#1196" class="InductiveConstructor">BiI.predl</a> <a id="2518" href="Cubical.Experiments.IsoInt.Base.html#2518" class="Bound">x</a><a id="2519" class="Symbol">)</a>       <a id="2527" class="Symbol">=</a> <a id="2529" href="Cubical.Experiments.IsoInt.Base.html#2375" class="Function">toS¹</a> <a id="2534" href="Cubical.Experiments.IsoInt.Base.html#2518" class="Bound">x</a>
  <a id="2538" href="Cubical.Experiments.IsoInt.Base.html#2375" class="Function">toS¹</a> <a id="2543" class="Symbol">(</a><a id="2544" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#1157" class="InductiveConstructor">BiI.suc-predr</a> <a id="2558" href="Cubical.Experiments.IsoInt.Base.html#2558" class="Bound">x</a> <a id="2560" href="Cubical.Experiments.IsoInt.Base.html#2560" class="Bound">i</a><a id="2561" class="Symbol">)</a> <a id="2563" class="Symbol">=</a> <a id="2565" href="Cubical.Foundations.Prelude.html#915" class="Function">refl</a> <a id="2570" class="Symbol">{</a><a id="2571" class="Argument">x</a> <a id="2573" class="Symbol">=</a> <a id="2575" href="Cubical.Experiments.IsoInt.Base.html#2375" class="Function">toS¹</a> <a id="2580" href="Cubical.Experiments.IsoInt.Base.html#2558" class="Bound">x</a><a id="2581" class="Symbol">}</a> <a id="2583" href="Cubical.Experiments.IsoInt.Base.html#2560" class="Bound">i</a>
  <a id="2587" href="Cubical.Experiments.IsoInt.Base.html#2375" class="Function">toS¹</a> <a id="2592" class="Symbol">(</a><a id="2593" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#1223" class="InductiveConstructor">BiI.predl-suc</a> <a id="2607" href="Cubical.Experiments.IsoInt.Base.html#2607" class="Bound">x</a> <a id="2609" href="Cubical.Experiments.IsoInt.Base.html#2609" class="Bound">i</a><a id="2610" class="Symbol">)</a> <a id="2612" class="Symbol">=</a> <a id="2614" href="Cubical.HITs.S1.Base.html#14398" class="Function">rotLoop</a> <a id="2622" class="Symbol">(</a><a id="2623" href="Cubical.Experiments.IsoInt.Base.html#2375" class="Function">toS¹</a> <a id="2628" href="Cubical.Experiments.IsoInt.Base.html#2607" class="Bound">x</a><a id="2629" class="Symbol">)</a> <a id="2631" href="Cubical.Experiments.IsoInt.Base.html#2609" class="Bound">i</a>

  <a id="2636" class="Comment">-- still p₂ maps to loop...</a>
  <a id="p₂≡loop"></a><a id="2666" href="Cubical.Experiments.IsoInt.Base.html#2666" class="Function">p₂≡loop</a> <a id="2674" class="Symbol">:</a> <a id="2676" href="Cubical.Foundations.Prelude.html#1430" class="Function">cong</a> <a id="2681" href="Cubical.Experiments.IsoInt.Base.html#2375" class="Function">toS¹</a> <a id="2686" href="Cubical.Experiments.IsoInt.Base.html#2179" class="Function">p₂</a> <a id="2689" href="Agda.Builtin.Cubical.Path.html#388" class="Function Operator">≡</a> <a id="2691" href="Cubical.HITs.S1.Base.html#648" class="InductiveConstructor">loop</a>
  <a id="2698" href="Cubical.Experiments.IsoInt.Base.html#2666" class="Function">p₂≡loop</a> <a id="2706" class="Symbol">=</a> <a id="2708" href="Cubical.Foundations.Prelude.html#915" class="Function">refl</a>

  <a id="2716" class="Keyword">open</a> <a id="2721" class="Keyword">import</a> <a id="2728" href="Cubical.Foundations.GroupoidLaws.html" class="Module">Cubical.Foundations.GroupoidLaws</a>

  <a id="2764" class="Comment">-- ...but now so does p₁!</a>
  <a id="p₁≡loop"></a><a id="2792" href="Cubical.Experiments.IsoInt.Base.html#2792" class="Function">p₁≡loop</a> <a id="2800" class="Symbol">:</a> <a id="2802" href="Cubical.Foundations.Prelude.html#1430" class="Function">cong</a> <a id="2807" href="Cubical.Experiments.IsoInt.Base.html#2375" class="Function">toS¹</a> <a id="2812" href="Cubical.Experiments.IsoInt.Base.html#2176" class="Function">p₁</a> <a id="2815" href="Agda.Builtin.Cubical.Path.html#388" class="Function Operator">≡</a> <a id="2817" href="Cubical.HITs.S1.Base.html#648" class="InductiveConstructor">loop</a>
  <a id="2824" href="Cubical.Experiments.IsoInt.Base.html#2792" class="Function">p₁≡loop</a> <a id="2832" class="Symbol">=</a> <a id="2834" href="Cubical.Foundations.Prelude.html#968" class="Function">sym</a> <a id="2838" class="Symbol">(</a><a id="2839" href="Cubical.HITs.S1.Base.html#2290" class="Function">decodeEncode</a> <a id="2852" href="Cubical.HITs.S1.Base.html#636" class="InductiveConstructor">base</a> <a id="2857" class="Symbol">(</a><a id="2858" href="Cubical.Foundations.Prelude.html#1430" class="Function">cong</a> <a id="2863" href="Cubical.Experiments.IsoInt.Base.html#2375" class="Function">toS¹</a> <a id="2868" href="Cubical.Experiments.IsoInt.Base.html#2176" class="Function">p₁</a><a id="2870" class="Symbol">))</a> <a id="2873" href="Cubical.Foundations.Prelude.html#4447" class="Function Operator">∙</a> <a id="2875" href="Cubical.Foundations.Prelude.html#968" class="Function">sym</a> <a id="2879" class="Symbol">(</a><a id="2880" href="Cubical.Foundations.GroupoidLaws.html#989" class="Function">lUnit</a> <a id="2886" href="Cubical.HITs.S1.Base.html#648" class="InductiveConstructor">loop</a><a id="2890" class="Symbol">)</a>

  <a id="2895" class="Comment">-- if we use BiI.predr instead of BiI.pred (≡ BiI.predl) in p₁ and p₂,</a>
  <a id="2968" class="Comment">--  both paths in S¹ are refl</a>
</pre></body></html>