<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Cubical.Experiments.IsoInt.Base</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">{-

The naive, but incorrect, way to define the integers as a HIT.

This file mainly contains a proof that IsoInt ≢ Int, and ends with a
 demonstration of how the same proof strategy fails for BiInvℤ.

-}</a>
<a id="206" class="Keyword">module</a> <a id="213" href="Cubical.Experiments.IsoInt.Base.html" class="Module">Cubical.Experiments.IsoInt.Base</a> <a id="245" class="Keyword">where</a>

<a id="252" class="Keyword">open</a> <a id="257" class="Keyword">import</a> <a id="264" href="Cubical.Foundations.Prelude.html" class="Module">Cubical.Foundations.Prelude</a>
<a id="292" class="Keyword">open</a> <a id="297" class="Keyword">import</a> <a id="304" href="Cubical.Foundations.Function.html" class="Module">Cubical.Foundations.Function</a>
<a id="333" class="Keyword">open</a> <a id="338" class="Keyword">import</a> <a id="345" href="Cubical.Foundations.Isomorphism.html" class="Module">Cubical.Foundations.Isomorphism</a>

<a id="378" class="Keyword">open</a> <a id="383" class="Keyword">import</a> <a id="390" href="Cubical.Data.Int.html" class="Module">Cubical.Data.Int</a>
<a id="407" class="Keyword">open</a> <a id="412" class="Keyword">import</a> <a id="419" href="Cubical.Data.Nat.html" class="Module">Cubical.Data.Nat</a>
<a id="436" class="Keyword">open</a> <a id="441" class="Keyword">import</a> <a id="448" href="Cubical.Data.Empty.html" class="Module">Cubical.Data.Empty</a>

<a id="468" class="Keyword">open</a> <a id="473" class="Keyword">import</a> <a id="480" href="Cubical.Relation.Nullary.html" class="Module">Cubical.Relation.Nullary</a>


<a id="507" class="Keyword">data</a> <a id="IsoInt"></a><a id="512" href="Cubical.Experiments.IsoInt.Base.html#512" class="Datatype">IsoInt</a> <a id="519" class="Symbol">:</a> <a id="521" href="Agda.Primitive.html#388" class="Primitive">Type₀</a> <a id="527" class="Keyword">where</a>
  <a id="IsoInt.zero"></a><a id="535" href="Cubical.Experiments.IsoInt.Base.html#535" class="InductiveConstructor">zero</a> <a id="540" class="Symbol">:</a> <a id="542" href="Cubical.Experiments.IsoInt.Base.html#512" class="Datatype">IsoInt</a>
  <a id="IsoInt.suc"></a><a id="551" href="Cubical.Experiments.IsoInt.Base.html#551" class="InductiveConstructor">suc</a> <a id="555" class="Symbol">:</a> <a id="557" href="Cubical.Experiments.IsoInt.Base.html#512" class="Datatype">IsoInt</a> <a id="564" class="Symbol">-&gt;</a> <a id="567" href="Cubical.Experiments.IsoInt.Base.html#512" class="Datatype">IsoInt</a>

  <a id="577" class="Comment">-- suc is an isomorphism:</a>
  <a id="IsoInt.pred"></a><a id="605" href="Cubical.Experiments.IsoInt.Base.html#605" class="InductiveConstructor">pred</a> <a id="610" class="Symbol">:</a> <a id="612" href="Cubical.Experiments.IsoInt.Base.html#512" class="Datatype">IsoInt</a> <a id="619" class="Symbol">-&gt;</a> <a id="622" href="Cubical.Experiments.IsoInt.Base.html#512" class="Datatype">IsoInt</a>
  <a id="IsoInt.suc-pred"></a><a id="631" href="Cubical.Experiments.IsoInt.Base.html#631" class="InductiveConstructor">suc-pred</a> <a id="640" class="Symbol">:</a> <a id="642" class="Symbol">∀</a> <a id="644" href="Cubical.Experiments.IsoInt.Base.html#644" class="Bound">z</a> <a id="646" class="Symbol">-&gt;</a> <a id="649" href="Cubical.Experiments.IsoInt.Base.html#551" class="InductiveConstructor">suc</a> <a id="653" class="Symbol">(</a><a id="654" href="Cubical.Experiments.IsoInt.Base.html#605" class="InductiveConstructor">pred</a> <a id="659" href="Cubical.Experiments.IsoInt.Base.html#644" class="Bound">z</a><a id="660" class="Symbol">)</a> <a id="662" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="664" href="Cubical.Experiments.IsoInt.Base.html#644" class="Bound">z</a>
  <a id="IsoInt.pred-suc"></a><a id="668" href="Cubical.Experiments.IsoInt.Base.html#668" class="InductiveConstructor">pred-suc</a> <a id="677" class="Symbol">:</a> <a id="679" class="Symbol">∀</a> <a id="681" href="Cubical.Experiments.IsoInt.Base.html#681" class="Bound">z</a> <a id="683" class="Symbol">-&gt;</a> <a id="686" href="Cubical.Experiments.IsoInt.Base.html#605" class="InductiveConstructor">pred</a> <a id="691" class="Symbol">(</a><a id="692" href="Cubical.Experiments.IsoInt.Base.html#551" class="InductiveConstructor">suc</a> <a id="696" href="Cubical.Experiments.IsoInt.Base.html#681" class="Bound">z</a><a id="697" class="Symbol">)</a> <a id="699" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="701" href="Cubical.Experiments.IsoInt.Base.html#681" class="Bound">z</a>


<a id="suc-iso"></a><a id="705" href="Cubical.Experiments.IsoInt.Base.html#705" class="Function">suc-iso</a> <a id="713" class="Symbol">:</a> <a id="715" href="Cubical.Foundations.Isomorphism.html#744" class="Record">Iso</a> <a id="719" href="Cubical.Experiments.IsoInt.Base.html#512" class="Datatype">IsoInt</a> <a id="726" href="Cubical.Experiments.IsoInt.Base.html#512" class="Datatype">IsoInt</a>
<a id="733" href="Cubical.Experiments.IsoInt.Base.html#705" class="Function">suc-iso</a> <a id="741" class="Symbol">=</a> <a id="743" class="Keyword">record</a> <a id="750" class="Symbol">{</a> <a id="752" href="Cubical.Foundations.Isomorphism.html#856" class="Field">fun</a> <a id="756" class="Symbol">=</a> <a id="758" href="Cubical.Experiments.IsoInt.Base.html#551" class="InductiveConstructor">suc</a> <a id="762" class="Symbol">;</a> <a id="764" href="Cubical.Foundations.Isomorphism.html#872" class="Field">inv</a> <a id="768" class="Symbol">=</a> <a id="770" href="Cubical.Experiments.IsoInt.Base.html#605" class="InductiveConstructor">pred</a> <a id="775" class="Symbol">;</a> <a id="777" href="Cubical.Foundations.Isomorphism.html#888" class="Field">rightInv</a> <a id="786" class="Symbol">=</a> <a id="788" href="Cubical.Experiments.IsoInt.Base.html#631" class="InductiveConstructor">suc-pred</a> <a id="797" class="Symbol">;</a> <a id="799" href="Cubical.Foundations.Isomorphism.html#919" class="Field">leftInv</a> <a id="807" class="Symbol">=</a> <a id="809" href="Cubical.Experiments.IsoInt.Base.html#668" class="InductiveConstructor">pred-suc</a> <a id="818" class="Symbol">}</a>


<a id="822" class="Comment">-- this submodule is adapted from Section 5 of</a>
<a id="869" class="Comment">--  http://www.cs.ru.nl/~herman/PUBS/HIT-programming.pdf</a>

<a id="927" class="Keyword">module</a> <a id="NonTrivial"></a><a id="934" href="Cubical.Experiments.IsoInt.Base.html#934" class="Module">NonTrivial</a> <a id="945" class="Keyword">where</a>

  <a id="954" class="Comment">-- these two paths are distinct!</a>
  <a id="NonTrivial.p₁"></a><a id="989" href="Cubical.Experiments.IsoInt.Base.html#989" class="Function">p₁</a> <a id="NonTrivial.p₂"></a><a id="992" href="Cubical.Experiments.IsoInt.Base.html#992" class="Function">p₂</a> <a id="995" class="Symbol">:</a> <a id="997" href="Cubical.Core.Primitives.html#1805" class="Function">Path</a> <a id="1002" href="Cubical.Experiments.IsoInt.Base.html#512" class="Datatype">IsoInt</a> <a id="1009" class="Symbol">(</a><a id="1010" href="Cubical.Experiments.IsoInt.Base.html#551" class="InductiveConstructor">suc</a> <a id="1014" class="Symbol">(</a><a id="1015" href="Cubical.Experiments.IsoInt.Base.html#605" class="InductiveConstructor">pred</a> <a id="1020" class="Symbol">(</a><a id="1021" href="Cubical.Experiments.IsoInt.Base.html#551" class="InductiveConstructor">suc</a> <a id="1025" href="Cubical.Experiments.IsoInt.Base.html#535" class="InductiveConstructor">zero</a><a id="1029" class="Symbol">)))</a> <a id="1033" class="Symbol">(</a><a id="1034" href="Cubical.Experiments.IsoInt.Base.html#551" class="InductiveConstructor">suc</a> <a id="1038" href="Cubical.Experiments.IsoInt.Base.html#535" class="InductiveConstructor">zero</a><a id="1042" class="Symbol">)</a>
  <a id="1046" href="Cubical.Experiments.IsoInt.Base.html#989" class="Function">p₁</a> <a id="1049" href="Cubical.Experiments.IsoInt.Base.html#1049" class="Bound">i</a> <a id="1051" class="Symbol">=</a> <a id="1053" href="Cubical.Experiments.IsoInt.Base.html#631" class="InductiveConstructor">suc-pred</a> <a id="1062" class="Symbol">(</a><a id="1063" href="Cubical.Experiments.IsoInt.Base.html#551" class="InductiveConstructor">suc</a> <a id="1067" href="Cubical.Experiments.IsoInt.Base.html#535" class="InductiveConstructor">zero</a><a id="1071" class="Symbol">)</a> <a id="1073" href="Cubical.Experiments.IsoInt.Base.html#1049" class="Bound">i</a>
  <a id="1077" href="Cubical.Experiments.IsoInt.Base.html#992" class="Function">p₂</a> <a id="1080" href="Cubical.Experiments.IsoInt.Base.html#1080" class="Bound">i</a> <a id="1082" class="Symbol">=</a> <a id="1084" href="Cubical.Experiments.IsoInt.Base.html#551" class="InductiveConstructor">suc</a> <a id="1088" class="Symbol">(</a><a id="1089" href="Cubical.Experiments.IsoInt.Base.html#668" class="InductiveConstructor">pred-suc</a> <a id="1098" href="Cubical.Experiments.IsoInt.Base.html#535" class="InductiveConstructor">zero</a> <a id="1103" href="Cubical.Experiments.IsoInt.Base.html#1080" class="Bound">i</a><a id="1104" class="Symbol">)</a>

  <a id="1109" class="Comment">-- to prove this we map into S¹, sending p₁ to refl and p₂ to loop</a>

  <a id="1179" class="Keyword">open</a> <a id="1184" class="Keyword">import</a> <a id="1191" href="Cubical.HITs.S1.html" class="Module">Cubical.HITs.S1</a>

  <a id="NonTrivial.toS¹"></a><a id="1210" href="Cubical.Experiments.IsoInt.Base.html#1210" class="Function">toS¹</a> <a id="1215" class="Symbol">:</a> <a id="1217" href="Cubical.Experiments.IsoInt.Base.html#512" class="Datatype">IsoInt</a> <a id="1224" class="Symbol">→</a> <a id="1226" href="Cubical.HITs.S1.Base.html#594" class="Datatype">S¹</a>
  <a id="1231" href="Cubical.Experiments.IsoInt.Base.html#1210" class="Function">toS¹</a> <a id="1236" href="Cubical.Experiments.IsoInt.Base.html#535" class="InductiveConstructor">zero</a>           <a id="1251" class="Symbol">=</a> <a id="1253" href="Cubical.HITs.S1.Base.html#613" class="InductiveConstructor">base</a>
  <a id="1260" href="Cubical.Experiments.IsoInt.Base.html#1210" class="Function">toS¹</a> <a id="1265" class="Symbol">(</a><a id="1266" href="Cubical.Experiments.IsoInt.Base.html#551" class="InductiveConstructor">suc</a> <a id="1270" href="Cubical.Experiments.IsoInt.Base.html#1270" class="Bound">x</a><a id="1271" class="Symbol">)</a>        <a id="1280" class="Symbol">=</a> <a id="1282" href="Cubical.Experiments.IsoInt.Base.html#1210" class="Function">toS¹</a> <a id="1287" href="Cubical.Experiments.IsoInt.Base.html#1270" class="Bound">x</a>
  <a id="1291" href="Cubical.Experiments.IsoInt.Base.html#1210" class="Function">toS¹</a> <a id="1296" class="Symbol">(</a><a id="1297" href="Cubical.Experiments.IsoInt.Base.html#605" class="InductiveConstructor">pred</a> <a id="1302" href="Cubical.Experiments.IsoInt.Base.html#1302" class="Bound">x</a><a id="1303" class="Symbol">)</a>       <a id="1311" class="Symbol">=</a> <a id="1313" href="Cubical.Experiments.IsoInt.Base.html#1210" class="Function">toS¹</a> <a id="1318" href="Cubical.Experiments.IsoInt.Base.html#1302" class="Bound">x</a>
  <a id="1322" href="Cubical.Experiments.IsoInt.Base.html#1210" class="Function">toS¹</a> <a id="1327" class="Symbol">(</a><a id="1328" href="Cubical.Experiments.IsoInt.Base.html#631" class="InductiveConstructor">suc-pred</a> <a id="1337" href="Cubical.Experiments.IsoInt.Base.html#1337" class="Bound">x</a> <a id="1339" href="Cubical.Experiments.IsoInt.Base.html#1339" class="Bound">i</a><a id="1340" class="Symbol">)</a> <a id="1342" class="Symbol">=</a> <a id="1344" href="Cubical.Foundations.Prelude.html#892" class="Function">refl</a> <a id="1349" class="Symbol">{</a><a id="1350" class="Argument">x</a> <a id="1352" class="Symbol">=</a> <a id="1354" href="Cubical.Experiments.IsoInt.Base.html#1210" class="Function">toS¹</a> <a id="1359" href="Cubical.Experiments.IsoInt.Base.html#1337" class="Bound">x</a><a id="1360" class="Symbol">}</a> <a id="1362" href="Cubical.Experiments.IsoInt.Base.html#1339" class="Bound">i</a>
  <a id="1366" href="Cubical.Experiments.IsoInt.Base.html#1210" class="Function">toS¹</a> <a id="1371" class="Symbol">(</a><a id="1372" href="Cubical.Experiments.IsoInt.Base.html#668" class="InductiveConstructor">pred-suc</a> <a id="1381" href="Cubical.Experiments.IsoInt.Base.html#1381" class="Bound">x</a> <a id="1383" href="Cubical.Experiments.IsoInt.Base.html#1383" class="Bound">i</a><a id="1384" class="Symbol">)</a> <a id="1386" class="Symbol">=</a> <a id="1388" href="Cubical.HITs.S1.Base.html#14375" class="Function">rotLoop</a> <a id="1396" class="Symbol">(</a><a id="1397" href="Cubical.Experiments.IsoInt.Base.html#1210" class="Function">toS¹</a> <a id="1402" href="Cubical.Experiments.IsoInt.Base.html#1381" class="Bound">x</a><a id="1403" class="Symbol">)</a> <a id="1405" href="Cubical.Experiments.IsoInt.Base.html#1383" class="Bound">i</a>

  <a id="NonTrivial.p₁≡refl"></a><a id="1410" href="Cubical.Experiments.IsoInt.Base.html#1410" class="Function">p₁≡refl</a> <a id="1418" class="Symbol">:</a> <a id="1420" href="Cubical.Foundations.Prelude.html#1407" class="Function">cong</a> <a id="1425" href="Cubical.Experiments.IsoInt.Base.html#1210" class="Function">toS¹</a> <a id="1430" href="Cubical.Experiments.IsoInt.Base.html#989" class="Function">p₁</a> <a id="1433" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="1435" href="Cubical.Foundations.Prelude.html#892" class="Function">refl</a>
  <a id="1442" href="Cubical.Experiments.IsoInt.Base.html#1410" class="Function">p₁≡refl</a> <a id="1450" class="Symbol">=</a> <a id="1452" href="Cubical.Foundations.Prelude.html#892" class="Function">refl</a>

  <a id="NonTrivial.p₂≡loop"></a><a id="1460" href="Cubical.Experiments.IsoInt.Base.html#1460" class="Function">p₂≡loop</a> <a id="1468" class="Symbol">:</a> <a id="1470" href="Cubical.Foundations.Prelude.html#1407" class="Function">cong</a> <a id="1475" href="Cubical.Experiments.IsoInt.Base.html#1210" class="Function">toS¹</a> <a id="1480" href="Cubical.Experiments.IsoInt.Base.html#992" class="Function">p₂</a> <a id="1483" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="1485" href="Cubical.HITs.S1.Base.html#625" class="InductiveConstructor">loop</a>
  <a id="1492" href="Cubical.Experiments.IsoInt.Base.html#1460" class="Function">p₂≡loop</a> <a id="1500" class="Symbol">=</a> <a id="1502" href="Cubical.Foundations.Prelude.html#892" class="Function">refl</a>

  <a id="1510" class="Comment">-- this is enough to show that p₁ and p₂ cannot be equal</a>
  <a id="NonTrivial.p₁≢p₂"></a><a id="1569" href="Cubical.Experiments.IsoInt.Base.html#1569" class="Function">p₁≢p₂</a> <a id="1575" class="Symbol">:</a> <a id="1577" href="Cubical.Relation.Nullary.Base.html#332" class="Function Operator">¬</a> <a id="1579" class="Symbol">(</a><a id="1580" href="Cubical.Experiments.IsoInt.Base.html#989" class="Function">p₁</a> <a id="1583" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="1585" href="Cubical.Experiments.IsoInt.Base.html#992" class="Function">p₂</a><a id="1587" class="Symbol">)</a>
  <a id="1591" href="Cubical.Experiments.IsoInt.Base.html#1569" class="Function">p₁≢p₂</a> <a id="1597" href="Cubical.Experiments.IsoInt.Base.html#1597" class="Bound">eq</a> <a id="1600" class="Symbol">=</a> <a id="1602" href="Cubical.Data.Nat.Properties.html#1020" class="Function">znots</a> <a id="1608" href="Cubical.Experiments.IsoInt.Base.html#1680" class="Function">0≡1</a>
    <a id="1616" class="Keyword">where</a> <a id="1622" class="Comment">-- using winding numbers, p₁ ≡ p₂ implies 0 ≡ 1</a>
          <a id="1680" href="Cubical.Experiments.IsoInt.Base.html#1680" class="Function">0≡1</a> <a id="1684" class="Symbol">:</a> <a id="1686" class="Number">0</a> <a id="1688" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="1690" class="Number">1</a>
          <a id="1702" href="Cubical.Experiments.IsoInt.Base.html#1680" class="Function">0≡1</a> <a id="1706" class="Symbol">=</a> <a id="1708" href="Cubical.Data.Int.Properties.html#17527" class="Function">injPos</a> <a id="1715" class="Symbol">(</a><a id="1716" href="Cubical.Foundations.Prelude.html#1407" class="Function">cong</a> <a id="1721" class="Symbol">(</a><a id="1722" href="Cubical.HITs.S1.Base.html#1139" class="Function">winding</a> <a id="1730" href="Cubical.Foundations.Function.html#898" class="Function Operator">∘</a> <a id="1732" href="Cubical.Foundations.Prelude.html#1407" class="Function">cong</a> <a id="1737" href="Cubical.Experiments.IsoInt.Base.html#1210" class="Function">toS¹</a><a id="1741" class="Symbol">)</a> <a id="1743" href="Cubical.Experiments.IsoInt.Base.html#1597" class="Bound">eq</a><a id="1745" class="Symbol">)</a>


<a id="¬isSet-IsoInt"></a><a id="1749" href="Cubical.Experiments.IsoInt.Base.html#1749" class="Function">¬isSet-IsoInt</a> <a id="1763" class="Symbol">:</a> <a id="1765" href="Cubical.Relation.Nullary.Base.html#332" class="Function Operator">¬</a> <a id="1767" class="Symbol">(</a><a id="1768" href="Cubical.Foundations.Prelude.html#15579" class="Function">isSet</a> <a id="1774" href="Cubical.Experiments.IsoInt.Base.html#512" class="Datatype">IsoInt</a><a id="1780" class="Symbol">)</a>
<a id="1782" href="Cubical.Experiments.IsoInt.Base.html#1749" class="Function">¬isSet-IsoInt</a> <a id="1796" href="Cubical.Experiments.IsoInt.Base.html#1796" class="Bound">pf</a> <a id="1799" class="Symbol">=</a> <a id="1801" href="Cubical.Experiments.IsoInt.Base.html#1569" class="Function">NonTrivial.p₁≢p₂</a> <a id="1818" class="Symbol">(</a><a id="1819" href="Cubical.Experiments.IsoInt.Base.html#1796" class="Bound">pf</a> <a id="1822" class="Symbol">_</a> <a id="1824" class="Symbol">_</a> <a id="1826" href="Cubical.Experiments.IsoInt.Base.html#989" class="Function">NonTrivial.p₁</a> <a id="1840" href="Cubical.Experiments.IsoInt.Base.html#992" class="Function">NonTrivial.p₂</a><a id="1853" class="Symbol">)</a>

<a id="¬Int≡IsoInt"></a><a id="1856" href="Cubical.Experiments.IsoInt.Base.html#1856" class="Function">¬Int≡IsoInt</a> <a id="1868" class="Symbol">:</a> <a id="1870" href="Cubical.Relation.Nullary.Base.html#332" class="Function Operator">¬</a> <a id="1872" class="Symbol">(</a><a id="1873" href="Cubical.Data.Int.Base.html#419" class="Datatype">ℤ</a> <a id="1875" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="1877" href="Cubical.Experiments.IsoInt.Base.html#512" class="Datatype">IsoInt</a><a id="1883" class="Symbol">)</a>
<a id="1885" href="Cubical.Experiments.IsoInt.Base.html#1856" class="Function">¬Int≡IsoInt</a> <a id="1897" href="Cubical.Experiments.IsoInt.Base.html#1897" class="Bound">p</a> <a id="1899" class="Symbol">=</a> <a id="1901" href="Cubical.Experiments.IsoInt.Base.html#1749" class="Function">¬isSet-IsoInt</a> <a id="1915" class="Symbol">(</a><a id="1916" href="Cubical.Foundations.Prelude.html#10205" class="Function">subst</a> <a id="1922" href="Cubical.Foundations.Prelude.html#15579" class="Function">isSet</a> <a id="1928" href="Cubical.Experiments.IsoInt.Base.html#1897" class="Bound">p</a> <a id="1930" href="Cubical.Data.Int.Properties.html#18762" class="Function">isSetℤ</a><a id="1936" class="Symbol">)</a>



<a id="1941" class="Keyword">private</a>

  <a id="1952" class="Comment">-- Note: this same proof strategy fails for BiInvℤ!</a>

  <a id="2007" class="Keyword">open</a> <a id="2012" class="Keyword">import</a> <a id="2019" href="Cubical.Data.Int.MoreInts.BiInvInt.html" class="Module">Cubical.Data.Int.MoreInts.BiInvInt</a> <a id="2054" class="Keyword">hiding</a> <a id="2061" class="Symbol">(</a><a id="2062" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#986" class="InductiveConstructor">zero</a><a id="2066" class="Symbol">;</a> <a id="2068" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#1002" class="InductiveConstructor">suc</a><a id="2071" class="Symbol">;</a> <a id="2073" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#2495" class="Function">pred</a><a id="2077" class="Symbol">;</a> <a id="2079" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#2533" class="Function">suc-pred</a><a id="2087" class="Symbol">;</a> <a id="2089" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#2554" class="Function">pred-suc</a><a id="2097" class="Symbol">)</a>
  <a id="2101" class="Keyword">import</a> <a id="2108" href="Cubical.Data.Int.MoreInts.BiInvInt.html" class="Module">Cubical.Data.Int.MoreInts.BiInvInt</a> <a id="2143" class="Symbol">as</a> <a id="2146" class="Module">BiI</a>

  <a id="p₁"></a><a id="2153" href="Cubical.Experiments.IsoInt.Base.html#2153" class="Function">p₁</a> <a id="p₂"></a><a id="2156" href="Cubical.Experiments.IsoInt.Base.html#2156" class="Function">p₂</a> <a id="2159" class="Symbol">:</a> <a id="2161" href="Cubical.Core.Primitives.html#1805" class="Function">Path</a> <a id="2166" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#963" class="Datatype">BiInvℤ</a> <a id="2173" class="Symbol">(</a><a id="2174" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#1002" class="InductiveConstructor">BiI.suc</a> <a id="2182" class="Symbol">(</a><a id="2183" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#2495" class="Function">BiI.pred</a> <a id="2192" class="Symbol">(</a><a id="2193" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#1002" class="InductiveConstructor">BiI.suc</a> <a id="2201" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#986" class="InductiveConstructor">BiI.zero</a><a id="2209" class="Symbol">)))</a> <a id="2213" class="Symbol">(</a><a id="2214" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#1002" class="InductiveConstructor">BiI.suc</a> <a id="2222" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#986" class="InductiveConstructor">BiI.zero</a><a id="2230" class="Symbol">)</a>
  <a id="2234" href="Cubical.Experiments.IsoInt.Base.html#2153" class="Function">p₁</a> <a id="2237" href="Cubical.Experiments.IsoInt.Base.html#2237" class="Bound">i</a> <a id="2239" class="Symbol">=</a> <a id="2241" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#2533" class="Function">BiI.suc-pred</a> <a id="2254" class="Symbol">(</a><a id="2255" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#1002" class="InductiveConstructor">BiI.suc</a> <a id="2263" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#986" class="InductiveConstructor">BiI.zero</a><a id="2271" class="Symbol">)</a> <a id="2273" href="Cubical.Experiments.IsoInt.Base.html#2237" class="Bound">i</a>
  <a id="2277" href="Cubical.Experiments.IsoInt.Base.html#2156" class="Function">p₂</a> <a id="2280" href="Cubical.Experiments.IsoInt.Base.html#2280" class="Bound">i</a> <a id="2282" class="Symbol">=</a> <a id="2284" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#1002" class="InductiveConstructor">BiI.suc</a> <a id="2292" class="Symbol">(</a><a id="2293" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#2554" class="Function">BiI.pred-suc</a> <a id="2306" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#986" class="InductiveConstructor">BiI.zero</a> <a id="2315" href="Cubical.Experiments.IsoInt.Base.html#2280" class="Bound">i</a><a id="2316" class="Symbol">)</a>

  <a id="2321" class="Keyword">open</a> <a id="2326" class="Keyword">import</a> <a id="2333" href="Cubical.HITs.S1.html" class="Module">Cubical.HITs.S1</a>

  <a id="toS¹"></a><a id="2352" href="Cubical.Experiments.IsoInt.Base.html#2352" class="Function">toS¹</a> <a id="2357" class="Symbol">:</a> <a id="2359" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#963" class="Datatype">BiInvℤ</a> <a id="2366" class="Symbol">→</a> <a id="2368" href="Cubical.HITs.S1.Base.html#594" class="Datatype">S¹</a>
  <a id="2373" href="Cubical.Experiments.IsoInt.Base.html#2352" class="Function">toS¹</a> <a id="2378" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#986" class="InductiveConstructor">BiI.zero</a>            <a id="2398" class="Symbol">=</a> <a id="2400" href="Cubical.HITs.S1.Base.html#613" class="InductiveConstructor">base</a>
  <a id="2407" href="Cubical.Experiments.IsoInt.Base.html#2352" class="Function">toS¹</a> <a id="2412" class="Symbol">(</a><a id="2413" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#1002" class="InductiveConstructor">BiI.suc</a> <a id="2421" href="Cubical.Experiments.IsoInt.Base.html#2421" class="Bound">x</a><a id="2422" class="Symbol">)</a>         <a id="2432" class="Symbol">=</a> <a id="2434" href="Cubical.Experiments.IsoInt.Base.html#2352" class="Function">toS¹</a> <a id="2439" href="Cubical.Experiments.IsoInt.Base.html#2421" class="Bound">x</a>
  <a id="2443" href="Cubical.Experiments.IsoInt.Base.html#2352" class="Function">toS¹</a> <a id="2448" class="Symbol">(</a><a id="2449" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#1069" class="InductiveConstructor">BiI.predr</a> <a id="2459" href="Cubical.Experiments.IsoInt.Base.html#2459" class="Bound">x</a><a id="2460" class="Symbol">)</a>       <a id="2468" class="Symbol">=</a> <a id="2470" href="Cubical.Experiments.IsoInt.Base.html#2352" class="Function">toS¹</a> <a id="2475" href="Cubical.Experiments.IsoInt.Base.html#2459" class="Bound">x</a>
  <a id="2479" href="Cubical.Experiments.IsoInt.Base.html#2352" class="Function">toS¹</a> <a id="2484" class="Symbol">(</a><a id="2485" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#1135" class="InductiveConstructor">BiI.predl</a> <a id="2495" href="Cubical.Experiments.IsoInt.Base.html#2495" class="Bound">x</a><a id="2496" class="Symbol">)</a>       <a id="2504" class="Symbol">=</a> <a id="2506" href="Cubical.Experiments.IsoInt.Base.html#2352" class="Function">toS¹</a> <a id="2511" href="Cubical.Experiments.IsoInt.Base.html#2495" class="Bound">x</a>
  <a id="2515" href="Cubical.Experiments.IsoInt.Base.html#2352" class="Function">toS¹</a> <a id="2520" class="Symbol">(</a><a id="2521" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#1096" class="InductiveConstructor">BiI.suc-predr</a> <a id="2535" href="Cubical.Experiments.IsoInt.Base.html#2535" class="Bound">x</a> <a id="2537" href="Cubical.Experiments.IsoInt.Base.html#2537" class="Bound">i</a><a id="2538" class="Symbol">)</a> <a id="2540" class="Symbol">=</a> <a id="2542" href="Cubical.Foundations.Prelude.html#892" class="Function">refl</a> <a id="2547" class="Symbol">{</a><a id="2548" class="Argument">x</a> <a id="2550" class="Symbol">=</a> <a id="2552" href="Cubical.Experiments.IsoInt.Base.html#2352" class="Function">toS¹</a> <a id="2557" href="Cubical.Experiments.IsoInt.Base.html#2535" class="Bound">x</a><a id="2558" class="Symbol">}</a> <a id="2560" href="Cubical.Experiments.IsoInt.Base.html#2537" class="Bound">i</a>
  <a id="2564" href="Cubical.Experiments.IsoInt.Base.html#2352" class="Function">toS¹</a> <a id="2569" class="Symbol">(</a><a id="2570" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#1162" class="InductiveConstructor">BiI.predl-suc</a> <a id="2584" href="Cubical.Experiments.IsoInt.Base.html#2584" class="Bound">x</a> <a id="2586" href="Cubical.Experiments.IsoInt.Base.html#2586" class="Bound">i</a><a id="2587" class="Symbol">)</a> <a id="2589" class="Symbol">=</a> <a id="2591" href="Cubical.HITs.S1.Base.html#14375" class="Function">rotLoop</a> <a id="2599" class="Symbol">(</a><a id="2600" href="Cubical.Experiments.IsoInt.Base.html#2352" class="Function">toS¹</a> <a id="2605" href="Cubical.Experiments.IsoInt.Base.html#2584" class="Bound">x</a><a id="2606" class="Symbol">)</a> <a id="2608" href="Cubical.Experiments.IsoInt.Base.html#2586" class="Bound">i</a>

  <a id="2613" class="Comment">-- still p₂ maps to loop...</a>
  <a id="p₂≡loop"></a><a id="2643" href="Cubical.Experiments.IsoInt.Base.html#2643" class="Function">p₂≡loop</a> <a id="2651" class="Symbol">:</a> <a id="2653" href="Cubical.Foundations.Prelude.html#1407" class="Function">cong</a> <a id="2658" href="Cubical.Experiments.IsoInt.Base.html#2352" class="Function">toS¹</a> <a id="2663" href="Cubical.Experiments.IsoInt.Base.html#2156" class="Function">p₂</a> <a id="2666" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="2668" href="Cubical.HITs.S1.Base.html#625" class="InductiveConstructor">loop</a>
  <a id="2675" href="Cubical.Experiments.IsoInt.Base.html#2643" class="Function">p₂≡loop</a> <a id="2683" class="Symbol">=</a> <a id="2685" href="Cubical.Foundations.Prelude.html#892" class="Function">refl</a>

  <a id="2693" class="Keyword">open</a> <a id="2698" class="Keyword">import</a> <a id="2705" href="Cubical.Foundations.GroupoidLaws.html" class="Module">Cubical.Foundations.GroupoidLaws</a>

  <a id="2741" class="Comment">-- ...but now so does p₁!</a>
  <a id="p₁≡loop"></a><a id="2769" href="Cubical.Experiments.IsoInt.Base.html#2769" class="Function">p₁≡loop</a> <a id="2777" class="Symbol">:</a> <a id="2779" href="Cubical.Foundations.Prelude.html#1407" class="Function">cong</a> <a id="2784" href="Cubical.Experiments.IsoInt.Base.html#2352" class="Function">toS¹</a> <a id="2789" href="Cubical.Experiments.IsoInt.Base.html#2153" class="Function">p₁</a> <a id="2792" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="2794" href="Cubical.HITs.S1.Base.html#625" class="InductiveConstructor">loop</a>
  <a id="2801" href="Cubical.Experiments.IsoInt.Base.html#2769" class="Function">p₁≡loop</a> <a id="2809" class="Symbol">=</a> <a id="2811" href="Cubical.Foundations.Prelude.html#945" class="Function">sym</a> <a id="2815" class="Symbol">(</a><a id="2816" href="Cubical.HITs.S1.Base.html#2267" class="Function">decodeEncode</a> <a id="2829" href="Cubical.HITs.S1.Base.html#613" class="InductiveConstructor">base</a> <a id="2834" class="Symbol">(</a><a id="2835" href="Cubical.Foundations.Prelude.html#1407" class="Function">cong</a> <a id="2840" href="Cubical.Experiments.IsoInt.Base.html#2352" class="Function">toS¹</a> <a id="2845" href="Cubical.Experiments.IsoInt.Base.html#2153" class="Function">p₁</a><a id="2847" class="Symbol">))</a> <a id="2850" href="Cubical.Foundations.Prelude.html#5184" class="Function Operator">∙</a> <a id="2852" href="Cubical.Foundations.Prelude.html#945" class="Function">sym</a> <a id="2856" class="Symbol">(</a><a id="2857" href="Cubical.Foundations.GroupoidLaws.html#966" class="Function">lUnit</a> <a id="2863" href="Cubical.HITs.S1.Base.html#625" class="InductiveConstructor">loop</a><a id="2867" class="Symbol">)</a>

  <a id="2872" class="Comment">-- if we use BiI.predr instead of BiI.pred (≡ BiI.predl) in p₁ and p₂,</a>
  <a id="2945" class="Comment">--  both paths in S¹ are refl</a>
</pre></body></html>