<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Cubical.Experiments.IsoInt.Base</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">{-

The naive, but incorrect, way to define the integers as a HIT.

This file mainly contains a proof that IsoInt ≢ Int, and ends with a
 demonstration of how the same proof strategy fails for BiInvℤ.

-}</a>
<a id="206" class="Keyword">module</a> <a id="213" href="Cubical.Experiments.IsoInt.Base.html" class="Module">Cubical.Experiments.IsoInt.Base</a> <a id="245" class="Keyword">where</a>

<a id="252" class="Keyword">open</a> <a id="257" class="Keyword">import</a> <a id="264" href="Cubical.Foundations.Prelude.html" class="Module">Cubical.Foundations.Prelude</a>
<a id="292" class="Keyword">open</a> <a id="297" class="Keyword">import</a> <a id="304" href="Cubical.Foundations.Function.html" class="Module">Cubical.Foundations.Function</a>
<a id="333" class="Keyword">open</a> <a id="338" class="Keyword">import</a> <a id="345" href="Cubical.Foundations.Isomorphism.html" class="Module">Cubical.Foundations.Isomorphism</a>

<a id="378" class="Keyword">open</a> <a id="383" class="Keyword">import</a> <a id="390" href="Cubical.Data.Int.html" class="Module">Cubical.Data.Int</a>
<a id="407" class="Keyword">open</a> <a id="412" class="Keyword">import</a> <a id="419" href="Cubical.Data.Nat.html" class="Module">Cubical.Data.Nat</a>
<a id="436" class="Keyword">open</a> <a id="441" class="Keyword">import</a> <a id="448" href="Cubical.Data.Empty.html" class="Module">Cubical.Data.Empty</a>

<a id="468" class="Keyword">open</a> <a id="473" class="Keyword">import</a> <a id="480" href="Cubical.Relation.Nullary.html" class="Module">Cubical.Relation.Nullary</a>


<a id="507" class="Keyword">data</a> <a id="IsoInt"></a><a id="512" href="Cubical.Experiments.IsoInt.Base.html#512" class="Datatype">IsoInt</a> <a id="519" class="Symbol">:</a> <a id="521" href="Agda.Primitive.html#388" class="Primitive">Type₀</a> <a id="527" class="Keyword">where</a>
  <a id="IsoInt.zero"></a><a id="535" href="Cubical.Experiments.IsoInt.Base.html#535" class="InductiveConstructor">zero</a> <a id="540" class="Symbol">:</a> <a id="542" href="Cubical.Experiments.IsoInt.Base.html#512" class="Datatype">IsoInt</a>
  <a id="IsoInt.suc"></a><a id="551" href="Cubical.Experiments.IsoInt.Base.html#551" class="InductiveConstructor">suc</a> <a id="555" class="Symbol">:</a> <a id="557" href="Cubical.Experiments.IsoInt.Base.html#512" class="Datatype">IsoInt</a> <a id="564" class="Symbol">-&gt;</a> <a id="567" href="Cubical.Experiments.IsoInt.Base.html#512" class="Datatype">IsoInt</a>

  <a id="577" class="Comment">-- suc is an isomorphism:</a>
  <a id="IsoInt.pred"></a><a id="605" href="Cubical.Experiments.IsoInt.Base.html#605" class="InductiveConstructor">pred</a> <a id="610" class="Symbol">:</a> <a id="612" href="Cubical.Experiments.IsoInt.Base.html#512" class="Datatype">IsoInt</a> <a id="619" class="Symbol">-&gt;</a> <a id="622" href="Cubical.Experiments.IsoInt.Base.html#512" class="Datatype">IsoInt</a>
  <a id="IsoInt.suc-pred"></a><a id="631" href="Cubical.Experiments.IsoInt.Base.html#631" class="InductiveConstructor">suc-pred</a> <a id="640" class="Symbol">:</a> <a id="642" class="Symbol">∀</a> <a id="644" href="Cubical.Experiments.IsoInt.Base.html#644" class="Bound">z</a> <a id="646" class="Symbol">-&gt;</a> <a id="649" href="Cubical.Experiments.IsoInt.Base.html#551" class="InductiveConstructor">suc</a> <a id="653" class="Symbol">(</a><a id="654" href="Cubical.Experiments.IsoInt.Base.html#605" class="InductiveConstructor">pred</a> <a id="659" href="Cubical.Experiments.IsoInt.Base.html#644" class="Bound">z</a><a id="660" class="Symbol">)</a> <a id="662" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="664" href="Cubical.Experiments.IsoInt.Base.html#644" class="Bound">z</a>
  <a id="IsoInt.pred-suc"></a><a id="668" href="Cubical.Experiments.IsoInt.Base.html#668" class="InductiveConstructor">pred-suc</a> <a id="677" class="Symbol">:</a> <a id="679" class="Symbol">∀</a> <a id="681" href="Cubical.Experiments.IsoInt.Base.html#681" class="Bound">z</a> <a id="683" class="Symbol">-&gt;</a> <a id="686" href="Cubical.Experiments.IsoInt.Base.html#605" class="InductiveConstructor">pred</a> <a id="691" class="Symbol">(</a><a id="692" href="Cubical.Experiments.IsoInt.Base.html#551" class="InductiveConstructor">suc</a> <a id="696" href="Cubical.Experiments.IsoInt.Base.html#681" class="Bound">z</a><a id="697" class="Symbol">)</a> <a id="699" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="701" href="Cubical.Experiments.IsoInt.Base.html#681" class="Bound">z</a>


<a id="suc-iso"></a><a id="705" href="Cubical.Experiments.IsoInt.Base.html#705" class="Function">suc-iso</a> <a id="713" class="Symbol">:</a> <a id="715" href="Cubical.Foundations.Isomorphism.html#744" class="Record">Iso</a> <a id="719" href="Cubical.Experiments.IsoInt.Base.html#512" class="Datatype">IsoInt</a> <a id="726" href="Cubical.Experiments.IsoInt.Base.html#512" class="Datatype">IsoInt</a>
<a id="733" href="Cubical.Experiments.IsoInt.Base.html#705" class="Function">suc-iso</a> <a id="741" class="Symbol">=</a> <a id="743" class="Keyword">record</a> <a id="750" class="Symbol">{</a> <a id="752" href="Cubical.Foundations.Isomorphism.html#856" class="Field">fun</a> <a id="756" class="Symbol">=</a> <a id="758" href="Cubical.Experiments.IsoInt.Base.html#551" class="InductiveConstructor">suc</a> <a id="762" class="Symbol">;</a> <a id="764" href="Cubical.Foundations.Isomorphism.html#872" class="Field">inv</a> <a id="768" class="Symbol">=</a> <a id="770" href="Cubical.Experiments.IsoInt.Base.html#605" class="InductiveConstructor">pred</a> <a id="775" class="Symbol">;</a> <a id="777" href="Cubical.Foundations.Isomorphism.html#888" class="Field">sec</a> <a id="781" class="Symbol">=</a> <a id="783" href="Cubical.Experiments.IsoInt.Base.html#631" class="InductiveConstructor">suc-pred</a> <a id="792" class="Symbol">;</a> <a id="794" href="Cubical.Foundations.Isomorphism.html#914" class="Field">ret</a> <a id="798" class="Symbol">=</a> <a id="800" href="Cubical.Experiments.IsoInt.Base.html#668" class="InductiveConstructor">pred-suc</a> <a id="809" class="Symbol">}</a>


<a id="813" class="Comment">-- this submodule is adapted from Section 5 of</a>
<a id="860" class="Comment">--  http://www.cs.ru.nl/~herman/PUBS/HIT-programming.pdf</a>

<a id="918" class="Keyword">module</a> <a id="NonTrivial"></a><a id="925" href="Cubical.Experiments.IsoInt.Base.html#925" class="Module">NonTrivial</a> <a id="936" class="Keyword">where</a>

  <a id="945" class="Comment">-- these two paths are distinct!</a>
  <a id="NonTrivial.p₁"></a><a id="980" href="Cubical.Experiments.IsoInt.Base.html#980" class="Function">p₁</a> <a id="NonTrivial.p₂"></a><a id="983" href="Cubical.Experiments.IsoInt.Base.html#983" class="Function">p₂</a> <a id="986" class="Symbol">:</a> <a id="988" href="Cubical.Core.Primitives.html#1805" class="Function">Path</a> <a id="993" href="Cubical.Experiments.IsoInt.Base.html#512" class="Datatype">IsoInt</a> <a id="1000" class="Symbol">(</a><a id="1001" href="Cubical.Experiments.IsoInt.Base.html#551" class="InductiveConstructor">suc</a> <a id="1005" class="Symbol">(</a><a id="1006" href="Cubical.Experiments.IsoInt.Base.html#605" class="InductiveConstructor">pred</a> <a id="1011" class="Symbol">(</a><a id="1012" href="Cubical.Experiments.IsoInt.Base.html#551" class="InductiveConstructor">suc</a> <a id="1016" href="Cubical.Experiments.IsoInt.Base.html#535" class="InductiveConstructor">zero</a><a id="1020" class="Symbol">)))</a> <a id="1024" class="Symbol">(</a><a id="1025" href="Cubical.Experiments.IsoInt.Base.html#551" class="InductiveConstructor">suc</a> <a id="1029" href="Cubical.Experiments.IsoInt.Base.html#535" class="InductiveConstructor">zero</a><a id="1033" class="Symbol">)</a>
  <a id="1037" href="Cubical.Experiments.IsoInt.Base.html#980" class="Function">p₁</a> <a id="1040" href="Cubical.Experiments.IsoInt.Base.html#1040" class="Bound">i</a> <a id="1042" class="Symbol">=</a> <a id="1044" href="Cubical.Experiments.IsoInt.Base.html#631" class="InductiveConstructor">suc-pred</a> <a id="1053" class="Symbol">(</a><a id="1054" href="Cubical.Experiments.IsoInt.Base.html#551" class="InductiveConstructor">suc</a> <a id="1058" href="Cubical.Experiments.IsoInt.Base.html#535" class="InductiveConstructor">zero</a><a id="1062" class="Symbol">)</a> <a id="1064" href="Cubical.Experiments.IsoInt.Base.html#1040" class="Bound">i</a>
  <a id="1068" href="Cubical.Experiments.IsoInt.Base.html#983" class="Function">p₂</a> <a id="1071" href="Cubical.Experiments.IsoInt.Base.html#1071" class="Bound">i</a> <a id="1073" class="Symbol">=</a> <a id="1075" href="Cubical.Experiments.IsoInt.Base.html#551" class="InductiveConstructor">suc</a> <a id="1079" class="Symbol">(</a><a id="1080" href="Cubical.Experiments.IsoInt.Base.html#668" class="InductiveConstructor">pred-suc</a> <a id="1089" href="Cubical.Experiments.IsoInt.Base.html#535" class="InductiveConstructor">zero</a> <a id="1094" href="Cubical.Experiments.IsoInt.Base.html#1071" class="Bound">i</a><a id="1095" class="Symbol">)</a>

  <a id="1100" class="Comment">-- to prove this we map into S¹, sending p₁ to refl and p₂ to loop</a>

  <a id="1170" class="Keyword">open</a> <a id="1175" class="Keyword">import</a> <a id="1182" href="Cubical.HITs.S1.html" class="Module">Cubical.HITs.S1</a>

  <a id="NonTrivial.toS¹"></a><a id="1201" href="Cubical.Experiments.IsoInt.Base.html#1201" class="Function">toS¹</a> <a id="1206" class="Symbol">:</a> <a id="1208" href="Cubical.Experiments.IsoInt.Base.html#512" class="Datatype">IsoInt</a> <a id="1215" class="Symbol">→</a> <a id="1217" href="Cubical.HITs.S1.Base.html#594" class="Datatype">S¹</a>
  <a id="1222" href="Cubical.Experiments.IsoInt.Base.html#1201" class="Function">toS¹</a> <a id="1227" href="Cubical.Experiments.IsoInt.Base.html#535" class="InductiveConstructor">zero</a>           <a id="1242" class="Symbol">=</a> <a id="1244" href="Cubical.HITs.S1.Base.html#613" class="InductiveConstructor">base</a>
  <a id="1251" href="Cubical.Experiments.IsoInt.Base.html#1201" class="Function">toS¹</a> <a id="1256" class="Symbol">(</a><a id="1257" href="Cubical.Experiments.IsoInt.Base.html#551" class="InductiveConstructor">suc</a> <a id="1261" href="Cubical.Experiments.IsoInt.Base.html#1261" class="Bound">x</a><a id="1262" class="Symbol">)</a>        <a id="1271" class="Symbol">=</a> <a id="1273" href="Cubical.Experiments.IsoInt.Base.html#1201" class="Function">toS¹</a> <a id="1278" href="Cubical.Experiments.IsoInt.Base.html#1261" class="Bound">x</a>
  <a id="1282" href="Cubical.Experiments.IsoInt.Base.html#1201" class="Function">toS¹</a> <a id="1287" class="Symbol">(</a><a id="1288" href="Cubical.Experiments.IsoInt.Base.html#605" class="InductiveConstructor">pred</a> <a id="1293" href="Cubical.Experiments.IsoInt.Base.html#1293" class="Bound">x</a><a id="1294" class="Symbol">)</a>       <a id="1302" class="Symbol">=</a> <a id="1304" href="Cubical.Experiments.IsoInt.Base.html#1201" class="Function">toS¹</a> <a id="1309" href="Cubical.Experiments.IsoInt.Base.html#1293" class="Bound">x</a>
  <a id="1313" href="Cubical.Experiments.IsoInt.Base.html#1201" class="Function">toS¹</a> <a id="1318" class="Symbol">(</a><a id="1319" href="Cubical.Experiments.IsoInt.Base.html#631" class="InductiveConstructor">suc-pred</a> <a id="1328" href="Cubical.Experiments.IsoInt.Base.html#1328" class="Bound">x</a> <a id="1330" href="Cubical.Experiments.IsoInt.Base.html#1330" class="Bound">i</a><a id="1331" class="Symbol">)</a> <a id="1333" class="Symbol">=</a> <a id="1335" href="Cubical.Foundations.Prelude.html#892" class="Function">refl</a> <a id="1340" class="Symbol">{</a><a id="1341" class="Argument">x</a> <a id="1343" class="Symbol">=</a> <a id="1345" href="Cubical.Experiments.IsoInt.Base.html#1201" class="Function">toS¹</a> <a id="1350" href="Cubical.Experiments.IsoInt.Base.html#1328" class="Bound">x</a><a id="1351" class="Symbol">}</a> <a id="1353" href="Cubical.Experiments.IsoInt.Base.html#1330" class="Bound">i</a>
  <a id="1357" href="Cubical.Experiments.IsoInt.Base.html#1201" class="Function">toS¹</a> <a id="1362" class="Symbol">(</a><a id="1363" href="Cubical.Experiments.IsoInt.Base.html#668" class="InductiveConstructor">pred-suc</a> <a id="1372" href="Cubical.Experiments.IsoInt.Base.html#1372" class="Bound">x</a> <a id="1374" href="Cubical.Experiments.IsoInt.Base.html#1374" class="Bound">i</a><a id="1375" class="Symbol">)</a> <a id="1377" class="Symbol">=</a> <a id="1379" href="Cubical.HITs.S1.Base.html#14348" class="Function">rotLoop</a> <a id="1387" class="Symbol">(</a><a id="1388" href="Cubical.Experiments.IsoInt.Base.html#1201" class="Function">toS¹</a> <a id="1393" href="Cubical.Experiments.IsoInt.Base.html#1372" class="Bound">x</a><a id="1394" class="Symbol">)</a> <a id="1396" href="Cubical.Experiments.IsoInt.Base.html#1374" class="Bound">i</a>

  <a id="NonTrivial.p₁≡refl"></a><a id="1401" href="Cubical.Experiments.IsoInt.Base.html#1401" class="Function">p₁≡refl</a> <a id="1409" class="Symbol">:</a> <a id="1411" href="Cubical.Foundations.Prelude.html#1407" class="Function">cong</a> <a id="1416" href="Cubical.Experiments.IsoInt.Base.html#1201" class="Function">toS¹</a> <a id="1421" href="Cubical.Experiments.IsoInt.Base.html#980" class="Function">p₁</a> <a id="1424" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="1426" href="Cubical.Foundations.Prelude.html#892" class="Function">refl</a>
  <a id="1433" href="Cubical.Experiments.IsoInt.Base.html#1401" class="Function">p₁≡refl</a> <a id="1441" class="Symbol">=</a> <a id="1443" href="Cubical.Foundations.Prelude.html#892" class="Function">refl</a>

  <a id="NonTrivial.p₂≡loop"></a><a id="1451" href="Cubical.Experiments.IsoInt.Base.html#1451" class="Function">p₂≡loop</a> <a id="1459" class="Symbol">:</a> <a id="1461" href="Cubical.Foundations.Prelude.html#1407" class="Function">cong</a> <a id="1466" href="Cubical.Experiments.IsoInt.Base.html#1201" class="Function">toS¹</a> <a id="1471" href="Cubical.Experiments.IsoInt.Base.html#983" class="Function">p₂</a> <a id="1474" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="1476" href="Cubical.HITs.S1.Base.html#625" class="InductiveConstructor">loop</a>
  <a id="1483" href="Cubical.Experiments.IsoInt.Base.html#1451" class="Function">p₂≡loop</a> <a id="1491" class="Symbol">=</a> <a id="1493" href="Cubical.Foundations.Prelude.html#892" class="Function">refl</a>

  <a id="1501" class="Comment">-- this is enough to show that p₁ and p₂ cannot be equal</a>
  <a id="NonTrivial.p₁≢p₂"></a><a id="1560" href="Cubical.Experiments.IsoInt.Base.html#1560" class="Function">p₁≢p₂</a> <a id="1566" class="Symbol">:</a> <a id="1568" href="Cubical.Relation.Nullary.Base.html#332" class="Function Operator">¬</a> <a id="1570" class="Symbol">(</a><a id="1571" href="Cubical.Experiments.IsoInt.Base.html#980" class="Function">p₁</a> <a id="1574" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="1576" href="Cubical.Experiments.IsoInt.Base.html#983" class="Function">p₂</a><a id="1578" class="Symbol">)</a>
  <a id="1582" href="Cubical.Experiments.IsoInt.Base.html#1560" class="Function">p₁≢p₂</a> <a id="1588" href="Cubical.Experiments.IsoInt.Base.html#1588" class="Bound">eq</a> <a id="1591" class="Symbol">=</a> <a id="1593" href="Cubical.Data.Nat.Properties.html#1634" class="Function">znots</a> <a id="1599" href="Cubical.Experiments.IsoInt.Base.html#1671" class="Function">0≡1</a>
    <a id="1607" class="Keyword">where</a> <a id="1613" class="Comment">-- using winding numbers, p₁ ≡ p₂ implies 0 ≡ 1</a>
          <a id="1671" href="Cubical.Experiments.IsoInt.Base.html#1671" class="Function">0≡1</a> <a id="1675" class="Symbol">:</a> <a id="1677" class="Number">0</a> <a id="1679" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="1681" class="Number">1</a>
          <a id="1693" href="Cubical.Experiments.IsoInt.Base.html#1671" class="Function">0≡1</a> <a id="1697" class="Symbol">=</a> <a id="1699" href="Cubical.Data.Int.Properties.html#17507" class="Function">injPos</a> <a id="1706" class="Symbol">(</a><a id="1707" href="Cubical.Foundations.Prelude.html#1407" class="Function">cong</a> <a id="1712" class="Symbol">(</a><a id="1713" href="Cubical.HITs.S1.Base.html#1139" class="Function">winding</a> <a id="1721" href="Cubical.Foundations.Function.html#898" class="Function Operator">∘</a> <a id="1723" href="Cubical.Foundations.Prelude.html#1407" class="Function">cong</a> <a id="1728" href="Cubical.Experiments.IsoInt.Base.html#1201" class="Function">toS¹</a><a id="1732" class="Symbol">)</a> <a id="1734" href="Cubical.Experiments.IsoInt.Base.html#1588" class="Bound">eq</a><a id="1736" class="Symbol">)</a>


<a id="¬isSet-IsoInt"></a><a id="1740" href="Cubical.Experiments.IsoInt.Base.html#1740" class="Function">¬isSet-IsoInt</a> <a id="1754" class="Symbol">:</a> <a id="1756" href="Cubical.Relation.Nullary.Base.html#332" class="Function Operator">¬</a> <a id="1758" class="Symbol">(</a><a id="1759" href="Cubical.Foundations.Prelude.html#15579" class="Function">isSet</a> <a id="1765" href="Cubical.Experiments.IsoInt.Base.html#512" class="Datatype">IsoInt</a><a id="1771" class="Symbol">)</a>
<a id="1773" href="Cubical.Experiments.IsoInt.Base.html#1740" class="Function">¬isSet-IsoInt</a> <a id="1787" href="Cubical.Experiments.IsoInt.Base.html#1787" class="Bound">pf</a> <a id="1790" class="Symbol">=</a> <a id="1792" href="Cubical.Experiments.IsoInt.Base.html#1560" class="Function">NonTrivial.p₁≢p₂</a> <a id="1809" class="Symbol">(</a><a id="1810" href="Cubical.Experiments.IsoInt.Base.html#1787" class="Bound">pf</a> <a id="1813" class="Symbol">_</a> <a id="1815" class="Symbol">_</a> <a id="1817" href="Cubical.Experiments.IsoInt.Base.html#980" class="Function">NonTrivial.p₁</a> <a id="1831" href="Cubical.Experiments.IsoInt.Base.html#983" class="Function">NonTrivial.p₂</a><a id="1844" class="Symbol">)</a>

<a id="¬Int≡IsoInt"></a><a id="1847" href="Cubical.Experiments.IsoInt.Base.html#1847" class="Function">¬Int≡IsoInt</a> <a id="1859" class="Symbol">:</a> <a id="1861" href="Cubical.Relation.Nullary.Base.html#332" class="Function Operator">¬</a> <a id="1863" class="Symbol">(</a><a id="1864" href="Cubical.Data.Int.Base.html#409" class="Datatype">ℤ</a> <a id="1866" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="1868" href="Cubical.Experiments.IsoInt.Base.html#512" class="Datatype">IsoInt</a><a id="1874" class="Symbol">)</a>
<a id="1876" href="Cubical.Experiments.IsoInt.Base.html#1847" class="Function">¬Int≡IsoInt</a> <a id="1888" href="Cubical.Experiments.IsoInt.Base.html#1888" class="Bound">p</a> <a id="1890" class="Symbol">=</a> <a id="1892" href="Cubical.Experiments.IsoInt.Base.html#1740" class="Function">¬isSet-IsoInt</a> <a id="1906" class="Symbol">(</a><a id="1907" href="Cubical.Foundations.Prelude.html#10205" class="Function">subst</a> <a id="1913" href="Cubical.Foundations.Prelude.html#15579" class="Function">isSet</a> <a id="1919" href="Cubical.Experiments.IsoInt.Base.html#1888" class="Bound">p</a> <a id="1921" href="Cubical.Data.Int.Properties.html#18742" class="Function">isSetℤ</a><a id="1927" class="Symbol">)</a>



<a id="1932" class="Keyword">private</a>

  <a id="1943" class="Comment">-- Note: this same proof strategy fails for BiInvℤ!</a>

  <a id="1998" class="Keyword">open</a> <a id="2003" class="Keyword">import</a> <a id="2010" href="Cubical.Data.Int.MoreInts.BiInvInt.html" class="Module">Cubical.Data.Int.MoreInts.BiInvInt</a> <a id="2045" class="Keyword">hiding</a> <a id="2052" class="Symbol">(</a><a id="2053" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#986" class="InductiveConstructor">zero</a><a id="2057" class="Symbol">;</a> <a id="2059" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#1002" class="InductiveConstructor">suc</a><a id="2062" class="Symbol">;</a> <a id="2064" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#2495" class="Function">pred</a><a id="2068" class="Symbol">;</a> <a id="2070" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#2533" class="Function">suc-pred</a><a id="2078" class="Symbol">;</a> <a id="2080" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#2554" class="Function">pred-suc</a><a id="2088" class="Symbol">)</a>
  <a id="2092" class="Keyword">import</a> <a id="2099" href="Cubical.Data.Int.MoreInts.BiInvInt.html" class="Module">Cubical.Data.Int.MoreInts.BiInvInt</a> <a id="2134" class="Symbol">as</a> <a id="2137" class="Module">BiI</a>

  <a id="p₁"></a><a id="2144" href="Cubical.Experiments.IsoInt.Base.html#2144" class="Function">p₁</a> <a id="p₂"></a><a id="2147" href="Cubical.Experiments.IsoInt.Base.html#2147" class="Function">p₂</a> <a id="2150" class="Symbol">:</a> <a id="2152" href="Cubical.Core.Primitives.html#1805" class="Function">Path</a> <a id="2157" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#963" class="Datatype">BiInvℤ</a> <a id="2164" class="Symbol">(</a><a id="2165" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#1002" class="InductiveConstructor">BiI.suc</a> <a id="2173" class="Symbol">(</a><a id="2174" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#2495" class="Function">BiI.pred</a> <a id="2183" class="Symbol">(</a><a id="2184" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#1002" class="InductiveConstructor">BiI.suc</a> <a id="2192" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#986" class="InductiveConstructor">BiI.zero</a><a id="2200" class="Symbol">)))</a> <a id="2204" class="Symbol">(</a><a id="2205" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#1002" class="InductiveConstructor">BiI.suc</a> <a id="2213" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#986" class="InductiveConstructor">BiI.zero</a><a id="2221" class="Symbol">)</a>
  <a id="2225" href="Cubical.Experiments.IsoInt.Base.html#2144" class="Function">p₁</a> <a id="2228" href="Cubical.Experiments.IsoInt.Base.html#2228" class="Bound">i</a> <a id="2230" class="Symbol">=</a> <a id="2232" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#2533" class="Function">BiI.suc-pred</a> <a id="2245" class="Symbol">(</a><a id="2246" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#1002" class="InductiveConstructor">BiI.suc</a> <a id="2254" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#986" class="InductiveConstructor">BiI.zero</a><a id="2262" class="Symbol">)</a> <a id="2264" href="Cubical.Experiments.IsoInt.Base.html#2228" class="Bound">i</a>
  <a id="2268" href="Cubical.Experiments.IsoInt.Base.html#2147" class="Function">p₂</a> <a id="2271" href="Cubical.Experiments.IsoInt.Base.html#2271" class="Bound">i</a> <a id="2273" class="Symbol">=</a> <a id="2275" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#1002" class="InductiveConstructor">BiI.suc</a> <a id="2283" class="Symbol">(</a><a id="2284" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#2554" class="Function">BiI.pred-suc</a> <a id="2297" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#986" class="InductiveConstructor">BiI.zero</a> <a id="2306" href="Cubical.Experiments.IsoInt.Base.html#2271" class="Bound">i</a><a id="2307" class="Symbol">)</a>

  <a id="2312" class="Keyword">open</a> <a id="2317" class="Keyword">import</a> <a id="2324" href="Cubical.HITs.S1.html" class="Module">Cubical.HITs.S1</a>

  <a id="toS¹"></a><a id="2343" href="Cubical.Experiments.IsoInt.Base.html#2343" class="Function">toS¹</a> <a id="2348" class="Symbol">:</a> <a id="2350" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#963" class="Datatype">BiInvℤ</a> <a id="2357" class="Symbol">→</a> <a id="2359" href="Cubical.HITs.S1.Base.html#594" class="Datatype">S¹</a>
  <a id="2364" href="Cubical.Experiments.IsoInt.Base.html#2343" class="Function">toS¹</a> <a id="2369" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#986" class="InductiveConstructor">BiI.zero</a>            <a id="2389" class="Symbol">=</a> <a id="2391" href="Cubical.HITs.S1.Base.html#613" class="InductiveConstructor">base</a>
  <a id="2398" href="Cubical.Experiments.IsoInt.Base.html#2343" class="Function">toS¹</a> <a id="2403" class="Symbol">(</a><a id="2404" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#1002" class="InductiveConstructor">BiI.suc</a> <a id="2412" href="Cubical.Experiments.IsoInt.Base.html#2412" class="Bound">x</a><a id="2413" class="Symbol">)</a>         <a id="2423" class="Symbol">=</a> <a id="2425" href="Cubical.Experiments.IsoInt.Base.html#2343" class="Function">toS¹</a> <a id="2430" href="Cubical.Experiments.IsoInt.Base.html#2412" class="Bound">x</a>
  <a id="2434" href="Cubical.Experiments.IsoInt.Base.html#2343" class="Function">toS¹</a> <a id="2439" class="Symbol">(</a><a id="2440" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#1069" class="InductiveConstructor">BiI.predr</a> <a id="2450" href="Cubical.Experiments.IsoInt.Base.html#2450" class="Bound">x</a><a id="2451" class="Symbol">)</a>       <a id="2459" class="Symbol">=</a> <a id="2461" href="Cubical.Experiments.IsoInt.Base.html#2343" class="Function">toS¹</a> <a id="2466" href="Cubical.Experiments.IsoInt.Base.html#2450" class="Bound">x</a>
  <a id="2470" href="Cubical.Experiments.IsoInt.Base.html#2343" class="Function">toS¹</a> <a id="2475" class="Symbol">(</a><a id="2476" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#1135" class="InductiveConstructor">BiI.predl</a> <a id="2486" href="Cubical.Experiments.IsoInt.Base.html#2486" class="Bound">x</a><a id="2487" class="Symbol">)</a>       <a id="2495" class="Symbol">=</a> <a id="2497" href="Cubical.Experiments.IsoInt.Base.html#2343" class="Function">toS¹</a> <a id="2502" href="Cubical.Experiments.IsoInt.Base.html#2486" class="Bound">x</a>
  <a id="2506" href="Cubical.Experiments.IsoInt.Base.html#2343" class="Function">toS¹</a> <a id="2511" class="Symbol">(</a><a id="2512" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#1096" class="InductiveConstructor">BiI.suc-predr</a> <a id="2526" href="Cubical.Experiments.IsoInt.Base.html#2526" class="Bound">x</a> <a id="2528" href="Cubical.Experiments.IsoInt.Base.html#2528" class="Bound">i</a><a id="2529" class="Symbol">)</a> <a id="2531" class="Symbol">=</a> <a id="2533" href="Cubical.Foundations.Prelude.html#892" class="Function">refl</a> <a id="2538" class="Symbol">{</a><a id="2539" class="Argument">x</a> <a id="2541" class="Symbol">=</a> <a id="2543" href="Cubical.Experiments.IsoInt.Base.html#2343" class="Function">toS¹</a> <a id="2548" href="Cubical.Experiments.IsoInt.Base.html#2526" class="Bound">x</a><a id="2549" class="Symbol">}</a> <a id="2551" href="Cubical.Experiments.IsoInt.Base.html#2528" class="Bound">i</a>
  <a id="2555" href="Cubical.Experiments.IsoInt.Base.html#2343" class="Function">toS¹</a> <a id="2560" class="Symbol">(</a><a id="2561" href="Cubical.Data.Int.MoreInts.BiInvInt.Base.html#1162" class="InductiveConstructor">BiI.predl-suc</a> <a id="2575" href="Cubical.Experiments.IsoInt.Base.html#2575" class="Bound">x</a> <a id="2577" href="Cubical.Experiments.IsoInt.Base.html#2577" class="Bound">i</a><a id="2578" class="Symbol">)</a> <a id="2580" class="Symbol">=</a> <a id="2582" href="Cubical.HITs.S1.Base.html#14348" class="Function">rotLoop</a> <a id="2590" class="Symbol">(</a><a id="2591" href="Cubical.Experiments.IsoInt.Base.html#2343" class="Function">toS¹</a> <a id="2596" href="Cubical.Experiments.IsoInt.Base.html#2575" class="Bound">x</a><a id="2597" class="Symbol">)</a> <a id="2599" href="Cubical.Experiments.IsoInt.Base.html#2577" class="Bound">i</a>

  <a id="2604" class="Comment">-- still p₂ maps to loop...</a>
  <a id="p₂≡loop"></a><a id="2634" href="Cubical.Experiments.IsoInt.Base.html#2634" class="Function">p₂≡loop</a> <a id="2642" class="Symbol">:</a> <a id="2644" href="Cubical.Foundations.Prelude.html#1407" class="Function">cong</a> <a id="2649" href="Cubical.Experiments.IsoInt.Base.html#2343" class="Function">toS¹</a> <a id="2654" href="Cubical.Experiments.IsoInt.Base.html#2147" class="Function">p₂</a> <a id="2657" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="2659" href="Cubical.HITs.S1.Base.html#625" class="InductiveConstructor">loop</a>
  <a id="2666" href="Cubical.Experiments.IsoInt.Base.html#2634" class="Function">p₂≡loop</a> <a id="2674" class="Symbol">=</a> <a id="2676" href="Cubical.Foundations.Prelude.html#892" class="Function">refl</a>

  <a id="2684" class="Keyword">open</a> <a id="2689" class="Keyword">import</a> <a id="2696" href="Cubical.Foundations.GroupoidLaws.html" class="Module">Cubical.Foundations.GroupoidLaws</a>

  <a id="2732" class="Comment">-- ...but now so does p₁!</a>
  <a id="p₁≡loop"></a><a id="2760" href="Cubical.Experiments.IsoInt.Base.html#2760" class="Function">p₁≡loop</a> <a id="2768" class="Symbol">:</a> <a id="2770" href="Cubical.Foundations.Prelude.html#1407" class="Function">cong</a> <a id="2775" href="Cubical.Experiments.IsoInt.Base.html#2343" class="Function">toS¹</a> <a id="2780" href="Cubical.Experiments.IsoInt.Base.html#2144" class="Function">p₁</a> <a id="2783" href="Agda.Builtin.Cubical.Path.html#272" class="Function Operator">≡</a> <a id="2785" href="Cubical.HITs.S1.Base.html#625" class="InductiveConstructor">loop</a>
  <a id="2792" href="Cubical.Experiments.IsoInt.Base.html#2760" class="Function">p₁≡loop</a> <a id="2800" class="Symbol">=</a> <a id="2802" href="Cubical.Foundations.Prelude.html#945" class="Function">sym</a> <a id="2806" class="Symbol">(</a><a id="2807" href="Cubical.HITs.S1.Base.html#2267" class="Function">decodeEncode</a> <a id="2820" href="Cubical.HITs.S1.Base.html#613" class="InductiveConstructor">base</a> <a id="2825" class="Symbol">(</a><a id="2826" href="Cubical.Foundations.Prelude.html#1407" class="Function">cong</a> <a id="2831" href="Cubical.Experiments.IsoInt.Base.html#2343" class="Function">toS¹</a> <a id="2836" href="Cubical.Experiments.IsoInt.Base.html#2144" class="Function">p₁</a><a id="2838" class="Symbol">))</a> <a id="2841" href="Cubical.Foundations.Prelude.html#5184" class="Function Operator">∙</a> <a id="2843" href="Cubical.Foundations.Prelude.html#945" class="Function">sym</a> <a id="2847" class="Symbol">(</a><a id="2848" href="Cubical.Foundations.GroupoidLaws.html#966" class="Function">lUnit</a> <a id="2854" href="Cubical.HITs.S1.Base.html#625" class="InductiveConstructor">loop</a><a id="2858" class="Symbol">)</a>

  <a id="2863" class="Comment">-- if we use BiI.predr instead of BiI.pred (≡ BiI.predl) in p₁ and p₂,</a>
  <a id="2936" class="Comment">--  both paths in S¹ are refl</a>
</pre></body></html>