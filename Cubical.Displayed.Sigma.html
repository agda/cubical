<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Cubical.Displayed.Sigma</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">{-

  Functions building UARels and DUARels on Σ-types

-}</a>
<a id="60" class="Symbol">{-#</a> <a id="64" class="Keyword">OPTIONS</a> <a id="72" class="Pragma">--safe</a> <a id="79" class="Symbol">#-}</a>
<a id="83" class="Keyword">module</a> <a id="90" href="Cubical.Displayed.Sigma.html" class="Module">Cubical.Displayed.Sigma</a> <a id="114" class="Keyword">where</a>

<a id="121" class="Keyword">open</a> <a id="126" class="Keyword">import</a> <a id="133" href="Cubical.Foundations.Prelude.html" class="Module">Cubical.Foundations.Prelude</a>
<a id="161" class="Keyword">open</a> <a id="166" class="Keyword">import</a> <a id="173" href="Cubical.Foundations.Equiv.html" class="Module">Cubical.Foundations.Equiv</a>

<a id="200" class="Keyword">open</a> <a id="205" class="Keyword">import</a> <a id="212" href="Cubical.Data.Sigma.html" class="Module">Cubical.Data.Sigma</a>

<a id="232" class="Keyword">open</a> <a id="237" class="Keyword">import</a> <a id="244" href="Cubical.Displayed.Base.html" class="Module">Cubical.Displayed.Base</a>
<a id="267" class="Keyword">open</a> <a id="272" class="Keyword">import</a> <a id="279" href="Cubical.Displayed.Subst.html" class="Module">Cubical.Displayed.Subst</a>
<a id="303" class="Keyword">open</a> <a id="308" class="Keyword">import</a> <a id="315" href="Cubical.Displayed.Morphism.html" class="Module">Cubical.Displayed.Morphism</a>
<a id="342" class="Keyword">open</a> <a id="347" class="Keyword">import</a> <a id="354" href="Cubical.Displayed.Constant.html" class="Module">Cubical.Displayed.Constant</a>

<a id="382" class="Keyword">private</a>
  <a id="392" class="Keyword">variable</a>
    <a id="405" href="Cubical.Displayed.Sigma.html#405" class="Generalizable">ℓ</a> <a id="407" href="Cubical.Displayed.Sigma.html#407" class="Generalizable">ℓA</a> <a id="410" href="Cubical.Displayed.Sigma.html#410" class="Generalizable">ℓA&#39;</a> <a id="414" href="Cubical.Displayed.Sigma.html#414" class="Generalizable">ℓP</a> <a id="417" href="Cubical.Displayed.Sigma.html#417" class="Generalizable">ℓ≅A</a> <a id="421" href="Cubical.Displayed.Sigma.html#421" class="Generalizable">ℓ≅A&#39;</a> <a id="426" href="Cubical.Displayed.Sigma.html#426" class="Generalizable">ℓB</a> <a id="429" href="Cubical.Displayed.Sigma.html#429" class="Generalizable">ℓB&#39;</a> <a id="433" href="Cubical.Displayed.Sigma.html#433" class="Generalizable">ℓ≅B</a> <a id="437" href="Cubical.Displayed.Sigma.html#437" class="Generalizable">ℓ≅B&#39;</a> <a id="442" href="Cubical.Displayed.Sigma.html#442" class="Generalizable">ℓC</a> <a id="445" href="Cubical.Displayed.Sigma.html#445" class="Generalizable">ℓ≅C</a> <a id="449" class="Symbol">:</a> <a id="451" href="Agda.Primitive.html#742" class="Postulate">Level</a>

<a id="458" class="Comment">-- UARel on a Σ-type</a>

<a id="∫ˢ"></a><a id="480" href="Cubical.Displayed.Sigma.html#480" class="Function">∫ˢ</a> <a id="483" class="Symbol">:</a> <a id="485" class="Symbol">{</a><a id="486" href="Cubical.Displayed.Sigma.html#486" class="Bound">A</a> <a id="488" class="Symbol">:</a> <a id="490" href="Agda.Primitive.html#388" class="Primitive">Type</a> <a id="495" href="Cubical.Displayed.Sigma.html#407" class="Generalizable">ℓA</a><a id="497" class="Symbol">}</a> <a id="499" class="Symbol">{</a><a id="500" href="Cubical.Displayed.Sigma.html#500" class="Bound">𝒮-A</a> <a id="504" class="Symbol">:</a> <a id="506" href="Cubical.Displayed.Base.html#433" class="Record">UARel</a> <a id="512" href="Cubical.Displayed.Sigma.html#486" class="Bound">A</a> <a id="514" href="Cubical.Displayed.Sigma.html#417" class="Generalizable">ℓ≅A</a><a id="517" class="Symbol">}</a> <a id="519" class="Symbol">{</a><a id="520" href="Cubical.Displayed.Sigma.html#520" class="Bound">B</a> <a id="522" class="Symbol">:</a> <a id="524" href="Cubical.Displayed.Sigma.html#486" class="Bound">A</a> <a id="526" class="Symbol">→</a> <a id="528" href="Agda.Primitive.html#388" class="Primitive">Type</a> <a id="533" href="Cubical.Displayed.Sigma.html#426" class="Generalizable">ℓB</a><a id="535" class="Symbol">}</a> <a id="537" class="Symbol">(</a><a id="538" href="Cubical.Displayed.Sigma.html#538" class="Bound">𝒮ˢ-B</a> <a id="543" class="Symbol">:</a> <a id="545" href="Cubical.Displayed.Subst.html#682" class="Record">SubstRel</a> <a id="554" href="Cubical.Displayed.Sigma.html#500" class="Bound">𝒮-A</a> <a id="558" href="Cubical.Displayed.Sigma.html#520" class="Bound">B</a><a id="559" class="Symbol">)</a>
  <a id="563" class="Symbol">→</a> <a id="565" href="Cubical.Displayed.Base.html#433" class="Record">UARel</a> <a id="571" class="Symbol">(</a><a id="572" href="Agda.Builtin.Sigma.html#165" class="Record">Σ</a> <a id="574" href="Cubical.Displayed.Sigma.html#486" class="Bound">A</a> <a id="576" href="Cubical.Displayed.Sigma.html#520" class="Bound">B</a><a id="577" class="Symbol">)</a> <a id="579" class="Symbol">(</a><a id="580" href="Agda.Primitive.html#961" class="Primitive">ℓ-max</a> <a id="586" href="Cubical.Displayed.Sigma.html#417" class="Generalizable">ℓ≅A</a> <a id="590" href="Cubical.Displayed.Sigma.html#426" class="Generalizable">ℓB</a><a id="592" class="Symbol">)</a>
<a id="594" href="Cubical.Displayed.Sigma.html#480" class="Function">∫ˢ</a> <a id="597" href="Cubical.Displayed.Sigma.html#597" class="Bound">𝒮ˢ-B</a> <a id="602" class="Symbol">=</a> <a id="604" href="Cubical.Displayed.Base.html#2148" class="Function">∫</a> <a id="606" class="Symbol">(</a><a id="607" href="Cubical.Displayed.Subst.html#1483" class="Function">Subst→DUA</a> <a id="617" href="Cubical.Displayed.Sigma.html#597" class="Bound">𝒮ˢ-B</a><a id="621" class="Symbol">)</a>

<a id="_×𝒮_"></a><a id="624" href="Cubical.Displayed.Sigma.html#624" class="Function Operator">_×𝒮_</a> <a id="629" class="Symbol">:</a> <a id="631" class="Symbol">{</a><a id="632" href="Cubical.Displayed.Sigma.html#632" class="Bound">A</a> <a id="634" class="Symbol">:</a> <a id="636" href="Agda.Primitive.html#388" class="Primitive">Type</a> <a id="641" href="Cubical.Displayed.Sigma.html#407" class="Generalizable">ℓA</a><a id="643" class="Symbol">}</a> <a id="645" class="Symbol">(</a><a id="646" href="Cubical.Displayed.Sigma.html#646" class="Bound">𝒮-A</a> <a id="650" class="Symbol">:</a> <a id="652" href="Cubical.Displayed.Base.html#433" class="Record">UARel</a> <a id="658" href="Cubical.Displayed.Sigma.html#632" class="Bound">A</a> <a id="660" href="Cubical.Displayed.Sigma.html#417" class="Generalizable">ℓ≅A</a><a id="663" class="Symbol">)</a> <a id="665" class="Symbol">{</a><a id="666" href="Cubical.Displayed.Sigma.html#666" class="Bound">B</a> <a id="668" class="Symbol">:</a> <a id="670" href="Agda.Primitive.html#388" class="Primitive">Type</a> <a id="675" href="Cubical.Displayed.Sigma.html#426" class="Generalizable">ℓB</a><a id="677" class="Symbol">}</a> <a id="679" class="Symbol">(</a><a id="680" href="Cubical.Displayed.Sigma.html#680" class="Bound">𝒮-B</a> <a id="684" class="Symbol">:</a> <a id="686" href="Cubical.Displayed.Base.html#433" class="Record">UARel</a> <a id="692" href="Cubical.Displayed.Sigma.html#666" class="Bound">B</a> <a id="694" href="Cubical.Displayed.Sigma.html#433" class="Generalizable">ℓ≅B</a><a id="697" class="Symbol">)</a>
  <a id="701" class="Symbol">→</a> <a id="703" href="Cubical.Displayed.Base.html#433" class="Record">UARel</a> <a id="709" class="Symbol">(</a><a id="710" href="Cubical.Displayed.Sigma.html#632" class="Bound">A</a> <a id="712" href="Cubical.Data.Sigma.Base.html#461" class="Function Operator">×</a> <a id="714" href="Cubical.Displayed.Sigma.html#666" class="Bound">B</a><a id="715" class="Symbol">)</a> <a id="717" class="Symbol">(</a><a id="718" href="Agda.Primitive.html#961" class="Primitive">ℓ-max</a> <a id="724" href="Cubical.Displayed.Sigma.html#417" class="Generalizable">ℓ≅A</a> <a id="728" href="Cubical.Displayed.Sigma.html#433" class="Generalizable">ℓ≅B</a><a id="731" class="Symbol">)</a>
<a id="733" href="Cubical.Displayed.Sigma.html#733" class="Bound">𝒮-A</a> <a id="737" href="Cubical.Displayed.Sigma.html#624" class="Function Operator">×𝒮</a> <a id="740" href="Cubical.Displayed.Sigma.html#740" class="Bound">𝒮-B</a> <a id="744" class="Symbol">=</a> <a id="746" href="Cubical.Displayed.Base.html#2148" class="Function">∫</a> <a id="748" class="Symbol">(</a><a id="749" href="Cubical.Displayed.Constant.html#491" class="Function">𝒮ᴰ-const</a> <a id="758" href="Cubical.Displayed.Sigma.html#733" class="Bound">𝒮-A</a> <a id="762" href="Cubical.Displayed.Sigma.html#740" class="Bound">𝒮-B</a><a id="765" class="Symbol">)</a>

<a id="768" class="Comment">-- Projection UARel morphisms</a>

<a id="𝒮-fst"></a><a id="799" href="Cubical.Displayed.Sigma.html#799" class="Function">𝒮-fst</a> <a id="805" class="Symbol">:</a> <a id="807" class="Symbol">{</a><a id="808" href="Cubical.Displayed.Sigma.html#808" class="Bound">A</a> <a id="810" class="Symbol">:</a> <a id="812" href="Agda.Primitive.html#388" class="Primitive">Type</a> <a id="817" href="Cubical.Displayed.Sigma.html#407" class="Generalizable">ℓA</a><a id="819" class="Symbol">}</a> <a id="821" class="Symbol">{</a><a id="822" href="Cubical.Displayed.Sigma.html#822" class="Bound">𝒮-A</a> <a id="826" class="Symbol">:</a> <a id="828" href="Cubical.Displayed.Base.html#433" class="Record">UARel</a> <a id="834" href="Cubical.Displayed.Sigma.html#808" class="Bound">A</a> <a id="836" href="Cubical.Displayed.Sigma.html#417" class="Generalizable">ℓ≅A</a><a id="839" class="Symbol">}</a> <a id="841" class="Symbol">{</a><a id="842" href="Cubical.Displayed.Sigma.html#842" class="Bound">B</a> <a id="844" class="Symbol">:</a> <a id="846" href="Cubical.Displayed.Sigma.html#808" class="Bound">A</a> <a id="848" class="Symbol">→</a> <a id="850" href="Agda.Primitive.html#388" class="Primitive">Type</a> <a id="855" href="Cubical.Displayed.Sigma.html#426" class="Generalizable">ℓB</a><a id="857" class="Symbol">}</a> <a id="859" class="Symbol">{</a><a id="860" href="Cubical.Displayed.Sigma.html#860" class="Bound">𝒮ᴰ-B</a> <a id="865" class="Symbol">:</a> <a id="867" href="Cubical.Displayed.Base.html#1242" class="Record">DUARel</a> <a id="874" href="Cubical.Displayed.Sigma.html#822" class="Bound">𝒮-A</a> <a id="878" href="Cubical.Displayed.Sigma.html#842" class="Bound">B</a> <a id="880" href="Cubical.Displayed.Sigma.html#433" class="Generalizable">ℓ≅B</a><a id="883" class="Symbol">}</a>
  <a id="887" class="Symbol">→</a> <a id="889" href="Cubical.Displayed.Morphism.html#617" class="Record">UARelHom</a> <a id="898" class="Symbol">(</a><a id="899" href="Cubical.Displayed.Base.html#2148" class="Function">∫</a> <a id="901" href="Cubical.Displayed.Sigma.html#860" class="Bound">𝒮ᴰ-B</a><a id="905" class="Symbol">)</a> <a id="907" href="Cubical.Displayed.Sigma.html#822" class="Bound">𝒮-A</a>
<a id="911" href="Cubical.Displayed.Sigma.html#799" class="Function">𝒮-fst</a> <a id="917" class="Symbol">.</a><a id="918" href="Cubical.Displayed.Morphism.html#800" class="Field">UARelHom.fun</a> <a id="931" class="Symbol">=</a> <a id="933" href="Agda.Builtin.Sigma.html#251" class="Field">fst</a>
<a id="937" href="Cubical.Displayed.Sigma.html#799" class="Function">𝒮-fst</a> <a id="943" class="Symbol">.</a><a id="944" href="Cubical.Displayed.Morphism.html#816" class="Field">UARelHom.rel</a> <a id="957" class="Symbol">=</a> <a id="959" href="Agda.Builtin.Sigma.html#251" class="Field">fst</a>
<a id="963" href="Cubical.Displayed.Sigma.html#799" class="Function">𝒮-fst</a> <a id="969" class="Symbol">.</a><a id="970" href="Cubical.Displayed.Morphism.html#889" class="Field">UARelHom.ua</a> <a id="982" href="Cubical.Displayed.Sigma.html#982" class="Bound">p</a> <a id="984" class="Symbol">=</a> <a id="986" href="Cubical.Foundations.Prelude.html#915" class="Function">refl</a>

<a id="𝒮-snd"></a><a id="992" href="Cubical.Displayed.Sigma.html#992" class="Function">𝒮-snd</a> <a id="998" class="Symbol">:</a> <a id="1000" class="Symbol">{</a><a id="1001" href="Cubical.Displayed.Sigma.html#1001" class="Bound">A</a> <a id="1003" class="Symbol">:</a> <a id="1005" href="Agda.Primitive.html#388" class="Primitive">Type</a> <a id="1010" href="Cubical.Displayed.Sigma.html#407" class="Generalizable">ℓA</a><a id="1012" class="Symbol">}</a> <a id="1014" class="Symbol">{</a><a id="1015" href="Cubical.Displayed.Sigma.html#1015" class="Bound">𝒮-A</a> <a id="1019" class="Symbol">:</a> <a id="1021" href="Cubical.Displayed.Base.html#433" class="Record">UARel</a> <a id="1027" href="Cubical.Displayed.Sigma.html#1001" class="Bound">A</a> <a id="1029" href="Cubical.Displayed.Sigma.html#417" class="Generalizable">ℓ≅A</a><a id="1032" class="Symbol">}</a> <a id="1034" class="Symbol">{</a><a id="1035" href="Cubical.Displayed.Sigma.html#1035" class="Bound">B</a> <a id="1037" class="Symbol">:</a> <a id="1039" href="Agda.Primitive.html#388" class="Primitive">Type</a> <a id="1044" href="Cubical.Displayed.Sigma.html#426" class="Generalizable">ℓB</a><a id="1046" class="Symbol">}</a> <a id="1048" class="Symbol">{</a><a id="1049" href="Cubical.Displayed.Sigma.html#1049" class="Bound">𝒮-B</a> <a id="1053" class="Symbol">:</a> <a id="1055" href="Cubical.Displayed.Base.html#433" class="Record">UARel</a> <a id="1061" href="Cubical.Displayed.Sigma.html#1035" class="Bound">B</a> <a id="1063" href="Cubical.Displayed.Sigma.html#433" class="Generalizable">ℓ≅B</a><a id="1066" class="Symbol">}</a>
  <a id="1070" class="Symbol">→</a> <a id="1072" href="Cubical.Displayed.Morphism.html#617" class="Record">UARelHom</a> <a id="1081" class="Symbol">(</a><a id="1082" href="Cubical.Displayed.Sigma.html#1015" class="Bound">𝒮-A</a> <a id="1086" href="Cubical.Displayed.Sigma.html#624" class="Function Operator">×𝒮</a> <a id="1089" href="Cubical.Displayed.Sigma.html#1049" class="Bound">𝒮-B</a><a id="1092" class="Symbol">)</a> <a id="1094" href="Cubical.Displayed.Sigma.html#1049" class="Bound">𝒮-B</a>
<a id="1098" href="Cubical.Displayed.Sigma.html#992" class="Function">𝒮-snd</a> <a id="1104" class="Symbol">.</a><a id="1105" href="Cubical.Displayed.Morphism.html#800" class="Field">UARelHom.fun</a> <a id="1118" class="Symbol">=</a> <a id="1120" href="Agda.Builtin.Sigma.html#263" class="Field">snd</a>
<a id="1124" href="Cubical.Displayed.Sigma.html#992" class="Function">𝒮-snd</a> <a id="1130" class="Symbol">.</a><a id="1131" href="Cubical.Displayed.Morphism.html#816" class="Field">UARelHom.rel</a> <a id="1144" class="Symbol">=</a> <a id="1146" href="Agda.Builtin.Sigma.html#263" class="Field">snd</a>
<a id="1150" href="Cubical.Displayed.Sigma.html#992" class="Function">𝒮-snd</a> <a id="1156" class="Symbol">.</a><a id="1157" href="Cubical.Displayed.Morphism.html#889" class="Field">UARelHom.ua</a> <a id="1169" href="Cubical.Displayed.Sigma.html#1169" class="Bound">p</a> <a id="1171" class="Symbol">=</a> <a id="1173" href="Cubical.Foundations.Prelude.html#915" class="Function">refl</a>

<a id="1179" class="Comment">-- Lift a DUARel to live over a Σ-type</a>

<a id="𝒮ᴰ-Lift"></a><a id="1219" href="Cubical.Displayed.Sigma.html#1219" class="Function">𝒮ᴰ-Lift</a> <a id="1227" class="Symbol">:</a> <a id="1229" class="Symbol">{</a><a id="1230" href="Cubical.Displayed.Sigma.html#1230" class="Bound">A</a> <a id="1232" class="Symbol">:</a> <a id="1234" href="Agda.Primitive.html#388" class="Primitive">Type</a> <a id="1239" href="Cubical.Displayed.Sigma.html#407" class="Generalizable">ℓA</a><a id="1241" class="Symbol">}</a> <a id="1243" class="Symbol">(</a><a id="1244" href="Cubical.Displayed.Sigma.html#1244" class="Bound">𝒮-A</a> <a id="1248" class="Symbol">:</a> <a id="1250" href="Cubical.Displayed.Base.html#433" class="Record">UARel</a> <a id="1256" href="Cubical.Displayed.Sigma.html#1230" class="Bound">A</a> <a id="1258" href="Cubical.Displayed.Sigma.html#417" class="Generalizable">ℓ≅A</a><a id="1261" class="Symbol">)</a> <a id="1263" class="Symbol">{</a><a id="1264" href="Cubical.Displayed.Sigma.html#1264" class="Bound">B</a> <a id="1266" class="Symbol">:</a> <a id="1268" href="Cubical.Displayed.Sigma.html#1230" class="Bound">A</a> <a id="1270" class="Symbol">→</a> <a id="1272" href="Agda.Primitive.html#388" class="Primitive">Type</a> <a id="1277" href="Cubical.Displayed.Sigma.html#426" class="Generalizable">ℓB</a><a id="1279" class="Symbol">}</a> <a id="1281" class="Symbol">(</a><a id="1282" href="Cubical.Displayed.Sigma.html#1282" class="Bound">𝒮ᴰ-B</a> <a id="1287" class="Symbol">:</a> <a id="1289" href="Cubical.Displayed.Base.html#1242" class="Record">DUARel</a> <a id="1296" href="Cubical.Displayed.Sigma.html#1244" class="Bound">𝒮-A</a> <a id="1300" href="Cubical.Displayed.Sigma.html#1264" class="Bound">B</a> <a id="1302" href="Cubical.Displayed.Sigma.html#433" class="Generalizable">ℓ≅B</a><a id="1305" class="Symbol">)</a>
  <a id="1309" class="Symbol">{</a><a id="1310" href="Cubical.Displayed.Sigma.html#1310" class="Bound">C</a> <a id="1312" class="Symbol">:</a> <a id="1314" href="Cubical.Displayed.Sigma.html#1230" class="Bound">A</a> <a id="1316" class="Symbol">→</a> <a id="1318" href="Agda.Primitive.html#388" class="Primitive">Type</a> <a id="1323" href="Cubical.Displayed.Sigma.html#442" class="Generalizable">ℓC</a><a id="1325" class="Symbol">}</a> <a id="1327" class="Symbol">(</a><a id="1328" href="Cubical.Displayed.Sigma.html#1328" class="Bound">𝒮ᴰ-C</a> <a id="1333" class="Symbol">:</a> <a id="1335" href="Cubical.Displayed.Base.html#1242" class="Record">DUARel</a> <a id="1342" href="Cubical.Displayed.Sigma.html#1244" class="Bound">𝒮-A</a> <a id="1346" href="Cubical.Displayed.Sigma.html#1310" class="Bound">C</a> <a id="1348" href="Cubical.Displayed.Sigma.html#445" class="Generalizable">ℓ≅C</a><a id="1351" class="Symbol">)</a>
  <a id="1355" class="Symbol">→</a> <a id="1357" href="Cubical.Displayed.Base.html#1242" class="Record">DUARel</a> <a id="1364" class="Symbol">(</a><a id="1365" href="Cubical.Displayed.Base.html#2148" class="Function">∫</a> <a id="1367" href="Cubical.Displayed.Sigma.html#1328" class="Bound">𝒮ᴰ-C</a><a id="1371" class="Symbol">)</a> <a id="1373" class="Symbol">(λ</a> <a id="1376" class="Symbol">(</a><a id="1377" href="Cubical.Displayed.Sigma.html#1377" class="Bound">a</a> <a id="1379" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1381" class="Symbol">_)</a> <a id="1384" class="Symbol">→</a> <a id="1386" href="Cubical.Displayed.Sigma.html#1264" class="Bound">B</a> <a id="1388" href="Cubical.Displayed.Sigma.html#1377" class="Bound">a</a><a id="1389" class="Symbol">)</a> <a id="1391" href="Cubical.Displayed.Sigma.html#433" class="Generalizable">ℓ≅B</a>
<a id="1395" href="Cubical.Displayed.Sigma.html#1219" class="Function">𝒮ᴰ-Lift</a> <a id="1403" class="Symbol">_</a>  <a id="1406" href="Cubical.Displayed.Sigma.html#1406" class="Bound">𝒮ᴰ-B</a> <a id="1411" class="Symbol">_</a> <a id="1413" class="Symbol">=</a> <a id="1415" href="Cubical.Displayed.Morphism.html#1474" class="Function">𝒮ᴰ-reindex</a> <a id="1426" href="Cubical.Displayed.Sigma.html#799" class="Function">𝒮-fst</a> <a id="1432" href="Cubical.Displayed.Sigma.html#1406" class="Bound">𝒮ᴰ-B</a>

<a id="1438" class="Comment">-- DUARel on a Σ-type</a>

<a id="1461" class="Keyword">module</a> <a id="1468" href="Cubical.Displayed.Sigma.html#1468" class="Module">_</a> <a id="1470" class="Symbol">{</a><a id="1471" href="Cubical.Displayed.Sigma.html#1471" class="Bound">A</a> <a id="1473" class="Symbol">:</a> <a id="1475" href="Agda.Primitive.html#388" class="Primitive">Type</a> <a id="1480" href="Cubical.Displayed.Sigma.html#407" class="Generalizable">ℓA</a><a id="1482" class="Symbol">}</a> <a id="1484" class="Symbol">{</a><a id="1485" href="Cubical.Displayed.Sigma.html#1485" class="Bound">𝒮-A</a> <a id="1489" class="Symbol">:</a> <a id="1491" href="Cubical.Displayed.Base.html#433" class="Record">UARel</a> <a id="1497" href="Cubical.Displayed.Sigma.html#1471" class="Bound">A</a> <a id="1499" href="Cubical.Displayed.Sigma.html#417" class="Generalizable">ℓ≅A</a><a id="1502" class="Symbol">}</a>
  <a id="1506" class="Symbol">{</a><a id="1507" href="Cubical.Displayed.Sigma.html#1507" class="Bound">B</a> <a id="1509" class="Symbol">:</a> <a id="1511" href="Cubical.Displayed.Sigma.html#1471" class="Bound">A</a> <a id="1513" class="Symbol">→</a> <a id="1515" href="Agda.Primitive.html#388" class="Primitive">Type</a> <a id="1520" href="Cubical.Displayed.Sigma.html#426" class="Generalizable">ℓB</a><a id="1522" class="Symbol">}</a> <a id="1524" class="Symbol">{</a><a id="1525" href="Cubical.Displayed.Sigma.html#1525" class="Bound">ℓ≅B</a> <a id="1529" class="Symbol">:</a> <a id="1531" href="Agda.Primitive.html#742" class="Postulate">Level</a><a id="1536" class="Symbol">}</a> <a id="1538" class="Symbol">(</a><a id="1539" href="Cubical.Displayed.Sigma.html#1539" class="Bound">𝒮ᴰ-B</a> <a id="1544" class="Symbol">:</a> <a id="1546" href="Cubical.Displayed.Base.html#1242" class="Record">DUARel</a> <a id="1553" href="Cubical.Displayed.Sigma.html#1485" class="Bound">𝒮-A</a> <a id="1557" href="Cubical.Displayed.Sigma.html#1507" class="Bound">B</a> <a id="1559" href="Cubical.Displayed.Sigma.html#1525" class="Bound">ℓ≅B</a><a id="1562" class="Symbol">)</a>
  <a id="1566" class="Symbol">{</a><a id="1567" href="Cubical.Displayed.Sigma.html#1567" class="Bound">C</a> <a id="1569" class="Symbol">:</a> <a id="1571" href="Agda.Builtin.Sigma.html#165" class="Record">Σ</a> <a id="1573" href="Cubical.Displayed.Sigma.html#1471" class="Bound">A</a> <a id="1575" href="Cubical.Displayed.Sigma.html#1507" class="Bound">B</a> <a id="1577" class="Symbol">→</a> <a id="1579" href="Agda.Primitive.html#388" class="Primitive">Type</a> <a id="1584" href="Cubical.Displayed.Sigma.html#442" class="Generalizable">ℓC</a><a id="1586" class="Symbol">}</a> <a id="1588" class="Symbol">{</a><a id="1589" href="Cubical.Displayed.Sigma.html#1589" class="Bound">ℓ≅C</a> <a id="1593" class="Symbol">:</a> <a id="1595" href="Agda.Primitive.html#742" class="Postulate">Level</a><a id="1600" class="Symbol">}</a> <a id="1602" class="Symbol">(</a><a id="1603" href="Cubical.Displayed.Sigma.html#1603" class="Bound">𝒮ᴰ-C</a> <a id="1608" class="Symbol">:</a> <a id="1610" href="Cubical.Displayed.Base.html#1242" class="Record">DUARel</a> <a id="1617" class="Symbol">(</a><a id="1618" href="Cubical.Displayed.Base.html#2148" class="Function">∫</a> <a id="1620" href="Cubical.Displayed.Sigma.html#1539" class="Bound">𝒮ᴰ-B</a><a id="1624" class="Symbol">)</a> <a id="1626" href="Cubical.Displayed.Sigma.html#1567" class="Bound">C</a> <a id="1628" href="Cubical.Displayed.Sigma.html#1589" class="Bound">ℓ≅C</a><a id="1631" class="Symbol">)</a>
  <a id="1635" class="Keyword">where</a>

  <a id="1644" class="Keyword">open</a> <a id="1649" href="Cubical.Displayed.Base.html#433" class="Module">UARel</a> <a id="1655" href="Cubical.Displayed.Sigma.html#1485" class="Bound">𝒮-A</a>
  <a id="1661" class="Keyword">private</a>
    <a id="1673" class="Keyword">module</a> <a id="1680" href="Cubical.Displayed.Sigma.html#1680" class="Module">B</a> <a id="1682" class="Symbol">=</a> <a id="1684" href="Cubical.Displayed.Base.html#1242" class="Module">DUARel</a> <a id="1691" href="Cubical.Displayed.Sigma.html#1539" class="Bound">𝒮ᴰ-B</a>
    <a id="1700" class="Keyword">module</a> <a id="1707" href="Cubical.Displayed.Sigma.html#1707" class="Module">C</a> <a id="1709" class="Symbol">=</a> <a id="1711" href="Cubical.Displayed.Base.html#1242" class="Module">DUARel</a> <a id="1718" href="Cubical.Displayed.Sigma.html#1603" class="Bound">𝒮ᴰ-C</a>

  <a id="1726" href="Cubical.Displayed.Sigma.html#1726" class="Function">𝒮ᴰ-Σ</a> <a id="1731" class="Symbol">:</a> <a id="1733" href="Cubical.Displayed.Base.html#1242" class="Record">DUARel</a> <a id="1740" href="Cubical.Displayed.Sigma.html#1485" class="Bound">𝒮-A</a> <a id="1744" class="Symbol">(λ</a> <a id="1747" href="Cubical.Displayed.Sigma.html#1747" class="Bound">a</a> <a id="1749" class="Symbol">→</a> <a id="1751" href="Cubical.Core.Primitives.html#6268" class="Function">Σ[</a> <a id="1754" href="Cubical.Displayed.Sigma.html#1754" class="Bound">b</a> <a id="1756" href="Cubical.Core.Primitives.html#6268" class="Function">∈</a> <a id="1758" href="Cubical.Displayed.Sigma.html#1507" class="Bound">B</a> <a id="1760" href="Cubical.Displayed.Sigma.html#1747" class="Bound">a</a> <a id="1762" href="Cubical.Core.Primitives.html#6268" class="Function">]</a> <a id="1764" href="Cubical.Displayed.Sigma.html#1567" class="Bound">C</a> <a id="1766" class="Symbol">(</a><a id="1767" href="Cubical.Displayed.Sigma.html#1747" class="Bound">a</a> <a id="1769" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1771" href="Cubical.Displayed.Sigma.html#1754" class="Bound">b</a><a id="1772" class="Symbol">))</a> <a id="1775" class="Symbol">(</a><a id="1776" href="Agda.Primitive.html#961" class="Primitive">ℓ-max</a> <a id="1782" href="Cubical.Displayed.Sigma.html#1525" class="Bound">ℓ≅B</a> <a id="1786" href="Cubical.Displayed.Sigma.html#1589" class="Bound">ℓ≅C</a><a id="1789" class="Symbol">)</a>
  <a id="1793" href="Cubical.Displayed.Base.html#1457" class="Field Operator">DUARel._≅ᴰ⟨_⟩_</a> <a id="1808" href="Cubical.Displayed.Sigma.html#1726" class="Function">𝒮ᴰ-Σ</a> <a id="1813" class="Symbol">(</a><a id="1814" href="Cubical.Displayed.Sigma.html#1814" class="Bound">b</a> <a id="1816" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1818" href="Cubical.Displayed.Sigma.html#1818" class="Bound">c</a><a id="1819" class="Symbol">)</a> <a id="1821" href="Cubical.Displayed.Sigma.html#1821" class="Bound">p</a> <a id="1823" class="Symbol">(</a><a id="1824" href="Cubical.Displayed.Sigma.html#1824" class="Bound">b&#39;</a> <a id="1827" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1829" href="Cubical.Displayed.Sigma.html#1829" class="Bound">c&#39;</a><a id="1831" class="Symbol">)</a> <a id="1833" class="Symbol">=</a>
    <a id="1839" href="Cubical.Core.Primitives.html#6268" class="Function">Σ[</a> <a id="1842" href="Cubical.Displayed.Sigma.html#1842" class="Bound">q</a> <a id="1844" href="Cubical.Core.Primitives.html#6268" class="Function">∈</a> <a id="1846" href="Cubical.Displayed.Sigma.html#1814" class="Bound">b</a> <a id="1848" href="Cubical.Displayed.Base.html#1457" class="Function Operator">B.≅ᴰ⟨</a> <a id="1854" href="Cubical.Displayed.Sigma.html#1821" class="Bound">p</a> <a id="1856" href="Cubical.Displayed.Base.html#1457" class="Function Operator">⟩</a> <a id="1858" href="Cubical.Displayed.Sigma.html#1824" class="Bound">b&#39;</a> <a id="1861" href="Cubical.Core.Primitives.html#6268" class="Function">]</a>  <a id="1864" class="Symbol">(</a><a id="1865" href="Cubical.Displayed.Sigma.html#1818" class="Bound">c</a> <a id="1867" href="Cubical.Displayed.Base.html#1457" class="Field Operator">C.≅ᴰ⟨</a> <a id="1873" href="Cubical.Displayed.Sigma.html#1821" class="Bound">p</a> <a id="1875" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1877" href="Cubical.Displayed.Sigma.html#1842" class="Bound">q</a> <a id="1879" href="Cubical.Displayed.Base.html#1457" class="Field Operator">⟩</a> <a id="1881" href="Cubical.Displayed.Sigma.html#1829" class="Bound">c&#39;</a><a id="1883" class="Symbol">)</a>
  <a id="1887" href="Cubical.Displayed.Base.html#1515" class="Field">DUARel.uaᴰ</a> <a id="1898" href="Cubical.Displayed.Sigma.html#1726" class="Function">𝒮ᴰ-Σ</a> <a id="1903" class="Symbol">(</a><a id="1904" href="Cubical.Displayed.Sigma.html#1904" class="Bound">b</a> <a id="1906" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a>  <a id="1909" href="Cubical.Displayed.Sigma.html#1909" class="Bound">c</a><a id="1910" class="Symbol">)</a> <a id="1912" href="Cubical.Displayed.Sigma.html#1912" class="Bound">p</a> <a id="1914" class="Symbol">(</a><a id="1915" href="Cubical.Displayed.Sigma.html#1915" class="Bound">b&#39;</a> <a id="1918" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1920" href="Cubical.Displayed.Sigma.html#1920" class="Bound">c&#39;</a><a id="1922" class="Symbol">)</a> <a id="1924" class="Symbol">=</a>
    <a id="1930" href="Cubical.Foundations.Equiv.html#3542" class="Function">compEquiv</a>
      <a id="1946" class="Symbol">(</a><a id="1947" href="Cubical.Data.Sigma.Properties.html#9787" class="Function">Σ-cong-equiv</a> <a id="1960" class="Symbol">(</a><a id="1961" href="Cubical.Displayed.Base.html#1515" class="Function">B.uaᴰ</a> <a id="1967" href="Cubical.Displayed.Sigma.html#1904" class="Bound">b</a> <a id="1969" href="Cubical.Displayed.Sigma.html#1912" class="Bound">p</a> <a id="1971" href="Cubical.Displayed.Sigma.html#1915" class="Bound">b&#39;</a><a id="1973" class="Symbol">)</a> <a id="1975" class="Symbol">(λ</a> <a id="1978" href="Cubical.Displayed.Sigma.html#1978" class="Bound">q</a> <a id="1980" class="Symbol">→</a> <a id="1982" href="Cubical.Displayed.Base.html#1515" class="Field">C.uaᴰ</a> <a id="1988" href="Cubical.Displayed.Sigma.html#1909" class="Bound">c</a> <a id="1990" class="Symbol">(</a><a id="1991" href="Cubical.Displayed.Sigma.html#1912" class="Bound">p</a> <a id="1993" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1995" href="Cubical.Displayed.Sigma.html#1978" class="Bound">q</a><a id="1996" class="Symbol">)</a> <a id="1998" href="Cubical.Displayed.Sigma.html#1920" class="Bound">c&#39;</a><a id="2000" class="Symbol">))</a>
      <a id="2009" href="Cubical.Data.Sigma.Properties.html#2775" class="Function">ΣPath≃PathΣ</a>

<a id="2022" class="Comment">-- DUARel on a non-dependent Σ-type</a>

<a id="2059" class="Keyword">module</a> <a id="2066" href="Cubical.Displayed.Sigma.html#2066" class="Module">_</a> <a id="2068" class="Symbol">{</a><a id="2069" href="Cubical.Displayed.Sigma.html#2069" class="Bound">A</a> <a id="2071" class="Symbol">:</a> <a id="2073" href="Agda.Primitive.html#388" class="Primitive">Type</a> <a id="2078" href="Cubical.Displayed.Sigma.html#407" class="Generalizable">ℓA</a><a id="2080" class="Symbol">}</a> <a id="2082" class="Symbol">{</a><a id="2083" href="Cubical.Displayed.Sigma.html#2083" class="Bound">𝒮-A</a> <a id="2087" class="Symbol">:</a> <a id="2089" href="Cubical.Displayed.Base.html#433" class="Record">UARel</a> <a id="2095" href="Cubical.Displayed.Sigma.html#2069" class="Bound">A</a> <a id="2097" href="Cubical.Displayed.Sigma.html#417" class="Generalizable">ℓ≅A</a><a id="2100" class="Symbol">}</a>
  <a id="2104" class="Symbol">{</a><a id="2105" href="Cubical.Displayed.Sigma.html#2105" class="Bound">B</a> <a id="2107" class="Symbol">:</a> <a id="2109" href="Cubical.Displayed.Sigma.html#2069" class="Bound">A</a> <a id="2111" class="Symbol">→</a> <a id="2113" href="Agda.Primitive.html#388" class="Primitive">Type</a> <a id="2118" href="Cubical.Displayed.Sigma.html#426" class="Generalizable">ℓB</a><a id="2120" class="Symbol">}</a> <a id="2122" class="Symbol">{</a><a id="2123" href="Cubical.Displayed.Sigma.html#2123" class="Bound">ℓ≅B</a> <a id="2127" class="Symbol">:</a> <a id="2129" href="Agda.Primitive.html#742" class="Postulate">Level</a><a id="2134" class="Symbol">}</a> <a id="2136" class="Symbol">(</a><a id="2137" href="Cubical.Displayed.Sigma.html#2137" class="Bound">𝒮ᴰ-B</a> <a id="2142" class="Symbol">:</a> <a id="2144" href="Cubical.Displayed.Base.html#1242" class="Record">DUARel</a> <a id="2151" href="Cubical.Displayed.Sigma.html#2083" class="Bound">𝒮-A</a> <a id="2155" href="Cubical.Displayed.Sigma.html#2105" class="Bound">B</a> <a id="2157" href="Cubical.Displayed.Sigma.html#2123" class="Bound">ℓ≅B</a><a id="2160" class="Symbol">)</a>
  <a id="2164" class="Symbol">{</a><a id="2165" href="Cubical.Displayed.Sigma.html#2165" class="Bound">C</a> <a id="2167" class="Symbol">:</a> <a id="2169" href="Cubical.Displayed.Sigma.html#2069" class="Bound">A</a> <a id="2171" class="Symbol">→</a> <a id="2173" href="Agda.Primitive.html#388" class="Primitive">Type</a> <a id="2178" href="Cubical.Displayed.Sigma.html#442" class="Generalizable">ℓC</a><a id="2180" class="Symbol">}</a> <a id="2182" class="Symbol">{</a><a id="2183" href="Cubical.Displayed.Sigma.html#2183" class="Bound">ℓ≅C</a> <a id="2187" class="Symbol">:</a> <a id="2189" href="Agda.Primitive.html#742" class="Postulate">Level</a><a id="2194" class="Symbol">}</a> <a id="2196" class="Symbol">(</a><a id="2197" href="Cubical.Displayed.Sigma.html#2197" class="Bound">𝒮ᴰ-C</a> <a id="2202" class="Symbol">:</a> <a id="2204" href="Cubical.Displayed.Base.html#1242" class="Record">DUARel</a> <a id="2211" href="Cubical.Displayed.Sigma.html#2083" class="Bound">𝒮-A</a> <a id="2215" href="Cubical.Displayed.Sigma.html#2165" class="Bound">C</a> <a id="2217" href="Cubical.Displayed.Sigma.html#2183" class="Bound">ℓ≅C</a><a id="2220" class="Symbol">)</a>
  <a id="2224" class="Keyword">where</a>

  <a id="2233" href="Cubical.Displayed.Sigma.html#2233" class="Function Operator">_×𝒮ᴰ_</a> <a id="2239" class="Symbol">:</a> <a id="2241" href="Cubical.Displayed.Base.html#1242" class="Record">DUARel</a> <a id="2248" href="Cubical.Displayed.Sigma.html#2083" class="Bound">𝒮-A</a> <a id="2252" class="Symbol">(λ</a> <a id="2255" href="Cubical.Displayed.Sigma.html#2255" class="Bound">a</a> <a id="2257" class="Symbol">→</a> <a id="2259" href="Cubical.Displayed.Sigma.html#2105" class="Bound">B</a> <a id="2261" href="Cubical.Displayed.Sigma.html#2255" class="Bound">a</a> <a id="2263" href="Cubical.Data.Sigma.Base.html#461" class="Function Operator">×</a> <a id="2265" href="Cubical.Displayed.Sigma.html#2165" class="Bound">C</a> <a id="2267" href="Cubical.Displayed.Sigma.html#2255" class="Bound">a</a><a id="2268" class="Symbol">)</a> <a id="2270" class="Symbol">(</a><a id="2271" href="Agda.Primitive.html#961" class="Primitive">ℓ-max</a> <a id="2277" href="Cubical.Displayed.Sigma.html#2123" class="Bound">ℓ≅B</a> <a id="2281" href="Cubical.Displayed.Sigma.html#2183" class="Bound">ℓ≅C</a><a id="2284" class="Symbol">)</a>
  <a id="2288" href="Cubical.Displayed.Sigma.html#2233" class="Function Operator">_×𝒮ᴰ_</a> <a id="2294" class="Symbol">=</a> <a id="2296" href="Cubical.Displayed.Sigma.html#1726" class="Function">𝒮ᴰ-Σ</a> <a id="2301" href="Cubical.Displayed.Sigma.html#2137" class="Bound">𝒮ᴰ-B</a> <a id="2306" class="Symbol">(</a><a id="2307" href="Cubical.Displayed.Sigma.html#1219" class="Function">𝒮ᴰ-Lift</a> <a id="2315" href="Cubical.Displayed.Sigma.html#2083" class="Bound">𝒮-A</a> <a id="2319" href="Cubical.Displayed.Sigma.html#2197" class="Bound">𝒮ᴰ-C</a> <a id="2324" href="Cubical.Displayed.Sigma.html#2137" class="Bound">𝒮ᴰ-B</a><a id="2328" class="Symbol">)</a>

<a id="2331" class="Comment">-- SubstRel on a Σ-type</a>

<a id="2356" class="Keyword">module</a> <a id="2363" href="Cubical.Displayed.Sigma.html#2363" class="Module">_</a> <a id="2365" class="Symbol">{</a><a id="2366" href="Cubical.Displayed.Sigma.html#2366" class="Bound">A</a> <a id="2368" class="Symbol">:</a> <a id="2370" href="Agda.Primitive.html#388" class="Primitive">Type</a> <a id="2375" href="Cubical.Displayed.Sigma.html#407" class="Generalizable">ℓA</a><a id="2377" class="Symbol">}</a> <a id="2379" class="Symbol">{</a><a id="2380" href="Cubical.Displayed.Sigma.html#2380" class="Bound">𝒮-A</a> <a id="2384" class="Symbol">:</a> <a id="2386" href="Cubical.Displayed.Base.html#433" class="Record">UARel</a> <a id="2392" href="Cubical.Displayed.Sigma.html#2366" class="Bound">A</a> <a id="2394" href="Cubical.Displayed.Sigma.html#417" class="Generalizable">ℓ≅A</a><a id="2397" class="Symbol">}</a>
  <a id="2401" class="Symbol">{</a><a id="2402" href="Cubical.Displayed.Sigma.html#2402" class="Bound">B</a> <a id="2404" class="Symbol">:</a> <a id="2406" href="Cubical.Displayed.Sigma.html#2366" class="Bound">A</a> <a id="2408" class="Symbol">→</a> <a id="2410" href="Agda.Primitive.html#388" class="Primitive">Type</a> <a id="2415" href="Cubical.Displayed.Sigma.html#426" class="Generalizable">ℓB</a><a id="2417" class="Symbol">}</a> <a id="2419" class="Symbol">(</a><a id="2420" href="Cubical.Displayed.Sigma.html#2420" class="Bound">𝒮ˢ-B</a> <a id="2425" class="Symbol">:</a> <a id="2427" href="Cubical.Displayed.Subst.html#682" class="Record">SubstRel</a> <a id="2436" href="Cubical.Displayed.Sigma.html#2380" class="Bound">𝒮-A</a> <a id="2440" href="Cubical.Displayed.Sigma.html#2402" class="Bound">B</a><a id="2441" class="Symbol">)</a>
  <a id="2445" class="Symbol">{</a><a id="2446" href="Cubical.Displayed.Sigma.html#2446" class="Bound">C</a> <a id="2448" class="Symbol">:</a> <a id="2450" href="Agda.Builtin.Sigma.html#165" class="Record">Σ</a> <a id="2452" href="Cubical.Displayed.Sigma.html#2366" class="Bound">A</a> <a id="2454" href="Cubical.Displayed.Sigma.html#2402" class="Bound">B</a> <a id="2456" class="Symbol">→</a> <a id="2458" href="Agda.Primitive.html#388" class="Primitive">Type</a> <a id="2463" href="Cubical.Displayed.Sigma.html#442" class="Generalizable">ℓC</a><a id="2465" class="Symbol">}</a> <a id="2467" class="Symbol">(</a><a id="2468" href="Cubical.Displayed.Sigma.html#2468" class="Bound">𝒮ˢ-C</a> <a id="2473" class="Symbol">:</a> <a id="2475" href="Cubical.Displayed.Subst.html#682" class="Record">SubstRel</a> <a id="2484" class="Symbol">(</a><a id="2485" href="Cubical.Displayed.Sigma.html#480" class="Function">∫ˢ</a> <a id="2488" href="Cubical.Displayed.Sigma.html#2420" class="Bound">𝒮ˢ-B</a><a id="2492" class="Symbol">)</a> <a id="2494" href="Cubical.Displayed.Sigma.html#2446" class="Bound">C</a><a id="2495" class="Symbol">)</a>
  <a id="2499" class="Keyword">where</a>

  <a id="2508" class="Keyword">open</a> <a id="2513" href="Cubical.Displayed.Base.html#433" class="Module">UARel</a> <a id="2519" href="Cubical.Displayed.Sigma.html#2380" class="Bound">𝒮-A</a>
  <a id="2525" class="Keyword">open</a> <a id="2530" href="Cubical.Displayed.Subst.html#682" class="Module">SubstRel</a>
  <a id="2541" class="Keyword">private</a>
    <a id="2553" class="Keyword">module</a> <a id="2560" href="Cubical.Displayed.Sigma.html#2560" class="Module">B</a> <a id="2562" class="Symbol">=</a> <a id="2564" href="Cubical.Displayed.Subst.html#682" class="Module">SubstRel</a> <a id="2573" href="Cubical.Displayed.Sigma.html#2420" class="Bound">𝒮ˢ-B</a>
    <a id="2582" class="Keyword">module</a> <a id="2589" href="Cubical.Displayed.Sigma.html#2589" class="Module">C</a> <a id="2591" class="Symbol">=</a> <a id="2593" href="Cubical.Displayed.Subst.html#682" class="Module">SubstRel</a> <a id="2602" href="Cubical.Displayed.Sigma.html#2468" class="Bound">𝒮ˢ-C</a>

  <a id="2610" href="Cubical.Displayed.Sigma.html#2610" class="Function">𝒮ˢ-Σ</a> <a id="2615" class="Symbol">:</a> <a id="2617" href="Cubical.Displayed.Subst.html#682" class="Record">SubstRel</a> <a id="2626" href="Cubical.Displayed.Sigma.html#2380" class="Bound">𝒮-A</a> <a id="2630" class="Symbol">(λ</a> <a id="2633" href="Cubical.Displayed.Sigma.html#2633" class="Bound">a</a> <a id="2635" class="Symbol">→</a> <a id="2637" href="Cubical.Core.Primitives.html#6268" class="Function">Σ[</a> <a id="2640" href="Cubical.Displayed.Sigma.html#2640" class="Bound">b</a> <a id="2642" href="Cubical.Core.Primitives.html#6268" class="Function">∈</a> <a id="2644" href="Cubical.Displayed.Sigma.html#2402" class="Bound">B</a> <a id="2646" href="Cubical.Displayed.Sigma.html#2633" class="Bound">a</a> <a id="2648" href="Cubical.Core.Primitives.html#6268" class="Function">]</a> <a id="2650" href="Cubical.Displayed.Sigma.html#2446" class="Bound">C</a> <a id="2652" class="Symbol">(</a><a id="2653" href="Cubical.Displayed.Sigma.html#2633" class="Bound">a</a> <a id="2655" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="2657" href="Cubical.Displayed.Sigma.html#2640" class="Bound">b</a><a id="2658" class="Symbol">))</a>
  <a id="2663" href="Cubical.Displayed.Sigma.html#2610" class="Function">𝒮ˢ-Σ</a> <a id="2668" class="Symbol">.</a><a id="2669" href="Cubical.Displayed.Subst.html#872" class="Field">act</a> <a id="2673" href="Cubical.Displayed.Sigma.html#2673" class="Bound">p</a> <a id="2675" class="Symbol">=</a> <a id="2677" href="Cubical.Data.Sigma.Properties.html#9787" class="Function">Σ-cong-equiv</a> <a id="2690" class="Symbol">(</a><a id="2691" href="Cubical.Displayed.Subst.html#872" class="Function">B.act</a> <a id="2697" href="Cubical.Displayed.Sigma.html#2673" class="Bound">p</a><a id="2698" class="Symbol">)</a> <a id="2700" class="Symbol">(λ</a> <a id="2703" href="Cubical.Displayed.Sigma.html#2703" class="Bound">b</a> <a id="2705" class="Symbol">→</a> <a id="2707" href="Cubical.Displayed.Subst.html#872" class="Field">C.act</a> <a id="2713" class="Symbol">(</a><a id="2714" href="Cubical.Displayed.Sigma.html#2673" class="Bound">p</a> <a id="2716" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="2718" href="Cubical.Foundations.Prelude.html#915" class="Function">refl</a><a id="2722" class="Symbol">))</a>
  <a id="2727" href="Cubical.Displayed.Sigma.html#2610" class="Function">𝒮ˢ-Σ</a> <a id="2732" class="Symbol">.</a><a id="2733" href="Cubical.Displayed.Subst.html#915" class="Field">uaˢ</a> <a id="2737" href="Cubical.Displayed.Sigma.html#2737" class="Bound">p</a> <a id="2739" class="Symbol">_</a> <a id="2741" class="Symbol">=</a>
    <a id="2747" href="Cubical.Foundations.Prelude.html#13161" class="Function">fromPathP</a>
      <a id="2763" class="Symbol">(</a><a id="2764" href="Cubical.Displayed.Base.html#1515" class="Field">DUARel.uaᴰ</a> <a id="2775" class="Symbol">(</a><a id="2776" href="Cubical.Displayed.Sigma.html#1726" class="Function">𝒮ᴰ-Σ</a> <a id="2781" class="Symbol">(</a><a id="2782" href="Cubical.Displayed.Subst.html#1483" class="Function">Subst→DUA</a> <a id="2792" href="Cubical.Displayed.Sigma.html#2420" class="Bound">𝒮ˢ-B</a><a id="2796" class="Symbol">)</a> <a id="2798" class="Symbol">(</a><a id="2799" href="Cubical.Displayed.Subst.html#1483" class="Function">Subst→DUA</a> <a id="2809" href="Cubical.Displayed.Sigma.html#2468" class="Bound">𝒮ˢ-C</a><a id="2813" class="Symbol">))</a>  <a id="2817" class="Symbol">_</a> <a id="2819" href="Cubical.Displayed.Sigma.html#2737" class="Bound">p</a> <a id="2821" class="Symbol">_</a> <a id="2823" class="Symbol">.</a><a id="2824" href="Agda.Builtin.Sigma.html#251" class="Field">fst</a> <a id="2828" class="Symbol">(</a><a id="2829" href="Cubical.Foundations.Prelude.html#915" class="Function">refl</a> <a id="2834" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="2836" href="Cubical.Foundations.Prelude.html#915" class="Function">refl</a><a id="2840" class="Symbol">))</a>

<a id="2844" class="Comment">-- SubstRel on a non-dependent product</a>

<a id="2884" class="Keyword">module</a> <a id="2891" href="Cubical.Displayed.Sigma.html#2891" class="Module">_</a> <a id="2893" class="Symbol">{</a><a id="2894" href="Cubical.Displayed.Sigma.html#2894" class="Bound">A</a> <a id="2896" class="Symbol">:</a> <a id="2898" href="Agda.Primitive.html#388" class="Primitive">Type</a> <a id="2903" href="Cubical.Displayed.Sigma.html#407" class="Generalizable">ℓA</a><a id="2905" class="Symbol">}</a> <a id="2907" class="Symbol">{</a><a id="2908" href="Cubical.Displayed.Sigma.html#2908" class="Bound">𝒮-A</a> <a id="2912" class="Symbol">:</a> <a id="2914" href="Cubical.Displayed.Base.html#433" class="Record">UARel</a> <a id="2920" href="Cubical.Displayed.Sigma.html#2894" class="Bound">A</a> <a id="2922" href="Cubical.Displayed.Sigma.html#417" class="Generalizable">ℓ≅A</a><a id="2925" class="Symbol">}</a>
  <a id="2929" class="Symbol">{</a><a id="2930" href="Cubical.Displayed.Sigma.html#2930" class="Bound">B</a> <a id="2932" class="Symbol">:</a> <a id="2934" href="Cubical.Displayed.Sigma.html#2894" class="Bound">A</a> <a id="2936" class="Symbol">→</a> <a id="2938" href="Agda.Primitive.html#388" class="Primitive">Type</a> <a id="2943" href="Cubical.Displayed.Sigma.html#426" class="Generalizable">ℓB</a><a id="2945" class="Symbol">}</a> <a id="2947" class="Symbol">(</a><a id="2948" href="Cubical.Displayed.Sigma.html#2948" class="Bound">𝒮ˢ-B</a> <a id="2953" class="Symbol">:</a> <a id="2955" href="Cubical.Displayed.Subst.html#682" class="Record">SubstRel</a> <a id="2964" href="Cubical.Displayed.Sigma.html#2908" class="Bound">𝒮-A</a> <a id="2968" href="Cubical.Displayed.Sigma.html#2930" class="Bound">B</a><a id="2969" class="Symbol">)</a>
  <a id="2973" class="Symbol">{</a><a id="2974" href="Cubical.Displayed.Sigma.html#2974" class="Bound">C</a> <a id="2976" class="Symbol">:</a> <a id="2978" href="Cubical.Displayed.Sigma.html#2894" class="Bound">A</a> <a id="2980" class="Symbol">→</a> <a id="2982" href="Agda.Primitive.html#388" class="Primitive">Type</a> <a id="2987" href="Cubical.Displayed.Sigma.html#442" class="Generalizable">ℓC</a><a id="2989" class="Symbol">}</a> <a id="2991" class="Symbol">(</a><a id="2992" href="Cubical.Displayed.Sigma.html#2992" class="Bound">𝒮ˢ-C</a> <a id="2997" class="Symbol">:</a> <a id="2999" href="Cubical.Displayed.Subst.html#682" class="Record">SubstRel</a> <a id="3008" href="Cubical.Displayed.Sigma.html#2908" class="Bound">𝒮-A</a> <a id="3012" href="Cubical.Displayed.Sigma.html#2974" class="Bound">C</a><a id="3013" class="Symbol">)</a>
  <a id="3017" class="Keyword">where</a>

  <a id="3026" class="Keyword">open</a> <a id="3031" href="Cubical.Displayed.Base.html#433" class="Module">UARel</a> <a id="3037" href="Cubical.Displayed.Sigma.html#2908" class="Bound">𝒮-A</a>
  <a id="3043" class="Keyword">open</a> <a id="3048" href="Cubical.Displayed.Subst.html#682" class="Module">SubstRel</a>
  <a id="3059" class="Keyword">private</a>
    <a id="3071" class="Keyword">module</a> <a id="3078" href="Cubical.Displayed.Sigma.html#3078" class="Module">B</a> <a id="3080" class="Symbol">=</a> <a id="3082" href="Cubical.Displayed.Subst.html#682" class="Module">SubstRel</a> <a id="3091" href="Cubical.Displayed.Sigma.html#2948" class="Bound">𝒮ˢ-B</a>
    <a id="3100" class="Keyword">module</a> <a id="3107" href="Cubical.Displayed.Sigma.html#3107" class="Module">C</a> <a id="3109" class="Symbol">=</a> <a id="3111" href="Cubical.Displayed.Subst.html#682" class="Module">SubstRel</a> <a id="3120" href="Cubical.Displayed.Sigma.html#2992" class="Bound">𝒮ˢ-C</a>

  <a id="3128" href="Cubical.Displayed.Sigma.html#3128" class="Function Operator">_×𝒮ˢ_</a> <a id="3134" class="Symbol">:</a> <a id="3136" href="Cubical.Displayed.Subst.html#682" class="Record">SubstRel</a> <a id="3145" href="Cubical.Displayed.Sigma.html#2908" class="Bound">𝒮-A</a> <a id="3149" class="Symbol">(λ</a> <a id="3152" href="Cubical.Displayed.Sigma.html#3152" class="Bound">a</a> <a id="3154" class="Symbol">→</a> <a id="3156" href="Cubical.Displayed.Sigma.html#2930" class="Bound">B</a> <a id="3158" href="Cubical.Displayed.Sigma.html#3152" class="Bound">a</a> <a id="3160" href="Cubical.Data.Sigma.Base.html#461" class="Function Operator">×</a> <a id="3162" href="Cubical.Displayed.Sigma.html#2974" class="Bound">C</a> <a id="3164" href="Cubical.Displayed.Sigma.html#3152" class="Bound">a</a><a id="3165" class="Symbol">)</a>
  <a id="3169" href="Cubical.Displayed.Sigma.html#3128" class="Function Operator">_×𝒮ˢ_</a> <a id="3175" class="Symbol">.</a><a id="3176" href="Cubical.Displayed.Subst.html#872" class="Field">act</a> <a id="3180" href="Cubical.Displayed.Sigma.html#3180" class="Bound">p</a> <a id="3182" class="Symbol">=</a> <a id="3184" href="Cubical.Data.Sigma.Properties.html#13932" class="Function">≃-×</a> <a id="3188" class="Symbol">(</a><a id="3189" href="Cubical.Displayed.Subst.html#872" class="Function">B.act</a> <a id="3195" href="Cubical.Displayed.Sigma.html#3180" class="Bound">p</a><a id="3196" class="Symbol">)</a> <a id="3198" class="Symbol">(</a><a id="3199" href="Cubical.Displayed.Subst.html#872" class="Field">C.act</a> <a id="3205" href="Cubical.Displayed.Sigma.html#3180" class="Bound">p</a><a id="3206" class="Symbol">)</a>
  <a id="3210" href="Cubical.Displayed.Sigma.html#3128" class="Function Operator">_×𝒮ˢ_</a> <a id="3216" class="Symbol">.</a><a id="3217" href="Cubical.Displayed.Subst.html#915" class="Field">uaˢ</a> <a id="3221" href="Cubical.Displayed.Sigma.html#3221" class="Bound">p</a> <a id="3223" class="Symbol">(</a><a id="3224" href="Cubical.Displayed.Sigma.html#3224" class="Bound">b</a> <a id="3226" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="3228" href="Cubical.Displayed.Sigma.html#3228" class="Bound">c</a><a id="3229" class="Symbol">)</a> <a id="3231" class="Symbol">=</a> <a id="3233" href="Cubical.Data.Sigma.Properties.html#1955" class="Function">ΣPathP</a> <a id="3240" class="Symbol">(</a><a id="3241" href="Cubical.Displayed.Subst.html#915" class="Function">B.uaˢ</a> <a id="3247" href="Cubical.Displayed.Sigma.html#3221" class="Bound">p</a> <a id="3249" href="Cubical.Displayed.Sigma.html#3224" class="Bound">b</a> <a id="3251" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="3253" href="Cubical.Displayed.Subst.html#915" class="Field">C.uaˢ</a> <a id="3259" href="Cubical.Displayed.Sigma.html#3221" class="Bound">p</a> <a id="3261" href="Cubical.Displayed.Sigma.html#3228" class="Bound">c</a><a id="3262" class="Symbol">)</a>
</pre></body></html>